#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступом.ЗаполнитьНаборыЗначенийДоступа
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	СтрокаТаблицы = Таблица.Добавить();
	СтрокаТаблицы.ЗначениеДоступа = Подразделение;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = Документы.ФактПоказателейЭффективностиПодразделения.ПолучитьДанныеДляПроведения(Ссылка);
	КлючевыеПоказателиЭффективности.СформироватьДвиженияФактическиеЗначенияПоказателейЭффективностиПодразделений(Движения, ДанныеДляПроведения.ФактПодразделений);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТаблицаФактаПодразделений = РегистрыНакопления.ФактическиеЗначенияПоказателейЭффективностиПодразделений.ТаблицаИзмеренийПланаПодразделений();
	
	СуммируемыйСоответствие = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Показатели.ВыгрузитьКолонку("Показатель"), "Суммируемый");
	
	Для каждого СтрокаПоказателя Из Показатели Цикл
		Если СуммируемыйСоответствие.Получить(СтрокаПоказателя.Показатель) = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаФактаПодразделений.Добавить();
		НоваяСтрока.ДатаНачала = НачалоМесяца(Период);
		НоваяСтрока.ДатаОкончания = КонецМесяца(Период);
		НоваяСтрока.Горизонт = Горизонт;
		НоваяСтрока.Подразделение = Подразделение;
		НоваяСтрока.Показатель = СтрокаПоказателя.Показатель;
	КонецЦикла; 
	
	ТаблицаВведенныхЗначений = КлючевыеПоказателиЭффективности.ВведенныеФактическиеЗначенияПоказателейПодразделений(ТаблицаФактаПодразделений, Ссылка);
	
	Для каждого СтрокаВведенныхЗначений Из ТаблицаВведенныхЗначений Цикл
		НайденныеСтроки = Показатели.НайтиСтроки(Новый Структура("Показатель", СтрокаВведенныхЗначений.Показатель));
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Для подразделения %1 уже введено фактическое значение показателя %2.'"), Подразделение, СтрокаВведенныхЗначений.Показатель);
		Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Показатели[%1].Показатель", Показатели.Индекс(НайденнаяСтрока)),, Отказ);
		КонецЦикла; 
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказателиСтрокой = КлючевыеПоказателиЭффективности.ПоказателиСтрокой(Показатели);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда	

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(ФизическиеЛица.ФизическоеЛицо, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.НаправлениеНаВнешнююАттестацию;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьАттестацииСотрудников") Тогда
		
		АттестацииСотрудников.ДобавитьКомандуПечатиАттестационногоЛиста(КомандыПечати);
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.Медицина") Тогда
			
			Модуль = ОбщегоНазначения.ОбщийМодуль("Медицина");
			Модуль.ДобавитьКомандыПечатиПриказаОбАттестации(КомандыПечати);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПодписиДокументов

Функция ОписаниеПодписейДокумента() Экспорт
	
	ОписаниеПодписей = ПодписиДокументов.ОписаниеТаблицыПодписей();
	
	ОписаниеРеквизитовПодписанта = ПодписиДокументов.ОписаниеРеквизитовПодписанта();
	ОписаниеРеквизитовПодписанта.ФизическоеЛицо = "НачальникОтделаКадров";
	ОписаниеРеквизитовПодписанта.Должность = "ДолжностьНачальникаОтделаКадров";
	ОписаниеРеквизитовПодписанта.ОснованиеПодписи = "ОснованиеПодписиНачальникаОтделаКадров";
	
	ПереопределяемыеИменаРеквизитов = Новый Соответствие;
	ПереопределяемыеИменаРеквизитов.Вставить("РуководительКадровойСлужбы", ОписаниеРеквизитовПодписанта);
	
	ПодписиДокументов.ДобавитьОписаниеПодписейОрганизации(
		ОписаниеПодписей,
		"Руководитель,РуководительКадровойСлужбы",
		ПереопределяемыеИменаРеквизитов);
		
	Возврат ОписаниеПодписей;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли

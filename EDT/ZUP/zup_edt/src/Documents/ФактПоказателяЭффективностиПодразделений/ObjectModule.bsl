#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступом.ЗаполнитьНаборыЗначенийДоступа.
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	МассивПодразделений = Подразделения.ВыгрузитьКолонку("Подразделение");
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицуИзМассива(Таблица, МассивПодразделений, "ЗначениеДоступа");
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = Документы.ФактПоказателяЭффективностиПодразделений.ПолучитьДанныеДляПроведения(Ссылка);
	КлючевыеПоказателиЭффективности.СформироватьДвиженияФактическиеЗначенияПоказателейЭффективностиПодразделений(Движения, ДанныеДляПроведения.ФактПодразделений);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЭтоСуммируемыйПоказатель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Показатель, "Суммируемый");
	Если ЭтоСуммируемыйПоказатель = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаФактаПодразделений = РегистрыНакопления.ФактическиеЗначенияПоказателейЭффективностиПодразделений.ТаблицаИзмеренийПланаПодразделений();
	
	Для каждого СтрокаПодразделений Из Подразделения Цикл
		НоваяСтрока = ТаблицаФактаПодразделений.Добавить();
		НоваяСтрока.ДатаНачала = НачалоМесяца(Период);
		НоваяСтрока.ДатаОкончания = КонецМесяца(Период);
		НоваяСтрока.Горизонт = Горизонт;
		НоваяСтрока.Подразделение = СтрокаПодразделений.Подразделение;
		НоваяСтрока.Показатель = Показатель;
	КонецЦикла; 
	
	ТаблицаВведенныхЗначений = КлючевыеПоказателиЭффективности.ВведенныеФактическиеЗначенияПоказателейПодразделений(ТаблицаФактаПодразделений, Ссылка);
	
	Для каждого СтрокаВведенныхЗначений Из ТаблицаВведенныхЗначений Цикл
		НайденныеСтроки = Подразделения.НайтиСтроки(Новый Структура("Подразделение", СтрокаВведенныхЗначений.Подразделение));
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Для подразделения %1 уже введено фактическое значение показателя %2.'"), СтрокаВведенныхЗначений.Подразделение, Показатель);
		Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Подразделения[%1].Подразделение", Подразделения.Индекс(НайденнаяСтрока)),, Отказ);
		КонецЦикла; 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	ИспытательныйСрокСотрудников.СформироватьДвиженияИспытательногоСрока(Движения, ДанныеДляПроведения);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ДатаПродления = ТекущаяДатаСеанса() + ЗарплатаКадрыКлиентСервер.ДлительностьСутокВСекундах();
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		Сотрудник = ДанныеЗаполнения;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	КадровыеДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудников(
		Истина, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник), "ДатаПриема,ИспытательныйСрокДатаЗавершения", Дата);
		
	Если ДатаПродления < КадровыеДанныеСотрудника[0].ИспытательныйСрокДатаЗавершения 
		И ДатаПродления > КадровыеДанныеСотрудника[0].ДатаПриема Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Сотрудник %1 находится на испытательном сроке.'"), Сотрудник);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ДатаПродления", ,Отказ);
	КонецЕсли;
	
	Если ДатаПродления < КадровыеДанныеСотрудника[0].ИспытательныйСрокДатаЗавершения 
		И ДатаПродления < КадровыеДанныеСотрудника[0].ДатаПриема Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Сотрудник %1 не принят.'"), Сотрудник);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ДатаПродления", ,Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИсходныеДанные = СостоянияСотрудников.ПустаяТаблицаДанныхСостоянийСотрудника();
	
	НоваяСтрока = ИсходныеДанные.Добавить();
	НоваяСтрока.Сотрудник = Сотрудник;
	НоваяСтрока.Состояние = Перечисления.СостоянияСотрудника.Работа;
	НоваяСтрока.Начало = ДатаПродления;
	НоваяСтрока.Окончание = ДатаПродления;
	
	РезультатПроверки = СостоянияСотрудников.ПроверитьПересечениеПериодовОтсутствия(ИсходныеДанные, Ссылка);
	ДанныеСотрудника = РезультатПроверки.ДанныеСотрудников.Получить(Сотрудник);
	
	Если ДанныеСотрудника <> Неопределено Тогда
		ТекстСообщения = ?(ДанныеСотрудника.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияСотрудника.Увольнение"),
			НСтр("ru = 'Сотрудник был уволен %1 (%2).'"),
			НСтр("ru = 'На период %1 сотруднику уже зарегистрировано отсутствие документом %2.'"));
		ТекстСообщения = СтрШаблон(ТекстСообщения, ДанныеСотрудника.ПредставлениеПериода, ДанныеСотрудника.Регистратор);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ДатаПродления", ,Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПродлениеИспытательногоСрока.Дата КАК Период,
	|	ПродлениеИспытательногоСрока.Организация КАК Организация,
	|	ПродлениеИспытательногоСрока.Сотрудник КАК Сотрудник,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПродлениеИспытательногоСрока.ДатаПродления КАК ДатаЗавершения,
	|	ЛОЖЬ КАК ИспытательныйСрокЗавершен,
	|	ИСТИНА КАК ПродлениеИспытательногоСрока
	|ИЗ
	|	Документ.ПродлениеИспытательногоСрока КАК ПродлениеИспытательногоСрока
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО ПродлениеИспытательногоСрока.Сотрудник = Сотрудники.Ссылка
	|ГДЕ
	|	ПродлениеИспытательногоСрока.ДатаПродления <> ДАТАВРЕМЯ(1, 1, 1)
	|	И ПродлениеИспытательногоСрока.Ссылка = &Ссылка";
	
	ДанныеДляПроведения = Новый Структура("ИспытательныйСрокСотрудников");
	ДанныеДляПроведения.ИспытательныйСрокСотрудников = Запрос.Выполнить().Выгрузить();
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
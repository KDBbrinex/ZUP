#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено Тогда
		ЗаполнитьНовыйОбъект();
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ПрофилиДолжностей") Тогда
		ЗаполнитьПоПрофилюДолжности(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ПланируемаяДатаЗакрытия, "Объект.ПланируемаяДатаЗакрытия", Отказ, НСтр("ru='Дата закрытия'"), , , Ложь);

	ПроверитьЗаполнениеПозиции(Отказ, ПроверяемыеРеквизиты);
	ПроверитьЗаполнениеДолжности(Отказ, ПроверяемыеРеквизиты);
	ПроверитьЗаполнениеПодразделения(Отказ, ПроверяемыеРеквизиты);
	ПроверитьЗаполнениеОписанияЗаявки(Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверим, нет ли зарегистрированных вакансий с параметрами заявки
	Если СпособНабора = Перечисления.СпособНабораПерсоналаНаВакансию.МассовыйНабор Тогда
		Если Справочники.Вакансии.ЕстьДругиеВакансииПоКомбинацииДолжностьПодразделение(Должность, Подразделение, Ссылка) Тогда
			ВызватьИсключение НСтр("ru = 'Существует вакансия с аналогичными должностью и подразделением.'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоПрофилюДолжности(Профиль)
	
	ПрофильДолжности = Профиль;
	
	ПараметрыПолучения = Справочники.ПрофилиДолжностей.ПараметрыПолученияДанныхПрофиляДолжности();
	ПараметрыПолучения.ЭтапыРаботыСКандидатами = Истина;
	
	ДанныеПрофиля = Справочники.ПрофилиДолжностей.ДанныеПрофиляДолжности(Профиль, ПараметрыПолучения);
	
	// Попробуем подобрать позицию штатного расписания, если штатное расписание используется.
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание") Тогда
		// Штатное расписание не используется, заполняем независимо от позиции подразделение и должность.
		Подразделение = ДанныеПрофиля.Подразделение;
	Иначе
		ТекстЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПозицииШтатногоРасписания.Ссылка,
			|	ПозицииШтатногоРасписания.Приоритет
			|ИЗ
			|	(ВЫБРАТЬ
			|		ШтатноеРасписание.Ссылка КАК Ссылка,
			|		1 КАК Приоритет
			|	ИЗ
			|		Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МестоПозицииШтатногоРасписанияВСтруктуреПредприятия КАК МестоПозиции
			|			ПО (МестоПозиции.Позиция = ШтатноеРасписание.Ссылка)
			|				И (МестоПозиции.Подразделение = &Подразделение)
			|				И (ШтатноеРасписание.Должность = &Должность)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ШтатноеРасписание.Ссылка,
			|		2
			|	ИЗ
			|		Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|	ГДЕ
			|		ШтатноеРасписание.Должность = &Должность) КАК ПозицииШтатногоРасписания
			|
			|УПОРЯДОЧИТЬ ПО
			|	ПозицииШтатногоРасписания.Приоритет";

		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Должность", ДанныеПрофиля.Должность);
		Запрос.УстановитьПараметр("Подразделение", ДанныеПрофиля.Подразделение);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьПоПозицииШтатногоРасписания(Выборка.Ссылка);
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеПрофиля.Свойство("ХарактеристикиПерсонала") Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеПрофиля.ХарактеристикиПерсонала, ХарактеристикиПерсонала);
	КонецЕсли;
	Если ДанныеПрофиля.Свойство("ДействияСотрудников") Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеПрофиля.ДействияСотрудников, ДействияСотрудников);
	КонецЕсли;
	
	Требования = ДанныеПрофиля.Требования;
	Обязанности = ДанныеПрофиля.Обязанности;
	Условия = ДанныеПрофиля.Условия;
	
КонецПроцедуры

Процедура ЗаполнитьПоПозицииШтатногоРасписания(ПозицияШтатногоРасписания)
	
	Позиция = ПозицияШтатногоРасписания;
	
	ДанныеПозиции = Справочники.ШтатноеРасписание.ДанныеПозицииШтатногоРасписания(ПозицияШтатногоРасписания);
	
	Подразделение = ДанныеПозиции.МестоВСтруктуреПредприятия;
	ПредполагаемыйДоход = ДанныеПозиции.ФОТ;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеПозиции(Отказ, ПроверяемыеРеквизиты)
	
	Если НазначениеНабора = Перечисления.НазначениеНабораПерсоналаНаВакансию.ДолжностьПодразделение Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Позиция");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеПодразделения(Отказ, ПроверяемыеРеквизиты)
	
	Если СпособНабора = Перечисления.СпособНабораПерсоналаНаВакансию.МассовыйНабор Или НоваяПозиция Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Подразделение");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеДолжности(Отказ, ПроверяемыеРеквизиты)
	
	Если НоваяПозиция Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Должность");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеОписанияЗаявки(Отказ, ПроверяемыеРеквизиты)
	
	Если СоответствуетПрофилюДолжности Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПрофильДолжности");
		
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронноеИнтервью") Тогда
		ПроверяемыеРеквизиты.Добавить("ДействияСотрудников");
	Иначе
		ПроверяемыеРеквизиты.Добавить("Требования");
		ПроверяемыеРеквизиты.Добавить("Обязанности");
		ПроверяемыеРеквизиты.Добавить("Условия");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНовыйОбъект() Экспорт
	
	Если Не Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Дата)И Не ЗначениеЗаполнено(ПланируемаяДатаЗакрытия) Тогда
		ДатаОткрытия = ТекущаяДатаСеанса();
		ПланируемаяДатаЗакрытия =
			ПодборПерсонала.ПланируемаяДатаЗакрытияПоУмолчанию(ДатаОткрытия);
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	МассивСотрудников = Сотрудники.Выгрузить(, "Сотрудник").ВыгрузитьКолонку("Сотрудник");
	
	СтруктураИменТаблиц = МедицинскоеСтрахованиеФормы.СтруктураИменТаблиц();
	СтруктураИменТаблиц.ИмяТаблицыСотрудники = "Сотрудники";
	СтруктураИменТаблиц.ИмяТаблицыПрограммыСтрахованияСотрудников = "ПрограммыСтрахованияСотрудников";
	СтруктураИменТаблиц.ИмяТаблицыРасширенийПрограммСтрахованияСотрудников = "РасширенияПрограммСтрахованияСотрудников";
	
	Для каждого ЗначениеСтруктуры Из СтруктураИменТаблиц Цикл
		Если ЗначениеСтруктуры.Значение = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		СтрокиДляУдаления = Новый Массив;
		Для Каждого СтрокаТаблицы Из ЭтотОбъект[ЗначениеСтруктуры.Значение] Цикл
			Если МассивСотрудников.Найти(СтрокаТаблицы.Сотрудник) = Неопределено Тогда
				СтрокиДляУдаления.Добавить(СтрокаТаблицы);
			КонецЕсли;
		КонецЦикла;
		Для Каждого СтрокаДляУдаления Из СтрокиДляУдаления Цикл
			ЭтотОбъект[ЗначениеСтруктуры.Значение].Удалить(СтрокаДляУдаления);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ДанныеДокумента = Новый МенеджерВременныхТаблиц;
	СоздатьВТДанныеДокументов(ДанныеДокумента);
	
	// Проведение документа
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = РасчетЗарплатыРасширенный.СоздатьДанныеДляРегистрацииПлановыхУдержаний();
	ЗаполнитьДанныеДляРегистрацииПлановыхУдержанийСпискаСотрудников(ДанныеДляПроведения, Ссылка);
	
	ДвиженияУдержаний = Новый Структура;
	ДвиженияУдержаний.Вставить("ДанныеПлановыхУдержаний", ДанныеДляПроведения.ДанныеПлановыхУдержаний);
	
	РасчетЗарплатыРасширенный.СформироватьДвиженияПлановыхУдержаний(Движения, ДвиженияУдержаний);
	
	СформироватьДвиженияОткрепленияОтПрограммСтрахования(Движения, ДанныеДокумента);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьВТДанныеДокументов(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ОткреплениеСотрудники.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	"""" КАК Родственник,
	|	ОткреплениеПрограммыСтрахованияСотрудников.ПрограммаСтрахования КАК ПрограммаСтрахования,
	|	ЛОЖЬ КАК Действует,
	|	ОткреплениеСотрудники.ДатаОткрепления КАК Период,
	|	ОткреплениеСотрудники.ДатаОткрепления КАК ДействуетДо,
	|	ОткреплениеПрограммыСтрахованияСотрудников.СтраховаяПремия КАК СтраховаяПремия
	|ПОМЕСТИТЬ ВТСуммыСтраховыхПремийПрограммСтрахования
	|ИЗ
	|	Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.ПрограммыСтрахованияСотрудников КАК ОткреплениеПрограммыСтрахованияСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.Сотрудники КАК ОткреплениеСотрудники
	|		ПО ОткреплениеПрограммыСтрахованияСотрудников.Ссылка = ОткреплениеСотрудники.Ссылка
	|			И ОткреплениеПрограммыСтрахованияСотрудников.Сотрудник = ОткреплениеСотрудники.Сотрудник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОткреплениеОтПрограммМедицинскогоСтрахования КАК ТаблицаДокумента
	|		ПО ОткреплениеПрограммыСтрахованияСотрудников.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Организация,
	|	ОткреплениеРодственники.ФизическоеЛицо,
	|	ОткреплениеПрограммыСтрахованияРодственников.Родственник,
	|	ОткреплениеПрограммыСтрахованияРодственников.ПрограммаСтрахования,
	|	ЛОЖЬ,
	|	ОткреплениеРодственники.ДатаОткрепления,
	|	ОткреплениеРодственники.ДатаОткрепления,
	|	ОткреплениеПрограммыСтрахованияРодственников.СтраховаяПремия
	|ИЗ
	|	Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.ПрограммыСтрахованияРодственников КАК ОткреплениеПрограммыСтрахованияРодственников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.Родственники КАК ОткреплениеРодственники
	|		ПО ОткреплениеПрограммыСтрахованияРодственников.Ссылка = ОткреплениеРодственники.Ссылка
	|			И ОткреплениеПрограммыСтрахованияРодственников.ФизическоеЛицо = ОткреплениеРодственники.ФизическоеЛицо
	|			И ОткреплениеПрограммыСтрахованияРодственников.Родственник = ОткреплениеРодственники.Родственник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОткреплениеОтПрограммМедицинскогоСтрахования КАК ТаблицаДокумента
	|		ПО ОткреплениеПрограммыСтрахованияРодственников.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ОткреплениеСотрудники.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	"""" КАК Родственник,
	|	ОткреплениеРасширенияПрограммСтрахованияСотрудников.РасширениеСтрахования КАК РасширениеПрограммСтрахования,
	|	ЛОЖЬ КАК Действует,
	|	ОткреплениеСотрудники.ДатаОткрепления КАК Период,
	|	ОткреплениеСотрудники.ДатаОткрепления КАК ДействуетДо,
	|	ОткреплениеРасширенияПрограммСтрахованияСотрудников.СтраховаяПремия КАК СтраховаяПремия
	|ПОМЕСТИТЬ ВТСуммыСтраховыхПремийРасширенийПрограммСтрахования
	|ИЗ
	|	Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.РасширенияПрограммСтрахованияСотрудников КАК ОткреплениеРасширенияПрограммСтрахованияСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.Сотрудники КАК ОткреплениеСотрудники
	|		ПО ОткреплениеРасширенияПрограммСтрахованияСотрудников.Ссылка = ОткреплениеСотрудники.Ссылка
	|			И ОткреплениеРасширенияПрограммСтрахованияСотрудников.Сотрудник = ОткреплениеСотрудники.Сотрудник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОткреплениеОтПрограммМедицинскогоСтрахования КАК ТаблицаДокумента
	|		ПО ОткреплениеРасширенияПрограммСтрахованияСотрудников.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Организация,
	|	ОткреплениеРодственники.ФизическоеЛицо,
	|	ОткреплениеРасширенияПрограммСтрахованияРодственников.Родственник,
	|	ОткреплениеРасширенияПрограммСтрахованияРодственников.РасширениеСтрахования,
	|	ЛОЖЬ,
	|	ОткреплениеРодственники.ДатаОткрепления,
	|	ОткреплениеРодственники.ДатаОткрепления,
	|	ОткреплениеРасширенияПрограммСтрахованияРодственников.СтраховаяПремия
	|ИЗ
	|	Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.РасширенияПрограммСтрахованияРодственников КАК ОткреплениеРасширенияПрограммСтрахованияРодственников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.Родственники КАК ОткреплениеРодственники
	|		ПО ОткреплениеРасширенияПрограммСтрахованияРодственников.Ссылка = ОткреплениеРодственники.Ссылка
	|			И ОткреплениеРасширенияПрограммСтрахованияРодственников.ФизическоеЛицо = ОткреплениеРодственники.ФизическоеЛицо
	|			И ОткреплениеРасширенияПрограммСтрахованияРодственников.Родственник = ОткреплениеРодственники.Родственник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОткреплениеОтПрограммМедицинскогоСтрахования КАК ТаблицаДокумента
	|		ПО ОткреплениеРасширенияПрограммСтрахованияРодственников.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Регистратор";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Заполняет данные для проведения плановых удержаний для многосотрудникового документа.
//
// Параметры:
//		ДанныеДляПроведения - Структура, описанная в СоздатьДанныеДляРегистрацииПлановыхУдержаний.
//		Документ - Ссылка на документ.
//
Процедура ЗаполнитьДанныеДляРегистрацииПлановыхУдержанийСпискаСотрудников(ДанныеДляПроведения, Документ) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Сотрудники.ДатаОткрепления КАК ДатаСобытия,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДействуетДо,
	|	ДокументУдержания.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДокументУдержания.Ссылка.Организация.ГоловнаяОрганизация КАК Организация,
	|	ДокументУдержания.Удержание КАК Удержание,
	|	ДокументУдержания.ДокументОснование КАК ДокументОснование,
	|	0 КАК Размер,
	|	ЛОЖЬ КАК Используется
	|ИЗ
	|	Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.Удержания КАК ДокументУдержания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.Сотрудники КАК Сотрудники
	|		ПО ДокументУдержания.Ссылка = Сотрудники.Ссылка
	|			И ДокументУдержания.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
	|ГДЕ
	|	ДокументУдержания.Ссылка = &Документ
	|	И Сотрудники.ПрекратитьУдержания";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Документ", Документ);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляПроведения.ДанныеПлановыхУдержаний = РезультатыЗапроса[0].Выгрузить();
	
КонецПроцедуры

Процедура СформироватьДвиженияОткрепленияОтПрограммСтрахования(Движения, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СуммыСтраховыхПремийПрограммСтрахования.Период КАК Период,
	|	СуммыСтраховыхПремийПрограммСтрахования.Организация КАК Организация,
	|	СуммыСтраховыхПремийПрограммСтрахования.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СуммыСтраховыхПремийПрограммСтрахования.Родственник КАК Родственник,
	|	СуммыСтраховыхПремийПрограммСтрахования.ПрограммаСтрахования КАК ПрограммаСтрахования,
	|	СуммыСтраховыхПремийПрограммСтрахования.Действует КАК Действует,
	|	СуммыСтраховыхПремийПрограммСтрахования.ДействуетДо КАК ДействуетДо,
	|	СуммыСтраховыхПремийПрограммСтрахования.СтраховаяПремия КАК СтраховаяПремия
	|ИЗ
	|	ВТСуммыСтраховыхПремийПрограммСтрахования КАК СуммыСтраховыхПремийПрограммСтрахования";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Движения.ПрограммыМедицинскогоСтрахованияСотрудников.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Движения.ПрограммыМедицинскогоСтрахованияСотрудников.Записывать = Истина;
	КонецЦикла;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.Период КАК Период,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.Организация КАК Организация,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.Родственник КАК Родственник,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.РасширениеПрограммСтрахования КАК РасширениеПрограммСтрахования,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.Действует КАК Действует,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.ДействуетДо КАК ДействуетДо,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.СтраховаяПремия КАК СтраховаяПремия
	|ИЗ
	|	ВТСуммыСтраховыхПремийРасширенийПрограммСтрахования КАК СуммыСтраховыхПремийРасширенийПрограммСтрахования";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Движения.РасширенияПрограммМедицинскогоСтрахованияСотрудников.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Движения.РасширенияПрограммМедицинскогоСтрахованияСотрудников.Записывать = Истина;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
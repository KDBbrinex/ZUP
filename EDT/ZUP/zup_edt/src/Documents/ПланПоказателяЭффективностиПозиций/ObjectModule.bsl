#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступом.ЗаполнитьНаборыЗначенийДоступа.
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	СтрокаТаблицы = Таблица.Добавить();
	СтрокаТаблицы.ЗначениеДоступа = Подразделение;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = Документы.ПланПоказателяЭффективностиПозиций.ПолучитьДанныеДляПроведения(Ссылка);
	КлючевыеПоказателиЭффективности.СформироватьДвиженияПлановыеЗначенияПоказателейЭффективностиПозиций(Движения, ДанныеДляПроведения.ПланПозиций);
	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТаблицаПланаПозиций = РегистрыСведений.ПлановыеЗначенияПоказателейЭффективностиПозиций.ТаблицаИзмеренийПланаПозиций();
	
	Для каждого СтрокаПозиции Из Позиции Цикл
		НоваяСтрока = ТаблицаПланаПозиций.Добавить();
		НоваяСтрока.ПериодДействия = Период;
		НоваяСтрока.Горизонт = Горизонт;
		НоваяСтрока.Позиция = СтрокаПозиции.Позиция;
		НоваяСтрока.Показатель = Показатель;
	КонецЦикла; 
	
	ТаблицаВведенныхЗначений = КлючевыеПоказателиЭффективности.ВведенныеПлановыеЗначенияПоказателейПозиций(ТаблицаПланаПозиций, Ссылка);
	
	Для каждого СтрокаВведенныхЗначений Из ТаблицаВведенныхЗначений Цикл
		НайденныеСтроки = Позиции.НайтиСтроки(Новый Структура("Позиция", СтрокаВведенныхЗначений.Позиция));
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Для позиции %1 уже введено плановое значение показателя %2.'"), СтрокаВведенныхЗначений.Позиция, Показатель);
		Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Позиции[%1].Позиция", Позиции.Индекс(НайденнаяСтрока)),, Отказ);
		КонецЦикла; 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
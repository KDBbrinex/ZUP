#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	МассивСотрудников = Сотрудники.Выгрузить(, "Сотрудник").ВыгрузитьКолонку("Сотрудник");
	МассивФизическихЛиц = Новый Массив;
	ФизическиеЛицаСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивСотрудников, "ФизическоеЛицо");
	Для каждого ЭлементСоответствия Из ФизическиеЛицаСотрудников Цикл
		Если МассивФизическихЛиц.Найти(ЭлементСоответствия.Значение) = Неопределено Тогда
			МассивФизическихЛиц.Добавить(ЭлементСоответствия.Значение);
		КонецЕсли;
	КонецЦикла;
	
	СтруктураИменТаблиц = МедицинскоеСтрахованиеФормы.СтруктураИменТаблиц();
	СтруктураИменТаблиц.ИмяТаблицыСотрудники = "Сотрудники";
	СтруктураИменТаблиц.ИмяТаблицыСведенийСотрудников = "СведенияСотрудников";
	СтруктураИменТаблиц.ИмяТаблицыПрограммыСтрахованияСотрудников = "ПрограммыСтрахованияСотрудников";
	СтруктураИменТаблиц.ИмяТаблицыРасширенийПрограммСтрахованияСотрудников = "РасширенияПрограммСтрахованияСотрудников";
	СтруктураИменТаблиц.ИмяТаблицыРодственники = "Родственники";
	СтруктураИменТаблиц.ИмяТаблицыСведенийРодственников = "СведенияРодственников";
	СтруктураИменТаблиц.ИмяТаблицыПрограммыСтрахованияРодственников = "ПрограммыСтрахованияРодственников";
	СтруктураИменТаблиц.ИмяТаблицыРасширенийПрограммСтрахованияРодственников = "РасширенияПрограммСтрахованияРодственников";
	
	Для каждого ЗначениеСтруктуры Из СтруктураИменТаблиц Цикл
		СтрокиДляУдаления = Новый Массив;
		Для Каждого СтрокаТаблицы Из ЭтотОбъект[ЗначениеСтруктуры.Значение] Цикл
			Если СтрНайти(ЗначениеСтруктуры.Ключ, "Сотрудник") > 0 Тогда
				Если МассивСотрудников.Найти(СтрокаТаблицы.Сотрудник) = Неопределено Тогда
					СтрокиДляУдаления.Добавить(СтрокаТаблицы);
				КонецЕсли;
			Иначе
				Если МассивФизическихЛиц.Найти(СтрокаТаблицы.ФизическоеЛицо) = Неопределено Тогда
					СтрокиДляУдаления.Добавить(СтрокаТаблицы);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Для Каждого СтрокаДляУдаления Из СтрокиДляУдаления Цикл
			ЭтотОбъект[ЗначениеСтруктуры.Значение].Удалить(СтрокаДляУдаления);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ОтражениеЗарплатыВБухучетеРасширенный.УточнитьСоставПроверяемыхРеквизитовБухучетПлановыхУдержаний(ЭтотОбъект, ПроверяемыеРеквизиты);
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаНачалаСтрахования, "Объект.ДатаНачалаСтрахования", Отказ, НСтр("ru='Дата начала события'"), , , Ложь);
	
	УникальныеЗначения = Новый Соответствие;
	ИндексСтроки = 0;
	Для Каждого ДанныеФизическогоЛица Из Сотрудники Цикл
		Если УникальныеЗначения[ДанныеФизическогоЛица.Сотрудник] = Неопределено Тогда
			УникальныеЗначения.Вставить(ДанныеФизическогоЛица.Сотрудник, Истина);
		Иначе
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Информация о сотруднике %1 была введена в документе ранее.'"), ДанныеФизическогоЛица.Сотрудник);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка, "Объект.Сотрудники[" + Формат(ИндексСтроки, "ЧН=0; ЧГ=0") + "].Сотрудник", ,Отказ);
		КонецЕсли;
		Если ДанныеФизическогоЛица.СтраховатьСотрудника Тогда
			ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДанныеФизическогоЛица.ДатаПрикрепления, "Объект.Сотрудники[" + Формат(ИндексСтроки, "ЧН=0; ЧГ=0") + "].ДатаПрикрепления", Отказ,
				НСтр("ru='Дата прикрепления'"), ДатаНачалаСтрахования, НСтр("ru='Даты начала страхования'"));
		КонецЕсли;
		ИндексСтроки = ИндексСтроки + 1;
	КонецЦикла;
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияРабочихМестВОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Организация 		= Организация;
	ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода		= ДатаНачалаСтрахования;
	ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода	= ДатаОкончанияСтрахования;
	
	КадровыйУчет.ПроверитьРаботающихСотрудников(
		ОбщегоНазначения.ВыгрузитьКолонку(Сотрудники, "Сотрудник"),
		ПараметрыПолученияСотрудниковОрганизаций,
		Отказ,
		Новый Структура("ИмяПоляСотрудник, ИмяОбъекта", "Сотрудник", "Объект"));
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПрочиеУдержанияВПользуТретьихЛиц") Тогда
		НайденныйРеквизит = ПроверяемыеРеквизиты.Найти("Удержание");
		Если НайденныйРеквизит <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(НайденныйРеквизит);
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники.Выгрузить());
	Запрос.УстановитьПараметр("ДатаОкончанияСтрахования", ДатаОкончанияСтрахования);
	Запрос.УстановитьПараметр("ПрограммыСтрахованияСотрудников", ПрограммыСтрахованияСотрудников.Выгрузить());
	Запрос.УстановитьПараметр("РасширенияПрограммСтрахованияСотрудников", РасширенияПрограммСтрахованияСотрудников.Выгрузить());
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Сотрудники.ДатаПрикрепления КАК ДатаПрикрепления,
	|	&ДатаОкончанияСтрахования КАК ДатаОкончанияСтрахования,
	|	Сотрудники.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	&Сотрудники КАК Сотрудники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрограммыСтрахованияСотрудников.Сотрудник КАК Сотрудник,
	|	ПрограммыСтрахованияСотрудников.ПрограммаСтрахования КАК ПрограммаСтрахования
	|ПОМЕСТИТЬ ВТПрограммыСтрахованияСотрудников
	|ИЗ
	|	&ПрограммыСтрахованияСотрудников КАК ПрограммыСтрахованияСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасширенияПрограммСтрахованияСотрудников.Сотрудник КАК Сотрудник,
	|	РасширенияПрограммСтрахованияСотрудников.РасширениеСтрахования КАК РасширениеСтрахования
	|ПОМЕСТИТЬ ВТРасширенияПрограммСтрахованияСотрудников
	|ИЗ
	|	&РасширенияПрограммСтрахованияСотрудников КАК РасширенияПрограммСтрахованияСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.ДатаПрикрепления КАК ДатаПрикрепления,
	|	Сотрудники.ДатаОкончанияСтрахования КАК ДатаОкончанияСтрахования,
	|	ПрограммыСтрахованияСотрудников.ПрограммаСтрахования КАК ПрограммаСтрахования,
	|	Сотрудники.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТСотрудники КАК Сотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПрограммыСтрахованияСотрудников КАК ПрограммыСтрахованияСотрудников
	|		ПО Сотрудники.Сотрудник = ПрограммыСтрахованияСотрудников.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрограммыМедицинскогоСтрахованияСотрудников КАК ПрограммыМедицинскогоСтрахованияСотрудников
	|		ПО (ПрограммыМедицинскогоСтрахованияСотрудников.Организация = &Организация)
	|			И (ПрограммыСтрахованияСотрудников.ПрограммаСтрахования = ПрограммыМедицинскогоСтрахованияСотрудников.ПрограммаСтрахования)
	|			И Сотрудники.ФизическоеЛицо = ПрограммыМедицинскогоСтрахованияСотрудников.ФизическоеЛицо
	|ГДЕ
	|	ПрограммыМедицинскогоСтрахованияСотрудников.Регистратор <> &Ссылка
	|	И ПрограммыМедицинскогоСтрахованияСотрудников.Действует
	|	И (Сотрудники.ДатаПрикрепления МЕЖДУ ПрограммыМедицинскогоСтрахованияСотрудников.Период И ПрограммыМедицинскогоСтрахованияСотрудников.ДействуетДо
	|			ИЛИ Сотрудники.ДатаОкончанияСтрахования МЕЖДУ ПрограммыМедицинскогоСтрахованияСотрудников.Период И ПрограммыМедицинскогоСтрахованияСотрудников.ДействуетДо)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.ДатаПрикрепления КАК ДатаПрикрепления,
	|	Сотрудники.ДатаОкончанияСтрахования КАК ДатаОкончанияСтрахования,
	|	РасширенияПрограммСтрахованияСотрудников.РасширениеСтрахования КАК РасширениеСтрахования,
	|	Сотрудники.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТСотрудники КАК Сотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасширенияПрограммСтрахованияСотрудников КАК РасширенияПрограммСтрахованияСотрудников
	|		ПО Сотрудники.Сотрудник = РасширенияПрограммСтрахованияСотрудников.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасширенияПрограммМедицинскогоСтрахованияСотрудников КАК РасширенияПрограммМедицинскогоСтрахованияСотрудников
	|		ПО (РасширенияПрограммМедицинскогоСтрахованияСотрудников.Организация = &Организация)
	|			И (РасширенияПрограммСтрахованияСотрудников.РасширениеСтрахования = РасширенияПрограммМедицинскогоСтрахованияСотрудников.РасширениеПрограммСтрахования)
	|			И Сотрудники.ФизическоеЛицо = РасширенияПрограммМедицинскогоСтрахованияСотрудников.ФизическоеЛицо
	|ГДЕ
	|	РасширенияПрограммМедицинскогоСтрахованияСотрудников.Регистратор <> &Ссылка
	|	И РасширенияПрограммМедицинскогоСтрахованияСотрудников.Действует
	|	И (Сотрудники.ДатаПрикрепления МЕЖДУ РасширенияПрограммМедицинскогоСтрахованияСотрудников.Период И РасширенияПрограммМедицинскогоСтрахованияСотрудников.ДействуетДо
	|			ИЛИ Сотрудники.ДатаОкончанияСтрахования МЕЖДУ РасширенияПрограммМедицинскогоСтрахованияСотрудников.Период И РасширенияПрограммМедицинскогоСтрахованияСотрудников.ДействуетДо)";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ШаблонТекстаОшибки = НСтр("ru = 'Сотрудник %1 прикреплен к программе страхования ""%2"" в периоде с %3 по %4.'");
	ВыборкаПрограммСтрахования = РезультатЗапроса[3].Выбрать();
	Пока ВыборкаПрограммСтрахования.Следующий() Цикл
		ТекстОшибки = СтрШаблон(
			ШаблонТекстаОшибки,
			ВыборкаПрограммСтрахования.Сотрудник,
			ВыборкаПрограммСтрахования.ПрограммаСтрахования,
			Формат(ВыборкаПрограммСтрахования.ДатаПрикрепления, "ДЛФ=D"),
			Формат(ВыборкаПрограммСтрахования.ДатаОкончанияСтрахования, "ДЛФ=D"));
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка, "Объект.Сотрудники[" + Формат(ВыборкаПрограммСтрахования.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].Сотрудник", , Отказ);
	КонецЦикла;
	
	ШаблонТекстаОшибки = НСтр("ru = 'Сотрудник %1 прикреплен к расширению программы страхования ""%2"" в периоде с %3 по %4.'");
	ВыборкаРасширенийПрограммСтрахования = РезультатЗапроса[4].Выбрать();
	Пока ВыборкаРасширенийПрограммСтрахования.Следующий() Цикл
		ТекстОшибки = СтрШаблон(
			ШаблонТекстаОшибки,
			ВыборкаРасширенийПрограммСтрахования.Сотрудник,
			ВыборкаРасширенийПрограммСтрахования.РасширениеСтрахования,
			Формат(ВыборкаРасширенийПрограммСтрахования.ДатаПрикрепления, "ДЛФ=D"),
			Формат(ВыборкаРасширенийПрограммСтрахования.ДатаОкончанияСтрахования, "ДЛФ=D"));
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка, "Объект.Сотрудники[" + Формат(ВыборкаРасширенийПрограммСтрахования.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].Сотрудник", , Отказ);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Подготовка к регистрации перерасчетов
	ДанныеДокумента = Новый МенеджерВременныхТаблиц;
	СоздатьВТДанныеДокументов(ДанныеДокумента);
	
	// Проведение документа
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = РасчетЗарплатыРасширенный.СоздатьДанныеДляРегистрацииПлановыхУдержаний();
	ЗаполнитьДанныеДляРегистрацииПлановыхУдержанийСпискаСотрудников(ДанныеДляПроведения, Ссылка);
	ЗаполнитьПредельныеСуммыУдержанийСотрудников(ДанныеДляПроведения);
	
	ДвиженияУдержаний = Новый Структура;
	ДвиженияУдержаний.Вставить("ДанныеПлановыхУдержаний", ДанныеДляПроведения.ДанныеПлановыхУдержаний);
	ДвиженияУдержаний.Вставить("ЗначенияПоказателей", ДанныеДляПроведения.ЗначенияПоказателей);
	ДвиженияУдержаний.Вставить("РабочиеМестаУдержаний", ДанныеДляПроведения.РабочиеМестаУдержаний);
	
	РасчетЗарплатыРасширенный.СформироватьДвиженияПлановыхУдержаний(Движения, ДвиженияУдержаний);
	РасчетЗарплатыРасширенный.СформироватьДвиженияПредельныхСуммУдержанийСотрудников(Движения, ДанныеДляПроведения.ПредельныеСуммыУдержанийСотрудников);
	
	СформироватьДвиженияПрикрепленияКПрограммамСтрахования(Движения, ДанныеДокумента);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьВТДанныеДокументов(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ПрикреплениеСотрудники.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	"""" КАК Родственник,
	|	ПрикреплениеПрограммыСтрахованияСотрудников.ПрограммаСтрахования КАК ПрограммаСтрахования,
	|	ИСТИНА КАК Действует,
	|	ПрикреплениеСотрудники.ДатаПрикрепления КАК Период,
	|	ТаблицаДокумента.ДатаОкончанияСтрахования КАК ДействуетДо,
	|	ПрикреплениеПрограммыСтрахованияСотрудников.СтраховаяПремия КАК СтраховаяПремия
	|ПОМЕСТИТЬ ВТСуммыСтраховыхПремийПрограммСтрахования
	|ИЗ
	|	Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.ПрограммыСтрахованияСотрудников КАК ПрикреплениеПрограммыСтрахованияСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Сотрудники КАК ПрикреплениеСотрудники
	|		ПО ПрикреплениеПрограммыСтрахованияСотрудников.Ссылка = ПрикреплениеСотрудники.Ссылка
	|			И ПрикреплениеПрограммыСтрахованияСотрудников.Сотрудник = ПрикреплениеСотрудники.Сотрудник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования КАК ТаблицаДокумента
	|		ПО ПрикреплениеПрограммыСтрахованияСотрудников.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Организация,
	|	ПрикреплениеРодственники.ФизическоеЛицо,
	|	ПрикреплениеРодственники.Родственник,
	|	ПрикреплениеПрограммыСтрахованияРодственников.ПрограммаСтрахования,
	|	ИСТИНА,
	|	ПрикреплениеРодственники.ДатаПрикрепления,
	|	ПрикреплениеРодственники.ДатаОткрепления,
	|	ПрикреплениеПрограммыСтрахованияРодственников.СтраховаяПремия
	|ИЗ
	|	Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.ПрограммыСтрахованияРодственников КАК ПрикреплениеПрограммыСтрахованияРодственников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Родственники КАК ПрикреплениеРодственники
	|		ПО ПрикреплениеПрограммыСтрахованияРодственников.Ссылка = ПрикреплениеРодственники.Ссылка
	|			И ПрикреплениеПрограммыСтрахованияРодственников.Родственник = ПрикреплениеРодственники.Родственник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования КАК ТаблицаДокумента
	|		ПО ПрикреплениеПрограммыСтрахованияРодственников.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ПрикреплениеСотрудники.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	"""" КАК Родственник,
	|	ПрикреплениеРасширенияПрограммСтрахованияСотрудников.РасширениеСтрахования КАК РасширениеПрограммСтрахования,
	|	ИСТИНА КАК Действует,
	|	ПрикреплениеСотрудники.ДатаПрикрепления КАК Период,
	|	ТаблицаДокумента.ДатаОкончанияСтрахования КАК ДействуетДо,
	|	ПрикреплениеРасширенияПрограммСтрахованияСотрудников.СтраховаяПремия КАК СтраховаяПремия
	|ПОМЕСТИТЬ ВТСуммыСтраховыхПремийРасширенийПрограммСтрахования
	|ИЗ
	|	Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.РасширенияПрограммСтрахованияСотрудников КАК ПрикреплениеРасширенияПрограммСтрахованияСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Сотрудники КАК ПрикреплениеСотрудники
	|		ПО ПрикреплениеРасширенияПрограммСтрахованияСотрудников.Ссылка = ПрикреплениеСотрудники.Ссылка
	|			И ПрикреплениеРасширенияПрограммСтрахованияСотрудников.Сотрудник = ПрикреплениеСотрудники.Сотрудник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования КАК ТаблицаДокумента
	|		ПО ПрикреплениеРасширенияПрограммСтрахованияСотрудников.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Организация,
	|	ПрикреплениеРодственники.ФизическоеЛицо,
	|	ПрикреплениеРодственники.Родственник,
	|	ПрикреплениеРасширенияПрограммСтрахованияРодственников.РасширениеСтрахования,
	|	ИСТИНА,
	|	ПрикреплениеРодственники.ДатаПрикрепления,
	|	ПрикреплениеРодственники.ДатаОткрепления,
	|	ПрикреплениеРасширенияПрограммСтрахованияРодственников.СтраховаяПремия
	|ИЗ
	|	Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.РасширенияПрограммСтрахованияРодственников КАК ПрикреплениеРасширенияПрограммСтрахованияРодственников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Родственники КАК ПрикреплениеРодственники
	|		ПО ПрикреплениеРасширенияПрограммСтрахованияРодственников.Ссылка = ПрикреплениеРодственники.Ссылка
	|			И ПрикреплениеРасширенияПрограммСтрахованияРодственников.Родственник = ПрикреплениеРодственники.Родственник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования КАК ТаблицаДокумента
	|		ПО ПрикреплениеРасширенияПрограммСтрахованияРодственников.Ссылка = ТаблицаДокумента.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Регистратор";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ЗаполнитьПредельныеСуммыУдержанийСотрудников(ДанныеДляПроведения)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Документ", Ссылка);
	ВидРасчетаИнфо = ЗарплатаКадрыРасширенныйПовтИсп.ПолучитьИнформациюОВидеРасчета(Удержание);
	Запрос.УстановитьПараметр("ВидРасчетаРассчитывается", ?(ВидРасчетаИнфо.Рассчитывается = Неопределено, Ложь, ВидРасчетаИнфо.Рассчитывается));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрикреплениеКПрограммамМедицинскогоСтрахованияСотрудники.ДатаПрикрепления КАК Период,
	|	ПрикреплениеКПрограммамМедицинскогоСтрахования.Организация.ГоловнаяОрганизация КАК Организация,
	|	ПрикреплениеКПрограммамМедицинскогоСтрахованияСотрудники.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПрикреплениеКПрограммамМедицинскогоСтрахования.Удержание КАК Удержание,
	|	ПрикреплениеКПрограммамМедицинскогоСтрахованияСотрудники.Ссылка КАК ДокументОснование,
	|	ИСТИНА КАК ПрекратитьПоДостижениюПредела,
	|	ПрикреплениеКПрограммамМедицинскогоСтрахованияСотрудники.СуммаПредела КАК Сумма
	|ИЗ
	|	Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Сотрудники КАК ПрикреплениеКПрограммамМедицинскогоСтрахованияСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования КАК ПрикреплениеКПрограммамМедицинскогоСтрахования
	|		ПО ПрикреплениеКПрограммамМедицинскогоСтрахованияСотрудники.Ссылка = ПрикреплениеКПрограммамМедицинскогоСтрахования.Ссылка
	|ГДЕ
	|	ПрикреплениеКПрограммамМедицинскогоСтрахованияСотрудники.Ссылка = &Документ
	|	И ПрикреплениеКПрограммамМедицинскогоСтрахованияСотрудники.СуммаПредела <> 0
	|	И НЕ &ВидРасчетаРассчитывается
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументУдержания.ДатаПрикрепления,
	|	ДокументУдержания.ОрганизацияГоловнаяОрганизация,
	|	ДокументУдержания.ФизическоеЛицо,
	|	ДокументУдержания.Удержание,
	|	ДокументУдержания.Ссылка,
	|	ИСТИНА,
	|	СУММА(ДокументУдержания.СтраховаяПремия)
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДокументУдержания.ДатаПрикрепления КАК ДатаПрикрепления,
	|		ПрикреплениеКПрограммамМедицинскогоСтрахования.Организация.ГоловнаяОрганизация КАК ОрганизацияГоловнаяОрганизация,
	|		ДокументУдержания.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ПрикреплениеКПрограммамМедицинскогоСтрахования.Удержание КАК Удержание,
	|		ДокументУдержания.Ссылка КАК Ссылка,
	|		ДокументУдержания.СтраховаяПремия КАК СтраховаяПремия
	|	ИЗ
	|		Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Сотрудники КАК ДокументУдержания
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования КАК ПрикреплениеКПрограммамМедицинскогоСтрахования
	|			ПО ДокументУдержания.Ссылка = ПрикреплениеКПрограммамМедицинскогоСтрахования.Ссылка
	|	ГДЕ
	|		ДокументУдержания.СтраховаяПремия <> 0
	|		И ДокументУдержания.Ссылка = &Документ
	|		И &ВидРасчетаРассчитывается
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДокументУдержания.ДатаПрикрепления,
	|		ПрикреплениеКПрограммамМедицинскогоСтрахования.Организация.ГоловнаяОрганизация,
	|		ДокументУдержания.ФизическоеЛицо,
	|		ПрикреплениеКПрограммамМедицинскогоСтрахования.Удержание,
	|		ДокументУдержания.Ссылка,
	|		ДокументУдержания.СтраховаяПремия
	|	ИЗ
	|		Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Родственники КАК ДокументУдержания
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования КАК ПрикреплениеКПрограммамМедицинскогоСтрахования
	|			ПО ДокументУдержания.Ссылка = ПрикреплениеКПрограммамМедицинскогоСтрахования.Ссылка
	|	ГДЕ
	|		ДокументУдержания.СтраховаяПремия <> 0
	|		И ДокументУдержания.Ссылка = &Документ
	|		И &ВидРасчетаРассчитывается) КАК ДокументУдержания
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументУдержания.ДатаПрикрепления,
	|	ДокументУдержания.ОрганизацияГоловнаяОрганизация,
	|	ДокументУдержания.ФизическоеЛицо,
	|	ДокументУдержания.Удержание,
	|	ДокументУдержания.Ссылка";
	
	ДанныеДляПроведения.ПредельныеСуммыУдержанийСотрудников = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

// Заполняет данные для проведения плановых удержаний для многосотрудникового документа.
//
// Параметры:
//		ДанныеДляПроведения - Структура, описанная в СоздатьДанныеДляРегистрацииПлановыхУдержаний.
//		Документ - Ссылка на документ.
//
Процедура ЗаполнитьДанныеДляРегистрацииПлановыхУдержанийСпискаСотрудников(ДанныеДляПроведения, Документ) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДокументУдержания.ДатаПрикрепления КАК ДатаСобытия,
	|	ВЫБОР
	|		КОГДА ДокументУдержания.Ссылка.ДатаОкончанияСтрахования > ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДОБАВИТЬКДАТЕ(ДокументУдержания.Ссылка.ДатаОкончанияСтрахования, ДЕНЬ, 1)
	|		ИНАЧЕ ДокументУдержания.Ссылка.ДатаОкончанияСтрахования
	|	КОНЕЦ КАК ДействуетДо,
	|	ДокументУдержания.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДокументУдержания.Ссылка.Организация.ГоловнаяОрганизация КАК Организация,
	|	ДокументУдержания.Ссылка.Удержание КАК Удержание,
	|	ДокументУдержания.Ссылка КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА &ВидРасчетаРассчитывается
	|			ТОГДА 0
	|		ИНАЧЕ ДокументУдержания.СуммаУдержания
	|	КОНЕЦ КАК Размер,
	|	ИСТИНА КАК Используется
	|ИЗ
	|	Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Сотрудники КАК ДокументУдержания
	|ГДЕ
	|	ДокументУдержания.Ссылка = &Документ
	|	И (ДокументУдержания.СуммаУдержания <> 0
	|			ИЛИ &ВидРасчетаРассчитывается)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументУдержания.ДатаПрикрепления КАК ДатаСобытия,
	|	ВЫБОР
	|		КОГДА ДокументУдержания.Ссылка.ДатаОкончанияСтрахования > ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДОБАВИТЬКДАТЕ(ДокументУдержания.Ссылка.ДатаОкончанияСтрахования, ДЕНЬ, 1)
	|		ИНАЧЕ ДокументУдержания.Ссылка.ДатаОкончанияСтрахования
	|	КОНЕЦ КАК ДействуетДо,
	|	ДокументУдержания.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДокументУдержания.Ссылка.Организация.ГоловнаяОрганизация КАК Организация,
	|	ДокументУдержания.Ссылка КАК ДокументОснование,
	|	ЗначенияПоказателей.Показатель КАК Показатель,
	|	ЗначенияПоказателей.Значение КАК Значение
	|ИЗ
	|	Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Сотрудники КАК ДокументУдержания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Показатели КАК ЗначенияПоказателей
	|		ПО (ЗначенияПоказателей.Ссылка = ДокументУдержания.Ссылка)
	|			И (ЗначенияПоказателей.ИдентификаторСтрокиВидаРасчета = ДокументУдержания.ИдентификаторСтрокиВидаРасчета)
	|ГДЕ
	|	ДокументУдержания.Ссылка = &Документ
	|	И (ДокументУдержания.СуммаУдержания <> 0
	|			ИЛИ &ВидРасчетаРассчитывается)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументУдержания.ДатаПрикрепления,
	|	ДокументУдержания.ДействуетДо,
	|	ДокументУдержания.ФизическоеЛицо,
	|	ДокументУдержания.ОрганизацияГоловнаяОрганизация,
	|	ДокументУдержания.Ссылка,
	|	ДокументУдержания.Показатель,
	|	СУММА(ДокументУдержания.СтраховаяПремия)
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДокументУдержания.ДатаПрикрепления КАК ДатаПрикрепления,
	|		ВЫБОР
	|			КОГДА ДокументУдержания.Ссылка.ДатаОкончанияСтрахования > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ДОБАВИТЬКДАТЕ(ДокументУдержания.Ссылка.ДатаОкончанияСтрахования, ДЕНЬ, 1)
	|			ИНАЧЕ ДокументУдержания.Ссылка.ДатаОкончанияСтрахования
	|		КОНЕЦ КАК ДействуетДо,
	|		ДокументУдержания.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ДокументУдержания.Ссылка.Организация.ГоловнаяОрганизация КАК ОрганизацияГоловнаяОрганизация,
	|		ДокументУдержания.Ссылка КАК Ссылка,
	|		ЗНАЧЕНИЕ(Справочник.ПоказателиРасчетаЗарплаты.СтраховаяПремия) КАК Показатель,
	|		ДокументУдержания.СтраховаяПремия КАК СтраховаяПремия
	|	ИЗ
	|		Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Сотрудники КАК ДокументУдержания
	|	ГДЕ
	|		ДокументУдержания.СтраховаяПремия <> 0
	|		И ДокументУдержания.Ссылка = &Документ
	|		И &ВидРасчетаРассчитывается
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДокументУдержания.ДатаПрикрепления,
	|		ВЫБОР
	|			КОГДА ДокументУдержания.Ссылка.ДатаОкончанияСтрахования > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ДОБАВИТЬКДАТЕ(ДокументУдержания.Ссылка.ДатаОкончанияСтрахования, ДЕНЬ, 1)
	|			ИНАЧЕ ДокументУдержания.Ссылка.ДатаОкончанияСтрахования
	|		КОНЕЦ,
	|		ДокументУдержания.ФизическоеЛицо,
	|		ДокументУдержания.Ссылка.Организация.ГоловнаяОрганизация,
	|		ДокументУдержания.Ссылка,
	|		ЗНАЧЕНИЕ(Справочник.ПоказателиРасчетаЗарплаты.СтраховаяПремия),
	|		ДокументУдержания.СтраховаяПремия
	|	ИЗ
	|		Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Родственники КАК ДокументУдержания
	|	ГДЕ
	|		ДокументУдержания.СтраховаяПремия <> 0
	|		И ДокументУдержания.Ссылка = &Документ
	|		И &ВидРасчетаРассчитывается) КАК ДокументУдержания
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументУдержания.ДатаПрикрепления,
	|	ДокументУдержания.ДействуетДо,
	|	ДокументУдержания.ФизическоеЛицо,
	|	ДокументУдержания.ОрганизацияГоловнаяОрганизация,
	|	ДокументУдержания.Ссылка,
	|	ДокументУдержания.Показатель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументУдержания.ДатаПрикрепления,
	|	ВЫБОР
	|		КОГДА ДокументУдержания.Ссылка.ДатаОкончанияСтрахования > ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДОБАВИТЬКДАТЕ(ДокументУдержания.Ссылка.ДатаОкончанияСтрахования, ДЕНЬ, 1)
	|		ИНАЧЕ ДокументУдержания.Ссылка.ДатаОкончанияСтрахования
	|	КОНЕЦ,
	|	ДокументУдержания.ФизическоеЛицо,
	|	ДокументУдержания.Ссылка.Организация.ГоловнаяОрганизация,
	|	ДокументУдержания.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.ПоказателиРасчетаЗарплаты.КоличествоДнейПериодаСтрахования),
	|	РАЗНОСТЬДАТ(ДокументУдержания.Ссылка.ДатаНачалаСтрахования, ДокументУдержания.Ссылка.ДатаОкончанияСтрахования, ДЕНЬ) + 1
	|ИЗ
	|	Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Сотрудники КАК ДокументУдержания
	|ГДЕ
	|	РАЗНОСТЬДАТ(ДокументУдержания.Ссылка.ДатаНачалаСтрахования, ДокументУдержания.Ссылка.ДатаОкончанияСтрахования, ДЕНЬ) + 1 <> 0
	|	И ДокументУдержания.Ссылка = &Документ
	|	И &ВидРасчетаРассчитывается
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументУдержания.ДатаПрикрепления КАК Период,
	|	ДокументУдержания.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДокументУдержания.Ссылка КАК ДокументОснование,
	|	ДокументУдержания.Сотрудник КАК РабочееМесто
	|ИЗ
	|	Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Сотрудники КАК ДокументУдержания
	|ГДЕ
	|	ДокументУдержания.Ссылка = &Документ
	|	И (ДокументУдержания.СуммаУдержания <> 0
	|			ИЛИ &ВидРасчетаРассчитывается)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Документ", Документ);
	ВидРасчетаИнфо = ЗарплатаКадрыРасширенныйПовтИсп.ПолучитьИнформациюОВидеРасчета(Удержание);
	Запрос.УстановитьПараметр("ВидРасчетаРассчитывается", ?(ВидРасчетаИнфо.Рассчитывается = Неопределено, Ложь, ВидРасчетаИнфо.Рассчитывается));
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляПроведения.ДанныеПлановыхУдержаний = РезультатыЗапроса[0].Выгрузить();
	ДанныеДляПроведения.ЗначенияПоказателей = РезультатыЗапроса[1].Выгрузить();
	ДанныеДляПроведения.РабочиеМестаУдержаний = РезультатыЗапроса[2].Выгрузить();
	
КонецПроцедуры

Процедура СформироватьДвиженияПрикрепленияКПрограммамСтрахования(Движения, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СуммыСтраховыхПремийПрограммСтрахования.Период КАК Период,
	|	СуммыСтраховыхПремийПрограммСтрахования.Организация КАК Организация,
	|	СуммыСтраховыхПремийПрограммСтрахования.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СуммыСтраховыхПремийПрограммСтрахования.Родственник КАК Родственник,
	|	СуммыСтраховыхПремийПрограммСтрахования.ПрограммаСтрахования КАК ПрограммаСтрахования,
	|	СуммыСтраховыхПремийПрограммСтрахования.Действует КАК Действует,
	|	СуммыСтраховыхПремийПрограммСтрахования.ДействуетДо КАК ДействуетДо,
	|	СуммыСтраховыхПремийПрограммСтрахования.СтраховаяПремия КАК СтраховаяПремия
	|ИЗ
	|	ВТСуммыСтраховыхПремийПрограммСтрахования КАК СуммыСтраховыхПремийПрограммСтрахования";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Движения.ПрограммыМедицинскогоСтрахованияСотрудников.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Движения.ПрограммыМедицинскогоСтрахованияСотрудников.Записывать = Истина;
	КонецЦикла;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.Период КАК Период,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.Организация КАК Организация,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.Родственник КАК Родственник,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.РасширениеПрограммСтрахования КАК РасширениеПрограммСтрахования,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.Действует КАК Действует,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.ДействуетДо КАК ДействуетДо,
	|	СуммыСтраховыхПремийРасширенийПрограммСтрахования.СтраховаяПремия КАК СтраховаяПремия
	|ИЗ
	|	ВТСуммыСтраховыхПремийРасширенийПрограммСтрахования КАК СуммыСтраховыхПремийРасширенийПрограммСтрахования";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Движения.РасширенияПрограммМедицинскогоСтрахованияСотрудников.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Движения.РасширенияПрограммМедицинскогоСтрахованияСотрудников.Записывать = Истина;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
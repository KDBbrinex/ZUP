
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры,
		"Организация,Сотрудник,Основание,ДатаДокумента,СерияДокумента,НомерДокумента,КемВыданДокумент,"
		+ "ОтразитьВТрудовойКнижке,ТрудоваяФункция,ОписаниеДолжностиДляЗаписиОТрудовойДеятельности");
	
	УстановитьДоступностьЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтразитьВТрудовойКнижкеПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти




#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Оповестить("ВведеныРеквизитыДокументаПодтверждающегоКвалификацию", ВведенныеДанные(), ВладелецФормы);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

// ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки

&НаКлиенте
Процедура ОснованиеПриИзменении(Элемент)
	
	ЭлектронныеТрудовыеКнижкиКлиент.НаименованиеДокументаПриИзменении(Организация, "РезультатыАттестацииСотрудников", Основание);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЭлектронныеТрудовыеКнижкиКлиент.НаименованиеДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ЭлектронныеТрудовыеКнижкиКлиент.НаименованиеДокументаОбработкаВыбора(
		Основание, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЭлектронныеТрудовыеКнижкиКлиент.НаименованиеДокументаАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

// Конец ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ВведенныеДанные()
	
	ВведенныеДанные = Новый Структура;
	
	ВведенныеДанные.Вставить("Сотрудник", ЭтотОбъект.Сотрудник);
	ВведенныеДанные.Вставить("Основание", ЭтотОбъект.Основание);
	ВведенныеДанные.Вставить("ДатаДокумента", ЭтотОбъект.ДатаДокумента);
	ВведенныеДанные.Вставить("СерияДокумента", ЭтотОбъект.СерияДокумента);
	ВведенныеДанные.Вставить("НомерДокумента", ЭтотОбъект.НомерДокумента);
	ВведенныеДанные.Вставить("КемВыданДокумент", ЭтотОбъект.КемВыданДокумент);
	ВведенныеДанные.Вставить("ТрудоваяФункция", ЭтотОбъект.ТрудоваяФункция);
	ВведенныеДанные.Вставить("ОтразитьВТрудовойКнижке", ЭтотОбъект.ОтразитьВТрудовойКнижке);
	ВведенныеДанные.Вставить("ОписаниеДолжностиДляЗаписиОТрудовойДеятельности", ЭтотОбъект.ОписаниеДолжностиДляЗаписиОТрудовойДеятельности);
	
	Возврат ВведенныеДанные;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементовФормы(УправляемаяФорма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"СведенияТрудовойКнижкиГруппа",
		"Доступность",
		УправляемаяФорма.ОтразитьВТрудовойКнижке);
	
КонецПроцедуры

#КонецОбласти

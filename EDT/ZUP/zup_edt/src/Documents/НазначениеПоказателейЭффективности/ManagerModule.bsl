#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс		

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Подразделение)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДляПроведения(Ссылка)Экспорт
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НазначениеПоказателейЭффективностиПодразделения.Ссылка.Период КАК Период,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.Горизонт КАК Горизонт,
		|	НазначениеПоказателейЭффективностиПодразделения.Подразделение КАК Подразделение,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.Показатель КАК Показатель,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ЗначениеДо КАК ЗначениеДо,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ИнтервалОценкиДо КАК ИнтервалОценкиДо,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.Оценка КАК Оценка,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.Показатель.ОбратнаяШкала КАК ОбратнаяШкала,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ИдентификаторСтрокиПоказателя КАК ИдентификаторСтрокиПоказателя
		|ПОМЕСТИТЬ ВТШкалыПодразделений
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.Подразделения КАК НазначениеПоказателейЭффективностиПодразделения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПодразделений КАК НазначениеПоказателейЭффективностиПоказателиПодразделений
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ШкалыЗначенийПодразделений КАК НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений
		|			ПО НазначениеПоказателейЭффективностиПоказателиПодразделений.Ссылка = НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.Ссылка
		|				И НазначениеПоказателейЭффективностиПоказателиПодразделений.ИдентификаторСтрокиПоказателя = НазначениеПоказателейЭффективностиШкалыЗначенийПодразделений.ИдентификаторСтрокиПоказателя
		|		ПО НазначениеПоказателейЭффективностиПодразделения.Ссылка = НазначениеПоказателейЭффективностиПоказателиПодразделений.Ссылка
		|			И НазначениеПоказателейЭффективностиПодразделения.ИдентификаторСтрокиОбъектаНазначения = НазначениеПоказателейЭффективностиПоказателиПодразделений.ИдентификаторСтрокиОбъектаНазначения
		|ГДЕ
		|	НазначениеПоказателейЭффективностиПодразделения.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТШкалыПодразделений.ИдентификаторСтрокиПоказателя КАК ИдентификаторСтрокиПоказателя,
		|	ВТШкалыПодразделений.ЗначениеДо КАК ЗначениеДо,
		|	МАКСИМУМ(ВТШкалыПодразделений1.ЗначениеДо) КАК ЗначениеОт
		|ПОМЕСТИТЬ ВТЗначенияОтШкалПодразделений
		|ИЗ
		|	ВТШкалыПодразделений КАК ВТШкалыПодразделений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТШкалыПодразделений КАК ВТШкалыПодразделений1
		|		ПО ВТШкалыПодразделений.ИдентификаторСтрокиПоказателя = ВТШкалыПодразделений1.ИдентификаторСтрокиПоказателя
		|			И ВТШкалыПодразделений.ЗначениеДо > ВТШкалыПодразделений1.ЗначениеДо
		|ГДЕ
		|	ВТШкалыПодразделений.ОбратнаяШкала = ЛОЖЬ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТШкалыПодразделений.ИдентификаторСтрокиПоказателя,
		|	ВТШкалыПодразделений.ЗначениеДо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТШкалыПодразделений.ИдентификаторСтрокиПоказателя,
		|	ВТШкалыПодразделений.ЗначениеДо,
		|	МИНИМУМ(ВТШкалыПодразделений1.ЗначениеДо)
		|ИЗ
		|	ВТШкалыПодразделений КАК ВТШкалыПодразделений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТШкалыПодразделений КАК ВТШкалыПодразделений1
		|		ПО ВТШкалыПодразделений.ИдентификаторСтрокиПоказателя = ВТШкалыПодразделений1.ИдентификаторСтрокиПоказателя
		|			И ВТШкалыПодразделений.ЗначениеДо < ВТШкалыПодразделений1.ЗначениеДо
		|ГДЕ
		|	ВТШкалыПодразделений.ОбратнаяШкала = ИСТИНА
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТШкалыПодразделений.ИдентификаторСтрокиПоказателя,
		|	ВТШкалыПодразделений.ЗначениеДо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка.Период КАК Период,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Горизонт КАК Горизонт,
		|	НазначениеПоказателейЭффективностиПозиции.Позиция КАК Позиция,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Показатель КАК Показатель,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ЗначениеДо КАК ЗначениеДо,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ИнтервалОценкиДо КАК ИнтервалОценкиДо,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.Оценка КАК Оценка,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Показатель.ОбратнаяШкала КАК ОбратнаяШкала,
		|	НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ИдентификаторСтрокиПоказателя КАК ИдентификаторСтрокиПоказателя
		|ПОМЕСТИТЬ ВТШкалыПозиций
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.Позиции КАК НазначениеПоказателейЭффективностиПозиции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПозиций КАК НазначениеПоказателейЭффективностиПоказателиПозиций
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ШкалыЗначенийПозиций КАК НазначениеПоказателейЭффективностиШкалыЗначенийПозиций
		|			ПО НазначениеПоказателейЭффективностиПоказателиПозиций.Ссылка = НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.Ссылка
		|				И НазначениеПоказателейЭффективностиПоказателиПозиций.ИдентификаторСтрокиПоказателя = НазначениеПоказателейЭффективностиШкалыЗначенийПозиций.ИдентификаторСтрокиПоказателя
		|		ПО НазначениеПоказателейЭффективностиПозиции.Ссылка = НазначениеПоказателейЭффективностиПоказателиПозиций.Ссылка
		|			И НазначениеПоказателейЭффективностиПозиции.ИдентификаторСтрокиОбъектаНазначения = НазначениеПоказателейЭффективностиПоказателиПозиций.ИдентификаторСтрокиОбъектаНазначения
		|ГДЕ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТШкалыПозиций.ИдентификаторСтрокиПоказателя КАК ИдентификаторСтрокиПоказателя,
		|	ВТШкалыПозиций.ЗначениеДо КАК ЗначениеДо,
		|	МАКСИМУМ(ВТШкалыПозиций1.ЗначениеДо) КАК ЗначениеОт
		|ПОМЕСТИТЬ ВТЗначенияОтШкалПозиций
		|ИЗ
		|	ВТШкалыПозиций КАК ВТШкалыПозиций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТШкалыПозиций КАК ВТШкалыПозиций1
		|		ПО ВТШкалыПозиций.ИдентификаторСтрокиПоказателя = ВТШкалыПозиций1.ИдентификаторСтрокиПоказателя
		|			И ВТШкалыПозиций.ЗначениеДо > ВТШкалыПозиций1.ЗначениеДо
		|ГДЕ
		|	ВТШкалыПозиций.ОбратнаяШкала = ЛОЖЬ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТШкалыПозиций.ИдентификаторСтрокиПоказателя,
		|	ВТШкалыПозиций.ЗначениеДо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТШкалыПозиций.ИдентификаторСтрокиПоказателя,
		|	ВТШкалыПозиций.ЗначениеДо,
		|	МИНИМУМ(ВТШкалыПозиций1.ЗначениеДо)
		|ИЗ
		|	ВТШкалыПозиций КАК ВТШкалыПозиций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТШкалыПозиций КАК ВТШкалыПозиций1
		|		ПО ВТШкалыПозиций.ИдентификаторСтрокиПоказателя = ВТШкалыПозиций1.ИдентификаторСтрокиПоказателя
		|			И ВТШкалыПозиций.ЗначениеДо < ВТШкалыПозиций1.ЗначениеДо
		|ГДЕ
		|	ВТШкалыПозиций.ОбратнаяШкала = ИСТИНА
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТШкалыПозиций.ИдентификаторСтрокиПоказателя,
		|	ВТШкалыПозиций.ЗначениеДо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НазначениеПоказателейЭффективностиПодразделения.Ссылка.Период КАК Период,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.Горизонт КАК Горизонт,
		|	НазначениеПоказателейЭффективностиПодразделения.Подразделение КАК Подразделение,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.Показатель КАК Показатель,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.МестоВводаЗначений КАК МестоВводаЗначений,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.Вес КАК Вес
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.Подразделения КАК НазначениеПоказателейЭффективностиПодразделения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПодразделений КАК НазначениеПоказателейЭффективностиПоказателиПодразделений
		|		ПО НазначениеПоказателейЭффективностиПодразделения.Ссылка = НазначениеПоказателейЭффективностиПоказателиПодразделений.Ссылка
		|			И НазначениеПоказателейЭффективностиПодразделения.ИдентификаторСтрокиОбъектаНазначения = НазначениеПоказателейЭффективностиПоказателиПодразделений.ИдентификаторСтрокиОбъектаНазначения
		|ГДЕ
		|	НазначениеПоказателейЭффективностиПодразделения.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка.Период КАК Период,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Горизонт КАК Горизонт,
		|	НазначениеПоказателейЭффективностиПозиции.Позиция КАК Позиция,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Показатель КАК Показатель,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.МестоВводаЗначений КАК МестоВводаЗначений,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Вес КАК Вес
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.Позиции КАК НазначениеПоказателейЭффективностиПозиции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПозиций КАК НазначениеПоказателейЭффективностиПоказателиПозиций
		|		ПО НазначениеПоказателейЭффективностиПозиции.Ссылка = НазначениеПоказателейЭффективностиПоказателиПозиций.Ссылка
		|			И НазначениеПоказателейЭффективностиПозиции.ИдентификаторСтрокиОбъектаНазначения = НазначениеПоказателейЭффективностиПоказателиПозиций.ИдентификаторСтрокиОбъектаНазначения
		|ГДЕ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НазначениеПоказателейЭффективностиПодразделения.Ссылка.Период КАК Период,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.Горизонт КАК Горизонт,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.Показатель КАК Показатель,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.МестоВводаЗначений КАК Подразделение,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.ОтветственныйЗаВводПлана КАК ОтветственныйЗаВводПлана,
		|	НазначениеПоказателейЭффективностиПоказателиПодразделений.ОтветственныйЗаВводФакта КАК ОтветственныйЗаВводФакта
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.Подразделения КАК НазначениеПоказателейЭффективностиПодразделения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПодразделений КАК НазначениеПоказателейЭффективностиПоказателиПодразделений
		|		ПО НазначениеПоказателейЭффективностиПодразделения.Ссылка = НазначениеПоказателейЭффективностиПоказателиПодразделений.Ссылка
		|			И НазначениеПоказателейЭффективностиПодразделения.ИдентификаторСтрокиОбъектаНазначения = НазначениеПоказателейЭффективностиПоказателиПодразделений.ИдентификаторСтрокиОбъектаНазначения
		|ГДЕ
		|	НазначениеПоказателейЭффективностиПодразделения.Ссылка = &Ссылка
		|	И НазначениеПоказателейЭффективностиПодразделения.Подразделение = НазначениеПоказателейЭффективностиПоказателиПодразделений.МестоВводаЗначений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка.Период КАК Период,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Горизонт КАК Горизонт,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.Показатель КАК Показатель,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.МестоВводаЗначений КАК Позиция,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.ОтветственныйЗаВводПлана КАК ОтветственныйЗаВводПлана,
		|	НазначениеПоказателейЭффективностиПоказателиПозиций.ОтветственныйЗаВводФакта КАК ОтветственныйЗаВводФакта
		|ИЗ
		|	Документ.НазначениеПоказателейЭффективности.Позиции КАК НазначениеПоказателейЭффективностиПозиции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеПоказателейЭффективности.ПоказателиПозиций КАК НазначениеПоказателейЭффективностиПоказателиПозиций
		|		ПО НазначениеПоказателейЭффективностиПозиции.Ссылка = НазначениеПоказателейЭффективностиПоказателиПозиций.Ссылка
		|			И НазначениеПоказателейЭффективностиПозиции.ИдентификаторСтрокиОбъектаНазначения = НазначениеПоказателейЭффективностиПоказателиПозиций.ИдентификаторСтрокиОбъектаНазначения
		|ГДЕ
		|	НазначениеПоказателейЭффективностиПозиции.Ссылка = &Ссылка
		|	И НазначениеПоказателейЭффективностиПозиции.Позиция = НазначениеПоказателейЭффективностиПоказателиПозиций.МестоВводаЗначений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТШкалыПодразделений.Период КАК Период,
		|	ВТШкалыПодразделений.Горизонт КАК Горизонт,
		|	ВТШкалыПодразделений.Подразделение КАК Подразделение,
		|	ВТШкалыПодразделений.Показатель КАК Показатель,
		|	ВТШкалыПодразделений.ЗначениеДо КАК ЗначениеДо,
		|	ВЫБОР
		|		КОГДА ВТЗначенияОтШкалПодразделений.ЗначениеОт ЕСТЬ NULL
		|			ТОГДА ВЫБОР
		|					КОГДА ВТШкалыПодразделений.ОбратнаяШкала
		|						ТОГДА &ВерхнийПорогШкалы
		|					ИНАЧЕ &НижнийПорогШкалы
		|				КОНЕЦ
		|		ИНАЧЕ ВТЗначенияОтШкалПодразделений.ЗначениеОт
		|	КОНЕЦ КАК ЗначениеОт,
		|	ВТШкалыПодразделений.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	ВТШкалыПодразделений.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	ВТШкалыПодразделений.ИнтервалОценкиДо КАК ИнтервалОценкиДо,
		|	ВТШкалыПодразделений.Оценка КАК Оценка,
		|	ВТШкалыПодразделений.ОбратнаяШкала КАК ОбратнаяШкала
		|ИЗ
		|	ВТШкалыПодразделений КАК ВТШкалыПодразделений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗначенияОтШкалПодразделений КАК ВТЗначенияОтШкалПодразделений
		|		ПО ВТШкалыПодразделений.ИдентификаторСтрокиПоказателя = ВТЗначенияОтШкалПодразделений.ИдентификаторСтрокиПоказателя
		|			И ВТШкалыПодразделений.ЗначениеДо = ВТЗначенияОтШкалПодразделений.ЗначениеДо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТШкалыПозиций.Период КАК Период,
		|	ВТШкалыПозиций.Горизонт КАК Горизонт,
		|	ВТШкалыПозиций.Позиция КАК Позиция,
		|	ВТШкалыПозиций.Показатель КАК Показатель,
		|	ВТШкалыПозиций.ЗначениеДо КАК ЗначениеДо,
		|	ВЫБОР
		|		КОГДА ВТЗначенияОтШкалПозиций.ЗначениеОт ЕСТЬ NULL
		|			ТОГДА ВЫБОР
		|					КОГДА ВТШкалыПозиций.ОбратнаяШкала
		|						ТОГДА &ВерхнийПорогШкалы
		|					ИНАЧЕ &НижнийПорогШкалы
		|				КОНЕЦ
		|		ИНАЧЕ ВТЗначенияОтШкалПозиций.ЗначениеОт
		|	КОНЕЦ КАК ЗначениеОт,
		|	ВТШкалыПозиций.ОценкаЗадаетсяИнтервалом КАК ОценкаЗадаетсяИнтервалом,
		|	ВТШкалыПозиций.ИнтервалОценкиОт КАК ИнтервалОценкиОт,
		|	ВТШкалыПозиций.ИнтервалОценкиДо КАК ИнтервалОценкиДо,
		|	ВТШкалыПозиций.Оценка КАК Оценка,
		|	ВТШкалыПозиций.ОбратнаяШкала КАК ОбратнаяШкала
		|ИЗ
		|	ВТШкалыПозиций КАК ВТШкалыПозиций
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗначенияОтШкалПозиций КАК ВТЗначенияОтШкалПозиций
		|		ПО ВТШкалыПозиций.ИдентификаторСтрокиПоказателя = ВТЗначенияОтШкалПозиций.ИдентификаторСтрокиПоказателя
		|			И ВТШкалыПозиций.ЗначениеДо = ВТЗначенияОтШкалПозиций.ЗначениеДо";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("НижнийПорогШкалы", КлючевыеПоказателиЭффективностиКлиентСервер.НижнийПорогШкалы());
	Запрос.УстановитьПараметр("ВерхнийПорогШкалы", КлючевыеПоказателиЭффективностиКлиентСервер.ВерхнийПорогШкалы());
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	УдалитьВТ.Добавить("ВТШкалыПодразделений");
	УдалитьВТ.Добавить("ВТЗначенияОтШкалПодразделений");
	УдалитьВТ.Добавить("ВТШкалыПозиций");
	УдалитьВТ.Добавить("ВТЗначенияОтШкалПозиций");
	
	ДанныеДляПроведения = Новый Структура;
	
	ДанныеДляПроведения.Вставить("ПоказателиПодразделений", РезультатыЗапроса[РезультатыЗапроса.Количество() - 6].Выгрузить());
	ДанныеДляПроведения.Вставить("ПоказателиПозиций", РезультатыЗапроса[РезультатыЗапроса.Количество() - 5].Выгрузить());
	ДанныеДляПроведения.Вставить("ПодразделенияВводаЗначений", РезультатыЗапроса[РезультатыЗапроса.Количество() - 4].Выгрузить());
	ДанныеДляПроведения.Вставить("ПозицииВводаЗначений", РезультатыЗапроса[РезультатыЗапроса.Количество() - 3].Выгрузить());
	ДанныеДляПроведения.Вставить("ШкалыПодразделений", РезультатыЗапроса[РезультатыЗапроса.Количество() - 2].Выгрузить());
	ДанныеДляПроведения.Вставить("ШкалыПозиций", РезультатыЗапроса[РезультатыЗапроса.Количество() - 1].Выгрузить());
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат ДанныеДляПроведения;

КонецФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ДатаЗавершения = ТекущаяДатаСеанса();
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		Сотрудник = ДанныеЗаполнения;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	ИспытательныйСрокСотрудников.СформироватьДвиженияИспытательногоСрока(Движения, ДанныеДляПроведения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	КадровыеДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудников(
		Истина, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник), "ДатаПриема,ИспытательныйСрокДатаЗавершения", Дата);
		
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Сотрудник %1 не находится на испытательном сроке.'"), Сотрудник);
	
	Если Не ЗначениеЗаполнено(КадровыеДанныеСотрудника[0].ИспытательныйСрокДатаЗавершения)
		Или (ЗначениеЗаполнено(КадровыеДанныеСотрудника[0].ИспытательныйСрокДатаЗавершения)
			И ДатаЗавершения > КадровыеДанныеСотрудника[0].ИспытательныйСрокДатаЗавершения)
		Или ДатаЗавершения < КадровыеДанныеСотрудника[0].ДатаПриема Тогда
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Сотрудник", ,Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИсходныеДанные = СостоянияСотрудников.ПустаяТаблицаДанныхСостоянийСотрудника();
	
	НоваяСтрока = ИсходныеДанные.Добавить();
	НоваяСтрока.Сотрудник = Сотрудник;
	НоваяСтрока.Состояние = Перечисления.СостоянияСотрудника.Работа;
	НоваяСтрока.Начало = ДатаЗавершения;
	НоваяСтрока.Окончание = ДатаЗавершения;
	
	РезультатПроверки = СостоянияСотрудников.ПроверитьПересечениеПериодовОтсутствия(ИсходныеДанные, Ссылка);
	ДанныеСотрудника = РезультатПроверки.ДанныеСотрудников.Получить(Сотрудник);
	
	Если ДанныеСотрудника <> Неопределено Тогда
		ТекстСообщения = ?(ДанныеСотрудника.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияСотрудника.Увольнение"),
			НСтр("ru = 'Сотрудник был уволен %1 (%2).'"),
			НСтр("ru = 'На период %1 сотруднику уже зарегистрировано отсутствие документом %2.'"));
		ТекстСообщения = СтрШаблон(ТекстСообщения, ДанныеСотрудника.ПредставлениеПериода, ДанныеСотрудника.Регистратор);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ДатаЗавершения", ,Отказ);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗавершениеИспытательногоСрока.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗавершениеИспытательногоСрока КАК ЗавершениеИспытательногоСрока
	|ГДЕ
	|	ЗавершениеИспытательногоСрока.Сотрудник = &Сотрудник
	|	И ЗавершениеИспытательногоСрока.Ссылка <> &Ссылка
	|	И ЗавершениеИспытательногоСрока.Проведен";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			ТекстСообщения = НСтр("ru = 'По сотруднику уже зарегистрировано завершение испытательного срока %1.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Ссылка);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Выборка.Ссылка, "Сотрудник", ,Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗавершениеИспытательногоСрока.ДатаЗавершения КАК Период,
	|	ЗавершениеИспытательногоСрока.Организация КАК Организация,
	|	ЗавершениеИспытательногоСрока.Сотрудник КАК Сотрудник,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗавершениеИспытательногоСрока.ДатаЗавершения КАК ДатаЗавершения,
	|	ИСТИНА КАК ИспытательныйСрокЗавершен,
	|	ЛОЖЬ КАК ПродлениеИспытательногоСрока
	|ИЗ
	|	Документ.ЗавершениеИспытательногоСрока КАК ЗавершениеИспытательногоСрока
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО ЗавершениеИспытательногоСрока.Сотрудник = Сотрудники.Ссылка
	|ГДЕ
	|	ЗавершениеИспытательногоСрока.ДатаЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|	И ЗавершениеИспытательногоСрока.Ссылка = &Ссылка";
	
	ДанныеДляПроведения = Новый Структура("ИспытательныйСрокСотрудников");
	ДанныеДляПроведения.ИспытательныйСрокСотрудников = Запрос.Выполнить().Выгрузить();
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗагрузитьНастройкиИзРегистра();
	
	УстановитьПривилегированныйРежим(Истина);
	Пароли = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Пользователи.ТекущийПользователь(), "ПарольRabota, ПарольSuperJob");
	УстановитьПривилегированныйРежим(Ложь);
	
	ПарольRabota = ?(ЗначениеЗаполнено(Пароли.ПарольRabota), УникальныйИдентификатор, "");
	ПарольSuperJob = ?(ЗначениеЗаполнено(Пароли.ПарольSuperJob), УникальныйИдентификатор, "");
	
	УстановитьДоступностьЭлементовФормы();
	
	Если Параметры.Свойство("АвторизацииHeadHunter") Тогда
		АвторизацииHeadHunter(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьHeadHunterПриИзменении(Элемент)
	УстановитьДоступностьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьRabotaПриИзменении(Элемент)
	УстановитьДоступностьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьSuperJobПриИзменении(Элемент)
	УстановитьДоступностьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПарольRabotaПриИзменении(Элемент)
	ПарольRabotaИзменен = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПарольSuperJobПриИзменении(Элемент)
	ПарольSuperJobИзменен = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияHeadHunterНажатие(Элемент)
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("http://www.hh.ru");
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияRabotaНажатие(Элемент)
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("http://www.rabota.ru");
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияSuperJobНажатие(Элемент)
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("http://www.superjob.ru");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Отказ = Ложь;
	ПроверитьЗаполнениеПолей(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройкиВРегистре();
	УстановитьФОИспользуетсяВзаимодействиеСРекрутинговымиСайтами();
	Оповестить("ИзмененыНастройкиДоступаКСайтам");
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПройтиАвторизациюHeadHunter(Команда)
	
	АвторизацииHeadHunter(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКодHeadHunter(Команда)
	
	ПерейтиПоНавигационнойСсылке(СтрокаАвторизацииHeadHunter());
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаOkHeadHunter(Команда)

	Если Не ЗначениеЗаполнено(КодАвторизацииHeadHunter) Тогда
		ТекстПредупреждения = НСтр("ru = 'Введите код авторизации.'");
	Иначе
		Если СтруктураМаркеровHeadHunterПолучена() Тогда
			Элементы.СтраницыНастройкиHeadHunter.ТекущаяСтраница = Элементы.СтраницаНастройкиHeadHunter;
			ТекстПредупреждения = НСтр("ru = 'Авторизация на сайте hh.ru пройдена успешно'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Не удалось авторизоваться. Введен неверный код авторизации.'");
		КонецЕсли;
	КонецЕсли;
	
	ИнтеграцияРекрутинговыхСайтовВнутреннийКлиент.ОтобразитьПредупреждение(ТекстПредупреждения);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДоступНаСайтHeadHunter(Команда)

	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter();
	
	МаркерДоступаАктивен = ИнтеграцияРекрутинговыхСайтовКлиент.МаркерДоступаАктивен(Сайт, Неопределено);
	Если МаркерДоступаАктивен Тогда
		ТекстПредупреждения = НСтр("ru = 'Авторизация на сайте hh.ru пройдена успешно'");
		ИнтеграцияРекрутинговыхСайтовВнутреннийКлиент.ОтобразитьПредупреждение(ТекстПредупреждения);
	Иначе
		АвторизацииHeadHunter(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДоступНаСайтRabota(Команда)
	
	Отказ = Ложь;
	
	ПроверитьЗаполнениеПолейRabota(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota();
	Если ПарольRabota = Строка(УникальныйИдентификатор) Тогда
		Пароль = ПолучитьПарольНаСервере(Сайт);
	Иначе
		Пароль = ПарольRabota;
	КонецЕсли;
	
	ИнтеграцияРекрутинговыхСайтовКлиент.АвторизацияНаСайте(Сайт, ИмяПользователяRabota, Пароль);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДоступНаСайтSuperJob(Команда)
	
	Отказ = Ложь;
	
	ПроверитьЗаполнениеПолейSuperJob(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob();
	Если ПарольSuperJob = Строка(УникальныйИдентификатор) Тогда
		Пароль = ПолучитьПарольНаСервере(Сайт);
	Иначе
		Пароль = ПарольSuperJob;
	КонецЕсли;
	
	ИнтеграцияРекрутинговыхСайтовКлиент.АвторизацияНаСайте(Сайт, ИмяПользователяSuperJob, Пароль);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьДоступностьЭлементовФормы()
	
	Элементы.ПройтиАвторизациюHeadHunter.Доступность = ИспользоватьHeadHunter;
	Элементы.ПроверитьДоступНаСайтHeadHunter.Доступность = ИспользоватьHeadHunter;
	
	Элементы.ИмяПользователяRabota.Доступность = ИспользоватьRabota;
	Элементы.ПарольRabota.Доступность = ИспользоватьRabota;
	Элементы.ПроверитьДоступНаСайтRabota.Доступность = ИспользоватьRabota;
	
	Элементы.ИмяПользователяSuperJob.Доступность = ИспользоватьSuperJob;
	Элементы.ПарольSuperJob.Доступность = ИспользоватьSuperJob;
	Элементы.ПроверитьДоступНаСайтSuperJob.Доступность = ИспользоватьSuperJob;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиВРегистре()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ПарольRabotaИзменен Тогда
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Пользователи.ТекущийПользователь(), ПарольRabota, "ПарольRabota");
		ПарольRabota = ?(ЗначениеЗаполнено(ПарольRabota), УникальныйИдентификатор, "");
	КонецЕсли;
	
	Если ПарольSuperJobИзменен Тогда
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Пользователи.ТекущийПользователь(), ПарольSuperJob, "ПарольSuperJob");
		ПарольSuperJob = ?(ЗначениеЗаполнено(ПарольSuperJob), УникальныйИдентификатор, "");
	КонецЕсли;
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter();
	РегистрыСведений.НастройкиИнтеграцииРекрутинговыхСайтов.ЗаписатьНастройки(Сайт, ИспользоватьHeadHunter);
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota();
	РегистрыСведений.НастройкиИнтеграцииРекрутинговыхСайтов.ЗаписатьНастройки(Сайт, ИспользоватьRabota, ИмяПользователяRabota);
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob();
	РегистрыСведений.НастройкиИнтеграцииРекрутинговыхСайтов.ЗаписатьНастройки(Сайт, ИспользоватьSuperJob, ИмяПользователяSuperJob);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиИзРегистра()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииРекрутинговыхСайтов.РекрутинговыйСайт,
	|	НастройкиИнтеграцииРекрутинговыхСайтов.Использовать,
	|	НастройкиИнтеграцииРекрутинговыхСайтов.ИмяПользователя
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииРекрутинговыхСайтов КАК НастройкиИнтеграцииРекрутинговыхСайтов
	|ГДЕ
	|	НастройкиИнтеграцииРекрутинговыхСайтов.Пользователь = &ТекущийПользователь";
	
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.РекрутинговыйСайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
			ИспользоватьHeadHunter = Выборка.Использовать;
		ИначеЕсли Выборка.РекрутинговыйСайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
			ИспользоватьRabota = Выборка.Использовать;
			ИмяПользователяRabota = Выборка.ИмяПользователя;
		ИначеЕсли Выборка.РекрутинговыйСайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
			ИспользоватьSuperJob = Выборка.Использовать;
			ИмяПользователяSuperJob = Выборка.ИмяПользователя;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьФОИспользуетсяВзаимодействиеСРекрутинговымиСайтами()
	ИнтеграцияРекрутинговыхСайтов.УстановитьФОИспользуетсяВзаимодействиеСРекрутинговымиСайтами();
КонецПроцедуры

&НаСервере
Функция ПолучитьПарольНаСервере(Сайт)
	
	УстановитьПривилегированныйРежим(Истина);
	Пароли = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Пользователи.ТекущийПользователь(), "ПарольRabota, ПарольSuperJob");
	УстановитьПривилегированныйРежим(Ложь);
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		Возврат Пароли.ПарольRabota;
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		Возврат Пароли.ПарольSuperJob;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьЗаполнениеПолей(Отказ)
	
	ПроверитьЗаполнениеПолейRabota(Отказ);
	ПроверитьЗаполнениеПолейSuperJob(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеПолейRabota(Отказ)
	
	Если ИспользоватьRabota Тогда
		Если Не ЗначениеЗаполнено(ИмяПользователяRabota) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнено имя пользователя для доступа к сайту Rabota'"), , , , Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ПарольRabota) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнен пароль для доступа к сайту Rabota'"), , , , Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеПолейSuperJob(Отказ)

	Если ИспользоватьSuperJob Тогда
		Если Не ЗначениеЗаполнено(ИмяПользователяSuperJob) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнено имя пользователя для доступа к сайту SuperJob'"), , , , Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ПарольSuperJob) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнен пароль для доступа к сайту SuperJob'"), , , , Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура АвторизацииHeadHunter(Форма)
	
	Форма.КодАвторизацииHeadHunter = "";
	Форма.Элементы.СтраницыНастройкиHeadHunter.ТекущаяСтраница = Форма.Элементы.СтраницаАвторизацияHeadHunter;
	
КонецПроцедуры

&НаСервере
Функция СтрокаАвторизацииHeadHunter()
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter();
	Возврат ИнтеграцияРекрутинговыхСайтов.СтрокаАвторизацииOAuth(Сайт);
	
КонецФункции

&НаСервере
Функция СтруктураМаркеровHeadHunterПолучена()
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter();
	СтруктураМаркеров = ИнтеграцияРекрутинговыхСайтов.СтруктураМаркеров(Сайт, КодАвторизацииHeadHunter);
	Возврат Не (СтруктураМаркеров = Неопределено);
	
КонецФункции

#КонецОбласти

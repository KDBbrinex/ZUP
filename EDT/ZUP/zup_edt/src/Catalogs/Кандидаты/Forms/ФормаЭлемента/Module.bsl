
#Область ОписаниеПеременных

&НаКлиенте
Перем ОткрытыеФормы Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ПодключаемыеКомандыГруппаКнопок;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	ДополнительныеПараметры.Вставить("Объект", КандидатОбъект);
	ДополнительныеПараметры.Вставить("ОтложеннаяИнициализация", Истина);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства

	// СтандартныеПодсистемы.РаботаСФайлами
	ПараметрыГиперссылки = РаботаСФайлами.ГиперссылкаФайлов();
	ПараметрыГиперссылки.Размещение = "ГруппаКнопокРаботаСФайлами";
	ПараметрыГиперссылки.Владелец = "КандидатОбъект.Ссылка";
	РаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыГиперссылки);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	ИспользоватьЭлектронноеИнтервью = ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронноеИнтервью");
	Если Не ИспользоватьЭлектронноеИнтервью Тогда
		Элементы.РаботаСКандидатомОткрытьАнкету.Видимость = Ложь;
		Элементы.РаботаСКандидатомПечатьАнкеты.Видимость = Ложь;
		Элементы.ГруппаОценкаПоХарактеристикам.Видимость = Ложь;
		Элементы.КандидатыОтчетПоКомпетенциям.Видимость = Ложь;
		Элементы.ФормаПодробныйОтчетПоХарактеристикам.Видимость = Ложь;
	КонецЕсли;
		
	ИспользоватьШаблоныСообщений = ПолучитьФункциональнуюОпцию("ИспользоватьШаблоныСообщений");
	
	Если Параметры.Ключ.Пустая() Тогда
		// Если физическое лицо было указано в обработке заполнения.
		Если КандидатОбъект.ФизическоеЛицо.Пустая() Тогда
			// Генерим ссылку, при записи установим ее объекту.
			КандидатОбъект.ФизическоеЛицо = Справочники.ФизическиеЛица.ПолучитьСсылку();
			ВыполнятьПроверкуОднофамильцев = Истина;
		КонецЕсли;
		
		// Получаем ссылку нового.
		Если КандидатСсылка.Пустая() Тогда
			КандидатСсылка = Справочники.Кандидаты.ПолучитьСсылку();
			РедактированиеПериодическихСведений.ИнициализироватьЗаписьДляРедактированияВФорме(ЭтаФорма, "РаботаСКандидатами", КандидатСсылка);
			// Заполняем значения по умолчанию.
			ГражданствоФизическихЛиц.Страна = Справочники.СтраныМира.Россия;
			ПриПолученииДанныхНаСервере();
		КонецЕсли;
		СкрытьКомандуСоздатьКандидата();
	КонецЕсли;
	
	ПараметрСтруктураРезюме = Неопределено;
	
	Если Параметры.Свойство("СтруктураРезюме", ПараметрСтруктураРезюме) Тогда
		
		ПараметрРекрутинговыйСайт = Неопределено;
		Параметры.Свойство("Сайт", ПараметрРекрутинговыйСайт);
		КандидатЗаполнен = Ложь;
		
		ЗаполнитьКандидатаПоДаннымРезюмеРекрутинговогоСайта(ПараметрСтруктураРезюме, ПараметрРекрутинговыйСайт, КандидатЗаполнен);
		
		Если Не КандидатЗаполнен Тогда
			ЗаполнитьКандидатаПоДаннымРезюме(ПараметрСтруктураРезюме);
		КонецЕсли;
		
		Параметры.Свойство("ФайлРезюмеДляЗагрузки", ФайлРезюмеДляЗагрузки);
		Параметры.Свойство("АдресРезюмеДляЗагрузки", АдресРезюмеДляЗагрузки);
		
		ПараметрВакансия = Неопределено;
		Если Параметры.Свойство("Вакансия", ПараметрВакансия) Тогда
			КандидатОбъект.Вакансия = ПараметрВакансия;
			ЗаполнитьПоДаннымВакансии();
		Иначе
			ПараметрПодразделение = Неопределено;
			Если Параметры.Свойство("Подразделение", ПараметрПодразделение) Тогда
				Если ЗначениеЗаполнено(ПараметрПодразделение) Тогда
					КандидатОбъект.Подразделение = ПараметрПодразделение;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Параметры.Свойство("ГруппаДоступа") Тогда
			ФизическоеЛицо.ГруппаДоступа = Параметры.ГруппаДоступа;
		КонецЕсли;
		
		ОтобразитьАдресРезюме();
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
	Взаимодействия.ПодготовитьОповещения(ЭтотОбъект, Параметры, Ложь);
	
	ТекущаяВакансия = КандидатОбъект.Вакансия;
	
	Если Не Параметры.Ключ.Пустая() Тогда
		ЭтотОбъект.ТекущийЭлемент = Элементы.ДобавитьКомментарий;
	КонецЕсли;
	
	УстановитьВидимостьНаписатьПисьмо();
	УстановитьВидимостьЗвонокSMS();
	
	ВключитьРежимРедактированияHTML();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	Если КандидатОбъект.Ссылка.Пустая() Тогда
		// Проверить дубли.
		Если ВыполнятьПроверкуОднофамильцев Тогда
			ПодключитьОбработчикОжидания("ПроверитьОднофамильцев", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
	
	ИнициализироватьРезюмеHTML();

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, КандидатОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	КандидатСсылка = ТекущийОбъект.Ссылка;
	ИспользоватьЭлектронноеИнтервью = ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронноеИнтервью");
	ПриПолученииДанныхНаСервере(ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ЗащитаПерсональныхДанных
	ЗащитаПерсональныхДанныхКлиент.ОбработкаОповещенияФормы(ЭтотОбъект, ИмяСобытия);
	// Конец СтандартныеПодсистемы.ЗащитаПерсональныхДанных
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	Если ИмяСобытия = "ИзмененыДанныеДополнительнойФормы" И Источник = ЭтаФорма Тогда
		ПрочитатьДанныеИзХранилищаВФормуНаСервере(Параметр);
		СотрудникиКлиентСервер.УстановитьПризнакРедактированияДанныхВДополнительнойФорме(Параметр.ИмяФормы, ЭтаФорма);
		ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
		ЗаполнитьОбразованиеКвалификацию();
	ИначеЕсли ИмяСобытия = "Проведение_Анкета" Тогда
		ЭтаФорма.Прочитать();
	КонецЕсли;
	
	Если ИмяСобытия = "ЗагруженаФотография" И Источник = ЭтотОбъект Тогда
		ЗавершитьВыборФотографии();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОбработкаНавигационнойСсылкиТекущийЭтапЗапланировать(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	ОбработкаНавигационнойСсылкиТекущийЭтапВыполнить(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	ОбработкаНавигационнойСсылкиТекущийЭтапПропустить(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	ОбработкаНавигационнойСсылкиТекущийЭтапПройти(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	ИзвлечьСодержимоеРезюмеHTML();

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Параметры.Ключ.Пустая() Тогда
		ТекущийОбъект.УстановитьСсылкуНового(КандидатСсылка);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущийОбъект.ФизическоеЛицо) Тогда
		ТекущийОбъект.ФизическоеЛицо = ФизическоеЛицоСсылка;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗаписатьФизическоеЛицо(Отказ);
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ЗаполнитьСреднийРейтинг(ТекущийОбъект);
	
	ПодготовитьРезюмеHTMLКЗаписи(Отказ, ТекущийОбъект);
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ЗаписьИзФормы", Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не Параметры.Ключ.Пустая() Тогда
		ЗаписатьФизическоеЛицо(Отказ);
	КонецЕсли;
	
	ЗаписатьРаботуСКандидатами();
	
	НаборЗаписей = РеквизитФормыВЗначение("ДанныеПубликацииРезюмеНаборЗаписей");
	НаборЗаписей.Отбор.Кандидат.Установить(КандидатСсылка);
	НаборЗаписей.Записать();
	
	ЗаписатьКомментарии();

	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайлами.ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи, Параметры);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ФайлРезюмеДляЗагрузки) Тогда
		ПараметрыЗаписи.Вставить("ФайлРезюмеДляЗагрузки", ФайлРезюмеДляЗагрузки);
	КонецЕсли;
	ФайлРезюмеДляЗагрузки = "";

	ПараметрыЗаписи.Вставить("ФизическоеЛицо", ФизическоеЛицоСсылка);
	ПараметрыЗаписи.Вставить("Кандидат", КандидатСсылка);
	ВзаимодействияКлиент.ВзаимодействиеПредметПослеЗаписи(ЭтаФорма, КандидатОбъект, ПараметрыЗаписи, "Кандидаты");
	
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(ФизическоеЛицоСсылка);
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(КандидатСсылка, ПараметрыОповещения());
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	Если Параметры.Ключ.Пустая() Тогда
		ТекущийОбъект.УстановитьСсылкуНового(КандидатСсылка);
	КонецЕсли;
	
	РазблокироватьДанныеФормыДляРедактирования();
	
	ПриПолученииДанныхНаСервере(ТекущийОбъект);
	
	Если ФайлРезюмеДляЗагрузки <> "" Тогда
		
		СтруктураПути = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ФайлРезюмеДляЗагрузки);
		Расширение = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(СтруктураПути.Расширение);
		ИмяБезРасширения = СтруктураПути.ИмяБезРасширения;
		
		ПараметрыФайла = Новый Структура;
		
		ПараметрыФайла.Вставить("Автор", Неопределено);
		ПараметрыФайла.Вставить("ВладелецФайлов", КандидатОбъект.Ссылка);
		ПараметрыФайла.Вставить("ИмяБезРасширения", ИмяБезРасширения);
		ПараметрыФайла.Вставить("РасширениеБезТочки", Расширение);
		ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное", Неопределено);
		
		РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресРезюмеДляЗагрузки);
	
		АдресРезюмеДляЗагрузки = "";
	КонецЕсли;
	
	УдалитьФайлыУдаленныхИзображений(ТекущийОбъект);
	
	ПоказатьКомандуСоздатьКандидата();

	СинхронизацияДанныхЗарплатаКадры.ЗапуститьОтложеннуюОбработкуЗаполненияДанныхПоФизическимЛицам(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, КандидатОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФИОПриИзменении(Элемент)
	
	ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
	
	ФИОПриИзмененииНаСервере();
	
	// Проверить дубли.
	ПроверитьОднофамильцев();
	
КонецПроцедуры

&НаКлиенте
Процедура ВакансияПриИзменении(Элемент)
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("СостояниеЭтапа", ПредопределенноеЗначение("Перечисление.СостоянияЭтаповРаботыСКандидатами.Пройден"));
	Если РаботаСКандидатамиНаборЗаписей.НайтиСтроки(СтруктураПоиска).Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВакансияПриИзмененииЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Для кандидата существуют пройденные этапы. Текущий список этапов работы будет очищен при изменении вакансии. Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;
	
	ТекущаяВакансия = КандидатОбъект.Вакансия;
	ЗаполнитьПоДаннымВакансии();
	
КонецПроцедуры

&НаКлиенте
Процедура ПозицияПриИзменении(Элемент)
	
	ДанныеПозиции = ДанныеПозиции(КандидатОбъект.Позиция);
	КандидатОбъект.Подразделение = ДанныеПозиции.МестоВСтруктуреПредприятия;
	
	УстановитьДоступностьПодразделения(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаРожденияПриИзменении(Элемент)
	
	ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
	
	ЗаполнитьВозраст(ЭтаФорма, ОбщегоНазначенияКлиент.ДатаСеанса());
	
КонецПроцедуры

&НаКлиенте
Процедура ПолПриИзменении(Элемент)
	ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
КонецПроцедуры

&НаКлиенте
Процедура МестоРожденияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	НачатьРедактированиеМестаРождения(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура АдресПриИзменении(Элемент)
	ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
КонецПроцедуры

&НаКлиенте
Процедура ТелефонПриИзменении(Элемент)
	ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
КонецПроцедуры

&НаКлиенте
Процедура EMailПриИзменении(Элемент)
	ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
КонецПроцедуры

&НаКлиенте
Процедура ГражданствоСтранаПриИзменении(Элемент)
	
	ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
	
	Если Не ЗначениеЗаполнено(ГражданствоФизическихЛиц.Период) Тогда
		ГражданствоФизическихЛиц.Период = ОбщегоНазначенияКлиент.ДатаСеанса();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СостоянияВБракеПриИзменении(Элемент)
	
	ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
	
	Если Не ЗначениеЗаполнено(СостоянияВБракеФизическихЛиц.Период) Тогда
		СостоянияВБракеФизическихЛиц.Период = ОбщегоНазначенияКлиент.ДатаСеанса();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаДоступаПриИзменении(Элемент)
	ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
КонецПроцедуры

&НаКлиенте
Процедура Комментарий1ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ИзменитьКомментарий(1);
КонецПроцедуры

&НаКлиенте
Процедура Комментарий2ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ИзменитьКомментарий(2);
КонецПроцедуры

&НаКлиенте
Процедура ЗвездаНажатие(Элемент)
	
	Модифицированность = Истина;
	
	ЧастиИмени = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Элемент.Имя, "_");
	Рейтинг = Число(ЧастиИмени[2]);
	
	// В любом случае сбрасываем общий рейтинг.
	КандидатОбъект.Рейтинг = 0;
	
	// Устанавливаем индивидуальный рейтинг.
	СтрокаОценки = СтрокаИндивидуальнойОценки(КандидатОбъект.Оценки, ПользователиКлиент.АвторизованныйПользователь());
	Если СтрокаОценки = Неопределено Тогда
		СтрокаОценки = КандидатОбъект.Оценки.Добавить();
		СтрокаОценки.Пользователь = ПользователиКлиент.АвторизованныйПользователь();	
	КонецЕсли;
	СтрокаОценки.Оценка = Рейтинг;
	
	РассчитатьСреднийРейтинг(ЭтаФорма);
	ОтобразитьРейтинг(ЭтаФорма, Рейтинг);
	
КонецПроцедуры

&НаКлиенте
Процедура ОпытРаботыПриИзменении(Элемент)
	ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
КонецПроцедуры

&НаКлиенте
Процедура ОпытРаботыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.ФизическоеЛицо = ФизическоеЛицоСсылка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОпытРаботыОбязанностиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	МногострочныйЭлементНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СфераДеятельностиКомпанииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	МногострочныйЭлементНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);	
КонецПроцедуры

&НаКлиенте
Процедура РаботаСКандидатомПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.РаботаСКандидатом.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		НастроитьКомандыСпискаЭтаповРаботыСКандидатом(ЭтаФорма);
		Возврат;
	КонецЕсли;
	
	ВыбранныйЭлемент = Элементы.РаботаСКандидатом.ТекущиеДанные.ПолучитьИдентификатор();
	НастроитьКомандыСпискаЭтаповРаботыСКандидатом(ЭтаФорма, ВыбранныйЭлемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РаботаСКандидатомПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.РаботаСКандидатом.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.СостояниеЭтапа) Тогда
		НачатьИзменениеСостоянияЭтапаРаботы(ТекущиеДанные);
	Иначе
		СостояниеЭтапа = ПредопределенноеЗначение("Перечисление.СостоянияЭтаповРаботыСКандидатами.Запланирован");
		НачатьУстановкуСостоянияЭтапаРаботы(СостояниеЭтапа, ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПричинаОтклоненияОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьРешениеПоКандидату();
		
КонецПроцедуры

&НаКлиенте
Процедура КомментарийРешенияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьРешениеПоКандидату();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	// СтандартныеПодсистемы.Свойства
	Если ЭтотОбъект.ПараметрыСвойств.Свойство(ТекущаяСтраница.Имя)
		И Не ЭтотОбъект.ПараметрыСвойств.ВыполненаОтложеннаяИнициализация Тогда
		
		СвойстваВыполнитьОтложеннуюИнициализацию();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СообщениеОСогласииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПриОбработкеНавигационнойСсылкиНовоеСогласие(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	ПриОбработкеНавигационнойСсылкиУничтожениеПДн(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезюмеHTMLДокументСформирован(Элемент)
	ВключитьРежимРедактированияHTML();
КонецПроцедуры

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраНажатие(Элемент, СтандартнаяОбработка)
	РаботаСФайламиКлиент.ПолеПредпросмотраНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	РаботаСФайламиКлиент.ПолеПредпросмотраПеретаскивание(
		ЭтотОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	РаботаСФайламиКлиент.ПолеПредпросмотраПроверкаПеретаскивания(
		ЭтотОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

&НаКлиенте
Процедура ФотографияНажатие(Элемент, СтандартнаяОбработка)
	СотрудникиКлиентРасширенный.ФизическиеЛицаАдресФотографииНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отклонить(Команда)
	
	Решение = ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Отклонено");
	НачатьПринятиеРешения(Решение);
	
КонецПроцедуры

&НаКлиенте
Процедура Утвердить(Команда)
	
	Решение = ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Согласовано");
	НачатьПринятиеРешения(Решение);

КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРешение(Команда)
	НачатьОтменуРешения();
КонецПроцедуры

&НаКлиенте
Процедура СброситьРейтинг(Команда)
	
	// Сбрасываем общий рейтинг, если он есть.
	Если КандидатОбъект.Рейтинг <> 0 Тогда
		КандидатОбъект.Рейтинг = 0;
		ОтобразитьРейтинг(ЭтаФорма, 0);
		Возврат;
	КонецЕсли;
	
	// Если есть индивидуальный рейтинг, удаляем оценку ровно этого пользователя.
	СтрокаОценки = СтрокаИндивидуальнойОценки(КандидатОбъект.Оценки, ПользователиКлиент.АвторизованныйПользователь());
	Если СтрокаОценки <> Неопределено Тогда
		КандидатОбъект.Оценки.Удалить(СтрокаОценки);
		РассчитатьСреднийРейтинг(ЭтаФорма);
	КонецЕсли;
	
	ОтобразитьРейтинг(ЭтаФорма, 0);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОбразованиеКвалификацию(Команда)
	
	Если Не Параметры.Ключ.Пустая() Тогда
		ОткрытьОбразованиеКвалификацию();
		Возврат;
	КонецЕсли;
	
	// Если элемент пока не записан, редактировать образование и квалификацию не получится.
	ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
			|Переход к сведениям об образовании, квалификации возможен только после записи данных.
			|Данные будут записаны.'");
			
	Оповещение = Новый ОписаниеОповещения("ОбразованиеКвалификацияПослеОтветаНаПредложениеЗаписать", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);	
		
КонецПроцедуры

&НаКлиенте
Процедура ПолныйСписокКандидатов(Команда)
	
	Отбор = Новый Структура("ФизическоеЛицо");
	Отбор.ФизическоеЛицо = КандидатОбъект.ФизическоеЛицо;
	
	ПараметрыСписка = Новый Структура("Отбор");
	ПараметрыСписка.Отбор = Отбор;
	
	ОткрытьФорму("Справочник.Кандидаты.ФормаСписка", ПараметрыСписка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКандидата(Команда)
	ПодборПерсоналаКлиент.ОткрытьФормуНовогоКандидата(
		Новый Структура("ФизическоеЛицо", КандидатОбъект.ФизическоеЛицо), , КандидатОбъект.ФизическоеЛицо);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКомментарий(Команда)
	НачатьДобавлениеКомментария();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокКомментариев(Команда)
	НачатьОткрытиеСпискаКомментариев();
КонецПроцедуры

&НаКлиенте
Процедура ЗапланироватьЭтап(Команда)
	
	ТекущиеДанные = Элементы.РаботаСКандидатом.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СостояниеЭтапа = ПредопределенноеЗначение("Перечисление.СостоянияЭтаповРаботыСКандидатами.Запланирован");
	НачатьУстановкуСостоянияЭтапаРаботы(СостояниеЭтапа, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьЭтап(Команда)
	
	ТекущиеДанные = Элементы.РаботаСКандидатом.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СостояниеЭтапа = ПредопределенноеЗначение("Перечисление.СостоянияЭтаповРаботыСКандидатами.Пройден");
	НачатьУстановкуСостоянияЭтапаРаботы(СостояниеЭтапа, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПройтиИнтервью(Команда)
	
	ТекущиеДанные = Элементы.РаботаСКандидатом.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НачатьПрохождениеЭлектронногоИнтервьюЭтапаРаботы(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПропуститьЭтап(Команда)
	
	ТекущиеДанные = Элементы.РаботаСКандидатом.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СостояниеЭтапа = ПредопределенноеЗначение("Перечисление.СостоянияЭтаповРаботыСКандидатами.Пропущен");
	ТекущиеДанные.Период = ОбщегоНазначенияКлиент.ДатаСеанса();
	НачатьУстановкуСостоянияЭтапаРаботы(СостояниеЭтапа, ТекущиеДанные, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЭтап(Команда)
	
	ТекущиеДанные = Элементы.РаботаСКандидатом.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НачатьОтменуЭтапаРаботы(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьАнкету(Команда)
	
	ТекущиеДанные = Элементы.РаботаСКандидатом.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или Не ЗначениеЗаполнено(КандидатОбъект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураАнкеты = Анкета(КандидатОбъект.Ссылка, КандидатОбъект.Вакансия, ТекущиеДанные.ЭтапРаботы);
	Если СтруктураАнкеты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТолькоФормаЗаполнения", Истина);
	ПараметрыФормы.Вставить("ТолькоПросмотр", СтруктураАнкеты.ТолькоПросмотр);
	ПараметрыФормы.Вставить("Ключ", СтруктураАнкеты.Анкета);
	ПараметрыФормы.Вставить("ВозможностьПредварительногоСохранения", Истина);
	ОткрытьФорму("Документ.Анкета.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьАнкеты(Команда)
	
	ТекущиеДанные = Элементы.РаботаСКандидатом.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ШаблонАнкеты) Тогда
		Возврат;
	КонецЕсли;
	
	ЭлектронноеИнтервьюКлиент.ПечатьАнкеты(ТекущиеДанные.ШаблонАнкеты);
		
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПоСравнениюХарактеристик(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	СписокФизЛиц = Новый Массив;
	СписокФизЛиц.Добавить(КандидатОбъект.ФизическоеЛицо);
	ПараметрыОткрытия.Вставить("СписокФизЛиц", СписокФизЛиц);
	ПараметрыОткрытия.Вставить("Вакансия", КандидатОбъект.Вакансия);
	ОткрытьФорму("Обработка.СравнениеХарактеристикКандидатов.Форма", ПараметрыОткрытия,, Новый УникальныйИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура ПодробныйОтчетПоХарактеристикам(Команда)
	
	Если КандидатОбъект.Ссылка.Пустая() Или Не ЗначениеЗаполнено(КандидатОбъект.Вакансия) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Вакансия", КандидатОбъект.Вакансия);
	ПараметрыОткрытия.Вставить("Кандидат", КандидатОбъект.Ссылка);
	ОткрытьФорму("Отчет.СравнениеХарактеристикКандидатовПодробно.Форма", ПараметрыОткрытия,, Новый УникальныйИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьОценку(Команда)
	
	Если Не ЗначениеЗаполнено(КандидатОбъект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Кандидат", КандидатОбъект.Ссылка);
	ОткрытьФорму("Отчет.ОценкиКандидатаРасшифровка.Форма", ПараметрыОткрытия,, Новый УникальныйИдентификатор());	
	
КонецПроцедуры

&НаКлиенте
Процедура НаписатьПисьмо(Команда)
	
	Если ПустаяСтрока(EMailПредставление) Тогда
		ТекстПредупреждения = НСтр("ru = 'Чтобы написать письмо, требуется заполнить адрес электронной почты.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	НачатьНаписатьПисьмо();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗвонокSMSПодменю(Команда)
	
	Если ПустаяСтрока(ТелефонПредставление) Тогда
		ТекстПредупреждения = НСтр("ru = 'Для совершения звонка или отправки SMS требуется ввести номер телефона.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	НачатьЗвонокSMS();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФотографию(Команда)
	НачатьВыборФотографии();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФотографию(Команда)
	
	Если Не ЗначениеЗаполнено(АдресФотографии) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
	УдалитьИзВременногоХранилища(АдресФотографии);
	АдресФотографии = Неопределено;
	
КонецПроцедуры

#Область КомандыРезюмеHTML

&НаКлиенте
Процедура ИзменитьШрифт(Команда)
	НачатьИзменениеШрифта();
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьШрифт(Команда)
	
	HTMLДокумент = Элементы.РезюмеHTML.Документ; 
	Размер = HTMLДокумент.queryCommandValue("fontSize");
	
	Если Не ЗначениеЗаполнено(Размер) Тогда 
		Размер = 2;
	Иначе
		Размер = Число(Размер);
	КонецЕсли;
	HTMLДокумент.execCommand("fontSize", Ложь, Размер + 1);
	
	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура УменьшитьШрифт(Команда)

	HTMLДокумент = Элементы.РезюмеHTML.Документ; 
	Размер = HTMLДокумент.queryCommandValue("fontSize");
	Если Не ЗначениеЗаполнено(Размер) Тогда 
		Размер = 3;
	Иначе
		Размер = Число(Размер);
	КонецЕсли;
	HTMLДокумент.execCommand("fontSize", Ложь, Размер - 1);
	
	Модифицированность = Истина;
	ТекущийЭлемент = Элементы.РезюмеHTML;

КонецПроцедуры

&НаКлиенте
Процедура Полужирный(Команда)
	
	ВыполнитьHTMLКоманду("Bold");
	ТекущийЭлемент = Элементы.РезюмеHTML;
	
КонецПроцедуры

&НаКлиенте
Процедура Наклонный(Команда)
	
	ВыполнитьHTMLКоманду("italic");
	ТекущийЭлемент = Элементы.РезюмеHTML;
	
КонецПроцедуры

&НаКлиенте
Процедура Подчеркнутый(Команда)
	
	ВыполнитьHTMLКоманду("underline");
	ТекущийЭлемент = Элементы.РезюмеHTML;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФормат(Команда)
	
	HTMLДокумент = Элементы.РезюмеHTML.Документ; 
	HTMLДокумент.execCommand("removeFormat", Ложь, "");
	
	Модифицированность = Истина;
	ТекущийЭлемент = Элементы.РезюмеHTML;

КонецПроцедуры

&НаКлиенте
Процедура ВставитьИзображение(Команда)
	
	НачатьВставкуИзображения();
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьИзображениеИзБуфера(Команда)
	НачатьВставкуИзображенияИзБуфера();
КонецПроцедуры

&НаКлиенте
Процедура МаркированныйСписок(Команда)
	
	ВыполнитьHTMLКоманду("insertUnorderedList");
	ТекущийЭлемент = Элементы.РезюмеHTML;
	
КонецПроцедуры

&НаКлиенте
Процедура НумерованныйСписок(Команда)
	
	ВыполнитьHTMLКоманду("insertOrderedList");
	ТекущийЭлемент = Элементы.РезюмеHTML;
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСсылку(Команда)
	НачатьВставкуСсылки();
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, КандидатОбъект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, КандидатОбъект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, КандидатОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)
	РаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.КонтрольВеденияУчета
&НаКлиенте
Процедура Подключаемый_ОткрытьОтчетПоПроблемам(ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка)
	КонтрольВеденияУчетаКлиентБЗК.ОткрытьОтчетПоПроблемамОбъекта(ЭтотОбъект, КандидатОбъект.Ссылка, СтандартнаяОбработка);
КонецПроцедуры

// Конец СтандартныеПодсистемы.КонтрольВеденияУчета

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект, КандидатОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект, КандидатОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект, КандидатОбъект);
КонецПроцедуры

&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию()
	УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект, КандидатОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект = Неопределено)
	
	Если ТекущийОбъект = Неопределено Тогда
		ТекущийОбъект = КандидатОбъект;
	КонецЕсли;
	
	// Прочитать объект физического лица.
	ПрочитатьФизическоеЛицоОбъект();
	
	РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма, "РаботаСКандидатами", КандидатСсылка);
	
	РольДоступнаДобавлениеИзменениеДанныхКандидатов = ПравоДоступа("Изменение", Метаданные.Справочники.Кандидаты);
	Если Не РольДоступнаДобавлениеИзменениеДанныхКандидатов Тогда
		Элементы.РаботаСКандидатом.ТолькоПросмотр = Истина;
	КонецЕсли;
	ПрочитатьРаботуСКандидатом(ТекущийОбъект);
	
	Если ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ДанныеПубликацииРезюме) Тогда 
		НаборЗаписей = РеквизитФормыВЗначение("ДанныеПубликацииРезюмеНаборЗаписей");
		НаборЗаписей.Отбор.Кандидат.Установить(КандидатСсылка);
		НаборЗаписей.Прочитать();
		ЗначениеВРеквизитФормы(НаборЗаписей, "ДанныеПубликацииРезюмеНаборЗаписей");
	КонецЕсли;
	
	// Резюме в формате HTML.
	ПрочитатьРезюмеHTML(ТекущийОбъект);
	
	УстановитьПанельСостояния(ТекущийОбъект);
	УстановитьДоступностьПозиции(ЭтаФорма);
	УстановитьДоступностьПодразделения(ЭтаФорма);
	
	Рейтинг = РейтингДляОтображения(ТекущийОбъект);
	ОтобразитьРейтинг(ЭтаФорма, Рейтинг);
	РассчитатьСреднийРейтинг(ЭтаФорма);
	УстановитьДоступностьРейтинга();
	
	ПрочитатьКомментарии(КандидатСсылка);
	УстановитьДоступностьДобавленияИзмененияКомментариев();
	
	Если ИспользоватьЭлектронноеИнтервью Тогда
		ЗаполнитьОценкуПоХарактеристикам();
	КонецЕсли;
	
	УстановитьОтображениеНадписей(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьФизическоеЛицоОбъект()
	
	Если КандидатОбъект.ФизическоеЛицо.Пустая() Тогда
		КандидатОбъект.ФизическоеЛицо = Справочники.ФизическиеЛица.ПолучитьСсылку();
	КонецЕсли;
		
	ОбъектФизическоеЛицо = КандидатОбъект.ФизическоеЛицо.ПолучитьОбъект();
	Если ОбъектФизическоеЛицо <> Неопределено Тогда
		ЗначениеВРеквизитФормы(ОбъектФизическоеЛицо, "ФизическоеЛицо");
		РазблокироватьФизическоеЛицоОбъектДляРедактирования();
	КонецЕсли;
	
	ФизическоеЛицоВерсияДанных = ФизическоеЛицо.ВерсияДанных;
	ФизическоеЛицоСсылка = КандидатОбъект.ФизическоеЛицо;
	
	// ФИО физического лица.
	РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма, "ФИОФизическихЛиц", ФизическоеЛицоСсылка);
	// Состояние в браке физического лица.
	РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма, "СостоянияВБракеФизическихЛиц", ФизическоеЛицоСсылка);
	// Гражданство физического лица.
	РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма, "ГражданствоФизическихЛиц", ФизическоеЛицоСсылка);
	// Трудовая деятельность.
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписей(ЭтаФорма, "ТрудоваяДеятельностьФизическихЛиц", ФизическоеЛицоСсылка);
	// Знание языков
	ПрочитатьЗнаниеЯзыков(ФизическоеЛицоСсылка);
	ФизическоеЛицоМестоРождения = ПерсонифицированныйУчетКлиентСервер.ПредставлениеМестаРождения(ФизическоеЛицо.МестоРождения);
	
	// Контактная информация.
	АдресПредставление   = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ФизическоеЛицоСсылка, Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица);
	ТелефонПредставление = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ФизическоеЛицоСсылка, Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица);
	EMailПредставление   = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ФизическоеЛицоСсылка, Справочники.ВидыКонтактнойИнформации.EMailФизическиеЛица);
	
	ЗаполнитьВозраст(ЭтаФорма, ТекущаяДатаСеанса());
	ЗаполнитьДругиеОбращенияКандидата();
	
	ЗаполнитьОбразованиеКвалификацию();
	
	// Фотография.
	АдресФотографии = КадровыйУчетРасширенный.АдресФотографииФизическогоЛица(ФизическоеЛицо.Ссылка);

	ЗаполнитьНаименованиеКандидата();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьЗнаниеЯзыков(ФизическоеЛицо)
	
	НаборЗаписей = РегистрыСведений.ЗнаниеЯзыковФизическихЛиц.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицо);
	НаборЗаписей.Прочитать();
	
	ЗначениеВРеквизитФормы(НаборЗаписей, "ЗнаниеЯзыков");
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНаборЗаписейПериодическихСведений(ИмяРегистра, ВедущийОбъект) Экспорт
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписей(ЭтаФорма, ИмяРегистра, ВедущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДругиеОбращенияКандидата()
	
	ТаблицаКандидатов = Справочники.Кандидаты.КандидатыФизическихЛиц(КандидатОбъект.ФизическоеЛицо);
	
	КоличествоОбращений = 2;
	
	НомерОбращения = 1;
	Для Каждого СтрокаТаблицы Из ТаблицаКандидатов Цикл
		Если СтрокаТаблицы.Кандидат = КандидатСсылка Тогда 
			Продолжить;
		КонецЕсли;
		Если НомерОбращения > КоличествоОбращений Тогда
			Прервать;
		КонецЕсли;
		ПредставлениеОбращения = Строка(СтрокаТаблицы.Вакансия);
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Вакансия) Тогда
			ПредставлениеОбращения = Строка(СтрокаТаблицы.Подразделение);
		КонецЕсли;
		Элементы["ДругойКандидат_" + НомерОбращения].Заголовок = Новый ФорматированнаяСтрока(
			ПредставлениеОбращения, , , , ПолучитьНавигационнуюСсылку(СтрокаТаблицы.Кандидат));;
		Элементы["ДругойКандидат_" + НомерОбращения].Видимость = Истина;
		НомерОбращения = НомерОбращения + 1;
	КонецЦикла;
	
	Пока НомерОбращения <= КоличествоОбращений Цикл
		Элементы["ДругойКандидат_" + НомерОбращения].Видимость = Ложь;
		НомерОбращения = НомерОбращения + 1;
	КонецЦикла;
	
	Элементы.ПолныйСписокКандидатов.Видимость = ТаблицаКандидатов.Количество() > КоличествоОбращений;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоДаннымВакансии()
	
	Если Не ЗначениеЗаполнено(КандидатОбъект.Вакансия) Тогда
		Возврат;
	КонецЕсли;
	
	Объект = РеквизитФормыВЗначение("КандидатОбъект");
	Объект.Заполнить(КандидатОбъект.Вакансия);
	ЗначениеВРеквизитФормы(Объект, "КандидатОбъект");
	
	ПрочитатьРаботуСКандидатом();
	
	УстановитьДоступностьПозиции(ЭтаФорма);
	УстановитьДоступностьПодразделения(ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеВакансии(Вакансия)
	Возврат Справочники.Вакансии.ДанныеВакансии(Вакансия);
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеПозиции(Позиция)
	Возврат Справочники.ШтатноеРасписание.ДанныеПозицииШтатногоРасписания(Позиция);
КонецФункции

&НаСервере
Процедура УстановитьПанельСостояния(ТекущийОбъект = Неопределено)
	
	Если ТекущийОбъект = Неопределено Тогда
		ТекущийОбъект = КандидатОбъект;
	КонецЕсли;
	
	Элементы.Сотрудник.Видимость = Ложь;
	Элементы.ПричинаОтклонения.Видимость = Ложь;
	
	СостоянияКандидата = Новый Массив;
	СостоянияКандидата.Добавить(Перечисления.СостоянияСогласования.Согласовано);
	СостоянияКандидата.Добавить(Перечисления.СостоянияСогласования.Отклонено);
	
	// Работа с кандидатом активна — не отображаем панель согласования.
	Если СостоянияКандидата.Найти(ТекущийОбъект.Состояние) = Неопределено Тогда
		Элементы.СостояниеГруппа.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.СостояниеГруппа.Видимость = Истина;
	
	ПредставлениеСостояния = "";
	Если ТекущийОбъект.Состояние = Перечисления.СостоянияСогласования.Согласовано Тогда
		ОписаниеСотрудника = ПодборПерсонала.СотрудникКандидата(ФизическоеЛицоСсылка, КандидатОбъект.Позиция, КандидатОбъект.ДатаРегистрации);
		Сотрудник = ОписаниеСотрудника.Сотрудник;
		Если Не ЗначениеЗаполнено(Сотрудник) Тогда
			ПредставлениеСостояния = НСтр("ru = 'Одобрен, пока не принят на работу'");
		Иначе
			ПредставлениеСостояния = НСтр("ru = 'Одобрен, принят на работу'");
			Элементы.Сотрудник.Заголовок = ПредставлениеСотрудника(ОписаниеСотрудника);
			Элементы.Сотрудник.Видимость = Истина;
		КонецЕсли;
		Элементы.СостояниеГруппа.ЦветТекстаЗаголовка = Новый Цвет;
	КонецЕсли;
		
	Если ТекущийОбъект.Состояние = Перечисления.СостоянияСогласования.Отклонено Тогда
		ПредставлениеСостояния = НСтр("ru = 'Кандидат отклонен'");
		Элементы.ПричинаОтклонения.Видимость = ЗначениеЗаполнено(КандидатОбъект.ПричинаОтклонения);
		Элементы.СостояниеГруппа.ЦветТекстаЗаголовка = ЦветаСтиля.ПросроченныеДанныеЦвет;
	КонецЕсли;
	
	Элементы.СостояниеГруппа.Заголовок = ПредставлениеСостояния;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеСотрудника(ОписаниеСотрудника)
	
	// Шаблон текста: Позиция в Организация с ДатаПриема
	ОрганизацияТекст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'в %1 с %2'"), 
		ОписаниеСотрудника.Организация, 
		Формат(ОписаниеСотрудника.ДатаПриема, "ДЛФ=D"));
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(НСтр("ru = 'Место работы:'"));
	МассивСтрок.Добавить(" ");
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(Строка(ОписаниеСотрудника.Позиция), , , , ПолучитьНавигационнуюСсылку(ОписаниеСотрудника.Сотрудник)));
	МассивСтрок.Добавить(" ");
	МассивСтрок.Добавить(ОрганизацияТекст);
	
	Возврат Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьПозиции(Форма)
	
	КандидатОбъект = Форма.КандидатОбъект;
	Элементы = Форма.Элементы;
	
	Элементы.Позиция.Доступность = Не ЗначениеЗаполнено(КандидатОбъект.Вакансия);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьПодразделения(Форма)
	
	КандидатОбъект = Форма.КандидатОбъект;
	Элементы = Форма.Элементы;
	
	Если ЗначениеЗаполнено(КандидатОбъект.Вакансия) 
		Или ЗначениеЗаполнено(КандидатОбъект.Позиция) Тогда
		Элементы.Подразделение.Доступность = Ложь;
	Иначе
		Элементы.Подразделение.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьФизическоеЛицо(Отказ)
	
	Если Не ФизическоеЛицоЗаблокировано И ОбщегоНазначения.СсылкаСуществует(ФизическоеЛицоСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектФизическоеЛицо = РеквизитФормыВЗначение("ФизическоеЛицо");
	Если ОбъектФизическоеЛицо.Ссылка.Пустая() Тогда
		ОбъектФизическоеЛицо.УстановитьСсылкуНового(ФизическоеЛицоСсылка);
	КонецЕсли;
	
	УстановитьКонтактнуюИнформациюФизическогоЛица(ОбъектФизическоеЛицо);
	
	// Проверка и запись физического лица.
	Если Не ОбъектФизическоеЛицо.ПроверитьЗаполнение() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОбъектФизическоеЛицо.Записать();
	
	// ФИО физических лиц.
	РедактированиеПериодическихСведений.ЗаписатьЗаписьПослеРедактированияВФорме(ЭтаФорма, "ФИОФизическихЛиц", ФизическоеЛицоСсылка);
	// Состояние в браке.
	РедактированиеПериодическихСведений.ЗаписатьЗаписьПослеРедактированияВФорме(ЭтаФорма, "СостоянияВБракеФизическихЛиц", ФизическоеЛицоСсылка);
	// Гражданство физического лица.
	РедактированиеПериодическихСведений.ЗаписатьЗаписьПослеРедактированияВФорме(ЭтаФорма, "ГражданствоФизическихЛиц", ФизическоеЛицоСсылка);
	// Трудовая деятельность физических лиц.
	НаборЗаписей = РеквизитФормыВЗначение("ТрудоваяДеятельностьФизическихЛицНаборЗаписей");
	НаборЗаписей.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицоСсылка);
	НаборЗаписей.Записать();
	
	КадровыйУчетРасширенный.ЗаписатьДанныеОбОбразовании(ОбъектФизическоеЛицо, Образование);
	КадровыйУчетРасширенный.ЗаписатьДанныеОЗнанииЯзыков(ОбъектФизическоеЛицо, ЗнаниеЯзыков.Выгрузить());
	КадровыйУчетРасширенный.ЗаписатьФотографию(ОбъектФизическоеЛицо.Ссылка, АдресФотографии);
	
	ЗаполнитьОбразованиеКвалификацию();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКонтактнуюИнформациюФизическогоЛица(ОбъектФизическоеЛицо)
	
	КонтактнаяИнформация = УправлениеКонтактнойИнформацией.НоваяКонтактнаяИнформация(Ложь);
	
	// Адрес.
	СтрокаКИ = КонтактнаяИнформация.Добавить();
	СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица;
	СтрокаКИ.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес;
	СтрокаКИ.Значение = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(АдресПредставление, Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица);
	СтрокаКИ.Представление = АдресПредставление;
	
	// Телефон.
	СтрокаКИ = КонтактнаяИнформация.Добавить();
	СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица;
	СтрокаКИ.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
	СтрокаКИ.Значение = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(ТелефонПредставление, Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица);
	СтрокаКИ.Представление = ТелефонПредставление;
	
	// E-mail.
	СтрокаКИ = КонтактнаяИнформация.Добавить();
	СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.EMailФизическиеЛица;
	СтрокаКИ.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
	СтрокаКИ.Значение = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(EMailПредставление, Справочники.ВидыКонтактнойИнформации.EMailФизическиеЛица);
	СтрокаКИ.Представление = EMailПредставление;
	
	УправлениеКонтактнойИнформацией.УстановитьКонтактнуюИнформациюОбъекта(ОбъектФизическоеЛицо, КонтактнаяИнформация);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаблокироватьФизическоеЛицоОбъектДляРедактирования()
	
	Если ФизическоеЛицоЗаблокировано Тогда
		// Уже заблокирован.
		Возврат;
	КонецЕсли;
	
	ЗаблокироватьФизическоеЛицоОбъектДляРедактированияНаСервере();
		
	Если ФизическоеЛицоЗаблокировано Тогда
		Модифицированность = Истина;
		Возврат;
	КонецЕсли;
	
	// Не удалось заблокировать.
	ТекстПредупреждения = НСтр("ru = 'Не удается внести изменения в личные данные кандидата. 
                                |Возможно, личные данные этого кандидата редактируются другим пользователем.'");
	ПоказатьПредупреждение(, ТекстПредупреждения);
	
КонецПроцедуры

&НаСервере
Процедура РазблокироватьФизическоеЛицоОбъектДляРедактирования()
	
	Если Не ФизическоеЛицоЗаблокировано Тогда
		Возврат;
	КонецЕсли;
	
	РазблокироватьДанныеДляРедактирования(ФизическоеЛицоСсылка, УникальныйИдентификатор);
	ФизическоеЛицоЗаблокировано = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗаблокироватьФизическоеЛицоОбъектДляРедактированияНаСервере()
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(ФизическоеЛицоСсылка, ФизическоеЛицоВерсияДанных, УникальныйИдентификатор);
		ФизическоеЛицоЗаблокировано = Истина;
	Исключение
		ФизическоеЛицоЗаблокировано = Ложь;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьРейтинг(Форма, Рейтинг)
	
	Элементы = Форма.Элементы;
	
	Для НомерЗвезды = 1 По 5 Цикл
		Страница = "Неактивная";
		Если НомерЗвезды <= Рейтинг Тогда
			Страница = "Активная";
		КонецЕсли;
		Если Элементы["Звезда" + НомерЗвезды + "Страницы"].ТекущаяСтраница <> Элементы["Звезда" + НомерЗвезды + "Страница" + Страница] Тогда
			Элементы["Звезда" + НомерЗвезды + "Страницы"].ТекущаяСтраница = Элементы["Звезда" + НомерЗвезды + "Страница" + Страница];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьРейтинга()
	
	Если Не ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Для НомерЗвезды = 1 По 5 Цикл
		Элементы["Звезда_Неактивная_" + НомерЗвезды].Доступность = Ложь;
		Элементы["Звезда_Активная_" + НомерЗвезды].Доступность = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеНадписей(ТекущийОбъект)
	
	ОтобразитьАдресРезюме();
	
	ПодборПерсоналаФормы.ФормаСубъектаПриПолученииДанныхНаСервере(ЭтотОбъект, КандидатОбъект, "ФизическоеЛицо");
	
КонецПроцедуры

&НаСервере
Процедура СкрытьКомандуСоздатьКандидата()
	Элементы.СоздатьКандидата.Видимость = Ложь;
КонецПроцедуры

&НаСервере
Процедура ПоказатьКомандуСоздатьКандидата()
	Элементы.СоздатьКандидата.Видимость = Истина;
КонецПроцедуры

&НаСервере
Функция РейтингДляОтображения(ТекущийОбъект)
	
	// Если есть общий рейтинг, показываем его.
	Если ТекущийОбъект.Рейтинг <> 0 Тогда
		Возврат ТекущийОбъект.Рейтинг;
	КонецЕсли;
	
	// Если нет, то ищем индивидуальный по пользователю.
	ИндивидуальнаяОценка = ИндивидуальнаяОценка(ТекущийОбъект.Оценки);
	Если ИндивидуальнаяОценка <> Неопределено Тогда
		Возврат ИндивидуальнаяОценка;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

&НаСервере
Функция ИндивидуальнаяОценка(Оценки)
	
	СтрокаОценки = СтрокаИндивидуальнойОценки(Оценки, Пользователи.АвторизованныйПользователь());
	Если СтрокаОценки <> Неопределено Тогда
		Возврат СтрокаОценки.Оценка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаИндивидуальнойОценки(Оценки, ТекущийПользователь)
	
	НайденныеСтроки = Оценки.НайтиСтроки(Новый Структура("Пользователь", ТекущийПользователь));
	Если НайденныеСтроки.Количество() > 0 Тогда
		Возврат НайденныеСтроки[0];
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьВозраст(Форма, ДатаВозраста)
	Форма.Возраст = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ПредставлениеВозраста(Форма.ФизическоеЛицо.ДатаРождения, ДатаВозраста);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаименованиеКандидата()
	
	ФИО = Новый Структура("Фамилия, Имя, Отчество");
	ЗаполнитьЗначенияСвойств(ФИО, ФИОФизическихЛиц);
	
	КандидатОбъект.Наименование = Справочники.Кандидаты.НаименованиеКандидата(
		ФИО, 
		ФизическоеЛицо.УточнениеНаименования, 
		КандидатОбъект.УточнениеНаименования);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьОднофамильцев()
	
	ПараметрыПроверки = СотрудникиВызовСервера.ПодобратьСписокФизЛиц(
		ФизическоеЛицоСсылка,
		ФИОФизическихЛиц.Фамилия,
		ФИОФизическихЛиц.Имя,
		ФИОФизическихЛиц.Отчество);
	
	Если ПараметрыПроверки.ФизическоеЛицоУникально Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыПроверки.ДанныеФизическихЛиц.Количество() = 0 Тогда
		ТекстПредупреждения = 
			НСтр("ru = 'Найдены люди с похожим именем, но один или несколько человек недоступны из-за ограничений прав доступа. 
                  |Если заполнены только фамилия или имя, попробуйте ввести ФИО полностью.
                  |Если проблема сохраняется, обратитесь, пожалуйста, к администратору.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = СотрудникиКлиент.ПараметрыОткрытияФормыФизическиеЛицаСПохожимиДанными(ПараметрыПроверки);
	
	Оповещение = Новый ОписаниеОповещения("ПроверкаОднофамильцевЗавершение", ЭтотОбъект);
	ОткрытьФорму(
		"Справочник.ФизическиеЛица.Форма.ФизическиеЛицаСПохожимиДанными", 
		ПараметрыОткрытия, 
		ЭтотОбъект, , , , 
		Оповещение, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаОднофамильцевЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт 
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатВыбора.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ФизическоеЛицоСсылка = РезультатВыбора;
	КандидатОбъект.ФизическоеЛицо = ФизическоеЛицоСсылка;
	ПрочитатьФизическоеЛицоОбъект();
		
	Если ЗначениеЗаполнено(АдресСтруктураЗаполнения) Тогда
		СтруктураЗаполнения = ПолучитьИзВременногоХранилища(АдресСтруктураЗаполнения);
		Если ЗначениеЗаполнено(СтруктураЗаполнения) Тогда
			ЗаполнитьДанныеФизическогоЛица(СтруктураЗаполнения, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АдресДанныхДополнительнойФормыНаСервере(ОписаниеДополнительнойФормы) Экспорт
	Возврат СотрудникиФормы.АдресДанныхДополнительнойФормы(ОписаниеДополнительнойФормы, ЭтаФорма);
КонецФункции

&НаСервере
Процедура ПрочитатьДанныеИзХранилищаВФормуНаСервере(Параметр) Экспорт
	
	СотрудникиФормы.ПрочитатьДанныеИзХранилищаВФорму(
		ЭтаФорма,
		СотрудникиКлиентСервер.ОписаниеДополнительнойФормы(Параметр.ИмяФормы),
		Параметр.АдресВХранилище);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОбразованиеКвалификацию(ЭтоДругойКандидатФизЛица = Ложь)
	
	// Образование.
	Если ОбщегоНазначения.СсылкаСуществует(ФизическоеЛицоСсылка) Тогда
		ТаблицаОбразования = Справочники.ОбразованиеФизическихЛиц.ОбразованиеФизическогоЛица(ФизическоеЛицоСсылка);
		Если ЭтоДругойКандидатФизЛица Тогда
			ДополнитьТаблицуОбразования(Образование, ТаблицаОбразования);
		КонецЕсли;
	Иначе
		ТаблицаОбразования = Образование;
	КонецЕсли;
	
	ОбразованиеПредставление = "";
	Для Каждого СтрокаТаблицы Из ТаблицаОбразования Цикл
		ПредставлениеЗаписи = "";
		Если СтрокаТаблицы.ОсновноеОбразование = Истина Тогда
			Если ЗначениеЗаполнено(СтрокаТаблицы.ВидОбразования) Тогда
				ПредставлениеЗаписи = ПредставлениеЗаписи + СтрокаТаблицы.ВидОбразования + ", ";
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(СтрокаТаблицы.ВидДополнительногоОбучения) Тогда
				ПредставлениеЗаписи = ПредставлениеЗаписи + СтрокаТаблицы.ВидДополнительногоОбучения + ", ";
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаТаблицы.УчебноеЗаведение) Тогда
			ПредставлениеЗаписи = ПредставлениеЗаписи + СтрокаТаблицы.УчебноеЗаведение + ", ";
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаТаблицы.Окончание) Тогда
			ПредставлениеЗаписи = ПредставлениеЗаписи + Формат(Год(СтрокаТаблицы.Окончание), "ЧГ=") + ", ";
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаТаблицы.Квалификация) Тогда
			ПредставлениеЗаписи = ПредставлениеЗаписи + СтрокаТаблицы.Квалификация + ", ";
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаТаблицы.Специальность) Тогда
			ПредставлениеЗаписи = ПредставлениеЗаписи + СтрокаТаблицы.Специальность + ", ";
		ИначеЕсли ЗначениеЗаполнено(СтрокаТаблицы.НаименованиеКурса) Тогда
			ПредставлениеЗаписи = ПредставлениеЗаписи + СтрокаТаблицы.НаименованиеКурса + ", ";
		КонецЕсли;
		
		СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(ПредставлениеЗаписи, 2);
		Если Не ПустаяСтрока(ОбразованиеПредставление) Тогда
			ОбразованиеПредставление = ОбразованиеПредставление + Символы.ПС;
		КонецЕсли;
		ОбразованиеПредставление = ОбразованиеПредставление + ПредставлениеЗаписи;
	КонецЦикла;
	
	Если ПустаяСтрока(ОбразованиеПредставление) Тогда
		ОбразованиеПредставление = НСтр("ru = 'Нет сведений.'");
	КонецЕсли;
	
	Элементы.ОбразованиеПредставление.Заголовок = ОбразованиеПредставление;
	
	// Знание языков.
	ЗаполнитьЗнаниеЯзыков(ЭтоДругойКандидатФизЛица);
	
	// Профессии.
	Элементы.ПрофессииПредставление.Заголовок = РегистрыСведений.ПрофессииФизическихЛиц.ПредставлениеПрофессийФизическогоЛица(ФизическоеЛицоСсылка);
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьТаблицуОбразования(ТаблицаИсточник, ТаблицаПриемник)
	
	Для Каждого СтрокаИсточника Из ТаблицаИсточник Цикл
		
		Отбор = Новый Структура("ВидОбразования, УчебноеЗаведение, Окончание");
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаИсточника);
		Отбор.УчебноеЗаведение = СтрокаИсточника.УчебноеЗаведениеСсылка;
		Если ЗначениеЗаполнено(ТаблицаПриемник.НайтиСтроки(Отбор)) Тогда
			 Продолжить;
		КонецЕсли;
		НоваяСтрока = ТаблицаПриемник.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИсточника);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗнаниеЯзыков(ЭтоДругойКандидатФизЛица = Ложь)
	
	Если ОбщегоНазначения.СсылкаСуществует(ФизическоеЛицоСсылка) И Не ЭтоДругойКандидатФизЛица Тогда
		Элементы.ЗнаниеЯзыковПредставление.Заголовок = РегистрыСведений.ЗнаниеЯзыковФизическихЛиц.ПредставлениеВладениеЯзыкамиФизическогоЛица(ФизическоеЛицоСсылка);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЗнаниеЯзыков) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ЗнаниеЯзыковПредставление.Заголовок = РегистрыСведений.ЗнаниеЯзыковФизическихЛиц.ПредставлениеВладениеЯзыкамиПоКоллекцииЗаписей(ЗнаниеЯзыков);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбразованиеКвалификацияПослеОтветаНаПредложениеЗаписать(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьОбразованиеКвалификацию();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбразованиеКвалификацию()
	
	ОписаниеФормы = СотрудникиКлиентСервер.ОписаниеДополнительнойФормы("Справочник.ФизическиеЛица.Форма.ОбразованиеКвалификация");
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьОбразованиеКвалификациюЗавершение", ЭтотОбъект);
	
	СотрудникиКлиент.ОткрытьДополнительнуюФорму(ОписаниеФормы, ЭтаФорма, , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбразованиеКвалификациюЗавершение(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ЗаполнитьОбразованиеКвалификацию();
	
КонецПроцедуры

&НаСервере
Процедура ФИОПриИзмененииНаСервере()

	// Сформировать ФИО.
	СтруктураФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(ФизическоеЛицо.ФИО);
	Если СтруктураФИО.Фамилия <> Неопределено Тогда
		ФИОФизическихЛиц.Фамилия  = СтруктураФИО.Фамилия;
		Если СтруктураФИО.Имя <> Неопределено Тогда
			ФИОФизическихЛиц.Имя = СтруктураФИО.Имя;
		КонецЕсли;
		Если СтруктураФИО.Отчество <> Неопределено Тогда
			ФИОФизическихЛиц.Отчество = СтруктураФИО.Отчество;
		КонецЕсли;
		// Подбираем дату записи о ФИО.
		Если Не ЗначениеЗаполнено(ФИОФизическихЛиц.Период) Тогда
			Если ЗначениеЗаполнено(ФизическоеЛицо.ДатаРождения) Тогда
				ФИОФизическихЛиц.Период = ФизическоеЛицо.ДатаРождения;
			Иначе
				ФИОФизическихЛиц.Период = ЗарплатаКадрыКлиентСервер.ДатаОтсчетаПериодическихСведений();
			КонецЕсли;
		КонецЕсли;
		// Подобрать пол.
		Если ЗначениеЗаполнено(ФИОФизическихЛиц.Отчество) И Не ЗначениеЗаполнено(ФизическоеЛицо.Пол) Тогда
			ФизическоеЛицо.Пол = СотрудникиКлиентСервер.ОпределитьПолПоОтчеству(ФИОФизическихЛиц.Отчество);
		КонецЕсли;
	КонецЕсли;
	
	// Изменяем наименование физического лица.
	ФизическоеЛицо.Наименование = КадровыйУчетКлиентСервер.ПолноеНаименованиеСотрудника(
		ФИОФизическихЛиц.Фамилия, 
		ФИОФизическихЛиц.Имя, 
		ФИОФизическихЛиц.Отчество, 
		ФизическоеЛицо.УточнениеНаименования);
	
	ЗаполнитьНаименованиеКандидата();
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФизическогоЛица(СтруктураЗаполнения, ЭтоДругойКандидатФизЛица = Ложь)
	
	ЗаблокироватьФизическоеЛицоОбъектДляРедактированияНаСервере();
	
	Если Не ЭтоДругойКандидатФизЛица Тогда
		Если СтруктураЗаполнения.Свойство("Фамилия") ИЛИ СтруктураЗаполнения.Свойство("Имя") ИЛИ СтруктураЗаполнения.Свойство("Отчество") Тогда
			ФизическоеЛицо.ФИО = ?(СтруктураЗаполнения.Свойство("Фамилия") И ЗначениеЗаполнено(СтруктураЗаполнения.Фамилия), СтруктураЗаполнения.Фамилия, "") 
				+ ?(СтруктураЗаполнения.Свойство("Имя") И ЗначениеЗаполнено(СтруктураЗаполнения.Имя), " " + СтруктураЗаполнения.Имя, "")
				+ ?(СтруктураЗаполнения.Свойство("Отчество") И ЗначениеЗаполнено(СтруктураЗаполнения.Отчество), " " + СтруктураЗаполнения.Отчество, "");
		КонецЕсли; 
		
		Если Не ЗначениеЗаполнено(ФизическоеЛицо.ФИО) Тогда
			ФизическоеЛицо.ФИО = "";
		КонецЕсли;
		
		ФИОПриИзмененииНаСервере();
		
	КонецЕсли;
	
	Если СтруктураЗаполнения.Свойство("ДатаРождения") Тогда
		Если ТипЗнч(СтруктураЗаполнения.ДатаРождения) = Тип("Дата") Тогда
			ФизическоеЛицо.ДатаРождения = СтруктураЗаполнения.ДатаРождения;
		Иначе
			ФизическоеЛицо.ДатаРождения = ?(ЗначениеЗаполнено(СтруктураЗаполнения.ДатаРождения), Дата(СтрЗаменить(СтруктураЗаполнения.ДатаРождения, "-", "")), Дата(1, 1, 1));
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьВозраст(ЭтаФорма, ТекущаяДатаСеанса());
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо.Пол) И СтруктураЗаполнения.Свойство("Пол") Тогда
		ФизическоеЛицо.Пол = СтруктураЗаполнения.Пол;
		Если ТипЗнч(СтруктураЗаполнения.Пол) = Тип("Строка") И ЗначениеЗаполнено(СтруктураЗаполнения.Пол) Тогда
			Если СтруктураЗаполнения.Пол = "Мужской" Тогда
				Пол = Перечисления.ПолФизическогоЛица.Мужской;
			ИначеЕсли СтруктураЗаполнения.Пол = "Женский" Тогда
				Пол = Перечисления.ПолФизическогоЛица.Женский;
			КонецЕсли;
			ФизическоеЛицо.Пол = Пол;
		КонецЕсли;
	КонецЕсли;

	Если СтруктураЗаполнения.Свойство("КонтактнаяИнформация") 
				И ЗначениеЗаполнено(СтруктураЗаполнения.КонтактнаяИнформация) Тогда
				
		Для Каждого КонтактнаяИнформацияСтрока Из СтруктураЗаполнения.КонтактнаяИнформация Цикл
			
			Если КонтактнаяИнформацияСтрока.Вид = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица") Тогда
				ТелефонПредставление = КонтактнаяИнформацияСтрока.Представление;
			КонецЕсли;
			
			Если КонтактнаяИнформацияСтрока.Вид = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.EMailФизическиеЛица") Тогда
				EMailПредставление = КонтактнаяИнформацияСтрока.Представление;
			КонецЕсли; 
			
			Если КонтактнаяИнформацияСтрока.Вид = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица") Тогда
				АдресПредставление = КонтактнаяИнформацияСтрока.Представление;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли; 
	
	Если СтруктураЗаполнения.Свойство("ТрудоваяДеятельность") 
				И ЗначениеЗаполнено(СтруктураЗаполнения.ТрудоваяДеятельность) Тогда
				
		Если Не ЭтоДругойКандидатФизЛица Тогда
			ТрудоваяДеятельностьФизическихЛицНаборЗаписей.Очистить();
		КонецЕсли;
		
		Для Каждого ТрудоваяДеятельностьСтрока Из СтруктураЗаполнения.ТрудоваяДеятельность Цикл
			
			Если ЭтоДругойКандидатФизЛица Тогда
				Отбор = Новый Структура("Организация, ДатаНачала, ДатаОкончания, Должность");
				ЗаполнитьЗначенияСвойств(Отбор, ТрудоваяДеятельностьСтрока);
				Если ЗначениеЗаполнено(ТрудоваяДеятельностьФизическихЛицНаборЗаписей.НайтиСтроки(Отбор)) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрокаСтаж = ТрудоваяДеятельностьФизическихЛицНаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСтаж, ТрудоваяДеятельностьСтрока, "Организация, Должность, Обязанности, СфераДеятельностиКомпании");
			НоваяСтрокаСтаж.ФизическоеЛицо = ФизическоеЛицоСсылка;
			Если ТипЗнч(ТрудоваяДеятельностьСтрока.ДатаНачала) = Тип("Дата") Тогда 
				НоваяСтрокаСтаж.ДатаНачала = ТрудоваяДеятельностьСтрока.ДатаНачала;
				НоваяСтрокаСтаж.ДатаОкончания = ТрудоваяДеятельностьСтрока.ДатаОкончания;
			Иначе
				НоваяСтрокаСтаж.ДатаНачала = ?(ЗначениеЗаполнено(ТрудоваяДеятельностьСтрока.ДатаНачала), Дата(СтрЗаменить(СтрЗаменить(СтрЗаменить(ТрудоваяДеятельностьСтрока.ДатаНачала, "-", ""), " ", ""), Символы.НПП, "")), Дата(1, 1, 1));
				НоваяСтрокаСтаж.ДатаОкончания = ?(ЗначениеЗаполнено(ТрудоваяДеятельностьСтрока.ДатаОкончания), Дата(СтрЗаменить(СтрЗаменить(СтрЗаменить(ТрудоваяДеятельностьСтрока.ДатаОкончания, "-", ""), " ", ""), Символы.НПП, "")), Дата(1, 1, 1));
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураЗаполнения.Свойство("СостояниеВБраке")
		И ЗначениеЗаполнено(СтруктураЗаполнения.СостояниеВБраке) 
		И СтруктураЗаполнения.СостояниеВБраке.Количество() = 1 Тогда
		
		СостояниеВБраке = Справочники.СостояниеВБраке.ПустаяСсылка();
		Если ТипЗнч(СтруктураЗаполнения.СостояниеВБраке[0].СостояниеВБраке) = Тип("СправочникСсылка.СостояниеВБраке") Тогда
			СостояниеВБраке = СтруктураЗаполнения.СостояниеВБраке[0].СостояниеВБраке;
		ИначеЕсли СтруктураЗаполнения.СостояниеВБраке[0].СостояниеВБраке = "Женат" 
			Или СтруктураЗаполнения.СостояниеВБраке[0].СостояниеВБраке = "Замужем" Тогда
			СостояниеВБраке = Справочники.СостояниеВБраке.НайтиПоКоду("2");
		ИначеЕсли СтруктураЗаполнения.СостояниеВБраке[0].СостояниеВБраке = "Не женат" 
			Или СтруктураЗаполнения.СостояниеВБраке[0].СостояниеВБраке = "Не замужем" Тогда
			СостояниеВБраке = Справочники.СостояниеВБраке.НайтиПоКоду("1");
		КонецЕсли;
		
		СостоянияВБракеФизическихЛиц.СостояниеВБраке = СостояниеВБраке;
		
		Если Не ЗначениеЗаполнено(СостоянияВБракеФизическихЛиц.Период) Тогда
			СостоянияВБракеФизическихЛиц.Период = ТекущаяДатаСеанса();
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураЗаполнения.Свойство("Гражданство")
		И ЗначениеЗаполнено(СтруктураЗаполнения.Гражданство) 
		И СтруктураЗаполнения.Гражданство.Количество() = 1 Тогда
		
		ГражданствоФизическихЛиц.Страна = Справочники.СтраныМира.НайтиПоНаименованию(СтруктураЗаполнения.Гражданство[0].Страна);
		
		Если Не ЗначениеЗаполнено(ГражданствоФизическихЛиц.Период) Тогда
			ГражданствоФизическихЛиц.Период = ТекущаяДатаСеанса();
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураЗаполнения.Свойство("Образование")
		И ЗначениеЗаполнено(СтруктураЗаполнения.Образование)
		И СтруктураЗаполнения.Образование.Количество() > 0 Тогда
		
		Образование.Загрузить(Справочники.Кандидаты.ПодготовитьДанныеОбОбразовании(СтруктураЗаполнения.Образование));
		
	КонецЕсли;
	
	Если СтруктураЗаполнения.Свойство("ЗнаниеЯзыков")
		И ЗначениеЗаполнено(СтруктураЗаполнения.ЗнаниеЯзыков) 
		И СтруктураЗаполнения.ЗнаниеЯзыков.Количество() > 0 Тогда

		Если ЭтоДругойКандидатФизЛица Тогда
			ДополнитьЗнанияЯзыков(Справочники.Кандидаты.ПодготовитьДанныеОЗнанияхЯзыков(СтруктураЗаполнения.ЗнаниеЯзыков), ЗнаниеЯзыков);
		Иначе
			ЗнаниеЯзыков.Загрузить(Справочники.Кандидаты.ПодготовитьДанныеОЗнанияхЯзыков(СтруктураЗаполнения.ЗнаниеЯзыков));
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьОбразованиеКвалификацию(ЭтоДругойКандидатФизЛица);
	
	Если СтруктураЗаполнения.Свойство("Фото") 
		И ЗначениеЗаполнено(СтруктураЗаполнения.Фото) Тогда
		
		АдресФотографии = ПоместитьВоВременноеХранилище(СтруктураЗаполнения.Фото, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьЗнанияЯзыков(Источник, Приемник)
	
	Для Каждого СтрокаИсточник Из Источник Цикл
		НайденныеСтроки = Приемник.НайтиСтроки(Новый Структура("Язык", СтрокаИсточник.Язык));
		Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
			Если Не НайденныеСтроки[0].СтепеньЗнанияЯзыка = СтрокаИсточник.СтепеньЗнанияЯзыка Тогда
				НайденныеСтроки[0].СтепеньЗнанияЯзыка = СтрокаИсточник.СтепеньЗнанияЯзыка;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		НоваяСтрока = Приемник.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИсточник);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьАдресРезюме()
	
	ЧастиСтроки = Новый Массив;
	ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(КандидатОбъект.АдресРезюме, , , , КандидатОбъект.АдресРезюме));
	
	Элементы.АдресРезюме.Заголовок = Новый ФорматированнаяСтрока(ЧастиСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура МногострочныйЭлементНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ТекущиеДанные = Элемент.Родитель.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	СтандартнаяОбработка = Ложь;

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ТекущиеДанные", ТекущиеДанные);
	ДополнительныеПараметры.Вставить("ИмяЭлемента", Элемент.Имя);

	Оповещение = Новый ОписаниеОповещения("МногострочныйЭлементНачалоВыбораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	 
	ПоказатьВводСтроки(Оповещение, ТекущиеДанные[Элемент.Имя],,, Истина); 
	              
КонецПроцедуры

&НаКлиенте
Процедура МногострочныйЭлементНачалоВыбораЗавершение(Строка, ДополнительныеПараметры) Экспорт
	
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.ТекущиеДанные[ДополнительныеПараметры.ИмяЭлемента] = Строка;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция Анкета(Кандидат, Вакансия, Этап)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Кандидат", Кандидат);
	Запрос.УстановитьПараметр("Вакансия", Вакансия);
	Запрос.УстановитьПараметр("Этап", Этап);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	АнкетыКандидатов.Анкета,
		|	АнкетыКандидатов.Анкета.Проведен КАК ТолькоПросмотр
		|ИЗ
		|	РегистрСведений.АнкетыКандидатов КАК АнкетыКандидатов
		|ГДЕ
		|	АнкетыКандидатов.Вакансия = &Вакансия
		|	И АнкетыКандидатов.ЭтапРаботыСКандидатом = &Этап
		|	И АнкетыКандидатов.Кандидат = &Кандидат";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтруктураАнкеты = Новый Структура;
		СтруктураАнкеты.Вставить("Анкета", Выборка.Анкета);
		СтруктураАнкеты.Вставить("ТолькоПросмотр", Выборка.ТолькоПросмотр);
		Возврат СтруктураАнкеты;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьСписокОценок(Команда)
	
	ПараметрыФормы = Новый Структура("Кандидат, Оценки");
	ПараметрыФормы.Кандидат = КандидатСсылка;
	ПараметрыФормы.Оценки = КандидатОбъект.Оценки;
	
	ОткрытьФорму("Справочник.Кандидаты.Форма.ФормаОценок", ПараметрыФормы, , ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСреднийРейтинг(Форма)
	
	КандидатОбъект = Форма.КандидатОбъект;
	ВсегоОценок = КандидатОбъект.Оценки.Количество();
	
	Если ВсегоОценок < 1 Тогда
		Форма.Элементы.НадписьАгрегированнаяОценка.Заголовок = Неопределено;
		Возврат;
	КонецЕсли;
	
	СреднийРейтинг = СреднийРейтинг(Форма, ВсегоОценок);
	
	Форма.Элементы.НадписьАгрегированнаяОценка.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Средняя: %1, всего оценок %2'"), Окр(СреднийРейтинг, 2), ВсегоОценок);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СреднийРейтинг(Форма, ВсегоОценок = Неопределено)
	
	Если ВсегоОценок = Неопределено Тогда
		ВсегоОценок = Форма.КандидатОбъект.Оценки.Количество();
	КонецЕсли;
	
	Если ВсегоОценок = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат Форма.КандидатОбъект.Оценки.Итог("Оценка") / ВсегоОценок;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСреднийРейтинг(ТекущийОбъект)
	ТекущийОбъект.СреднийРейтинг = СреднийРейтинг(ЭтаФорма);
КонецПроцедуры

&НаСервере
Процедура ЗаписатьРаботуСКандидатами()
	
	НаборЗаписей = РеквизитФормыВЗначение("РаботаСКандидатамиНаборЗаписей");
	Индекс = 0;
	Пока Индекс < НаборЗаписей.Количество() Цикл
		Если Не ЗначениеЗаполнено(НаборЗаписей[Индекс].Период) Тогда
			НаборЗаписей.Удалить(Индекс);
			Продолжить;
		КонецЕсли;
		Индекс = Индекс + 1;
	КонецЦикла;
	НаборЗаписей.Отбор.Кандидат.Установить(КандидатСсылка);
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРешениеПоКандидату()
	
	ПараметрыФормы = Новый Структура(
		"Кандидаты, 
		|ДатаРешения, 
		|Решение, 
		|ПричинаОтклонения, 
		|КомментарийРешения, 
		|ТолькоПросмотрРешения");
	ПараметрыФормы.Кандидаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КандидатСсылка);
	ПараметрыФормы.ДатаРешения = КандидатОбъект.ДатаРешения;
	ПараметрыФормы.Решение = КандидатОбъект.Состояние;
	ПараметрыФормы.ПричинаОтклонения = КандидатОбъект.ПричинаОтклонения;
	ПараметрыФормы.КомментарийРешения = КандидатОбъект.КомментарийРешения;
	ПараметрыФормы.ТолькоПросмотрРешения = Истина;
	
	ОткрытьФорму("Справочник.Кандидаты.Форма.ФормаРешенияПоКандидату", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьВыборФотографии()
	СотрудникиКлиентРасширенный.НачатьВыборФотографии(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВыборФотографии()
	ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
КонецПроцедуры

&НаКлиенте
Процедура НачатьРедактированиеМестаРождения(Элемент, СтандартнаяОбработка)
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьРедактированиеМестаРождения", ЭтотОбъект);
	СотрудникиКлиент.НачатьРедактированиеМестаРожденияФизическогоЛица(
		ЭтотОбъект, Элемент, СтандартнаяОбработка, ФизическоеЛицо.МестоРождения, ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактированиеМестаРождения(МестоРожденияИзменено, ДополнительныеПараметры) Экспорт
	Если МестоРожденияИзменено Тогда
		ЗаблокироватьФизическоеЛицоОбъектДляРедактирования();
	КонецЕсли;
КонецПроцедуры

#Область Комментарии

&НаСервере
Функция ПредставлениеКомментария(ДанныеКомментария)
	
	Автор = ДанныеКомментария.Пользователь;
	Время = ДанныеКомментария.Период;
	Текст = ДанныеКомментария.Комментарий;
	
	АвторПредставление = "";
	Если Автор = Пользователи.СсылкаНеуказанногоПользователя() Тогда
		АвторПредставление = Пользователи.ПолноеИмяНеуказанногоПользователя();
	Иначе
		ФизическоеЛицоАвтора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Автор, "ФизическоеЛицо");
		Если Не ЗначениеЗаполнено(ФизическоеЛицоАвтора) Тогда
			АвторПредставление = Строка(Автор);
		Иначе
			ФИОАвтора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ФизическоеЛицоАвтора, "Фамилия, Имя, Отчество");
			АвторПредставление = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ФИОАвтора);
		КонецЕсли;
	КонецЕсли;
	
	ВремяПредставление = "";
	Если НачалоДня(Время) = НачалоДня(ТекущаяДатаСеанса()) Тогда
		ВремяПредставление = Формат(Время, "ДФ=ЧЧ:мм; ДЛФ=T");
	Иначе
		ВремяПредставление = Формат(Время, "ДЛФ=D");
	КонецЕсли;
	
	ШаблонЗаголовка = НСтр("ru = '%1 (%2)'");
	ПредставлениеЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, АвторПредставление, ВремяПредставление);
	
	ЧастиСтроки = Новый Массив;
	ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(ПредставлениеЗаголовка, , , , "Ссылка"));
	ЧастиСтроки.Добавить(Символы.ПС);
	ЧастиСтроки.Добавить(Текст);
	
	Возврат Новый ФорматированнаяСтрока(ЧастиСтроки);
	
КонецФункции

&НаСервере
Процедура УстановитьДоступностьДобавленияИзмененияКомментариев()
	
	ДобавлениеИзменениеКомментариевДоступно = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.КомментарииКандидатов);
	Если ДобавлениеИзменениеКомментариевДоступно Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ДобавитьКомментарий.Доступность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредставлениеКомментариев()
	
	КомментарииКандидатовНаборЗаписей.Сортировать("Период Убыв");
	КоличествоКомментариев = КомментарииКандидатовНаборЗаписей.Количество();

	Если КоличествоКомментариев = 0 Тогда
		Элементы.ВспомогательнаяГруппаКомментарии.Видимость = Ложь;
		Возврат;
	КонецЕсли;
		
	Элементы.ВспомогательнаяГруппаКомментарии.Видимость = Истина;
	
	Индекс = 0;
	Пока Индекс < КоличествоКомментариев Цикл
		Если Индекс = 2 Тогда
			Прервать;
		КонецЕсли;
		НомерКомментария = Индекс + 1;
		СтрокаНабора = КомментарииКандидатовНаборЗаписей[Индекс];
		Элементы["Комментарий" + НомерКомментария].Видимость = Истина;
		Элементы["Комментарий" + НомерКомментария].Заголовок = ПредставлениеКомментария(СтрокаНабора);
		ЭтаФорма["Комментарий" + НомерКомментария] = СтрокаНабора.ПолучитьИдентификатор();
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Пока Индекс < 2 Цикл
		НомерКомментария = Индекс + 1;
		Элементы["Комментарий" + НомерКомментария].Видимость = Ложь;
		Элементы["Комментарий" + НомерКомментария].Заголовок = Неопределено;
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Если КоличествоКомментариев <= 2 Тогда
		Элементы.ДругиеКомментарии.Видимость = Ложь;
	Иначе
		Элементы.ДругиеКомментарии.Видимость = Истина;
		Элементы.ДругиеКомментарии.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Все комментарии (еще %1)'"), КоличествоКомментариев - 2);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьДобавлениеКомментария()
	
	ПараметрыФормы = Новый Структура("Пользователь");
	ПараметрыФормы.Пользователь = ПользователиКлиент.АвторизованныйПользователь();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьДобавлениеКомментария", ЭтотОбъект);
	ОткрытьФорму("РегистрСведений.КомментарииКандидатов.Форма.КомментарийКандидата", ПараметрыФормы, ЭтаФорма, ЭтаФорма, , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьДобавлениеКомментария(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	СтрокаНабора = КомментарииКандидатовНаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаНабора, Результат);
	СтрокаНабора.Период = ОбщегоНазначенияКлиент.ДатаСеанса();
	СтрокаНабора.Кандидат = КандидатСсылка;
	СтрокаНабора.Пользователь = ПользователиКлиент.АвторизованныйПользователь();
	
	ЗаполнитьПредставлениеКомментариев();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКомментарий(НомерКомментария)
	НачатьИзменениеКомментария(НомерКомментария);
КонецПроцедуры

&НаКлиенте
Процедура НачатьИзменениеКомментария(НомерКомментария)
	
	ПараметрыФормы = ПодборПерсоналаКлиентСервер.ПараметрыРедактированияКомментарияКандидата();
		
	СтрокаНабора = КомментарииКандидатовНаборЗаписей.НайтиПоИдентификатору(ЭтаФорма["Комментарий" + НомерКомментария]);
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, СтрокаНабора);
	
	ДополнительныеПараметры = Новый Структура("НомерКомментария");
	ДополнительныеПараметры.НомерКомментария = НомерКомментария;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьИзменениеКомментария", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("РегистрСведений.КомментарииКандидатов.Форма.КомментарийКандидата", ПараметрыФормы, ЭтаФорма, ЭтаФорма, , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьИзменениеКомментария(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	НомерКомментария = ДополнительныеПараметры.НомерКомментария;
	СтрокаНабора = КомментарииКандидатовНаборЗаписей.НайтиПоИдентификатору(ЭтаФорма["Комментарий" + НомерКомментария]);
	ЗаполнитьЗначенияСвойств(СтрокаНабора, Результат);
	СтрокаНабора.Комментарий = Результат.Комментарий;
	СтрокаНабора.Период = ОбщегоНазначенияКлиент.ДатаСеанса();
	СтрокаНабора.Кандидат = КандидатСсылка;
	
	ЗаполнитьПредставлениеКомментариев();
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОткрытиеСпискаКомментариев()
	
	КомментарииКандидатовНаборЗаписей.Сортировать("Период Убыв");
	
	Комментарии = Новый Массив;
	Для Каждого СтрокаНабора Из КомментарииКандидатовНаборЗаписей Цикл
		ЗаписьКомментария = ПодборПерсоналаКлиентСервер.ПараметрыРедактированияКомментарияКандидата();
		ЗаполнитьЗначенияСвойств(ЗаписьКомментария, СтрокаНабора);
		Комментарии.Добавить(ЗаписьКомментария);
	КонецЦикла;

	ПараметрыФормы = Новый Структура(
		"Кандидат, 
		|Комментарии");
	ПараметрыФормы.Кандидат = КандидатСсылка;
	ПараметрыФормы.Комментарии = Комментарии;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьРаботуСпискаКомментариев", ЭтотОбъект);
	ОткрытьФорму("Справочник.Кандидаты.Форма.СписокКомментариев", ПараметрыФормы, , ЭтаФорма, , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРаботуСпискаКомментариев(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	КомментарииКандидатовНаборЗаписей.Очистить();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Результат, КомментарииКандидатовНаборЗаписей);
	
	ЗаполнитьПредставлениеКомментариев()
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьКомментарии(КандидатСсылка)
	
	ЗагрузитьКомментарииРекрутинговыхСайтов(КандидатСсылка);
	ПрочитатьКомментарииИзРегистраСведений(КандидатСсылка);
	ЗаполнитьПредставлениеКомментариев();
			
КонецПроцедуры

&НаСервере
Процедура ЗаписатьКомментарии()
	
	// Пользователь имеет права только на изменение своих комментариев, поэтому меняем набор с отбором по текущему пользователю.
	НаборЗаписей = РегистрыСведений.КомментарииКандидатов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Кандидат.Установить(КандидатСсылка);
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователи.АвторизованныйПользователь());
	
	// Добавляем все комментарии текущего пользователя из текущей формы.
	Для Каждого СтрокаНабора Из КомментарииКандидатовНаборЗаписей Цикл
		Если СтрокаНабора.Пользователь = Пользователи.АвторизованныйПользователь() Тогда
			НоваяСтрока = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНабора);
			НоваяСтрока.Кандидат = КандидатСсылка;
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьКомментарииИзРегистраСведений(КандидатСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Кандидат", КандидатСсылка);
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.АвторизованныйПользователь());
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КомментарииКандидатов.Период КАК Период,
		|	КомментарииКандидатов.Кандидат КАК Кандидат,
		|	КомментарииКандидатов.Пользователь КАК Пользователь,
		|	КомментарииКандидатов.Комментарий КАК Комментарий,
		|	КомментарииКандидатов.Доступность КАК Доступность,
		|	КомментарииКандидатов.МестоПубликации КАК МестоПубликации,
		|	КомментарииКандидатов.Идентификатор КАК Идентификатор
		|ИЗ
		|	РегистрСведений.КомментарииКандидатов КАК КомментарииКандидатов
		|ГДЕ
		|	КомментарииКандидатов.Кандидат = &Кандидат
		|	И (КомментарииКандидатов.Доступность = ЗНАЧЕНИЕ(Перечисление.ДоступностьКомментарияКандидата.Общедоступный)
		|			ИЛИ КомментарииКандидатов.Пользователь = &ТекущийПользователь)";
		
	НаборЗаписей = РегистрыСведений.КомментарииКандидатов.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ЗначениеВРеквизитФормы(НаборЗаписей, "КомментарииКандидатовНаборЗаписей");
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьКомментарииРекрутинговыхСайтов(КандидатСсылка)
	ИнтеграцияРекрутинговыхСайтов.ЗагрузитьКомментарииКандидата(КандидатСсылка);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКандидатаПоДаннымРезюмеРекрутинговогоСайта(СтруктураРезюме, Сайт, Заполнен = Ложь)
	
	Если ТипЗнч(СтруктураРезюме) <> Тип("Структура") 
		И ТипЗнч(СтруктураРезюме) <> Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	ИспользуемыеСайты = ИнтеграцияРекрутинговыхСайтов.ИспользуемыеСайты();
	Если ИспользуемыеСайты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользуемыеСайты.Найти(Сайт) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияРекрутинговыхСайтов.ЗаполнитьДанныеПубликацииРезюмеНаборЗаписей(Сайт, ДанныеПубликацииРезюмеНаборЗаписей, СтруктураРезюме, КандидатСсылка);
	
	СтруктураЗаполнения = ИнтеграцияРекрутинговыхСайтов.СтруктураДанныхКандидата(Сайт, СтруктураРезюме);
	
	СвойстваСПроверкойЗаполнения = Новый Массив;
	СвойстваСПроверкойЗаполнения.Добавить("ФизическоеЛицо");
	СвойстваСПроверкойЗаполнения.Добавить("Вакансия");
	СвойстваСПроверкойЗаполнения.Добавить("Позиция");
	СвойстваСПроверкойЗаполнения.Добавить("Подразделение");
	
	ЗаполнитьЗначенияСвойств(КандидатОбъект, СтруктураЗаполнения, , СтрСоединить(СвойстваСПроверкойЗаполнения, ","));
	
	Для Каждого ИмяСвойства Из СвойстваСПроверкойЗаполнения Цикл
		Если ЗначениеЗаполнено(СтруктураЗаполнения[ИмяСвойства]) Тогда
			КандидатОбъект[ИмяСвойства] = СтруктураЗаполнения[ИмяСвойства];
		КонецЕсли;
	КонецЦикла;
	
	АдресСтруктураЗаполнения = ПоместитьВоВременноеХранилище(СтруктураЗаполнения, УникальныйИдентификатор);
	
	ЗаполнитьДанныеФизическогоЛица(СтруктураЗаполнения);
	
	ЗаполнитьКомментарииПоДаннымРезюмеРекрутинговогоСайта(СтруктураРезюме);
	
	ПолныйТекстРезюме = Неопределено;
	Если СтруктураЗаполнения.Свойство("ПолныйТекстРезюме", ПолныйТекстРезюме) Тогда
		ЗаполнитьТекстРезюме(ПолныйТекстРезюме);
	КонецЕсли;
	
	Если СтруктураЗаполнения.Свойство("ОСебе") Тогда
		КандидатОбъект.ОСебе = СтруктураЗаполнения.ОСебе;
	КонецЕсли;
		
	Заполнен = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКандидатаПоДаннымРезюме(СтруктураРезюме)
	
	СтруктураЗаполнения = Справочники.Кандидаты.СтруктураДанныхКандидата();
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтруктураЗаполнения, СтруктураРезюме, Истина);
	СтруктураЗаполнения.Вставить("Ответственный", Пользователи.ТекущийПользователь());
	СтруктураЗаполнения.Вставить("ДатаРегистрации", ТекущаяДатаСеанса());
	СтруктураЗаполнения.Вставить("ДатаОткрытия", ТекущаяДатаСеанса());
	
	ЗаполнитьЗначенияСвойств(КандидатОбъект, СтруктураЗаполнения, , "ФизическоеЛицо");
	
	Если ЗначениеЗаполнено(СтруктураЗаполнения.ФизическоеЛицо) Тогда
		КандидатОбъект.ФизическоеЛицо = СтруктураЗаполнения.ФизическоеЛицо;
	КонецЕсли;
	
	ЗаполнитьДанныеФизическогоЛица(СтруктураЗаполнения);
	
	ПолныйТекстРезюме = Неопределено;
	Если СтруктураЗаполнения.Свойство("ПолныйТекстРезюме", ПолныйТекстРезюме) Тогда
		ЗаполнитьТекстРезюме(ПолныйТекстРезюме);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКомментарииПоДаннымРезюмеРекрутинговогоСайта(СтруктураРезюме)
	
	Комментарии = СтруктураРезюме["Комментарии"];
	Если Комментарии = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаКомментария Из Комментарии Цикл
		Если Не ЗначениеЗаполнено(СтрокаКомментария.Пользователь) Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(КомментарииКандидатовНаборЗаписей.Добавить(), СтрокаКомментария);
	КонецЦикла;
	
	ЗаполнитьПредставлениеКомментариев();
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ВакансияПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		КандидатОбъект.Вакансия = ТекущаяВакансия;
		Возврат;
	КонецЕсли;
	
	ТекущаяВакансия = КандидатОбъект.Вакансия;
	ЗаполнитьПоДаннымВакансии();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОценкуПоХарактеристикам()
	
	Если КандидатОбъект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОценкиКандидатовПоХарактеристикам.Оценка КАК Оценка
		|ИЗ
		|	РегистрСведений.ОценкиКандидатовПоХарактеристикам КАК ОценкиКандидатовПоХарактеристикам
		|ГДЕ
		|	ОценкиКандидатовПоХарактеристикам.Кандидат = &Кандидат";
	Запрос.УстановитьПараметр("Кандидат", КандидатОбъект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если ЗначениеЗаполнено(Выборка.Оценка) Тогда
			ОценкаПоХарактеристикам = Выборка.Оценка;
		Иначе
			ОценкаПоХарактеристикам = 0;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьАнкетуКандидата(Кандидат, Вакансия, ЭтапРаботы)
	
	ЭлектронноеИнтервью.ОтменитьАнкетыПоЭтапу(Вакансия, ЭтапРаботы, Кандидат);
	Кандидаты = Новый Массив;
	Кандидаты.Добавить(Кандидат);
	ЭлектронноеИнтервью.РассчитатьОценкиКандидатов(Вакансия, Кандидаты);
	
КонецПроцедуры

#Область ЭтапыРаботыСКандидатом

&НаСервере
Процедура ЗаполнитьТекущийЭтапРаботы()
	
	// Определяем текущий этап.
	СтрокаНабора = ПодборПерсонала.ТекущийЭтапРаботыСКандидатом(РаботаСКандидатамиНаборЗаписей, КандидатОбъект.Состояние);
	
	ТекущийЭтапИдентификатор = Неопределено;
	Если СтрокаНабора <> Неопределено Тогда
		ТекущийЭтапИдентификатор = СтрокаНабора.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредставлениеТекущегоЭтапа()
	
	Если ТекущийЭтапИдентификатор = Неопределено Тогда
		// Список этапов отбора для вакансии не определен, или все этапы пройдены.
		Элементы.ТекущийЭтапСостояниеГруппа.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ДобавлятьКоманды = РольДоступнаДобавлениеИзменениеДанныхКандидатов;
	
	ТекущийЭтап = РаботаСКандидатамиНаборЗаписей.НайтиПоИдентификатору(ТекущийЭтапИдентификатор);
	
	Элементы.ТекущийЭтапСостояниеГруппа.Видимость = Истина;
	
	ТекстДатаВремя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 в %2'"), 
		ПредставлениеДаты(ТекущийЭтап.Период), 
		Формат(ТекущийЭтап.Период, "ДФ=ЧЧ:мм"));
	
	ЧастиСтроки = Новый Массив;
	ЧастиСтроки.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Текущий этап «%1»'"), 
			ТекущийЭтап.ЭтапРаботы));
	
	ДобавитьПробел(ЧастиСтроки);
	Если ТекущийЭтап.СостояниеЭтапа = Перечисления.СостоянияЭтаповРаботыСКандидатами.Запланирован Тогда
		ЧастиСтроки.Добавить(НСтр("ru = 'запланирован'"));
		ДобавитьПробел(ЧастиСтроки);
		ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(ТекстДатаВремя, , , , "ТекущийЭтапЗапланировать"));
	Иначе
		Если ТекущийЭтап.СостояниеЭтапа = Перечисления.СостоянияЭтаповРаботыСКандидатами.Пройден Тогда
			ЧастиСтроки.Добавить(НСтр("ru = 'пройден'"));
			ДобавитьПробел(ЧастиСтроки);
			ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(ТекстДатаВремя, , , , "ТекущийЭтапВыполнить"));
			ДобавлятьКоманды = Ложь;
		ИначеЕсли ТекущийЭтап.СостояниеЭтапа = Перечисления.СостоянияЭтаповРаботыСКандидатами.Пропущен Тогда
			ЧастиСтроки.Добавить(НСтр("ru = 'пропущен'"));
			ДобавитьПробел(ЧастиСтроки);
			ЧастиСтроки.Добавить(ТекстДатаВремя);
			ДобавлятьКоманды = Ложь;
		Иначе
			ЧастиСтроки.Добавить(НСтр("ru = 'пока не запланирован'"));
			Если ДобавлятьКоманды Тогда
				ДобавитьПробел(ЧастиСтроки);
				ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Запланировать'"), , , , "ТекущийЭтапЗапланировать"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ДобавлятьКоманды Тогда
		ДобавитьПробел(ЧастиСтроки);
		Если ЗначениеЗаполнено(ТекущийЭтап.ШаблонАнкеты) Тогда
			ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Пройти'"), , , , "ТекущийЭтапПройти"));	
		Иначе
			ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Отметить пройденным'"), , , , "ТекущийЭтапВыполнить"));
		КонецЕсли;
		ДобавитьПробел(ЧастиСтроки);
		ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Пропустить'"), , , , "ТекущийЭтапПропустить"));
	КонецЕсли;
	
	Элементы.ТекущийЭтапСостояние.Заголовок = Новый ФорматированнаяСтрока(ЧастиСтроки);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПробел(ЧастиСтроки)
	ЧастиСтроки.Добавить(" ");
КонецПроцедуры

&НаСервере
Функция ПредставлениеДаты(Дата)
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Если НачалоДня(ТекущаяДата) = НачалоДня(Дата) Тогда
		Возврат НСтр("ru = 'сегодня'");
	КонецЕсли;
	
	Если КонецДня(ТекущаяДата) + 1 = НачалоДня(Дата) Тогда
		Возврат НСтр("ru = 'завтра'");
	КонецЕсли;
	
	Если НачалоГода(ТекущаяДата) <> НачалоГода(Дата) Тогда
		Возврат Формат(Дата, "ДЛФ=D");
	КонецЕсли;
	
	Возврат Формат(Дата, "ДФ='д ММММ (ддд)'");
	
КонецФункции

&НаКлиенте
Процедура ОбработкаНавигационнойСсылкиТекущийЭтапЗапланировать(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки <> "ТекущийЭтапЗапланировать" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	НачатьУстановкуСостоянияТекущегоЭтапаРаботы(ПредопределенноеЗначение("Перечисление.СостоянияЭтаповРаботыСКандидатами.Запланирован"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылкиТекущийЭтапВыполнить(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки <> "ТекущийЭтапВыполнить" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	НачатьУстановкуСостоянияТекущегоЭтапаРаботы(ПредопределенноеЗначение("Перечисление.СостоянияЭтаповРаботыСКандидатами.Пройден"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылкиТекущийЭтапПройти(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки <> "ТекущийЭтапПройти" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	НачатьПрохождениеЭлектронногоИнтервьюТекущегоЭтапаРаботы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылкиТекущийЭтапПропустить(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки <> "ТекущийЭтапПропустить" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	НачатьУстановкуСостоянияТекущегоЭтапаРаботы(ПредопределенноеЗначение("Перечисление.СостоянияЭтаповРаботыСКандидатами.Пропущен"), Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПринятиеРешения(Решение)
	
	Если Не Модифицированность Тогда
		ПоказатьФормуПринятияРешения(Решение);
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура("Решение");
	ПараметрыОповещения.Решение = Решение;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ПринятьРешениеПослеВопросаЗаписать", ЭтотОбъект, ПараметрыОповещения);
	ПоказатьВопросЗаписиКандидата(ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОтменуРешения()
	
	Если Не Модифицированность Тогда
		ОтменитьРешениеНаКлиенте();
		Возврат;
	КонецЕсли;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ОтменитьРешениеПослеВопросаЗаписать", ЭтотОбъект);
	ПоказатьВопросЗаписиКандидата(ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьРешениеПослеВопросаЗаписать(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьФормуПринятияРешения(ДополнительныеПараметры.Решение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьФормуПринятияРешения(Решение)
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ЗавершитьПринятиеРешения", ЭтотОбъект);
	
	Кандидаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КандидатСсылка);
	ПодборПерсоналаКлиент.НачатьПринятиеРешенияПоКандидату(Кандидаты, Решение, ОбработчикОповещения, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПринятиеРешения(РезультатВыбора, ПараметрыОповещения) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьРешениеНаСервере(РезультатВыбора);
	
	ОповеститьОРешении();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРешениеНаСервере(РезультатВыбора)
	
	Кандидаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КандидатСсылка);
	ПодборПерсонала.УстановитьРешениеПоКандидатам(Кандидаты, РезультатВыбора);
	
	ЗаписанныйОбъект = КандидатСсылка.ПолучитьОбъект();
	ЗначениеВРеквизитФормы(ЗаписанныйОбъект, "КандидатОбъект");
	
	ПриПолученииДанныхНаСервере(ЗаписанныйОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРешениеПослеВопросаЗаписать(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ОтменитьРешениеНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРешениеНаКлиенте()
	
	ОтменитьРешениеНаСервере();
	
	ОповеститьОРешении();
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьРешениеНаСервере()
	
	Кандидаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КандидатСсылка);
	ПодборПерсонала.ОтменитьРешениеПоКандидатам(Кандидаты);
	
	ЗаписанныйОбъект = КандидатСсылка.ПолучитьОбъект();
	ЗначениеВРеквизитФормы(ЗаписанныйОбъект, "КандидатОбъект");
	
	ПриПолученииДанныхНаСервере(ЗаписанныйОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОРешении()
	
	ПараметрыОповещения = Новый Структура("Кандидаты, Вакансии");
	ПараметрыОповещения.Кандидаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КандидатСсылка);
	ПараметрыОповещения.Вакансии = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КандидатОбъект.Вакансия);
	
	Оповестить("РешениеПоКандидату", ПараметрыОповещения, ЭтаФорма);
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъектов(ПараметрыОповещения.Кандидаты);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросЗаписиКандидата(ОбработчикОповещения)
	
	ТекстВопроса = НСтр("ru = 'Данные кандидата не записаны.
                         |Для продолжения данные кандидата необходимо записать.'");
	СписокКнопок = Новый СписокЗначений;
	СписокКнопок.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Записать и продолжить'"));
	СписокКнопок.Добавить(КодВозвратаДиалога.Отмена);
	ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, СписокКнопок);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПрохождениеЭлектронногоИнтервьюТекущегоЭтапаРаботы()
	
	ТекущийЭтап = РаботаСКандидатамиНаборЗаписей.НайтиПоИдентификатору(ТекущийЭтапИдентификатор);
	НачатьПрохождениеЭлектронногоИнтервьюЭтапаРаботы(ТекущийЭтап);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПрохождениеЭлектронногоИнтервьюЭтапаРаботы(ДанныеЭтапа)
	
	Если Не Модифицированность Тогда
		ПройтиЭлектронноеИнтервьюЭтапаРаботы(ДанныеЭтапа);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("ДанныеЭтапа");
	ДополнительныеПараметры.ДанныеЭтапа = ДанныеЭтапа;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ПройтиЭлектронноеИнтервьюТекущегоЭтапаРаботыПослеВопросаЗаписать", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопросЗаписиКандидата(ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПройтиЭлектронноеИнтервьюТекущегоЭтапаРаботыПослеВопросаЗаписать(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ПройтиЭлектронноеИнтервьюЭтапаРаботы(ДополнительныеПараметры.ДанныеЭтапа);
	
КонецПроцедуры

&НаКлиенте
Процедура ПройтиЭлектронноеИнтервьюЭтапаРаботы(ДанныеЭтапа)
	
	СтруктураАнкеты = Анкета(КандидатСсылка, КандидатОбъект.Вакансия, ДанныеЭтапа.ЭтапРаботы);
	Если СтруктураАнкеты = Неопределено Или СтруктураАнкеты.ТолькоПросмотр Тогда
		ЭлектронноеИнтервьюКлиент.НачатьИнтервью(
			КандидатСсылка, ФизическоеЛицоСсылка, ДанныеЭтапа.ЭтапРаботы, ДанныеЭтапа.ШаблонАнкеты);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТолькоФормаЗаполнения", Истина);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Ложь);
	ПараметрыФормы.Вставить("Ключ", СтруктураАнкеты.Анкета);
	ПараметрыФормы.Вставить("ВозможностьПредварительногоСохранения", Истина);
	ОткрытьФорму("Документ.Анкета.ФормаОбъекта", ПараметрыФормы);	
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОтменуЭтапаРаботы(ТекущиеДанные)
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.СостояниеЭтапа) Тогда
		// Не нужно отменять этап, у которого и так пустое состояние.
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.СостояниеЭтапа = ПредопределенноеЗначение("Перечисление.СостоянияЭтаповРаботыСКандидатами.Пропущен") Тогда
		// Не спрашиваем, чтобы отменить пропущенный этап.
		ТекущиеДанные.Период = Неопределено;
		НачатьУстановкуСостоянияЭтапаРаботы(Неопределено, ТекущиеДанные, Ложь);
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.СостояниеЭтапа = ПредопределенноеЗначение("Перечисление.СостоянияЭтаповРаботыСКандидатами.Пройден") Тогда
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Этап «%1» отмечен как пройденный кандидатом. 
                  |При отмене этого результата убедитесь, что уведомлены все заинтересованные стороны.'"), 
			ТекущиеДанные.ЭтапРаботы);
		Если ЗначениеЗаполнено(ТекущиеДанные.ШаблонАнкеты) Тогда
			ТекстВопроса = ТекстВопроса + Символы.ПС + НСтр("ru = 'Кроме того, действие приведет к отмене результатов интервью по этому этапу.'");
		КонецЕсли;
		ТекстВопроса = ТекстВопроса + Символы.ПС + НСтр("ru = 'Продолжить?'");
	ИначеЕсли ТекущиеДанные.СостояниеЭтапа = ПредопределенноеЗначение("Перечисление.СостоянияЭтаповРаботыСКандидатами.Запланирован") Тогда
		ТекстВопроса = НСтр("ru = 'При отмене запланированного этапа убедитесь, что уведомлены все заинтересованные стороны.
                             |Продолжить?'"); 
		
	КонецЕсли;
	
	СписокКнопок = Новый СписокЗначений;
	СписокКнопок.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Отменить этап'"));
	СписокКнопок.Добавить(КодВозвратаДиалога.Отмена);
	
	ДополнительныеПараметры = Новый Структура("ДанныеЭтапа");
	ДополнительныеПараметры.ДанныеЭтапа = ТекущиеДанные;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ОтменитьЭтапПослеВопросаПодтверждения", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, СписокКнопок);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЭтапПослеВопросаПодтверждения(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.ДанныеЭтапа.Период = Неопределено;
	НачатьУстановкуСостоянияЭтапаРаботы(Неопределено, ДополнительныеПараметры.ДанныеЭтапа, Ложь);
	ОтменитьАнкетуКандидата(КандидатСсылка, КандидатОбъект.Вакансия, ДополнительныеПараметры.ДанныеЭтапа.ЭтапРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьУстановкуСостоянияТекущегоЭтапаРаботы(СостояниеЭтапа, ОткрыватьФорму = Истина)
	
	ТекущийЭтап = РаботаСКандидатамиНаборЗаписей.НайтиПоИдентификатору(ТекущийЭтапИдентификатор);
	НачатьУстановкуСостоянияЭтапаРаботы(СостояниеЭтапа, ТекущийЭтап, ОткрыватьФорму);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьУстановкуСостоянияЭтапаРаботы(СостояниеЭтапа, ДанныеЭтапа, ОткрыватьФорму = Истина)
	
	Если Не Модифицированность Тогда
		УстановитьСостояниеЭтапаРаботы(СостояниеЭтапа, ДанныеЭтапа, ОткрыватьФорму);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура(
		"СостояниеЭтапа, 
		|ДанныеЭтапа,
		|ОткрыватьФорму");
	ДополнительныеПараметры.СостояниеЭтапа = СостояниеЭтапа;
	ДополнительныеПараметры.ДанныеЭтапа = ДанныеЭтапа;
	ДополнительныеПараметры.ОткрыватьФорму = ОткрыватьФорму;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("УстановитьСостояниеЭтапаРаботыПослеВопросаЗаписать", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопросЗаписиКандидата(ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеЭтапаРаботыПослеВопросаЗаписать(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСостояниеЭтапаРаботы(ДополнительныеПараметры.СостояниеЭтапа, ДополнительныеПараметры.ДанныеЭтапа, ДополнительныеПараметры.ОткрыватьФорму);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьИзменениеСостоянияЭтапаРаботы(ДанныеЭтапа)
	
	Если Не Модифицированность Тогда
		ИзменитьСостояниеЭтапаРаботы(ДанныеЭтапа);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура(
		"ДанныеЭтапа");
	ДополнительныеПараметры.ДанныеЭтапа = ДанныеЭтапа;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ИзменитьСостояниеЭтапаРаботыПослеВопросаЗаписать", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопросЗаписиКандидата(ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСостояниеЭтапаРаботыПослеВопросаЗаписать(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьСостояниеЭтапаРаботы(ДополнительныеПараметры.ДанныеЭтапа);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСостояниеЭтапаРаботы(ДанныеЭтапа)
	
	ПараметрыФормы = ПодборПерсоналаКлиентСервер.ПараметрыРедактированияЭтапаРаботыСКандидатом();
	ПараметрыФормы.Кандидаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КандидатСсылка);
	ПараметрыФормы.СостояниеЭтапа = ДанныеЭтапа.СостояниеЭтапа;
	ПараметрыФормы.ЭтапРаботы = ДанныеЭтапа.ЭтапРаботы;
	ПараметрыФормы.Период = ДанныеЭтапа.Период;
	ПараметрыФормы.ТолькоПросмотр = Не РольДоступнаДобавлениеИзменениеДанныхКандидатов;
	ПараметрыФормы.ШаблонАнкеты = ДанныеЭтапа.ШаблонАнкеты;
	ПараметрыФормы.Комментарий = ДанныеЭтапа.Комментарий;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("УстановитьСостояниеЭтапаРаботыПослеВыбора", ЭтотОбъект);
	ОткрытьФорму("РегистрСведений.РаботаСКандидатами.Форма.ЭтапРаботыСКандидатом", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор, , , ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеЭтапаРаботы(Состояние, ДанныеЭтапа, ОткрыватьФорму = Истина)
	
	Если Не ОткрыватьФорму Тогда
		Результат = ПодборПерсоналаКлиентСервер.РезультатУстановкиЭтапаРаботыСКандидатом();
		Результат.Кандидаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КандидатСсылка);
		Результат.ЭтапРаботы.Этап = ДанныеЭтапа.ЭтапРаботы;
		Результат.ЭтапРаботы.Состояние = Состояние;
		Результат.ЭтапРаботы.Период = ДанныеЭтапа.Период;
		Если Не ЗначениеЗаполнено(ДанныеЭтапа.Период) Тогда
			Результат.ЭтапРаботы.Период = ОбщегоНазначенияКлиент.ДатаСеанса();
		КонецЕсли;
		УстановитьСостояниеЭтапаРаботыНаСервере(Результат);
		ОповеститьОбИзмененииЭтапаРаботы();
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ПодборПерсоналаКлиентСервер.ПараметрыРедактированияЭтапаРаботыСКандидатом();
	ПараметрыФормы.Кандидаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КандидатСсылка);
	ПараметрыФормы.СостояниеЭтапа = Состояние;
	ПараметрыФормы.ЭтапРаботы = ДанныеЭтапа.ЭтапРаботы;
	ПараметрыФормы.ТолькоПросмотр = Не РольДоступнаДобавлениеИзменениеДанныхКандидатов;
	ПараметрыФормы.ШаблонАнкеты = ДанныеЭтапа.ШаблонАнкеты;
	ПараметрыФормы.Комментарий = ДанныеЭтапа.Комментарий;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("УстановитьСостояниеЭтапаРаботыПослеВыбора", ЭтотОбъект);
	ОткрытьФорму("РегистрСведений.РаботаСКандидатами.Форма.ЭтапРаботыСКандидатом", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор, , , ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеЭтапаРаботыПослеВыбора(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСостояниеЭтапаРаботыНаСервере(РезультатВыбора);
	ОповеститьОбИзмененииЭтапаРаботы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеЭтапаРаботыНаСервере(РезультатВыбора)
	
	ПодборПерсонала.УстановитьСостояниеЭтапаРаботыКандидатов(РезультатВыбора);
	
	ЗаписанныйОбъект = КандидатСсылка.ПолучитьОбъект();
	ЗначениеВРеквизитФормы(ЗаписанныйОбъект, "КандидатОбъект");
	
	ПриПолученииДанныхНаСервере(ЗаписанныйОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОбИзмененииЭтапаРаботы()
	Оповестить("ИзмененЭтапРаботыСКандидатом", ПараметрыОповещения(), ЭтотОбъект);
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(КандидатСсылка, ПараметрыОповещения());
КонецПроцедуры

&НаКлиенте
Функция ПараметрыОповещения()
	ПараметрыОповещения = Новый Структура("Кандидаты, Вакансии");
	ПараметрыОповещения.Кандидаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КандидатСсылка);
	ПараметрыОповещения.Вакансии = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КандидатОбъект.Вакансия);
	Возврат ПараметрыОповещения;
КонецФункции

&НаСервере
Процедура ПрочитатьРаботуСКандидатом(ТекущийОбъект = Неопределено)
	
	Если ТекущийОбъект = Неопределено Тогда
		ТекущийОбъект = КандидатОбъект;
	КонецЕсли;
	
	РаботаСКандидатомТаблица = ПодборПерсонала.ТаблицаЭтаповРаботыСКандидатомНаВакансию(КандидатСсылка, КандидатОбъект.Вакансия);
	
	РаботаСКандидатамиНаборЗаписей.Очистить();
	Для Каждого СтрокаТаблицы Из РаботаСКандидатомТаблица Цикл
		НоваяСтрока = РаботаСКандидатамиНаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.Период = СтрокаТаблицы.ДатаВремя;
	КонецЦикла;
	
	ЗаполнитьТекущийЭтапРаботы();
	ЗаполнитьПредставлениеТекущегоЭтапа();
	НастроитьКомандыСпискаЭтаповРаботыСКандидатом(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьКомандыСпискаЭтаповРаботыСКандидатом(Форма, ВыбранныйЭтапИдентификатор = Неопределено)
	
	Элементы = Форма.Элементы;
	
	РольДоступнаДобавлениеИзменениеДанныхКандидатов = Форма.РольДоступнаДобавлениеИзменениеДанныхКандидатов;
	
	ТекущиеДанные = Неопределено;
	Если ВыбранныйЭтапИдентификатор <> Неопределено Тогда
		ТекущиеДанные = Форма.РаботаСКандидатамиНаборЗаписей.НайтиПоИдентификатору(ВыбранныйЭтапИдентификатор);
	КонецЕсли;
	Если ТекущиеДанные = Неопределено Тогда
		Элементы.РаботаСКандидатомОткрытьАнкету.Доступность = Ложь;
		Элементы.РаботаСКандидатомПечатьАнкеты.Доступность = Ложь;
		Элементы.РаботаСКандидатомЗавершитьЭтап.Доступность = Ложь;
		Элементы.РаботаСКандидатомПройтиИнтервью.Доступность = Ложь;
		Элементы.РаботаСКандидатомПропуститьЭтап.Доступность = Ложь;
		Элементы.РаботаСКандидатомЗапланироватьЭтап.Доступность = Ложь;
		Элементы.РаботаСКандидатомОтменитьЭтап.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.РаботаСКандидатомЗапланироватьЭтап.Доступность = РольДоступнаДобавлениеИзменениеДанныхКандидатов;
	Элементы.РаботаСКандидатомОтменитьЭтап.Доступность = Ложь;
	Если ЗначениеЗаполнено(ТекущиеДанные.Период) Тогда
		Элементы.РаботаСКандидатомОтменитьЭтап.Доступность =  РольДоступнаДобавлениеИзменениеДанныхКандидатов;
	КонецЕсли;
	Если ТекущиеДанные.СостояниеЭтапа = ПредопределенноеЗначение("Перечисление.СостоянияЭтаповРаботыСКандидатами.Пройден") Тогда
		Элементы.РаботаСКандидатомЗавершитьЭтап.Доступность = Ложь;
		Элементы.РаботаСКандидатомПройтиИнтервью.Доступность = Ложь;
		Элементы.РаботаСКандидатомПропуститьЭтап.Доступность = РольДоступнаДобавлениеИзменениеДанныхКандидатов;
	ИначеЕсли ТекущиеДанные.СостояниеЭтапа = ПредопределенноеЗначение("Перечисление.СостоянияЭтаповРаботыСКандидатами.Пропущен") Тогда
		Элементы.РаботаСКандидатомЗавершитьЭтап.Доступность = РольДоступнаДобавлениеИзменениеДанныхКандидатов;
		Элементы.РаботаСКандидатомПройтиИнтервью.Доступность = РольДоступнаДобавлениеИзменениеДанныхКандидатов;
		Элементы.РаботаСКандидатомПропуститьЭтап.Доступность = Ложь;
	Иначе
		Элементы.РаботаСКандидатомЗавершитьЭтап.Доступность = РольДоступнаДобавлениеИзменениеДанныхКандидатов;
		Элементы.РаботаСКандидатомПройтиИнтервью.Доступность = РольДоступнаДобавлениеИзменениеДанныхКандидатов;
		Элементы.РаботаСКандидатомПропуститьЭтап.Доступность = РольДоступнаДобавлениеИзменениеДанныхКандидатов;
	КонецЕсли;
	
	Если Не Форма.ИспользоватьЭлектронноеИнтервью Тогда
		Элементы.РаботаСКандидатомЗавершитьЭтап.Видимость = Истина;
		Элементы.РаботаСКандидатомПройтиИнтервью.Видимость = Ложь;
	Иначе	
		Элементы.РаботаСКандидатомОткрытьАнкету.Доступность = Ложь;
		Элементы.РаботаСКандидатомПечатьАнкеты.Доступность = Ложь;
		СтруктураАнкеты = Анкета(Форма.КандидатСсылка, Форма.КандидатОбъект.Вакансия, ТекущиеДанные.ЭтапРаботы);
		Если СтруктураАнкеты <> Неопределено Тогда
			Элементы.РаботаСКандидатомОткрытьАнкету.Доступность = РольДоступнаДобавлениеИзменениеДанныхКандидатов;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ТекущиеДанные.ШаблонАнкеты) Тогда
			Элементы.РаботаСКандидатомЗавершитьЭтап.Видимость = Истина;
			Элементы.РаботаСКандидатомПройтиИнтервью.Видимость = Ложь;
		Иначе	
			Элементы.РаботаСКандидатомПечатьАнкеты.Доступность = Истина;
			Элементы.РаботаСКандидатомЗавершитьЭтап.Видимость = Ложь;
			Элементы.РаботаСКандидатомПройтиИнтервью.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Взаимодействия

&НаСервере
Процедура УстановитьВидимостьНаписатьПисьмо()
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПрочиеВзаимодействия") Тогда
		Элементы.НаписатьПисьмо.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.НаписатьПисьмо.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЗвонокSMS()
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПрочиеВзаимодействия") Тогда
		Элементы.ЗвонокSMSПодменю.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.ЗвонокSMSПодменю.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗвонокSMS()
	
	Если Не Модифицированность Тогда
		ЗвонокSMSПоказатьПодменю();
		Возврат;
	КонецЕсли;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ЗвонокSMSПослеВопросаЗаписать", ЭтотОбъект);
	ПоказатьВопросЗаписиКандидата(ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗвонокSMSПослеВопросаЗаписать(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ЗвонокSMSПоказатьПодменю();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗвонокSMSПоказатьПодменю()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗвонокSMSПодменюПослеВыбора", ЭтотОбъект);
	
	ЗвонокSMSСписокВыбора = Новый СписокЗначений;
	ЗвонокSMSСписокВыбора.Добавить("Звонок", НСтр("ru = 'Позвонить'"), , БиблиотекаКартинок.Позвонить);
	ЗвонокSMSСписокВыбора.Добавить("SMS", НСтр("ru = 'Отправить SMS...'"), , БиблиотекаКартинок.ОтправитьSMS);
	
	ПоказатьВыборИзМеню(ОписаниеОповещения, ЗвонокSMSСписокВыбора, Элементы.ЗвонокSMSПодменю);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗвонокSMSПодменюПослеВыбора(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйЭлемент.Значение = "SMS" Тогда
		Если Не ИспользоватьШаблоныСообщений Тогда
			ВзаимодействияКлиент.СоздатьВзаимодействиеИлиПредмет("Документ.СообщениеSMS.ФормаОбъекта", КандидатСсылка, ЭтаФорма);
			Возврат;
		КонецЕсли;
		ШаблоныСообщенийКлиент.СформироватьСообщение(КандидатСсылка, "СообщениеSMS", Неопределено); 
	ИначеЕсли ВыбранныйЭлемент.Значение = "Звонок" Тогда
		ВзаимодействияКлиент.СоздатьВзаимодействиеИлиПредмет("Документ.ТелефонныйЗвонок.ФормаОбъекта", КандидатСсылка, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьНаписатьПисьмо()
	
	Если Не Модифицированность Тогда
		НаписатьПисьмоКандидату();
		Возврат;
	КонецЕсли;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("НаписатьПисьмоПослеВопросаЗаписать", ЭтотОбъект);
	ПоказатьВопросЗаписиКандидата(ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура НаписатьПисьмоПослеВопросаЗаписать(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	НаписатьПисьмоКандидату();
	
КонецПроцедуры

&НаКлиенте
Процедура НаписатьПисьмоКандидату()
	
	Если Не ИспользоватьШаблоныСообщений Тогда
		ВзаимодействияКлиент.СоздатьВзаимодействиеИлиПредмет("Документ.ЭлектронноеПисьмоИсходящее.ФормаОбъекта", КандидатСсылка, ЭтаФорма);
		Возврат;
	КонецЕсли;
	
	ШаблоныСообщенийКлиент.СформироватьСообщение(КандидатСсылка, "Письмо", Неопределено); 
	
КонецПроцедуры

#КонецОбласти

#Область ЗащитаПерсональныхДанных

&НаКлиенте
Процедура ПриОбработкеНавигационнойСсылкиНовоеСогласие(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки <> "НовоеСогласие" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Организация = Неопределено;
	Если ЗначениеЗаполнено(КандидатОбъект.Вакансия) Тогда
		Организация = ОрганизацияВакансии(КандидатОбъект.Вакансия);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("Субъект", ФизическоеЛицоСсылка);
	ПараметрыФормы.Вставить("ДатаСогласия", КандидатОбъект.ДатаРегистрации);
	
	ОткрытьФорму("Документ.СогласиеНаОбработкуПерсональныхДанных.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОбработкеНавигационнойСсылкиУничтожениеПДн(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки <> "УничтожениеПДн" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	СкрытьПерсональныеДанныеСубъекта(ФизическоеЛицоСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьПерсональныеДанныеСубъекта(Субъект)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Субъект", Субъект);
	
	ДлительнаяОперация = ДлительнаяОперацияСкрытияПДнКандидата(Субъект, УникальныйИдентификатор);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется скрытие персональных данных...'");
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст = НСтр("ru = 'Скрытие персональных данных субъекта выполнено.'");
	
	Обработчик = Новый ОписаниеОповещения("ПослеСкрытияПерсональныхДанныхСубъекта", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДлительнаяОперацияСкрытияПДнКандидата(Субъект, ИдентификаторФормы)
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("Субъект", Субъект);
	ПараметрыМетода.Вставить("СообщатьОбИсключениях", Истина);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Скрытие персональных данных субъекта'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("ПодборПерсонала.СкрытьПерсональныеДанныеСубъекта", ПараметрыМетода, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПослеСкрытияПерсональныхДанныхСубъекта(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось скрыть персональные данные субъекта %1.
                  |См. подробности в Журнале регистрации.'"), 
			ДополнительныеПараметры.Субъект);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	Оповестить("СкрытыПерсональныеДанные");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОрганизацияВакансии(Вакансия)
	
	Если Не ЗначениеЗаполнено(Вакансия) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеВакансии = ДанныеВакансии(Вакансия);
	
	Если Не ЗначениеЗаполнено(ДанныеВакансии.Позиция) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеПозиции = ДанныеПозиции(ДанныеВакансии.Позиция);
	
	Возврат ДанныеПозиции.Организация;
	
КонецФункции

#КонецОбласти

#Область РезюмеHTML

&НаСервере
Процедура ПрочитатьРезюмеHTML(ТекущийОбъект)

	Если ТекущийОбъект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли; 

	СохраненноеРезюме = ТекущийОбъект.Резюме.Получить();
	Если СохраненноеРезюме <> Неопределено Тогда
		СтруктураРезюме = ПодборПерсоналаКлиентСервер.СтруктураHTMLРезюме();
		ЗаполнитьЗначенияСвойств(СтруктураРезюме, СохраненноеРезюме);
		РезюмеHTML = СтруктураРезюме.HTML;
	КонецЕсли;

	ВставитьИзображенияВРезюмеHTML(ТекущийОбъект.Ссылка);

КонецПроцедуры

&НаСервере
Процедура ВставитьИзображенияВРезюмеHTML(ВладелецСсылка, ФайлыИзображений = Неопределено)
		
	НавигационнаяСсылкаИнформационнойБазы = ПолучитьНавигационнуюСсылкуИнформационнойБазы();
	
	Если ФайлыИзображений = Неопределено Тогда
		ФайлыИзображений = Новый Массив;
		РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(ВладелецСсылка, ФайлыИзображений);
	КонецЕсли;
	
	СоответствиеИдентификаторов = Новый Соответствие;
	Для Каждого ЭлементСписка Из ИдентификаторыИзображенийРезюме Цикл
		СоответствиеИдентификаторов.Вставить(ЭлементСписка.Значение.GUIDИзображения, ЭлементСписка);
	КонецЦикла;
	
	Для Каждого ФайлСсылка Из ФайлыИзображений Цикл
		GUIDИзображения = Строка(ФайлСсылка.УникальныйИдентификатор());
		Если СтрЧислоВхождений(РезюмеHTML, GUIDИзображения) = 0 Тогда
			Продолжить;
		КонецЕсли;
		ДанныеФайла = РаботаСФайлами.ДвоичныеДанныеФайла(ФайлСсылка);
		АдресФайла = ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор);
		АбсолютнаяСсылка = НавигационнаяСсылкаИнформационнойБазы + "/" + АдресФайла;
		РезюмеHTML = СтрЗаменить(РезюмеHTML, "http:cid:" + GUIDИзображения, АбсолютнаяСсылка); // коррекция ошибочного html
		РезюмеHTML = СтрЗаменить(РезюмеHTML, "cid:" + GUIDИзображения, АбсолютнаяСсылка);
		РезюмеHTML = СтрЗаменить(РезюмеHTML, "CID:" + GUIDИзображения, АбсолютнаяСсылка);
		РезюмеHTML = СтрЗаменить(РезюмеHTML, GUIDИзображения, АбсолютнаяСсылка);
		ОписаниеИзображения = СоответствиеИдентификаторов[GUIDИзображения];
		Если СоответствиеИдентификаторов[GUIDИзображения] = Неопределено Тогда
			ОписаниеИзображения = Новый Структура(
				"GUIDИзображения, 
				|НавигационнаяСсылка");
			ИдентификаторыИзображенийРезюме.Добавить(ОписаниеИзображения);
		КонецЕсли;
		ОписаниеИзображения.GUIDИзображения = GUIDИзображения;
		ОписаниеИзображения.НавигационнаяСсылка = АбсолютнаяСсылка;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзвлечьСодержимоеРезюмеHTML()

	HTMLДокумент = Элементы.РезюмеHTML.Документ; 
	Если HTMLДокумент = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ИзвлечьИзображенияРезюмеHTML(HTMLДокумент);
	
	ТекстРезюмеHTML = "<html><body>" + HTMLДокумент.body.innerHTML + "</body></html>";
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТекстРезюме(ТекстРезюме)
	РезюмеHTML = СтрЗаменить(ТекстРезюме, Символы.ПС, "<br>");
КонецПроцедуры

&НаКлиенте
Процедура ИзвлечьИзображенияРезюмеHTML(HTMLДокумент)
	
	ОписанияФайлов = Новый Массив;
	СсылкиФайлов = Новый Соответствие;
	
	ЧислоКартинок = HTMLДокумент.images.length;  
	Для Индекс = 0 По ЧислоКартинок - 1 Цикл  
		Изображение = HTMLДокумент.images.item(Индекс);
		ИзвлечьИзображениеРезюмеHTMLПоФайлуНаДиске(Изображение, ОписанияФайлов, СсылкиФайлов);
		ПроверитьИзображениеРезюмеHTMLПоНавигационнойСсылке(Изображение);
	КонецЦикла;	
	
	Если ОписанияФайлов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыЗагрузки.Интерактивно = Ложь;
	
	ПараметрыОповещения = Новый Структура(
		"Кандидат,
		|ОписанияФайлов,
		|СсылкиФайлов");
	ПараметрыОповещения.Кандидат = КандидатСсылка;
	ПараметрыОповещения.ОписанияФайлов = ОписанияФайлов;
	ПараметрыОповещения.СсылкиФайлов = СсылкиФайлов;
	
	ОбработчикЗавершения = Новый ОписаниеОповещения("ЗавершитьПомещениеФайлов", ЭтотОбъект, ПараметрыОповещения);
	ФайловаяСистемаКлиент.ЗагрузитьФайлы(ОбработчикЗавершения, ПараметрыЗагрузки, ОписанияФайлов);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПомещениеФайлов(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт

	Если ПомещенныеФайлы = Неопределено Или ПомещенныеФайлы.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Не удалось сохранить файлы изображений резюме'");
	КонецЕсли;
	
	ЗаписатьИзображенияРезюмеHTMLВФайлы(
		ДополнительныеПараметры.Кандидат, ПомещенныеФайлы, ДополнительныеПараметры.СсылкиФайлов);
	
	ВставитьИзображенияВРезюмеHTML(
		ДополнительныеПараметры.Кандидат, 
		ОбщегоНазначенияБЗККлиентСервер.ЗначенияСоответствия(ДополнительныеПараметры.СсылкиФайлов));
	
КонецПроцедуры

&НаКлиенте
Процедура ИзвлечьИзображениеРезюмеHTMLПоФайлуНаДиске(Изображение, ОписанияФайлов, СсылкиФайлов)

	Если Найти(ВРег(Изображение.src), ВРег("file://")) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПутьФайла = Сред(Изображение.src, СтрДлина("file://") + 1);
	ПутьФайла = СтрЗаменить(ПутьФайла, "%20", " ");
	
	ПервыйСимвол = Лев(ПутьФайла, 1);
	Если ПервыйСимвол = "/" Или ПервыйСимвол = "\" Тогда
		ПутьФайла = Сред(ПутьФайла, 2);
	КонецЕсли;	
	
	ОписанияФайлов.Добавить(Новый ОписаниеПередаваемогоФайла(ПутьФайла));
	
	НоваяСсылка = НоваяСсылкаПрисоединенногоФайла(); 
	СсылкиФайлов.Вставить(ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПутьФайла).ПолноеИмя, НоваяСсылка);
	
	Изображение.src = "cid:" + Строка(НоваяСсылка.УникальныйИдентификатор());
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НоваяСсылкаПрисоединенногоФайла()
	Возврат Справочники.КандидатыПрисоединенныеФайлы.ПолучитьСсылку();
КонецФункции

&НаКлиенте
Процедура ПроверитьИзображениеРезюмеHTMLПоНавигационнойСсылке(Изображение)

	Если СтрНайти(ВРег(Изображение.src), ВРег("e1cib/tempstorage")) = 0 Тогда
		Возврат;
	КонецЕсли;
				
	КартинкаНайдена = Ложь;
	
	Для Каждого ЭлементСписка Из ИдентификаторыИзображенийРезюме Цикл
		ОписаниеИсточника = ЭлементСписка.Значение;
		Если СтрНайти(Изображение.src, ОписаниеИсточника.НавигационнаяСсылка) <> 0 Тогда
			КартинкаНайдена = Истина;
			Прервать;
		КонецЕсли;		
	КонецЦикла;
	
	Если Не КартинкаНайдена Тогда
		ЕстьНеверноВставленныеИзображенияРезюме = Истина;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьРезюмеHTML()
	
	#Если ВебКлиент Тогда
		Возврат;
	#КонецЕсли
		
	НРегТекстHTML = НРег(РезюмеHTML);
	ПозицияТэгаHTML = Найти(НРегТекстHTML, "<html");
	ПозицияТэгаBODY = Найти(НРегТекстHTML, "<body");
	Если ПозицияТэгаHTML = 0 И ПозицияТэгаBODY = 0 Тогда
		РезюмеHTML = "<html><body style=""margin-top:1px; padding-top:1px"">" + РезюмеHTML + "</body></html>";
	ИначеЕсли ПозицияТэгаHTML = 0 И ПозицияТэгаBODY > 0 Тогда
		РезюмеHTML = "<html>" + РезюмеHTML + "</html>";
	ИначеЕсли ПозицияТэгаHTML > 0 И ПозицияТэгаBODY = 0 Тогда
		ПозицияОкончанияТэгаHTML = НайтиПосле(НРегТекстHTML, ">", ПозицияТэгаHTML);
		Голова = Лев(РезюмеHTML, ПозицияОкончанияТэгаHTML);
		ПозицияЗакрывающегосяТэгаHTML = Найти(НРегТекстHTML, "</html>");
		Хвост = Сред(РезюмеHTML, ПозицияЗакрывающегосяТэгаHTML);
		Середина = Сред(РезюмеHTML, ПозицияОкончанияТэгаHTML + 1, ПозицияЗакрывающегосяТэгаHTML - ПозицияОкончанияТэгаHTML - 1);
		РезюмеHTML = Голова + "<body style=""margin-top:1px; padding-top:1px"">" + Середина + "</body>" + Хвост;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НайтиПосле(Строка, Подстрока, НачальнаяПозиция = 0)
	
	Если СтрДлина(Строка) <= НачальнаяПозиция Тогда
		Возврат 0;
	КонецЕсли;
	
	Позиция = СтрНайти(Строка, Подстрока, НаправлениеПоиска.СНачала, НачальнаяПозиция + 1);
	Возврат Позиция;
	
КонецФункции

&НаСервере
Процедура ВключитьРежимРедактированияHTML()
		
	Если Найти(РезюмеHTML, "<body contentEditable") = 0 И Найти(РезюмеHTML, "<BODY contentEditable") = 0 Тогда
		Если Найти(РезюмеHTML, "<body") <> 0 Или Найти(РезюмеHTML, "<BODY") <> 0 Тогда
			РезюмеHTML = СтрЗаменить(РезюмеHTML, "<body", "<body contentEditable=true");
			РезюмеHTML = СтрЗаменить(РезюмеHTML, "<BODY", "<BODY contentEditable=true");
		Иначе	
			РезюмеHTML = СтрЗаменить(РезюмеHTML, "<html>", "<html><body contentEditable=true>");
			РезюмеHTML = СтрЗаменить(РезюмеHTML, "</html>", "</body></html>");
			
			РезюмеHTML = СтрЗаменить(РезюмеHTML, "<HTML>", "<HTML><BODY contentEditable=true>");
			РезюмеHTML = СтрЗаменить(РезюмеHTML, "</HTML>", "</BODY></HTML>");
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьРезюмеHTMLКЗаписи(Отказ, ТекущийОбъект)
	
	Если ЕстьНеверноВставленныеИзображенияРезюме Тогда
		ТекстСообщения = НСтр("ru = 'Некорректно вставлена картинка в тело письма.
							  |Используйте для вставки Ctrl-Shift-V.'"); 
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(ТекстРезюмеHTML, "<BR></DIV>") <> 0 Тогда
		ТекстРезюмеHTML = СтрЗаменить(ТекстРезюмеHTML, "<BR></DIV>", "<BR>&nbsp;</DIV>");
	КонецЕсли;	
	
	УдалитьСкриптыИзРезюмеHTML();
	ПодготовитьИзображенияРезюмеHTMLКЗаписи();
	
	СтруктураРезюме = ПодборПерсоналаКлиентСервер.СтруктураHTMLРезюме();
	СтруктураРезюме.HTML = ТекстРезюмеHTML;
	ТекущийОбъект.Резюме =  Новый ХранилищеЗначения(СтруктураРезюме);

	ИзвлечьТекстРезюме(ТекущийОбъект);			
	
КонецПроцедуры

&НаСервере
Процедура ИзвлечьТекстРезюме(ТекущийОбъект)

	ТекущийОбъект.РезюмеТекст = Неопределено;

	ТекстHTML = ТекстРезюмеHTML; 

	ТекстHTML = СтрЗаменить(ТекстHTML, "</o:p>", "</o:p>" + Символы.ПС);
	ТекстHTML = СтрЗаменить(ТекстHTML, "</o:p>" + Символы.ПС + Символы.ПС, "</o:p>" + Символы.ПС);
	ТекстHTML = СтрЗаменить(ТекстHTML, "</p>", "</p>" + Символы.ПС);
	ТекстHTML = СтрЗаменить(ТекстHTML, "</p>" + Символы.ПС + Символы.ПС, "</p>" + Символы.ПС);
	ТекстHTML = СтрЗаменить(ТекстHTML, "</div>", "</div>" + Символы.ПС);
	ТекстHTML = СтрЗаменить(ТекстHTML, "</div>" + Символы.ПС + Символы.ПС, "</div>" + Символы.ПС);
	ТекстHTML = СтрЗаменить(ТекстHTML, "<br>", Символы.ПС + Символы.ПС);
	
	Построитель = Новый ПостроительDOM;
	ЧтениеHTML = Новый ЧтениеHTML;
	ЧтениеHTML.УстановитьСтроку(ТекстHTML);
	
	ДокументHTML = Построитель.Прочитать(ЧтениеHTML);
	
	Если ДокументHTML.Тело = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийОбъект.РезюмеТекст = Новый ХранилищеЗначения(ДокументHTML.Тело.ТекстовоеСодержимое, Новый СжатиеДанных());
	ТекущийОбъект.СтатусИзвлеченияТекста = Перечисления.СтатусыИзвлеченияТекстаФайлов.Извлечен;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСкриптыИзРезюмеHTML()

	Пока СтрНайти(ТекстРезюмеHTML, "<script>") <> 0 Цикл 
		Если Не СтрНайти(ТекстРезюмеHTML, "</script>") <> 0 Тогда
			Прервать;
		КонецЕсли;
		НачалоСкрипта = СтрНайти(ТекстРезюмеHTML, "<script>");	
		КонецСкрипта = СтрНайти(ТекстРезюмеHTML, "</script>") + СтрДлина("</script>");
		ТекстРезюмеHTML = Лев(ТекстРезюмеHTML, НачалоСкрипта - 1) 
			+ Прав(ТекстРезюмеHTML, СтрДлина(ТекстРезюмеHTML) - КонецСкрипта + 1);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПодготовитьИзображенияРезюмеHTMLКЗаписи()

	Если ИдентификаторыИзображенийРезюме.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Для Каждого ЭлементСписка Из ИдентификаторыИзображенийРезюме Цикл
		ОписаниеИсточника = ЭлементСписка.Значение;
		Если Найти(ТекстРезюмеHTML, ОписаниеИсточника.НавигационнаяСсылка) <> 0 Тогда
			// При записи возвращаем GUID - а не навигационную ссылку на временное хранилище.
			ТекстРезюмеHTML = СтрЗаменить(
				ТекстРезюмеHTML, 
				ОписаниеИсточника.НавигационнаяСсылка,
				"cid:" + ОписаниеИсточника.GUIDИзображения);
		Иначе
			// Картинку из HTML удалили - пометим файл на удаление.
			УдаляемыеИзображенияРезюме.Добавить(ОписаниеИсточника.GUIDИзображения);
		КонецЕсли;
	КонецЦикла;

	ИдентификаторыИзображенийРезюме.Очистить();
		
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьИзображенияРезюмеHTMLВФайлы(Кандидат, ПомещенныеФайлы, СсылкиФайлов)

	Для Каждого ПомещенныйФайл Из ПомещенныеФайлы Цикл
		ИмяФайлаИнфо = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПомещенныйФайл.ПолноеИмя);
		ПараметрыФайла = Новый Структура(
			"Автор, 
			|ВладелецФайлов,
			|ИмяБезРасширения,
			|РасширениеБезТочки,
			|ВремяИзмененияУниверсальное,
			|Служебный");
		ПараметрыФайла.ВладелецФайлов = Кандидат;
		ПараметрыФайла.ИмяБезРасширения = ИмяФайлаИнфо.ИмяБезРасширения; 
		ПараметрыФайла.РасширениеБезТочки = ИмяФайлаИнфо.Расширение; 
		ПараметрыФайла.Служебный = Истина; 
		РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, ПомещенныйФайл.Хранение, , , СсылкиФайлов[ИмяФайлаИнфо.ПолноеИмя]); 
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьФайлыУдаленныхИзображений(ТекущийОбъект)
	
	Для Каждого УдаляемоеИзображение Из УдаляемыеИзображенияРезюме Цикл
		ИдентификаторФайла = Новый УникальныйИдентификатор(УдаляемоеИзображение.Значение);
		ФайлСсылка = Справочники.КандидатыПрисоединенныеФайлы.ПолучитьСсылку(ИдентификаторФайла);
		Если Не ОбщегоНазначения.СсылкаСуществует(ФайлСсылка) Тогда
			Продолжить;
		КонецЕсли;
		Попытка
			ЗаблокироватьДанныеДляРедактирования(ФайлСсылка);
			ФайлОбъект = ФайлСсылка.ПолучитьОбъект();
			ФайлОбъект.УстановитьПометкуУдаления(Истина);
			РазблокироватьДанныеДляРедактирования(ФайлСсылка);
		Исключение
			ВызватьИсключение;
		КонецПопытки;	
	КонецЦикла;
	
	УдаляемыеИзображенияРезюме.Очистить();
	
КонецПроцедуры

#Область КомандыHTML

&НаКлиенте
Процедура ВыполнитьHTMLКоманду(ИмяКоманды)

	Если ТолькоПросмотр Или Элементы.РезюмеHTML.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
		
	HTMLДокумент = Элементы.РезюмеHTML.Документ; 
	HTMLДокумент.execCommand(ИмяКоманды);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьИзменениеШрифта()

	ДиалогВыбораШрифта = Новый ДиалогВыбораШрифта;
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьИзменениеШрифта", ЭтотОбъект);
	ДиалогВыбораШрифта.Показать(ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьИзменениеШрифта(Шрифт, Параметры) Экспорт

	Если Шрифт = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПустаяСтрока(Шрифт.Имя) Тогда
		HTMLДокумент = Элементы.РезюмеHTML.Документ; 
		HTMLДокумент.execCommand("fontName", Ложь, Шрифт.Имя);
		Модифицированность = Истина;
	КонецЕсли;
	
	РазмерШрифта = 2;
	
	Если Шрифт.Размер <> -1 Тогда
		Если Шрифт.Размер < 8 Тогда
			РазмерШрифта = 1;
		ИначеЕсли Шрифт.Размер <= 10 Тогда
			РазмерШрифта = 2;	
		ИначеЕсли Шрифт.Размер <= 12 Тогда
			РазмерШрифта = 3;	
		ИначеЕсли Шрифт.Размер <= 14 Тогда
			РазмерШрифта = 4;	
		ИначеЕсли Шрифт.Размер <= 16 Тогда
			РазмерШрифта = 5;	
		ИначеЕсли Шрифт.Размер <= 18 Тогда
			РазмерШрифта = 6;	
		Иначе
			РазмерШрифта = 7;	
		КонецЕсли;	
	КонецЕсли;
	
	HTMLДокумент = Элементы.РезюмеHTML.Документ; 
	HTMLДокумент.execCommand("fontSize", Ложь, РазмерШрифта);
	Модифицированность = Истина;
	
	Если Шрифт.Зачеркивание = Истина Тогда
		HTMLДокумент = Элементы.РезюмеHTML.Документ; 
		ВыполнитьHTMLКоманду("strikeThrough");
		Модифицированность = Истина;
	КонецЕсли;	
	
	Если Шрифт.Жирный = Истина Тогда
		HTMLДокумент = Элементы.РезюмеHTML.Документ; 
		ВыполнитьHTMLКоманду("Bold");
		Модифицированность = Истина;
	КонецЕсли;	
	
	Если Шрифт.Наклонный = Истина Тогда
		HTMLДокумент = Элементы.РезюмеHTML.Документ; 
		ВыполнитьHTMLКоманду("italic");
		Модифицированность = Истина;
	КонецЕсли;	
	
	Если Шрифт.Подчеркивание = Истина Тогда
		HTMLДокумент = Элементы.РезюмеHTML.Документ; 
		ВыполнитьHTMLКоманду("underline");
		Модифицированность = Истина;
	КонецЕсли;	
	
КонецПроцедуры
	
&НаКлиенте
Процедура НачатьВставкуИзображенияИзБуфера()
	
	НачатьИнициализациюКомпонентыПолученияИзображенияИзБуфера();
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьИнициализациюКомпонентыПолученияИзображенияИзБуфера()

	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьИнициализациюКомпонентыПолученияИзображенияИзБуфера", ЭтотОбъект);
	РазборРезюмеКлиент.ПровестиИнициализациюВнешнихКомпонент(ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьИнициализациюКомпонентыПолученияИзображенияИзБуфера(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Результат.Подключено Тогда
		Возврат;
	КонецЕсли;
	
	ВставитьКартинкуИзБуфера(Результат.ПодключаемыйМодуль);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьКартинкуИзБуфера(Компонента)

	ПутьКФайлу = Компонента.ПолучитьКартинкуИзБуфера();

	Если Не ПустаяСтрока(ПутьКФайлу) Тогда
		HTMLДокумент = Элементы.РезюмеHTML.Документ; 
		HTMLДокумент.execCommand("InsertImage", Ложь, "file://" + ПутьКФайлу);
		Модифицированность = Истина;
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Буфер обмена не содержит картинки'"));
	КонецЕсли;
	
	ТекущийЭлемент = Элементы.РезюмеHTML;

КонецПроцедуры

&НаКлиенте
Процедура НачатьВставкуИзображения()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьВставкуИзображения", ЭтотОбъект);
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ПараметрыЗагрузки.Диалог.МножественныйВыбор = Ложь;
	ПараметрыЗагрузки.Диалог.Заголовок = НСтр("ru = 'Выбор изображения'");
	ПараметрыЗагрузки.Диалог.Фильтр = ФильтрФайловИзображений();
	
	ФайловаяСистемаКлиент.ЗагрузитьФайл(ОписаниеОповещения, ПараметрыЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВставкуИзображения(ОписаниеФайла, ДополнительныеПараметры) Экспорт
	
	Если ОписаниеФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	HTMLДокумент = Элементы.РезюмеHTML.Документ; 
	HTMLДокумент.execCommand("InsertImage", Ложь, "file://" + ОписаниеФайла.Имя);
	Модифицированность = Истина;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ФильтрФайловИзображений()

	Возврат НСтр("ru = 'Все картинки (*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf)|*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf'")
		+ НСтр("ru = '|Все файлы(*.*)|*.*'")
		+ НСтр("ru = '|Формат bmp(*.bmp*;*.dib;*.rle)|*.bmp;*.dib;*.rle'")
		+ НСтр("ru = '|Формат GIF(*.gif*)|*.gif'")
		+ НСтр("ru = '|Формат JPEG(*.jpeg;*.jpg)|*.jpeg;*.jpg'")
		+ НСтр("ru = '|Формат PNG(*.png*)|*.png'")
		+ НСтр("ru = '|Формат TIFF(*.tif)|*.tif'")
		+ НСтр("ru = '|Формат icon(*.ico)|*.ico'")
		+ НСтр("ru = '|Формат метафайл(*.wmf;*.emf)|*.wmf;*.emf'");

КонецФункции

&НаКлиенте
Процедура НачатьВставкуСсылки()
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьВставкуСсылки", ЭтотОбъект);
	ПоказатьВводСтроки(ОписаниеОповещения, , НСтр("ru = 'Ссылка'"), , Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВставкуСсылки(ТекстСсылки, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ТекстСсылки) Тогда
		Возврат;
	КонецЕсли;

	HTMLДокумент = Элементы.РезюмеHTML.Документ; 
	HTMLДокумент.execCommand("createLink", Ложь, ТекстСсылки);
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

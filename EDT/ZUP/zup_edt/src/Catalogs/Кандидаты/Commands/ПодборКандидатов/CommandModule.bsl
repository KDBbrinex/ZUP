
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	НачатьПодборКандидатов(ПараметрКоманды, ПараметрыВыполненияКоманды.Источник);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьПодборКандидатов(Вакансия, ФормаВладелец)
	
	ПараметрыФормы = Новый Структура(
		"РежимВыбора,
		|ИсключаяКандидатовПоВакансии");
	ПараметрыФормы.РежимВыбора = Истина;
	ПараметрыФормы.ИсключаяКандидатовПоВакансии = Вакансия;
	
	ОповещениеЗакрытия = Новый ОписаниеОповещения(
		"ЗавершитьПодборКандидатов", ЭтотОбъект, Новый Структура("Вакансия", Вакансия));
	
	ОткрытьФорму(
		"Справочник.Кандидаты.ФормаВыбора", 
		ПараметрыФормы, 
		ФормаВладелец,
		ФормаВладелец, , ,
		ОповещениеЗакрытия);

КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПодборКандидатов(Кандидат, ДополнительныеПараметры) Экспорт
	
	Если Кандидат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ДанныеКандидата = ДанныеКандидата(Кандидат);
	ВыбраннаяВакансия = ДополнительныеПараметры.Вакансия;

	Если Не ЗначениеЗаполнено(ДанныеКандидата.Вакансия) Тогда
		СменитьВакансиюКандидату(Кандидат, ВыбраннаяВакансия);
		Возврат;
	КонецЕсли;

	ПоказатьВопросСозданияНовогоКандидата(Кандидат, ДанныеКандидата, ВыбраннаяВакансия);

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросСозданияНовогоКандидата(Кандидат, ДанныеКандидата, ВыбраннаяВакансия)

	ТекстСообщения = СтрШаблон(
		НСтр("ru = '%1 рассматривается на вакансию «%2».
			 |
			 |Зарегистрировать на вакансию «%3» как нового кандидата или сменить вакансию существующего кандидата?'"),
		Кандидат,
		ДанныеКандидата.Вакансия,
		ВыбраннаяВакансия);
		
	КнопкиОтвета = Новый СписокЗначений;
	КнопкиОтвета.Добавить(Истина, НСтр("ru = 'Зарегистрировать нового'"));	
	КнопкиОтвета.Добавить(Ложь, НСтр("ru = 'Взять существующего'"));	
	КнопкиОтвета.Добавить(Неопределено, НСтр("ru = 'Отмена'"));	
	
	ПараметрыОповещения = Новый Структура(
		"Кандидат,
		|ДанныеКандидата,
		|ВыбраннаяВакансия");
	ПараметрыОповещения.Кандидат = Кандидат;
	ПараметрыОповещения.ДанныеКандидата = ДанныеКандидата;
	ПараметрыОповещения.ВыбраннаяВакансия = ВыбраннаяВакансия;
		
	ОбработчикОтвета = Новый ОписаниеОповещения(
		"ПодборКандидатаПослеОтветаОСозданииНовогоКандидата", ЭтотОбъект, ПараметрыОповещения);
	
	ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
	ПараметрыВопроса.Заголовок = НСтр("ru = 'Кандидат рассматривается на другую вакансию'");
	ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
	ПараметрыВопроса.КнопкаПоУмолчанию = Истина;
	
	СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(ОбработчикОтвета, ТекстСообщения, КнопкиОтвета, ПараметрыВопроса);

КонецПроцедуры

&НаКлиенте
Процедура ПодборКандидатаПослеОтветаОСозданииНовогоКандидата(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Или Ответ.Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Кандидат = ДополнительныеПараметры.Кандидат;
	ВыбраннаяВакансия = ДополнительныеПараметры.ВыбраннаяВакансия;
	
	ПараметрыОповещения = Новый Структура("Кандидаты, Вакансии");
	ПараметрыОповещения.Вакансии = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыбраннаяВакансия);

	Если Ответ.Значение = Ложь Тогда
		СменитьВакансиюКандидату(Кандидат, ВыбраннаяВакансия);
		ПараметрыОповещения.Кандидаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Кандидат);
		ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(Кандидат, ПараметрыОповещения);
		Возврат;
	КонецЕсли;
	
	НовыйКандидат = СоздатьКандидатаПоВакансии(ВыбраннаяВакансия, ДополнительныеПараметры.ДанныеКандидата);
	ПараметрыОповещения.Кандидаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НовыйКандидат);
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(НовыйКандидат, ПараметрыОповещения);

КонецПроцедуры

&НаСервере
Функция ДанныеКандидата(Кандидат)
	Возврат Справочники.Кандидаты.ДанныеКандидата(Кандидат); 
КонецФункции

&НаКлиенте
Процедура СменитьВакансиюКандидату(Кандидат, Вакансия)
	УстановитьВакансиюКандидату(Кандидат, Вакансия);
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(Кандидат);
КонецПроцедуры

&НаСервере
Функция СоздатьКандидатаПоВакансии(Вакансия, ДанныеКандидата)
	
	ЗначенияЗаполнения = Новый Структура(
		"Вакансия,
		|ФизическоеЛицо");
	ЗначенияЗаполнения.Вакансия = Вакансия;
	ЗначенияЗаполнения.ФизическоеЛицо = ДанныеКандидата.ФизическоеЛицо;

	Возврат Справочники.Кандидаты.СоздатьКандидата(ЗначенияЗаполнения).Ссылка;
	
КонецФункции

&НаСервере
Процедура УстановитьВакансиюКандидату(Кандидат, Вакансия)
	Справочники.Кандидаты.УстановитьВакансиюКандидату(Кандидат, Вакансия);
КонецПроцедуры

#КонецОбласти
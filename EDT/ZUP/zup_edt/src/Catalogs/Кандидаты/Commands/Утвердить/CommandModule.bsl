#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыОповещения = Новый Структура("Источник, Кандидаты");
	ПараметрыОповещения.Источник = ПараметрыВыполненияКоманды.Источник;
	ПараметрыОповещения.Кандидаты = ПараметрКоманды;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("УтвердитьКандидатовПослеРешения", ЭтотОбъект, ПараметрыОповещения);
	
	Кандидаты = ПараметрКоманды;
	Решение = ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Согласовано");
	
	ПодборПерсоналаКлиент.НачатьПринятиеРешенияПоКандидату(Кандидаты, Решение, ОбработчикОповещения, ПараметрыВыполненияКоманды.Источник);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УтвердитьКандидатовПослеРешения(РезультатВыбора, ПараметрыОповещения) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВакансииКандидатов = Новый Массив;
	УтвердитьКандидатовПослеРешенияНаСервере(ПараметрыОповещения.Кандидаты, РезультатВыбора, ВакансииКандидатов);
	
	// Отражаем изменения в открытых формах.
	ПараметрыОповещения.Вставить("Вакансии", ВакансииКандидатов);
	Оповестить("РешениеПоКандидату", ПараметрыОповещения, ПараметрыОповещения.Источник);
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъектов(ПараметрыОповещения.Кандидаты);
	
КонецПроцедуры

&НаСервере
Процедура УтвердитьКандидатовПослеРешенияНаСервере(Кандидаты, РезультатВыбора, ВакансииКандидатов)
	ПодборПерсонала.УстановитьРешениеПоКандидатам(Кандидаты, РезультатВыбора);
	ПодборПерсонала.ЗаполнитьВакансииКандидатов(Кандидаты, ВакансииКандидатов);
КонецПроцедуры

#КонецОбласти
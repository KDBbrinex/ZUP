
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПубликацииВакансий = Неопределено;
	
	ПриостановитьНабор(ПараметрКоманды, НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса()), ПубликацииВакансий);
	
	ОповеститьОбИзменении(Тип("СправочникСсылка.Вакансии"));
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.СостоянияВакансий"));
	
	НачатьСнятиеПубликаций(ПубликацииВакансий);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриостановитьНабор(Вакансии, ДатаПриостановки, ПубликацииВакансий)
	
	Справочники.Вакансии.ПриостановитьНабор(Вакансии, ДатаПриостановки);
	
	ПубликацииВакансий = ИнтеграцияРекрутинговыхСайтов.СайтыСПубликациямиВакансий(Вакансии, , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьСнятиеПубликаций(ПубликацииВакансий)
	
	Если ПубликацииВакансий = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПубликацииВакансий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Снять активные публикации на сайтах?'");
	
	ДополнительныеПараметры = Новый Структура("ПубликацииВакансий");
	ДополнительныеПараметры.ПубликацииВакансий = ПубликацииВакансий;
	
	Оповещение = Новый ОписаниеОповещения("ЗавершитьСнятиеПубликаций", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьСнятиеПубликаций(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияРекрутинговыхСайтовКлиент.СнятьПубликациюВакансий(ДополнительныеПараметры.ПубликацииВакансий);
	
КонецПроцедуры

#КонецОбласти

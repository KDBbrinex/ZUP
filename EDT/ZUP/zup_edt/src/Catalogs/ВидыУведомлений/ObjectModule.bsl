#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления И Не ДополнительныеСвойства.ПометкаУдаленияПередЗаписью Тогда
		МассивРассылокУведомлений = МассивРассылокУведомленийПоВиду(Ссылка);
		
		Если МассивРассылокУведомлений.Количество() > 0 Тогда
			НачатьТранзакцию();
			Попытка
				Для Каждого РассылкаУведомлений Из МассивРассылокУведомлений Цикл
					ОбъектРассылки = РассылкаУведомлений.ПолучитьОбъект();
					ОбъектРассылки.ПометкаУдаления = Истина;
					ОбъектРассылки.Подготовлена = Ложь;
					ОбъектРассылки.Записать();
				КонецЦикла;
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				Отказ = Истина;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ПометкаУдаленияПередЗаписью", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция МассивРассылокУведомленийПоВиду(ВидУведомления)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
		|	РассылкиУведомлений.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.РассылкиУведомлений КАК РассылкиУведомлений
		|ГДЕ
		|	РассылкиУведомлений.ВидУведомления = &ВидУведомления";
		
	Запрос.УстановитьПараметр("ВидУведомления", ВидУведомления);
	
	Массив = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат Массив;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
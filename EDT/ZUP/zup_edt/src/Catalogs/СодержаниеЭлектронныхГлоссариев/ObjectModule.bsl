#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат; 
	КонецЕсли;	
	
	Если ДополнительныеСвойства.Свойство("ЭтоКопирование") И ДополнительныеСвойства.ЭтоКопирование Тогда
		Возврат;
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(ТерминГлоссария) Тогда
		Текст = НСтр("ru = 'Не выбран термин глоссария'");
		ОбщегоНазначения.СообщитьПользователю(Текст,,,,Отказ);
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	СодержаниеЭлектронныхГлоссариев.Ссылка
	|ИЗ
	|	Справочник.СодержаниеЭлектронныхГлоссариев КАК СодержаниеЭлектронныхГлоссариев
	|ГДЕ
	|	СодержаниеЭлектронныхГлоссариев.ТерминГлоссария = &Термин И ";
	
	Если НЕ ЭтоНовый() Тогда
		ТекстЗапроса = ТекстЗапроса + "СодержаниеЭлектронныхГлоссариев.Ссылка <> &ЭтотЭлемент И ";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "СодержаниеЭлектронныхГлоссариев.Владелец = &Владелец";
	
	Запрос = Новый Запрос(ТекстЗапроса);

	Если НЕ ЭтоНовый() Тогда
		Запрос.УстановитьПараметр("ЭтотЭлемент", Ссылка); 
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Термин", ТерминГлоссария); 
	Запрос.УстановитьПараметр("Владелец", Владелец); 
	
	КоличествоТерминов = Запрос.Выполнить().Выбрать().Количество();
	
	Если КоличествоТерминов > 0 Тогда
		Текст = НСтр("ru = 'Добавляемый термин уже содержится в глоссарии'");
		ОбщегоНазначения.СообщитьПользователю(Текст,,,,Отказ);
		Возврат;
	КонецЕсли;
	
	РазработкаЭлектронныхКурсовСлужебный.ПроверитьВозможностьЗаписиЭлементаПередЗаписью(ЭтотОбъект, Отказ);
	РазработкаЭлектронныхКурсовСлужебный.УстановитьДатуИзмененияПередЗаписью(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти


#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
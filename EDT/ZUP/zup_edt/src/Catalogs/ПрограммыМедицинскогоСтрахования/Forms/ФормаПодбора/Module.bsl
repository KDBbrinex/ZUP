#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("МассивСтруктурПрограмм") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Страхователь = Параметры.Страхователь;
	Заголовок = Строка(Параметры.Страхователь);
	ДатаНачалаСтрахования = Параметры.ДатаНачалаСтрахования;
	ДатаОкончанияСтрахования = Параметры.ДатаОкончанияСтрахования;
	ДатаПрикрепления = Параметры.ДатаПрикрепления;
	ДатаОткрепления = Параметры.ДатаОткрепления;
	ДатаРождения = Параметры.ДатаРождения;
	СтраховаяКомпания = Параметры.СтраховаяКомпания;
	
	Для Каждого СтруктураПрограммы Из Параметры.МассивСтруктурПрограмм Цикл
		НоваяСтрока = ПрограммыСтрахования.Добавить();
		НоваяСтрока.ПрограммаСтрахования = СтруктураПрограммы.ПрограммаСтрахования;
		НоваяСтрока.СтраховаяПремия = СтруктураПрограммы.СтраховаяПремия;
		НоваяСтрока.ДатаНачала = СтруктураПрограммы.ДатаНачала;
		НоваяСтрока.ДатаОкончания = СтруктураПрограммы.ДатаОкончания;
	КонецЦикла;
	
	Для Каждого СтрокаШкалы Из Параметры.ШкалаВозрастов Цикл
		НоваяСтрока = ШкалаВозрастов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаШкалы);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСотрудники

&НаКлиенте
Процедура ПрограммыСтрахованияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ПрограммыСтрахования.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		ТекущиеДанные.ДатаНачала = ДатаПрикрепления;
		ТекущиеДанные.ДатаОкончания = ДатаОткрепления;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрограммыСтрахованияПрограммаСтрахованияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПрограммыСтрахования.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.СтраховаяПремия = СтраховаяПремия(ТекущиеДанные.ПрограммаСтрахования, ТекущиеДанные.ДатаНачала, ТекущиеДанные.ДатаОкончания);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	МассивСтруктурПрограмм = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из ПрограммыСтрахования Цикл
		СтруктураПрограммы = Новый Структура;
		СтруктураПрограммы.Вставить("ПрограммаСтрахования", СтрокаТаблицы.ПрограммаСтрахования);
		СтруктураПрограммы.Вставить("СтраховаяПремия", СтрокаТаблицы.СтраховаяПремия);
		СтруктураПрограммы.Вставить("ДатаНачала", СтрокаТаблицы.ДатаНачала);
		СтруктураПрограммы.Вставить("ДатаОкончания", СтрокаТаблицы.ДатаОкончания);
		МассивСтруктурПрограмм.Добавить(СтруктураПрограммы);
	КонецЦикла;
	
	Закрыть(МассивСтруктурПрограмм);
	
КонецПроцедуры

&НаКлиенте
Процедура Пересчитать(Команда)
	
	Для Каждого СтрокаТаблицы Из ПрограммыСтрахования Цикл
		СтрокаТаблицы.СтраховаяПремия = СтраховаяПремия(СтрокаТаблицы.ПрограммаСтрахования, СтрокаТаблицы.ДатаНачала, СтрокаТаблицы.ДатаОкончания);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СтраховаяПремия(ПрограммаСтрахования, ДатаНачала, ДатаОкончания)
	
	ПараметрыРасчета = МедицинскоеСтрахование.ПараметрыРасчетаСтраховойПремии();
	ПараметрыРасчета.ДатаНачала = ДатаНачала;
	ПараметрыРасчета.ДатаОкончания = ДатаОкончания;
	ПараметрыРасчета.ДатаНачалаСтрахования = ДатаНачалаСтрахования;
	ПараметрыРасчета.ДатаОкончанияСтрахования = ДатаОкончанияСтрахования;
	ПараметрыРасчета.ДатаРождения = ДатаРождения;
	ПараметрыРасчета.ШкалаВозрастов = ШкалаВозрастов;
	Если ТипЗнч(Страхователь) = Тип("СправочникСсылка.Сотрудники") Тогда
		ПараметрыРасчета.СтраховаяПремияСтрахователя = МедицинскоеСтрахование.СтраховаяПремияСотрудника(ПрограммаСтрахования);
	Иначе
		ПараметрыРасчета.СтраховаяПремияСтрахователя = МедицинскоеСтрахование.СтраховаяПремияРодственника(ПрограммаСтрахования);
	КонецЕсли;
	
	Возврат МедицинскоеСтрахование.СтраховаяПремия(ПараметрыРасчета);
	
КонецФункции

#КонецОбласти

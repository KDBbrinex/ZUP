#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("МассивСтруктурРасширений") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Страхователь = Параметры.Страхователь;
	Заголовок = Строка(Параметры.Страхователь);
	ДатаНачалаСтрахования = Параметры.ДатаНачалаСтрахования;
	ДатаОкончанияСтрахования = Параметры.ДатаОкончанияСтрахования;
	ДатаПрикрепления = Параметры.ДатаПрикрепления;
	ДатаОткрепления = Параметры.ДатаОткрепления;
	ДатаРождения = Параметры.ДатаРождения;
	СтраховаяКомпания = Параметры.СтраховаяКомпания;
	
	Для Каждого СтруктураРасширения Из Параметры.МассивСтруктурРасширений Цикл
		НоваяСтрока = РасширенияСтрахования.Добавить();
		НоваяСтрока.РасширениеСтрахования = СтруктураРасширения.РасширениеСтрахования;
		НоваяСтрока.СтраховаяПремия = СтруктураРасширения.СтраховаяПремия;
		НоваяСтрока.ДатаНачала = СтруктураРасширения.ДатаНачала;
		НоваяСтрока.ДатаОкончания = СтруктураРасширения.ДатаОкончания;
	КонецЦикла;
	
	Для Каждого СтрокаШкалы Из Параметры.ШкалаВозрастов Цикл
		НоваяСтрока = ШкалаВозрастов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаШкалы);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСотрудники

&НаКлиенте
Процедура РасширенияСтрахованияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.РасширенияСтрахования.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		ТекущиеДанные.ДатаНачала = ДатаПрикрепления;
		ТекущиеДанные.ДатаОкончания = ДатаОткрепления;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасширенияСтрахованияРасширениеСтрахованияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасширенияСтрахования.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.СтраховаяПремия = СтраховаяПремия(ТекущиеДанные.РасширениеСтрахования, ТекущиеДанные.ДатаНачала, ТекущиеДанные.ДатаОкончания);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	МассивСтруктурРасширений = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из РасширенияСтрахования Цикл
		СтруктураРасширения = Новый Структура;
		СтруктураРасширения.Вставить("РасширениеСтрахования", СтрокаТаблицы.РасширениеСтрахования);
		СтруктураРасширения.Вставить("СтраховаяПремия", СтрокаТаблицы.СтраховаяПремия);
		СтруктураРасширения.Вставить("ДатаНачала", СтрокаТаблицы.ДатаНачала);
		СтруктураРасширения.Вставить("ДатаОкончания", СтрокаТаблицы.ДатаОкончания);
		МассивСтруктурРасширений.Добавить(СтруктураРасширения);
	КонецЦикла;
	
	Закрыть(МассивСтруктурРасширений);
	
КонецПроцедуры

&НаКлиенте
Процедура Пересчитать(Команда)
	
	Для Каждого СтрокаТаблицы Из РасширенияСтрахования Цикл
		СтрокаТаблицы.СтраховаяПремия = СтраховаяПремия(СтрокаТаблицы.РасширениеСтрахования, СтрокаТаблицы.ДатаНачала, СтрокаТаблицы.ДатаОкончания);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СтраховаяПремия(РасширениеСтрахования, ДатаНачала, ДатаОкончания)
	
	ПараметрыРасчета = МедицинскоеСтрахование.ПараметрыРасчетаСтраховойПремии();
	ПараметрыРасчета.ДатаНачала = ДатаНачала;
	ПараметрыРасчета.ДатаОкончания = ДатаОкончания;
	ПараметрыРасчета.ДатаНачалаСтрахования = ДатаНачалаСтрахования;
	ПараметрыРасчета.ДатаОкончанияСтрахования = ДатаОкончанияСтрахования;
	ПараметрыРасчета.ДатаРождения = ДатаРождения;
	ПараметрыРасчета.ШкалаВозрастов = ШкалаВозрастов;
	Если ТипЗнч(Страхователь) = Тип("СправочникСсылка.Сотрудники") Тогда
		ПараметрыРасчета.СтраховаяПремияСтрахователя = МедицинскоеСтрахование.СтраховаяПремияСотрудника(РасширениеСтрахования);
	Иначе
		ПараметрыРасчета.СтраховаяПремияСтрахователя = МедицинскоеСтрахование.СтраховаяПремияРодственника(РасширениеСтрахования);
	КонецЕсли;
	
	Возврат МедицинскоеСтрахование.СтраховаяПремия(ПараметрыРасчета);
	
КонецФункции

#КонецОбласти

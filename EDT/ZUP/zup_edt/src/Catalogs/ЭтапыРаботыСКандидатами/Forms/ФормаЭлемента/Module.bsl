
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Параметры.Ключ.Пустая() Тогда
		ПриПолученииДанныхНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПриПолученииДанныхНаСервере(ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверятьКандидатовНаЗадержкуПриИзменении(Элемент)
	
	УстановитьСвойстваСпособаПроверкиКандидатовНаЗадержку(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверятьКандидатовНаЗадержкуКакВНастройкахПриИзменении(Элемент)
	
	ОбновитьФормуПоСпособуПроверкиКандидатовНаЗадержку(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверятьКандидатовНаЗадержкуКакВКарточкеПриИзменении(Элемент)
	
	ОбновитьФормуПоСпособуПроверкиКандидатовНаЗадержку(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонятьЗадержавшихсяКандидатовПриИзменении(Элемент)
	
	УстановитьСвойстваСпособаОтклоненияЗадержавшихсяКандидатов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонятьЗадержавшихсяКандидатовКакВНастройкахПриИзменении(Элемент)
	
	ОбновитьФормуПоСпособуОтклоненияЗадержавшихсяКандидатов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонятьЗадержавшихсяКандидатовКакВКарточкеПриИзменении(Элемент)
	
	ОбновитьФормуПоСпособуОтклоненияЗадержавшихсяКандидатов(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект = Неопределено)
	
	Если ТекущийОбъект = Неопределено Тогда
		ТекущийОбъект = Объект;
	КонецЕсли;
	
	ПроверятьКандидатовЗадержавшихсяНаОдномЭтапе = ПолучитьФункциональнуюОпцию("ПроверятьКандидатовЗадержавшихсяНаОдномЭтапе");
	ОтклонятьКандидатовЗадержавшихсяНаОдномЭтапе = ПолучитьФункциональнуюОпцию("ОтклонятьКандидатовЗадержавшихсяНаОдномЭтапе");
	Если Не ПроверятьКандидатовЗадержавшихсяНаОдномЭтапе И Не ОтклонятьКандидатовЗадержавшихсяНаОдномЭтапе Тогда
		Элементы.ГруппаЗадержкаНаЭтапеКандидатов.Видимость = Ложь;
	Иначе
		Элементы.ГруппаПроверятьКандидатовНаЗадержку.Видимость = ПроверятьКандидатовЗадержавшихсяНаОдномЭтапе;
		Элементы.ГруппаОтклонятьЗадержавшихсяКандидатов.Видимость = ОтклонятьКандидатовЗадержавшихсяНаОдномЭтапе;
		НастройкиПодбораПерсонала = РегистрыСведений.НастройкиПодбораПерсонала.ЗначенияНастроек();
	КонецЕсли;
	
	Если ПроверятьКандидатовЗадержавшихсяНаОдномЭтапе Тогда
		ДнейДоСообщения = НастройкиПодбораПерсонала.ДнейДоСообщения;
		УстановитьСвойстваСпособаПроверкиКандидатовНаЗадержку(ЭтотОбъект, ТекущийОбъект);
		ОбновитьФормуПоСпособуПроверкиКандидатовНаЗадержку(ЭтотОбъект, ТекущийОбъект)
	КонецЕсли;
	Если ОтклонятьКандидатовЗадержавшихсяНаОдномЭтапе Тогда
		ДнейДоОтклонения = НастройкиПодбораПерсонала.ДнейДоОтклонения;
		УстановитьСвойстваСпособаОтклоненияЗадержавшихсяКандидатов(ЭтотОбъект, ТекущийОбъект);
		ОбновитьФормуПоСпособуОтклоненияЗадержавшихсяКандидатов(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСвойстваСпособаПроверкиКандидатовНаЗадержку(Форма, Объект = Неопределено)
	
	Если Объект = Неопределено Тогда
		Объект = Форма.Объект;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ГруппаСпособПроверкиКандидатовНаЗадержку",
		"Доступность",
		Объект.ПроверятьКандидатовНаЗадержку = Истина);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьФормуПоСпособуПроверкиКандидатовНаЗадержку(Форма, Объект = Неопределено)
	
	Если Объект = Неопределено Тогда
		Объект = Форма.Объект;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ДнейДоСообщенияНаЭтапе",
		"ТолькоПросмотр",
		Объект.СпособПроверкиКандидатовНаЗадержку <>
			ПредопределенноеЗначение("Перечисление.СпособыОпределенияЗадержкиКандидатов.КакЗаданоВЭтапе"));
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСвойстваСпособаОтклоненияЗадержавшихсяКандидатов(Форма, Объект = Неопределено)
	
	Если Объект = Неопределено Тогда
		Объект = Форма.Объект;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ГруппаСпособОтклоненияЗадержавшихсяКандидатов",
		"Доступность",
		Объект.ОтклонятьЗадержавшихсяКандидатов = Истина);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьФормуПоСпособуОтклоненияЗадержавшихсяКандидатов(Форма, Объект = Неопределено)
	
	Если Объект = Неопределено Тогда
		Объект = Форма.Объект;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ДнейДоОтклоненияНаЭтапе",
		"ТолькоПросмотр",
		Объект.СпособОтклоненияЗадержавшихсяКандидатов <>
			ПредопределенноеЗначение("Перечисление.СпособыОпределенияЗадержкиКандидатов.КакЗаданоВЭтапе"));
		
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("ВремяЗамера");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = Формат(Данные.ВремяЗамера, "ДФ=HH:mm");
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет график значениями по умолчанию.
//
Процедура ЗаполнитьГрафикПоУмолчанию(ПараметрыОбновления = Неопределено) Экспорт
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ГрафикУчетаСамочувствияСотрудников.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ГрафикУчетаСамочувствияСотрудников КАК ГрафикУчетаСамочувствияСотрудников";
	
	РезультатЗапроса = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ВыполнитьЗапросПолученияОбновляемыхДанных(Запрос, ПараметрыОбновления);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбработчик(ПараметрыОбновления);
		Возврат;
	КонецЕсли;
	
	СоздатьТочкуЗамера(Дата(1,1,1,8,0,0));
	СоздатьТочкуЗамера(Дата(1,1,1,12,0,0));
	СоздатьТочкуЗамера(Дата(1,1,1,16,0,0));
	СоздатьТочкуЗамера(Дата(1,1,1,20,0,0));
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбработчик(ПараметрыОбновления);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьТочкуЗамера(ВремяЗамера)
	
	ТочкаЗамера = СоздатьЭлемент();
	ТочкаЗамера.ВремяЗамера = ВремяЗамера;
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ТочкаЗамера);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

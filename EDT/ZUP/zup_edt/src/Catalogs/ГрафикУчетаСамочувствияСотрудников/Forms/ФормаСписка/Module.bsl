#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформлениеСписка();
	УстановитьОтборПоказыватьНедействительные();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьОтборПоказыватьНедействительные();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьНедействительные(Команда)
	
	ПоказыватьНедействительные = Не ПоказыватьНедействительные;
	УстановитьОтборПоказыватьНедействительные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеСписка()
	
	ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементОформления.Использование = Истина;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование		= Истина;
	ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("Недействительная");
	ЭлементОтбора.ПравоеЗначение	= Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоказыватьНедействительные()
	
	Параметр = Список.Параметры.НайтиЗначениеПараметра(
		Новый ПараметрКомпоновкиДанных("ОтборПоказыватьНедействительные"));
	Параметр.Использование = Ложь;
	
	Элементы.ФормаПоказыватьНедействительные.Пометка = ПоказыватьНедействительные;
	
	Если Не ПоказыватьНедействительные Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ОтборПоказыватьНедействительные", Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Медицинское страхование"
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Функция ИспользуетсяМедицинскоеСтрахованиеСотрудников() Экспорт
	Возврат Константы.ИспользоватьМедицинскоеСтрахованиеСотрудников.Получить();
КонецФункции

Процедура УстановитьИспользованиеМедицинскогоСтрахования(Использовать) Экспорт
	Константы.ИспользоватьМедицинскоеСтрахованиеСотрудников.Установить(Использовать);
КонецПроцедуры

Процедура ДополнитьФормуЭлементаСправочникаОрганизации(Форма, ИмяГруппыДляВставки = "ГруппаНастройкиМедицинскогоСтрахования") Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМедицинскоеСтрахованиеСотрудников") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Чтение", Метаданные.РегистрыСведений.УсловияМедицинскогоСтрахования) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКоманды = "НастройкиМедицинскогоСтрахования";
	Если Форма.Команды.Найти(ИмяКоманды) = Неопределено Тогда
		КомандаФормы = Форма.Команды.Добавить(ИмяКоманды);
		КомандаФормы.Заголовок = НСтр("ru = 'Медицинское страхование'");
		КомандаФормы.Действие = "Подключаемый_" + ИмяКоманды;
	КонецЕсли;
	
	ГруппаФормы = Форма.Элементы.Найти(ИмяГруппыДляВставки);
	Если Форма.Элементы.Найти(ИмяКоманды) = Неопределено Тогда
		Элемент = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ГруппаФормы);
		Элемент.Вид = ВидКнопкиФормы.Гиперссылка;
		Элемент.ИмяКоманды = ИмяКоманды;
	КонецЕсли;
	
КонецПроцедуры

Функция ПередаваемыеСведенияВСтраховуюКомпанию() Экспорт
	
	Сведения = Новый ТаблицаЗначений;
	Сведения.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	Сведения.Колонки.Добавить("Тип", Новый ОписаниеТипов("ОписаниеТипов"));
	Сведения.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Сведения.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("СправочникСсылка.ВидыКонтактнойИнформации"));
	Сведения.Колонки.Добавить("ТипКИ", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыКонтактнойИнформации"));
	Сведения.Колонки.Добавить("КадровыеСведения", Новый ОписаниеТипов("Булево"));
	Сведения.Колонки.Добавить("ДокументУдостоверяющийЛичность", Новый ОписаниеТипов("Булево"));
	Сведения.Колонки.Добавить("ОтображатьЭлемент", Новый ОписаниеТипов("Булево"));
	Сведения.Колонки.Добавить("Маска", Новый ОписаниеТипов("Строка"));
	
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "ДатаРождения";
	НоваяСтрока.Представление = НСтр("ru = 'Дата рождения'");
	НоваяСтрока.Тип = ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата);
	НоваяСтрока.КадровыеСведения = Истина;
	НоваяСтрока.ОтображатьЭлемент = Истина;
	
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "СтраховойНомерПФР";
	НоваяСтрока.Представление = НСтр("ru = 'СНИЛС'");
	НоваяСтрока.Тип = ОбщегоНазначения.ОписаниеТипаСтрока(14);
	НоваяСтрока.КадровыеСведения = Истина;
	НоваяСтрока.ОтображатьЭлемент = Истина;
	НоваяСтрока.Маска = "999-999-999 99";
	
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "ДокументПредставление";
	НоваяСтрока.Представление = НСтр("ru = 'Документ удостоверяющий личность'");
	НоваяСтрока.Тип = Новый ОписаниеТипов("Строка");
	НоваяСтрока.КадровыеСведения = Истина;
	НоваяСтрока.ДокументУдостоверяющийЛичность = Истина;
	НоваяСтрока.ОтображатьЭлемент = Истина;
	
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "ДокументВид";
	НоваяСтрока.Представление = НСтр("ru = 'Вид документа'");
	НоваяСтрока.Тип = Новый ОписаниеТипов("СправочникСсылка.ВидыДокументовФизическихЛиц");
	НоваяСтрока.КадровыеСведения = Истина;
	НоваяСтрока.ДокументУдостоверяющийЛичность = Истина;
	НоваяСтрока.ОтображатьЭлемент = Ложь;
	
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "ДокументСерия";
	НоваяСтрока.Представление = НСтр("ru = 'Серия документа'");
	НоваяСтрока.Тип = Новый ОписаниеТипов("Строка");
	НоваяСтрока.КадровыеСведения = Истина;
	НоваяСтрока.ДокументУдостоверяющийЛичность = Истина;
	НоваяСтрока.ОтображатьЭлемент = Ложь;
	
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "ДокументНомер";
	НоваяСтрока.Представление = НСтр("ru = 'Номер документа'");
	НоваяСтрока.Тип = Новый ОписаниеТипов("Строка");
	НоваяСтрока.КадровыеСведения = Истина;
	НоваяСтрока.ДокументУдостоверяющийЛичность = Истина;
	НоваяСтрока.ОтображатьЭлемент = Ложь;
	
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "ДокументДатаВыдачи";
	НоваяСтрока.Представление = НСтр("ru = 'Дата выдачи документа'");
	НоваяСтрока.Тип = Новый ОписаниеТипов("Строка");
	НоваяСтрока.КадровыеСведения = Истина;
	НоваяСтрока.ДокументУдостоверяющийЛичность = Истина;
	НоваяСтрока.ОтображатьЭлемент = Ложь;
	
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "ДокументКемВыдан";
	НоваяСтрока.Представление = НСтр("ru = 'Кем выдан документ'");
	НоваяСтрока.Тип = Новый ОписаниеТипов("Строка");
	НоваяСтрока.КадровыеСведения = Истина;
	НоваяСтрока.ДокументУдостоверяющийЛичность = Истина;
	НоваяСтрока.ОтображатьЭлемент = Ложь;
	
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "ДокументСрокДействия";
	НоваяСтрока.Представление = НСтр("ru = 'Срок действия документа'");
	НоваяСтрока.Тип = Новый ОписаниеТипов("Строка");
	НоваяСтрока.КадровыеСведения = Истина;
	НоваяСтрока.ДокументУдостоверяющийЛичность = Истина;
	НоваяСтрока.ОтображатьЭлемент = Ложь;
	
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "ДокументКодПодразделения";
	НоваяСтрока.Представление = НСтр("ru = 'Код подразделения документа'");
	НоваяСтрока.Тип = Новый ОписаниеТипов("Строка");
	НоваяСтрока.КадровыеСведения = Истина;
	НоваяСтрока.ДокументУдостоверяющийЛичность = Истина;
	НоваяСтрока.ОтображатьЭлемент = Ложь;
	
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "Страна";
	НоваяСтрока.Представление = НСтр("ru = 'Гражданство'");
	НоваяСтрока.Тип = Новый ОписаниеТипов("СправочникСсылка.СтраныМира");
	НоваяСтрока.КадровыеСведения = Истина;
	НоваяСтрока.ОтображатьЭлемент = Истина;
	
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "Пол";
	НоваяСтрока.Представление = НСтр("ru = 'Пол'");
	НоваяСтрока.Тип = Новый ОписаниеТипов("ПеречислениеСсылка.ПолФизическогоЛица");
	НоваяСтрока.КадровыеСведения = Истина;
	НоваяСтрока.ОтображатьЭлемент = Истина;
	
	ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.АдресДляИнформированияФизическиеЛица;
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "КонтактнаяИнформация1";
	НоваяСтрока.Представление = ВидКонтактнойИнформации.Наименование;
	НоваяСтрока.Тип = Новый ОписаниеТипов("СправочникСсылка.ВидыКонтактнойИнформации");
	НоваяСтрока.Ссылка = ВидКонтактнойИнформации;
	НоваяСтрока.ТипКИ = Перечисления.ТипыКонтактнойИнформации.Адрес;
	НоваяСтрока.ОтображатьЭлемент = Истина;
	
	ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.ТелефонДомашнийФизическиеЛица;
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "КонтактнаяИнформация2";
	НоваяСтрока.Представление = ВидКонтактнойИнформации.Наименование;
	НоваяСтрока.Тип = Новый ОписаниеТипов("СправочникСсылка.ВидыКонтактнойИнформации");
	НоваяСтрока.Ссылка = ВидКонтактнойИнформации;
	НоваяСтрока.ТипКИ = Перечисления.ТипыКонтактнойИнформации.Телефон;
	НоваяСтрока.ОтображатьЭлемент = Истина;
	
	ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица;
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "КонтактнаяИнформация3";
	НоваяСтрока.Представление = ВидКонтактнойИнформации.Наименование;
	НоваяСтрока.Тип = Новый ОписаниеТипов("СправочникСсылка.ВидыКонтактнойИнформации");
	НоваяСтрока.Ссылка = ВидКонтактнойИнформации;
	НоваяСтрока.ТипКИ = Перечисления.ТипыКонтактнойИнформации.Телефон;
	НоваяСтрока.ОтображатьЭлемент = Истина;
	
	ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.EMailФизическиеЛица;
	НоваяСтрока = Сведения.Добавить();
	НоваяСтрока.Имя = "КонтактнаяИнформация4";
	НоваяСтрока.Представление = ВидКонтактнойИнформации.Наименование;
	НоваяСтрока.Тип = Новый ОписаниеТипов("СправочникСсылка.ВидыКонтактнойИнформации");
	НоваяСтрока.Ссылка = ВидКонтактнойИнформации;
	НоваяСтрока.ТипКИ = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
	НоваяСтрока.ОтображатьЭлемент = Истина;
	
	Возврат Сведения;
	
КонецФункции

Функция ПараметрыОтправкиПисьма(Форма, ВыбранныеПараметры, Получатели, ТемаПисьма) Экспорт
	
	НастройкиПечатныхФорм = Новый ТаблицаЗначений;
	НастройкиПечатныхФорм.Колонки.Добавить("Представление", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	НастройкиПечатныхФорм.Колонки.Добавить("Количество", ОбщегоНазначения.ОписаниеТипаЧисло(3));
	НастройкиПечатныхФорм.Колонки.Добавить("Печатать", Новый ОписаниеТипов("Булево"));
	НастройкиПечатныхФорм.Колонки.Добавить("ИмяМакета", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	НастройкиПечатныхФорм.Колонки.Добавить("Название", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	НастройкиПечатныхФорм.Колонки.Добавить("ИмяФайлаПечатнойФормы", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	НастройкиПечатныхФорм.Колонки.Добавить("ПолноеИмяОбъекта", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	НастройкиПечатныхФорм.Колонки.Добавить("ТабличныйДокумент", Неопределено);
	НастройкиПечатныхФорм.Колонки.Добавить("СсылкаДокумента", Неопределено);
	НастройкиПечатныхФорм.Колонки.Добавить("ДополнительныеПараметры", Неопределено);
	
	КомандыПечати = УправлениеПечатью.КомандыПечатиФормы(Форма);
	Для каждого КомандаПечати Из КомандыПечати Цикл
		НоваяСтрока = НастройкиПечатныхФорм.Добавить();
		НоваяСтрока.ИмяМакета = КомандаПечати.Идентификатор;
		НоваяСтрока.ПолноеИмяОбъекта = КомандаПечати.МенеджерПечати;
		НоваяСтрока.СсылкаДокумента = Форма.Объект.Ссылка;
		НоваяСтрока.ИмяФайлаПечатнойФормы = "<Undefined xmlns="""" xmlns:xs=""http://www.w3.org/2001/XMLSchema"" xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance"" xsi:nil=""true""/>";
		НоваяСтрока.Количество = 1;
		НоваяСтрока.Название = КомандаПечати.Представление;
		НоваяСтрока.Печатать = Истина;
		НоваяСтрока.Представление = КомандаПечати.Представление;
		НоваяСтрока.ДополнительныеПараметры = КомандаПечати.ДополнительныеПараметры;
	КонецЦикла;
	
	СписокВложений = ПоместитьТабличныеДокументыВоВременноеХранилище(Форма, ВыбранныеПараметры, НастройкиПечатныхФорм);
	
	// Контроль уникальности имен.
	ШаблонИмениФайла = "%1%2.%3";
	ИспользованныеИменаФайлов = Новый Соответствие;
	Для Каждого Вложение Из СписокВложений Цикл
		ИмяФайла = Вложение.Представление;
		НомерИспользования = ?(ИспользованныеИменаФайлов[ИмяФайла] <> Неопределено,
			ИспользованныеИменаФайлов[ИмяФайла] + 1, 1);
		ИспользованныеИменаФайлов.Вставить(ИмяФайла, НомерИспользования);
		Если НомерИспользования > 1 Тогда
			Файл = Новый Файл(ИмяФайла);
			ИмяФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениФайла,
				Файл.ИмяБезРасширения, " (" + НомерИспользования + ")", Файл.Расширение);
		КонецЕсли;
		Вложение.Представление = ИмяФайла;
	КонецЦикла;
	
	Если ВыбранныеПараметры.Свойство("Получатели") Тогда
		Получатели = ВыбранныеПараметры.Получатели;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Получатель", Получатели);
	Результат.Вставить("Тема", ТемаПисьма);
	Результат.Вставить("Вложения", СписокВложений);
	Результат.Вставить("УдалятьФайлыПослеОтправки", Истина);
	
	ПечатныеФормы = Новый ТаблицаЗначений;
	ПечатныеФормы.Колонки.Добавить("Название");
	ПечатныеФормы.Колонки.Добавить("ТабличныйДокумент");
	
	Для Каждого НастройкаПечатнойФормы Из НастройкиПечатныхФорм Цикл
		Если Не НастройкаПечатнойФормы.Печатать Тогда
			Продолжить;
		КонецЕсли;
		
		Если НастройкаПечатнойФормы.ТабличныйДокумент = Неопределено Тогда
			ТабличныйДокумент = ТабличныйДокументПоНастройкеПечатнойФормы(НастройкаПечатнойФормы);
		Иначе
			ТабличныйДокумент = НастройкаПечатнойФормы.ТабличныйДокумент;
		КонецЕсли;
		Если ПечатныеФормы.НайтиСтроки(Новый Структура("ТабличныйДокумент", ТабличныйДокумент)).Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ВычислитьИспользованиеВывода(ТабличныйДокумент) = ИспользованиеВывода.Запретить Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТабличныйДокумент.Защита Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТабличныйДокумент.ВысотаТаблицы = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеПечатнойФормы = ПечатныеФормы.Добавить();
		ОписаниеПечатнойФормы.Название = НастройкаПечатнойФормы.Название;
		ОписаниеПечатнойФормы.ТабличныйДокумент = ТабличныйДокумент;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СтраховаяПремияСотрудника(ЭлементСтрахования) Экспорт
	
	СтраховаяПремияСотрудника = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлементСтрахования, "СтраховаяПремияСотрудника");
	Возврат ?(СтраховаяПремияСотрудника = Неопределено, 0, СтраховаяПремияСотрудника);
	
КонецФункции

Функция СтраховаяПремияРодственника(ЭлементСтрахования) Экспорт
	
	СтраховаяПремияРодственника = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлементСтрахования, "СтраховаяПремияРодственника");
	Возврат ?(СтраховаяПремияРодственника = Неопределено, 0, СтраховаяПремияРодственника);
	
КонецФункции

Функция ПараметрыРасчетаСтраховойПремии() Экспорт
	
	ПараметрыРасчета = Новый Структура;
	ПараметрыРасчета.Вставить("ДатаНачала");
	ПараметрыРасчета.Вставить("ДатаОкончания");
	ПараметрыРасчета.Вставить("ДатаНачалаСтрахования");
	ПараметрыРасчета.Вставить("ДатаОкончанияСтрахования");
	ПараметрыРасчета.Вставить("ДатаРождения");
	ПараметрыРасчета.Вставить("СтраховаяПремияСтрахователя", 0);
	
	ШкалаВозрастов = Новый ТаблицаЗначений;
	ШкалаВозрастов.Колонки.Добавить("ЗначениеОт", ОбщегоНазначения.ОписаниеТипаЧисло(2));
	ШкалаВозрастов.Колонки.Добавить("ЗначениеДо", ОбщегоНазначения.ОписаниеТипаЧисло(3));
	ШкалаВозрастов.Колонки.Добавить("ЗначениеПоказателя", ОбщегоНазначения.ОписаниеТипаЧисло(15,3));
	ПараметрыРасчета.Вставить("ШкалаВозрастов", ШкалаВозрастов);
	
	Возврат ПараметрыРасчета;
	
КонецФункции

Функция СтраховаяПремия(ПараметрыРасчета) Экспорт
	
	КоличествоДнейВМесяце = День(КонецМесяца(Дата(Год(ПараметрыРасчета.ДатаНачала), Месяц(ПараметрыРасчета.ДатаРождения), 1)));
	ДеньРожденияТекущийГод = Дата(Год(ПараметрыРасчета.ДатаНачала), Месяц(ПараметрыРасчета.ДатаРождения), Мин(День(ПараметрыРасчета.ДатаРождения), КоличествоДнейВМесяце));
	КоличествоЛетНаНачалоПериода = Год(ПараметрыРасчета.ДатаНачала) - Год(ПараметрыРасчета.ДатаРождения) + ?(Месяц(ПараметрыРасчета.ДатаРождения) > Месяц(ПараметрыРасчета.ДатаНачала), -1,
		?(Месяц(ПараметрыРасчета.ДатаРождения) = Месяц(ПараметрыРасчета.ДатаНачала) И День(ПараметрыРасчета.ДатаРождения) > День(ПараметрыРасчета.ДатаНачала), -1, 0));
	КоличествоЛетНаТекущуюДатуРождения = Год(ДеньРожденияТекущийГод) - Год(ПараметрыРасчета.ДатаРождения);
	КоэффициентНаНачалоПериода = 0;
	КоэффициентНаТекущуюДатуРождения = 0;
	Для Каждого СтрокаШкалы Из ПараметрыРасчета.ШкалаВозрастов Цикл
		Если СтрокаШкалы.ЗначениеОт <= КоличествоЛетНаНачалоПериода И СтрокаШкалы.ЗначениеДо >= КоличествоЛетНаНачалоПериода Тогда
			КоэффициентНаНачалоПериода = СтрокаШкалы.ЗначениеПоказателя;
		КонецЕсли;
		Если СтрокаШкалы.ЗначениеОт <= КоличествоЛетНаТекущуюДатуРождения И СтрокаШкалы.ЗначениеДо >= КоличествоЛетНаТекущуюДатуРождения Тогда
			КоэффициентНаТекущуюДатуРождения = СтрокаШкалы.ЗначениеПоказателя;
		КонецЕсли;
	КонецЦикла;
	
	КоличествоДнейПериода = (ПараметрыРасчета.ДатаОкончанияСтрахования - ПараметрыРасчета.ДатаНачалаСтрахования) / ЗарплатаКадрыКлиентСервер.ДлительностьСутокВСекундах() + 1;
	Если КоэффициентНаНачалоПериода <> КоэффициентНаТекущуюДатуРождения Тогда
		КоличествоДней = (ДеньРожденияТекущийГод - ПараметрыРасчета.ДатаНачала - ЗарплатаКадрыКлиентСервер.ДлительностьСутокВСекундах()) / ЗарплатаКадрыКлиентСервер.ДлительностьСутокВСекундах() + 1;
		СтраховаяПремия = ПараметрыРасчета.СтраховаяПремияСтрахователя / КоличествоДнейПериода * КоличествоДней * КоэффициентНаНачалоПериода;
		КоличествоДней = (ПараметрыРасчета.ДатаОкончания - ДеньРожденияТекущийГод) / ЗарплатаКадрыКлиентСервер.ДлительностьСутокВСекундах() + 1;
		СтраховаяПремия = СтраховаяПремия + ПараметрыРасчета.СтраховаяПремияСтрахователя / КоличествоДнейПериода * КоличествоДней * КоэффициентНаТекущуюДатуРождения;
	Иначе
		КоличествоДней = (ПараметрыРасчета.ДатаОкончания - ПараметрыРасчета.ДатаНачала) / ЗарплатаКадрыКлиентСервер.ДлительностьСутокВСекундах() + 1;
		СтраховаяПремия = ПараметрыРасчета.СтраховаяПремияСтрахователя / КоличествоДнейПериода * КоличествоДней * КоэффициентНаНачалоПериода;
	КонецЕсли;
	
	Возврат СтраховаяПремия;
	
КонецФункции

#Область ЗащитаПерсональныхДанных

// Процедура обеспечивает сбор сведений о хранении данных, 
// относящихся к персональным.
//
// Параметры:
//		ТаблицаСведений - таблица значений с полями:
//			Объект 			- строка, содержащая полное имя объекта метаданных,
//			ПоляРегистрации - строка, в которой перечислены имена полей регистрации, 
//								отдельные поля регистрации отделяются запятой,
//								альтернативные - символом "|",
//			ПоляДоступа		- строка, в которой перечислены через запятую имена полей доступа.
//			ОбластьДанных	- строка с идентификатором области данных, необязательно для заполнения.
//
Процедура ЗаполнитьСведенияОПерсональныхДанных(ТаблицаСведений) Экспорт
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "Сотрудники.Сотрудник";
	НовыеСведения.ПоляДоступа		= "Сотрудники.ПрограммыСтрахования,Сотрудники.РасширенияПрограммСтрахования";
	НовыеСведения.ОбластьДанных		= "ЛичныеДанные";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "Сотрудники.Сотрудник";
	НовыеСведения.ПоляДоступа		= "Сотрудники.СтраховаяПремия,Сотрудники.СуммаУдержания,Сотрудники.СуммаПредела";
	НовыеСведения.ОбластьДанных		= "Доходы";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "ПрограммыСтрахованияСотрудников.Сотрудник";
	НовыеСведения.ПоляДоступа		= "ПрограммыСтрахованияСотрудников.ПрограммаСтрахования";
	НовыеСведения.ОбластьДанных		= "ЛичныеДанные";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "РасширенияПрограммСтрахованияСотрудников.Сотрудник";
	НовыеСведения.ПоляДоступа		= "РасширенияПрограммСтрахованияСотрудников.РасширениеСтрахования";
	НовыеСведения.ОбластьДанных		= "ЛичныеДанные";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "Родственники.ФизическоеЛицо";
	НовыеСведения.ПоляДоступа		= "Родственники.ПрограммыСтрахования,Родственники.РасширенияПрограммСтрахования";
	НовыеСведения.ОбластьДанных		= "ЛичныеДанные";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "Родственники.ФизическоеЛицо";
	НовыеСведения.ПоляДоступа		= "Родственники.СтраховаяПремия";
	НовыеСведения.ОбластьДанных		= "Доходы";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "ПрограммыСтрахованияРодственников.ФизическоеЛицо";
	НовыеСведения.ПоляДоступа		= "ПрограммыСтрахованияРодственников.ПрограммаСтрахования";
	НовыеСведения.ОбластьДанных		= "ЛичныеДанные";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "РасширенияПрограммСтрахованияРодственников.ФизическоеЛицо";
	НовыеСведения.ПоляДоступа		= "РасширенияПрограммСтрахованияРодственников.РасширениеСтрахования";
	НовыеСведения.ОбластьДанных		= "ЛичныеДанные";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "СведенияСотрудников.Сотрудник";
	НовыеСведения.ПоляДоступа		= "СведенияСотрудников.СведениеЗначение";
	НовыеСведения.ОбластьДанных		= "ЛичныеДанные";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "СведенияРодственников.ФизическоеЛицо";
	НовыеСведения.ПоляДоступа		= "СведенияРодственников.СведениеЗначение";
	НовыеСведения.ОбластьДанных		= "ЛичныеДанные";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ОткреплениеОтПрограммМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "Сотрудники.Сотрудник";
	НовыеСведения.ПоляДоступа		= "Сотрудники.ДатаРождения,Сотрудники.ПрограммыСтрахования,Сотрудники.РасширенияПрограммСтрахования,Сотрудники.ДатаОткрепления";
	НовыеСведения.ОбластьДанных		= "ЛичныеДанные";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ОткреплениеОтПрограммМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "Сотрудники.Сотрудник";
	НовыеСведения.ПоляДоступа		= "Сотрудники.СтраховаяПремия";
	НовыеСведения.ОбластьДанных		= "Доходы";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ОткреплениеОтПрограммМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "ПрограммыСтрахованияСотрудников.Сотрудник";
	НовыеСведения.ПоляДоступа		= "ПрограммыСтрахованияСотрудников.ПрограммаСтрахования";
	НовыеСведения.ОбластьДанных		= "ЛичныеДанные";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект			= "Документ.ОткреплениеОтПрограммМедицинскогоСтрахования";
	НовыеСведения.ПоляРегистрации	= "РасширенияПрограммСтрахованияСотрудников.Сотрудник";
	НовыеСведения.ПоляДоступа		= "РасширенияПрограммСтрахованияСотрудников.РасширениеСтрахования";
	НовыеСведения.ОбластьДанных		= "ЛичныеДанные";
	
КонецПроцедуры

#КонецОбласти

#Область ДатыЗапретаИзмененияДанных

// См. ДатыЗапретаИзмененияПереопределяемый.ПриЗаполненииРазделовДатЗапретаИзменения.
Процедура ПриЗаполненииРазделовДатЗапретаИзменения(Разделы) Экспорт
	
	Раздел = Разделы.Добавить();
	Раздел.Имя  = "МедицинскоеСтрахование";
	Раздел.Идентификатор = Новый УникальныйИдентификатор("20bfa77a-4395-4d2d-a4e6-d32ceab316f0");
	Раздел.Представление = НСтр("ru = 'Медицинское страхование'");
	Раздел.ТипыОбъектов.Добавить(Тип("СправочникСсылка.Организации"));
	
КонецПроцедуры

Процедура ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения(ИсточникиДанных) Экспорт
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования",	"Дата", "МедицинскоеСтрахование", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОткреплениеОтПрограммМедицинскогоСтрахования",	"Дата", "МедицинскоеСтрахование", "Организация");
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиВариантовОтчетов

// Определяет разделы, в которых доступна панель отчетов.
//
// Параметры:
//   Разделы (Массив) из (ОбъектМетаданных).
//
// Описание:
//   В Разделы необходимо добавить метаданные тех разделов,
//   в которых размещены команды вызова панелей отчетов.
//
// Например:
//	Разделы.Добавить(Метаданные.Подсистемы.ИмяПодсистемы);
//
Процедура ОпределитьРазделыСВариантамиОтчетов(Разделы) Экспорт
	
КонецПроцедуры

// Содержит настройки размещения вариантов отчетов в панели отчетов.
// Описание см. ЗарплатаКадрыВариантыОтчетов.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчетов(Настройки) Экспорт
	
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.МедицинскоеСтрахование);
	
	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Метаданные.Отчеты.МедицинскоеСтрахование, "МедицинскоеСтрахование");
	Вариант.ФункциональныеОпции.Добавить("ИспользоватьМедицинскоеСтрахованиеСотрудников");
	
	Вариант = ВариантыОтчетов.ОписаниеВарианта(Настройки, Метаданные.Отчеты.МедицинскоеСтрахование, "ПрограммыСтрахования");
	Вариант.ФункциональныеОпции.Добавить("ИспользоватьМедицинскоеСтрахованиеСотрудников");
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// См. УправлениеПечатьюПереопределяемый.ПриОпределенииОбъектовСКомандамиПечати.
Процедура ПриОпределенииОбъектовСКомандамиПечати(СписокОбъектов) Экспорт
	
	СписокОбъектов.Добавить(Документы.ПрикреплениеКПрограммамМедицинскогоСтрахования);
	СписокОбъектов.Добавить(Документы.ОткреплениеОтПрограммМедицинскогоСтрахования);
	
КонецПроцедуры

#КонецОбласти

#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
//
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.6.51";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("03829d6c-0ce7-4f9f-b7e8-b0297e2dd155");
	Обработчик.Процедура = "МедицинскоеСтрахование.ЗаполнитьДатыСтрахования";
	Обработчик.Комментарий = НСтр("ru = 'Обновление дат медицинского страхования работников и их родственников.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.11.32";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("501817a4-2608-4aef-b3bd-1b01a9a6ad44");
	Обработчик.Процедура = "МедицинскоеСтрахование.СоздатьПоказателиМедицинскогоСтрахования";
	Обработчик.Комментарий = НСтр("ru = 'Обновление показателей расчета зарплаты.'");
	
КонецПроцедуры

Процедура ЗаполнитьДатыСтрахования(ПараметрыОбновления) Экспорт
	
	ОбновляемыеДокументы = ДатыСтрахованияОбновляемыеДокументы(ПараметрыОбновления);
	
	Если ОбновляемыеДокументы.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	Иначе
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
		ВыполнитьЗаполнениеДатСтрахования(ПараметрыОбновления, ОбновляемыеДокументы)
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьПоказателиМедицинскогоСтрахования(ПараметрыОбновления) Экспорт
	
	ПараметрыПланаВидовРасчета = РасчетЗарплатыРасширенный.ОписаниеПараметровПланаВидовРасчета();
	
	ИспользоватьМедицинскоеСтрахованиеСотрудников = Константы.ИспользоватьМедицинскоеСтрахованиеСотрудников.Получить();
	
	Если ИспользоватьМедицинскоеСтрахованиеСотрудников Тогда
		
		// Страховая премия
		ОписаниеПоказателя = Справочники.ПоказателиРасчетаЗарплаты.ОписаниеПоказателя();
		ОписаниеПоказателя.Идентификатор = "СтраховаяПремия";
		ОписаниеПоказателя.Наименование = НСтр("ru = 'Страховая премия'");
		ОписаниеПоказателя.КраткоеНаименование = НСтр("ru = 'Страховая премия'");
		ОписаниеПоказателя.НачальнаяНастройка = ПараметрыПланаВидовРасчета.НачальнаяНастройкаПрограммы;
		ОписаниеПоказателя.СпособПримененияЗначений = Перечисления.СпособыПримененияЗначенийПоказателейРасчетаЗарплаты.Постоянное;
		ОписаниеПоказателя.ЗначениеРассчитываетсяАвтоматически = Истина;
		Справочники.ПоказателиРасчетаЗарплаты.ЗаписатьПоказатель(ОписаниеПоказателя);
		
		// Количество дней периода страхования
		ОписаниеПоказателя = Справочники.ПоказателиРасчетаЗарплаты.ОписаниеПоказателя();
		ОписаниеПоказателя.Идентификатор = "КоличествоДнейПериодаСтрахования";
		ОписаниеПоказателя.Наименование = НСтр("ru = 'Количество дней периода страхования'");
		ОписаниеПоказателя.КраткоеНаименование = НСтр("ru = 'Дней страхования'");
		ОписаниеПоказателя.НачальнаяНастройка = ПараметрыПланаВидовРасчета.НачальнаяНастройкаПрограммы;
		ОписаниеПоказателя.СпособПримененияЗначений = Перечисления.СпособыПримененияЗначенийПоказателейРасчетаЗарплаты.Постоянное;
		ОписаниеПоказателя.ЗначениеРассчитываетсяАвтоматически = Истина;
		Справочники.ПоказателиРасчетаЗарплаты.ЗаписатьПоказатель(ОписаниеПоказателя);
		
	Иначе
		Справочники.ПоказателиРасчетаЗарплаты.ОтключитьИспользованиеПредопределенногоЭлемента("СтраховаяПремия");
		Справочники.ПоказателиРасчетаЗарплаты.ОтключитьИспользованиеПредопределенногоЭлемента("КоличествоДнейПериодаСтрахования");
	КонецЕсли;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область Свойства

// См. УправлениеСвойствамиПереопределяемый.ПриПолученииПредопределенныхНаборовСвойств.
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbf27-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.ОткреплениеОтПрограммМедицинскогоСтрахования);
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbf41-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.ПрикреплениеКПрограммамМедицинскогоСтрахования);
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.Справочники.ПрикреплениеКПрограммамМедицинскогоСтрахованияПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Документы.ОткреплениеОтПрограммМедицинскогоСтрахования, Истина);
	Списки.Вставить(Метаданные.Документы.ПрикреплениеКПрограммамМедицинскогоСтрахования, Истина);
	Списки.Вставить(Метаданные.ЖурналыДокументов.МедицинскоеСтрахование, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.АдресаОтправкиУведомленийСтраховыхКомпаний, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.ВозрастныеКоэффициентыСтраховыхКомпаний, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.ОтложенноеМедицинскоеСтрахованиеСотрудников, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.ПрограммыМедицинскогоСтрахованияПоУмолчанию, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.ПрограммыМедицинскогоСтрахованияСотрудников, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РасширенияПрограммМедицинскогоСтрахованияСотрудников, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.УсловияМедицинскогоСтрахования, Истина);
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииВидовОграниченийПравОбъектовМетаданных.
Процедура ПриЗаполненииВидовОграниченийПравОбъектовМетаданных(Описание) Экспорт
	
	Описание = Описание + "
	|Справочник.ПрикреплениеКПрограммамМедицинскогоСтрахованияПрисоединенныеФайлы.Чтение.ГруппыФизическихЛиц
	|Справочник.ПрикреплениеКПрограммамМедицинскогоСтрахованияПрисоединенныеФайлы.Чтение.Организации
	|Справочник.ПрикреплениеКПрограммамМедицинскогоСтрахованияПрисоединенныеФайлы.Изменение.ГруппыФизическихЛиц
	|Справочник.ПрикреплениеКПрограммамМедицинскогоСтрахованияПрисоединенныеФайлы.Изменение.Организации
	|Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.Чтение.ГруппыФизическихЛиц
	|Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.Чтение.Организации
	|Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.Изменение.ГруппыФизическихЛиц
	|Документ.ОткреплениеОтПрограммМедицинскогоСтрахования.Изменение.Организации
	|Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Чтение.ГруппыФизическихЛиц
	|Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Чтение.Организации
	|Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Изменение.ГруппыФизическихЛиц
	|Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.Изменение.Организации
	|ЖурналДокументов.МедицинскоеСтрахование.Чтение.Организации
	|РегистрСведений.АдресаОтправкиУведомленийСтраховыхКомпаний.Чтение.Организации
	|РегистрСведений.АдресаОтправкиУведомленийСтраховыхКомпаний.Изменение.Организации
	|РегистрСведений.ВозрастныеКоэффициентыСтраховыхКомпаний.Чтение.Организации
	|РегистрСведений.ВозрастныеКоэффициентыСтраховыхКомпаний.Изменение.Организации
	|РегистрСведений.ОтложенноеМедицинскоеСтрахованиеСотрудников.Чтение.ГруппыФизическихЛиц
	|РегистрСведений.ОтложенноеМедицинскоеСтрахованиеСотрудников.Чтение.Организации
	|РегистрСведений.ОтложенноеМедицинскоеСтрахованиеСотрудников.Изменение.ГруппыФизическихЛиц
	|РегистрСведений.ОтложенноеМедицинскоеСтрахованиеСотрудников.Изменение.Организации
	|РегистрСведений.ПрограммыМедицинскогоСтрахованияПоУмолчанию.Чтение.Организации
	|РегистрСведений.ПрограммыМедицинскогоСтрахованияПоУмолчанию.Изменение.Организации
	|РегистрСведений.ПрограммыМедицинскогоСтрахованияСотрудников.Изменение.ГруппыФизическихЛиц
	|РегистрСведений.ПрограммыМедицинскогоСтрахованияСотрудников.Изменение.Организации
	|РегистрСведений.ПрограммыМедицинскогоСтрахованияСотрудников.Чтение.ГруппыФизическихЛиц
	|РегистрСведений.ПрограммыМедицинскогоСтрахованияСотрудников.Чтение.Организации
	|РегистрСведений.РасширенияПрограммМедицинскогоСтрахованияСотрудников.Изменение.ГруппыФизическихЛиц
	|РегистрСведений.РасширенияПрограммМедицинскогоСтрахованияСотрудников.Изменение.Организации
	|РегистрСведений.РасширенияПрограммМедицинскогоСтрахованияСотрудников.Чтение.ГруппыФизическихЛиц
	|РегистрСведений.РасширенияПрограммМедицинскогоСтрахованияСотрудников.Чтение.Организации
	|РегистрСведений.УсловияМедицинскогоСтрахования.Чтение.Организации
	|РегистрСведений.УсловияМедицинскогоСтрахования.Изменение.Организации";
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПараметрыОтправкиПисьма

Функция ТабличныйДокументПоНастройкеПечатнойФормы(НастройкаПечатнойФормы)
	
	МассивОбъектов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НастройкаПечатнойФормы.СсылкаДокумента);
	
	ПараметрыПечати = НастройкаПечатнойФормы.ДополнительныеПараметры;
	ДанныеПечати = УправлениеПечатью.СформироватьПечатныеФормы(
		НастройкаПечатнойФормы.СсылкаДокумента.Метаданные().ПолноеИмя(),
		НастройкаПечатнойФормы.ИмяМакета, МассивОбъектов, ПараметрыПечати);
	КоллекцияПечатныхФорм = ДанныеПечати.КоллекцияПечатныхФорм;
	ОбъектыПечати = ДанныеПечати.ОбъектыПечати;
	ПараметрыВывода = ДанныеПечати.ПараметрыВывода;
	
	ПечатнаяФорма = Новый ТабличныйДокумент;
	Если НастройкаПечатнойФормы.ПолноеИмяОбъекта = "СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки" Тогда
		ПараметрыИсточника = Новый Структура;
		ПараметрыИсточника.Вставить("ИдентификаторКоманды", НастройкаПечатнойФормы.ИмяМакета);
		ПараметрыИсточника.Вставить("ОбъектыНазначения", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НастройкаПечатнойФормы.СсылкаДокумента));
		УправлениеПечатью.ПечатьПоВнешнемуИсточнику(
			НастройкаПечатнойФормы.ДополнительныеПараметры.Ссылка, ПараметрыИсточника, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	Иначе
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(НастройкаПечатнойФормы.ПолноеИмяОбъекта);
		МенеджерОбъекта.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	Для Каждого ЭлементКоллекции Из КоллекцияПечатныхФорм Цикл
		НастройкаПечатнойФормы.ТабличныйДокумент = ЭлементКоллекции.ТабличныйДокумент;
		ПечатнаяФорма = ЭлементКоллекции.ТабличныйДокумент;
	КонецЦикла;
	
	Возврат ПечатнаяФорма;
	
КонецФункции

Функция ПоместитьТабличныеДокументыВоВременноеХранилище(Форма, ПереданныеНастройки, НастройкиПечатныхФорм)
	Перем ЗаписьZipФайла, ИмяАрхива;
	
	НастройкиСохранения = НастройкиСохранения();
	ЗаполнитьЗначенияСвойств(НастройкиСохранения, ПереданныеНастройки);
	
	Результат = Новый Массив;
	
	// подготовка архива
	Если НастройкиСохранения.УпаковатьВАрхив Тогда
		ИмяАрхива = ПолучитьИмяВременногоФайла("zip");
		ЗаписьZipФайла = Новый ЗаписьZipФайла(ИмяАрхива);
	КонецЕсли;
	
	// подготовка временной папки
	ИмяВременнойПапки = ПолучитьИмяВременногоФайла();
	СоздатьКаталог(ИмяВременнойПапки);
	ИспользованныеИменаФайлов = Новый Соответствие;
	
	ВыбранныеФорматыСохранения = НастройкиСохранения.ФорматыСохранения;
	ПереводитьИменаФайловВТранслит = НастройкиСохранения.ПереводитьИменаФайловВТранслит;
	ТаблицаФорматов = УправлениеПечатью.НастройкиФорматовСохраненияТабличногоДокумента();
	
	// сохранение печатных форм
	ОбработанныеПечатныеФормы = Новый Массив;
	Для Каждого НастройкаПечатнойФормы Из НастройкиПечатныхФорм Цикл
		
		Если Не НастройкаПечатнойФормы.Печатать Тогда
			Продолжить;
		КонецЕсли;
		
		Если НастройкаПечатнойФормы.ТабличныйДокумент = Неопределено Тогда
			ПечатнаяФорма = ТабличныйДокументПоНастройкеПечатнойФормы(НастройкаПечатнойФормы);
		Иначе
			ПечатнаяФорма = НастройкаПечатнойФормы.ТабличныйДокумент;
		КонецЕсли;
		Если ОбработанныеПечатныеФормы.Найти(ПечатнаяФорма) = Неопределено Тогда
			ОбработанныеПечатныеФормы.Добавить(ПечатнаяФорма);
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если ВычислитьИспользованиеВывода(ПечатнаяФорма) = ИспользованиеВывода.Запретить Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПечатнаяФорма.Защита Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПечатнаяФорма.ВысотаТаблицы = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ПечатныеФормыПоОбъектам = Новый Соответствие;
		ПечатныеФормыПоОбъектам.Вставить(Форма.Объект.Ссылка, ПечатнаяФорма);
		Для Каждого СоответствиеОбъектаПечатнойФорме Из ПечатныеФормыПоОбъектам Цикл
			ОбъектПечати = СоответствиеОбъектаПечатнойФорме.Ключ;
			ПечатнаяФорма = СоответствиеОбъектаПечатнойФорме.Значение;
			
			Для Каждого ВыбранныйФормат Из ВыбранныеФорматыСохранения Цикл
				ТипФайла = ТипФайлаТабличногоДокумента[ВыбранныйФормат];
				НастройкиФормата = ТаблицаФорматов.НайтиСтроки(Новый Структура("ТипФайлаТабличногоДокумента", ТипФайла))[0];
				ЗаданныеИменаПечатныхФорм = ОбщегоНазначения.ЗначениеИзСтрокиXML(НастройкаПечатнойФормы.ИмяФайлаПечатнойФормы);
				
				ИмяФайла = УправлениеПечатью.ИмяФайлаПечатнойФормыОбъекта(ОбъектПечати, ЗаданныеИменаПечатныхФорм, НастройкаПечатнойФормы.Название);
				ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайла);
				
				Если ПереводитьИменаФайловВТранслит Тогда
					ИмяФайла = СтроковыеФункции.СтрокаЛатиницей(ИмяФайла);
				КонецЕсли;
				
				ИмяФайла = ИмяФайла + "." + НастройкиФормата.Расширение;
				
				ПолноеИмяФайла = УникальноеИмяФайла(ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяВременнойПапки) + ИмяФайла);
				ПечатнаяФорма.Записать(ПолноеИмяФайла, ТипФайла);
				
				Если ТипФайла = ТипФайлаТабличногоДокумента.HTML Тогда
					УправлениеПечатью.ВставитьКартинкиВHTML(ПолноеИмяФайла);
				КонецЕсли;
				
				Если ЗаписьZipФайла <> Неопределено Тогда 
					ЗаписьZipФайла.Добавить(ПолноеИмяФайла);
				Иначе
					ДвоичныеДанные = Новый ДвоичныеДанные(ПолноеИмяФайла);
					ПутьВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, Форма.УникальныйИдентификатор);
					ОписаниеФайла = Новый Структура;
					ОписаниеФайла.Вставить("Представление", ИмяФайла);
					ОписаниеФайла.Вставить("АдресВоВременномХранилище", ПутьВоВременномХранилище);
					Если ТипФайла = ТипФайлаТабличногоДокумента.ANSITXT Тогда
						ОписаниеФайла.Вставить("Кодировка", "windows-1251");
					КонецЕсли;
					Результат.Добавить(ОписаниеФайла);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	// Если архив подготовлен, записываем и помещаем его во временное хранилище.
	Если ЗаписьZipФайла <> Неопределено Тогда 
		ЗаписьZipФайла.Записать();
		ФайлАрхива = Новый Файл(ИмяАрхива);
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяАрхива);
		ПутьВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, Форма.УникальныйИдентификатор);
		ОписаниеФайла = Новый Структура;
		ОписаниеФайла.Вставить("Представление", ПолучитьИмяФайлаДляАрхива(Форма, НастройкиПечатныхФорм, ПереводитьИменаФайловВТранслит));
		ОписаниеФайла.Вставить("АдресВоВременномХранилище", ПутьВоВременномХранилище);
		Результат.Добавить(ОписаниеФайла);
	КонецЕсли;
	
	УдалитьФайлы(ИмяВременнойПапки);
	Если ЗначениеЗаполнено(ИмяАрхива) Тогда
		УдалитьФайлы(ИмяАрхива);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НастройкиСохранения()
	
	НастройкиСохранения = Новый Структура;
	НастройкиСохранения.Вставить("ФорматыСохранения", Новый Массив);
	НастройкиСохранения.Вставить("УпаковатьВАрхив", Ложь);
	НастройкиСохранения.Вставить("ПереводитьИменаФайловВТранслит", Ложь);
	
	Возврат НастройкиСохранения;
	
КонецФункции

Функция ВычислитьИспользованиеВывода(ТабличныйДокумент)
	Если ТабличныйДокумент.Вывод = ИспользованиеВывода.Авто Тогда
		Возврат ?(ПравоДоступа("Вывод", Метаданные), ИспользованиеВывода.Разрешить, ИспользованиеВывода.Запретить);
	Иначе
		Возврат ТабличныйДокумент.Вывод;
	КонецЕсли;
КонецФункции

Функция УникальноеИмяФайла(ИмяФайла)
	
	Файл = Новый Файл(ИмяФайла);
	ИмяБезРасширения = Файл.ИмяБезРасширения;
	Расширение = Файл.Расширение;
	Папка = Файл.Путь;
	
	Счетчик = 1;
	Пока Файл.Существует() Цикл
		Счетчик = Счетчик + 1;
		Файл = Новый Файл(Папка + ИмяБезРасширения + " (" + Счетчик + ")" + Расширение);
	КонецЦикла;
	
	Возврат Файл.ПолноеИмя;

КонецФункции

Функция ПолучитьИмяФайлаДляАрхива(Форма, НастройкиПечатныхФорм, ПереводитьИменаФайловВТранслит)
	
	Результат = "";
	
	Для Каждого НастройкаПечатнойФормы Из НастройкиПечатныхФорм Цикл
		
		Если Не НастройкаПечатнойФормы.Печатать Тогда
			Продолжить;
		КонецЕсли;
		
		Если НастройкаПечатнойФормы.ТабличныйДокумент = Неопределено Тогда
			ПечатнаяФорма = ТабличныйДокументПоНастройкеПечатнойФормы(НастройкаПечатнойФормы);
		Иначе
			ПечатнаяФорма = НастройкаПечатнойФормы.ТабличныйДокумент;
		КонецЕсли;
		
		Если ВычислитьИспользованиеВывода(ПечатнаяФорма) = ИспользованиеВывода.Запретить Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПустаяСтрока(Результат) Тогда
			Результат = НастройкаПечатнойФормы.Название;
		Иначе
			Результат = НСтр("ru = 'Документы'");
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПереводитьИменаФайловВТранслит Тогда
		Результат = СтроковыеФункции.СтрокаЛатиницей(Результат);
	КонецЕсли;
	
	Возврат Результат + ".zip";
	
КонецФункции

#КонецОбласти

Функция ДатыСтрахованияОбновляемыеДокументы(ПараметрыОбновления)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПрограммыСтрахованияСотрудников.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТОбновляемыеДокументы
	|ИЗ
	|	Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.ПрограммыСтрахованияСотрудников КАК ПрограммыСтрахованияСотрудников
	|ГДЕ
	|	ПрограммыСтрахованияСотрудников.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|	И ПрограммыСтрахованияСотрудников.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрограммыСтрахованияСотрудников.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасширенияПрограммСтрахованияСотрудников.Ссылка
	|ИЗ
	|	Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.РасширенияПрограммСтрахованияСотрудников КАК РасширенияПрограммСтрахованияСотрудников
	|ГДЕ
	|	РасширенияПрограммСтрахованияСотрудников.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|	И РасширенияПрограммСтрахованияСотрудников.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасширенияПрограммСтрахованияСотрудников.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрограммыСтрахованияРодственников.Ссылка
	|ИЗ
	|	Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.ПрограммыСтрахованияРодственников КАК ПрограммыСтрахованияРодственников
	|ГДЕ
	|	ПрограммыСтрахованияРодственников.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|	И ПрограммыСтрахованияРодственников.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрограммыСтрахованияРодственников.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасширенияПрограммСтрахованияРодственников.Ссылка
	|ИЗ
	|	Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования.РасширенияПрограммСтрахованияРодственников КАК РасширенияПрограммСтрахованияРодственников
	|ГДЕ
	|	РасширенияПрограммСтрахованияРодственников.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|	И РасширенияПрограммСтрахованияРодственников.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасширенияПрограммСтрахованияРодственников.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
	|	Документы.Ссылка КАК Ссылка
	|ИЗ
	|	ВТОбновляемыеДокументы КАК Документы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документы.Ссылка.Дата УБЫВ";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 100", "");
	КонецЕсли;
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Процедура ВыполнитьЗаполнениеДатСтрахования(ПараметрыОбновления, ОбновляемыеДокументы)
	
	Выборка = ОбновляемыеДокументы.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
				ПараметрыОбновления, "Документ.ПрикреплениеКПрограммамМедицинскогоСтрахования", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Для каждого СтрокаДокумента Из ДокументОбъект.Сотрудники Цикл
			ОтборСтрок = Новый Структура("Сотрудник", СтрокаДокумента.Сотрудник);
			НайденныеСтроки = ДокументОбъект.ПрограммыСтрахованияСотрудников.НайтиСтроки(ОтборСтрок);
			Для Каждого ИзменяемаяСтрока Из НайденныеСтроки Цикл
				ИзменяемаяСтрока.ДатаНачала = СтрокаДокумента.ДатаПрикрепления;
				ИзменяемаяСтрока.ДатаОкончания = ДокументОбъект.ДатаОкончанияСтрахования;
			КонецЦикла;
			НайденныеСтроки = ДокументОбъект.РасширенияПрограммСтрахованияСотрудников.НайтиСтроки(ОтборСтрок);
			Для Каждого ИзменяемаяСтрока Из НайденныеСтроки Цикл
				ИзменяемаяСтрока.ДатаНачала = СтрокаДокумента.ДатаПрикрепления;
				ИзменяемаяСтрока.ДатаОкончания = ДокументОбъект.ДатаОкончанияСтрахования;
			КонецЦикла;
		КонецЦикла;
		Для каждого СтрокаДокумента Из ДокументОбъект.Родственники Цикл
			ОтборСтрок = Новый Структура("ФизическоеЛицо, Родственник", СтрокаДокумента.ФизическоеЛицо, СтрокаДокумента.Родственник);
			НайденныеСтроки = ДокументОбъект.ПрограммыСтрахованияРодственников.НайтиСтроки(ОтборСтрок);
			Для Каждого ИзменяемаяСтрока Из НайденныеСтроки Цикл
				ИзменяемаяСтрока.ДатаНачала = СтрокаДокумента.ДатаПрикрепления;
				ИзменяемаяСтрока.ДатаОкончания = СтрокаДокумента.ДатаОткрепления;
			КонецЦикла;
			НайденныеСтроки = ДокументОбъект.РасширенияПрограммСтрахованияРодственников.НайтиСтроки(ОтборСтрок);
			Для Каждого ИзменяемаяСтрока Из НайденныеСтроки Цикл
				ИзменяемаяСтрока.ДатаНачала = СтрокаДокумента.ДатаПрикрепления;
				ИзменяемаяСтрока.ДатаОкончания = СтрокаДокумента.ДатаОткрепления;
			КонецЦикла;
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

#Область ПоказателиРасчетаЗарплаты

Процедура ЗаполнитьИсточникДанныхПоказателейИсключений(ПоказательРасчетаЗарплаты) Экспорт
	
	Если ПоказательРасчетаЗарплаты.ИмяПредопределенныхДанных = "СтраховаяПремия"
		Или ПоказательРасчетаЗарплаты.ИмяПредопределенныхДанных = "КоличествоДнейПериодаСтрахования" Тогда
		ПоказательРасчетаЗарплаты.ПериодическийПоказательСотрудника = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьТаблицуУдержанийИзвестнымиПоказателями(ДатаУвольнения, МенеджерРасчета) Экспорт
	
	ЗначениеКалендарныеДни = ЗарплатаКадрыКлиентСервер.ДнейВПериоде(НачалоМесяца(ДатаУвольнения), ДатаУвольнения);
	ПоказательКалендарныеДни = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.КалендарныеДни");
	Для Каждого СтрокаУдержаний Из МенеджерРасчета.Зарплата.Удержания Цикл
		МенеджерРасчета.ДобавитьИзвестноеЗначениеПоказателя(СтрокаУдержаний, ПоказательКалендарныеДни, ЗначениеКалендарныеДни);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

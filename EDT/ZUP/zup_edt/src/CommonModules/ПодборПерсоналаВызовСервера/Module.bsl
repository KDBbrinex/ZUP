#Область СлужебныеПроцедурыИФункции

#Область ПанельНастроекУправлениеПерсоналом

Процедура ЗаписатьНастройкиПодбораПерсонала(УникальныйИдентификаторФормы, НастройкиПодбораПерсонала) Экспорт
	
	ПараметрыНастроек = Обработки.ПанельНастроекУправлениеПерсоналом.ЗаполнитьСтруктуруПараметровНастроек("НастройкиПодбораПерсонала");
	ПараметрыНастроек.НастройкиПодбораПерсонала = НастройкиПодбораПерсонала;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификаторФормы);
	Обработки.ПанельНастроекУправлениеПерсоналом.ЗаписатьНастройки(ПараметрыНастроек, АдресХранилища);
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

Функция ЗапуститьИзменениеНастроекКонтроляПДнВФоновомЗадании(Знач ИдентификаторФормы, Знач ИспользоватьСкрытиеПерсональныхДанных) Экспорт
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ИспользоватьСкрытиеПерсональныхДанных", ИспользоватьСкрытиеПерсональныхДанных);
	ПараметрыМетода.Вставить("ДнейДоСкрытияПерсональныхДанных", Константы.ДнейДоСкрытияПерсональныхДанныхСубъектов.Получить());
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Изменение настроек контроля персональных данных'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("ЗащитаПерсональныхДанных.ИзменитьНастройкиСкрытияПерсональныхДанных", ПараметрыМетода, ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти

Функция ДлительнаяОперацияЗаполненияСчетчиковКандидатовПоВакансии(Вакансия, ТолькоКандидатыВРаботе, УникальныйИдентификатор) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Заполнение информационной строки по кандидатам'");
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, 
		"Справочники.Вакансии.ИнформационнаяСтрокаКандидатовПоВакансии", Вакансия, ТолькоКандидатыВРаботе);	
	
КонецФункции

Функция ДлительнаяОперацияЗаполненияЗанятостиПоВакансии(Вакансия, УникальныИдентификатор) Экспорт
	Возврат ДлительнаяОперацияЗаполненияЗанятостиПоДаннымВакансии(
		Справочники.Вакансии.ДанныеВакансии(Вакансия), УникальныИдентификатор);
КонецФункции

Функция ДлительнаяОперацияЗаполненияЗанятостиПоДаннымВакансии(ДанныеВакансии, УникальныйИдентификатор) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Заполнение информационной строки занятости по вакансии'");
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"Справочники.Вакансии.ИнформационнаяСтрокаЗанятостиПоДаннымВакансии", ДанныеВакансии);

КонецФункции

#КонецОбласти
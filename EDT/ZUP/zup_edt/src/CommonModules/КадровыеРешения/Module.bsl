#Область СлужебныйПрограммныйИнтерфейс

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//	Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//										общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.13.54";
	Обработчик.Процедура = "КадровыеРешения.ЗаполнитьКадровыеДокументыПоРешениям";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("87676bf9-d384-47b3-a4fa-1430c73bea18");
	Обработчик.Комментарий = НСтр("ru = 'Заполнение списка кадровых документов, введенных на основании решений.'");
	
КонецПроцедуры

#Область ОбменДанными

Процедура ПриЗаполненииТаблицыОбъектовРегистрирующихЗависимыеОбъекты(ДанныеРегистрации) Экспорт
	
	// Метаданные.Документы.РешениеОКадровомПереводе
	НоваяСтрока = ДанныеРегистрации.Добавить();
	НоваяСтрока.ВедущиеМетаданные = Метаданные.Документы.РешениеОКадровомПереводе;
	
	// Метаданные.Документы.РешениеОПриемеНаРаботу
	НоваяСтрока = ДанныеРегистрации.Добавить();
	НоваяСтрока.ВедущиеМетаданные = Метаданные.Документы.РешениеОПриемеНаРаботу;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьКадровыеДокументыПоРешениям(ПараметрыОбновления) Экспорт
	
	Запрос = ЗапросЗаполненияКадровыхДокументовПоРешениям();
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ПараметрыОбновления.ОбновлениеЗавершено = Истина;
		Возврат;
	КонецЕсли;
	
	ПараметрыОбновления.ОбновлениеЗавершено = Ложь;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.КадровыеДокументыПоРешениям.СоздатьНаборЗаписей();
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		НаборЗаписей.Отбор.Решение.Установить(Выборка.Решение);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЦикла;
	
КонецПроцедуры

Функция ЗапросЗаполненияКадровыхДокументовПоРешениям()
		
	ПустыеРешения = Новый Массив;
	Для Каждого ТипРешения Из Метаданные.ОпределяемыеТипы.КадровоеРешение.Тип.Типы() Цикл
		ПустыеРешения.Добавить(Новый(ТипРешения));
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустыеРешения", ПустыеРешения);
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	КадровыеДокументы.Ссылка КАК КадровыйДокумент,
		|	КадровыеДокументы.Решение
		|ИЗ
		|	Документ.ПриемНаРаботу КАК КадровыеДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадровыеДокументыПоРешениям КАК РешенияПоКадровымДокументам
		|		ПО РешенияПоКадровымДокументам.КадровыйДокумент = КадровыеДокументы.Ссылка
		|ГДЕ
		|	НЕ ПометкаУдаления
		|	И КадровыеДокументы.Решение НЕ В (&ПустыеРешения)
		|	И РешенияПоКадровымДокументам.Решение ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КадровыеДокументы.Ссылка КАК КадровыйДокумент,
		|	КадровыеДокументы.Решение
		|ИЗ
		|	Документ.ПриемНаРаботуСписком.Сотрудники КАК КадровыеДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадровыеДокументыПоРешениям КАК РешенияПоКадровымДокументам
		|		ПО РешенияПоКадровымДокументам.КадровыйДокумент = КадровыеДокументы.Ссылка
		|ГДЕ
		|	НЕ КадровыеДокументы.Ссылка.ПометкаУдаления
		|	И КадровыеДокументы.Решение НЕ В (&ПустыеРешения)
		|	И РешенияПоКадровымДокументам.Решение ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КадровыеДокументы.Ссылка КАК КадровыйДокумент,
		|	КадровыеДокументы.Решение
		|ИЗ
		|	Документ.КадровыйПеревод КАК КадровыеДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадровыеДокументыПоРешениям КАК РешенияПоКадровымДокументам
		|		ПО РешенияПоКадровымДокументам.КадровыйДокумент = КадровыеДокументы.Ссылка
		|ГДЕ
		|	НЕ ПометкаУдаления
		|	И КадровыеДокументы.Решение НЕ В (&ПустыеРешения)
		|	И РешенияПоКадровымДокументам.Решение ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КадровыеДокументы.Ссылка КАК КадровыйДокумент,
		|	КадровыеДокументы.Решение
		|ИЗ
		|	Документ.КадровыйПереводСписком.Сотрудники КАК КадровыеДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадровыеДокументыПоРешениям КАК РешенияПоКадровымДокументам
		|		ПО РешенияПоКадровымДокументам.КадровыйДокумент = КадровыеДокументы.Ссылка
		|ГДЕ
		|	НЕ КадровыеДокументы.Ссылка.ПометкаУдаления
		|	И КадровыеДокументы.Решение НЕ В (&ПустыеРешения)
		|	И РешенияПоКадровымДокументам.Решение ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КадровыеДокументы.Ссылка КАК КадровыйДокумент,
		|	КадровыеДокументы.Решение
		|ИЗ
		|	Документ.Увольнение КАК КадровыеДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадровыеДокументыПоРешениям КАК РешенияПоКадровымДокументам
		|		ПО РешенияПоКадровымДокументам.КадровыйДокумент = КадровыеДокументы.Ссылка
		|ГДЕ
		|	НЕ ПометкаУдаления
		|	И КадровыеДокументы.Решение НЕ В (&ПустыеРешения)
		|	И РешенияПоКадровымДокументам.Решение ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КадровыеДокументы.Ссылка КАК КадровыйДокумент,
		|	КадровыеДокументы.Решение
		|ИЗ
		|	Документ.УвольнениеСписком.Сотрудники КАК КадровыеДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадровыеДокументыПоРешениям КАК РешенияПоКадровымДокументам
		|		ПО РешенияПоКадровымДокументам.КадровыйДокумент = КадровыеДокументы.Ссылка
		|ГДЕ
		|	НЕ КадровыеДокументы.Ссылка.ПометкаУдаления
		|	И КадровыеДокументы.Решение НЕ В (&ПустыеРешения)
		|	И РешенияПоКадровымДокументам.Решение ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КадровыеДокументы.Ссылка КАК КадровыйДокумент,
		|	КадровыеДокументы.Решение
		|ИЗ
		|	Документ.ОтпускПоУходуЗаРебенком КАК КадровыеДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадровыеДокументыПоРешениям КАК РешенияПоКадровымДокументам
		|		ПО РешенияПоКадровымДокументам.КадровыйДокумент = КадровыеДокументы.Ссылка
		|ГДЕ
		|	НЕ ПометкаУдаления
		|	И КадровыеДокументы.Решение НЕ В (&ПустыеРешения)
		|	И РешенияПоКадровымДокументам.Решение ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КадровыеДокументы.Ссылка КАК КадровыйДокумент,
		|	КадровыеДокументы.Решение
		|ИЗ
		|	Документ.ВозвратИзОтпускаПоУходуЗаРебенком КАК КадровыеДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадровыеДокументыПоРешениям КАК РешенияПоКадровымДокументам
		|		ПО РешенияПоКадровымДокументам.КадровыйДокумент = КадровыеДокументы.Ссылка
		|ГДЕ
		|	НЕ ПометкаУдаления
		|	И КадровыеДокументы.Решение НЕ В (&ПустыеРешения)
		|	И РешенияПоКадровымДокументам.Решение ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КадровыеДокументы.Ссылка КАК КадровыйДокумент,
		|	КадровыеДокументы.Решение
		|ИЗ
		|	Документ.ВосстановлениеВДолжности КАК КадровыеДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадровыеДокументыПоРешениям КАК РешенияПоКадровымДокументам
		|		ПО РешенияПоКадровымДокументам.КадровыйДокумент = КадровыеДокументы.Ссылка
		|ГДЕ
		|	НЕ ПометкаУдаления
		|	И КадровыеДокументы.Решение НЕ В (&ПустыеРешения)
		|	И РешенияПоКадровымДокументам.Решение ЕСТЬ NULL";
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти
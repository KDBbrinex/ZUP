#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПроверкаУвольненияБезКомпенсацийИУдержаний(Проверка, ПараметрыПроверки) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.КонтрольВеденияУчета") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = РезультатПроверкиУвольненияБезКомпенсацийИУдержаний();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Результат.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Для Каждого КлючИЗначение Из Результат Цикл
		МодульКонтрольВеденияУчета = ОбщегоНазначения.ОбщийМодуль("КонтрольВеденияУчета");
		Проблема = МодульКонтрольВеденияУчета.ОписаниеПроблемы(КлючИЗначение.Ключ, ПараметрыПроверки);;
		Проблема.УточнениеПроблемы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сотрудник уволен без компенсаций и удержаний в текущем расчетном месяце (%1).'"),
			ЗарплатаКадрыКлиентСервер.ПолучитьПредставлениеМесяца(КлючИЗначение.Значение));
		УстановитьПривилегированныйРежим(Истина);
		КонтрольВеденияУчетаБЗК.ЗаписатьПроблему(Проблема, ПараметрыПроверки);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РезультатПроверкиУвольненияБезКомпенсацийИУдержаний()
	
	ТекущийМесяц = ЗарплатаКадрыБазовый.РасчетныйМесяц(ТекущаяДатаСеанса());
	ПараметрыСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыСотрудников.КадровыеДанные = "ДатаУвольнения";
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыСотрудников.Отборы,
		"ДатаУвольнения", ">=", НачалоМесяца(ТекущийМесяц));
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыСотрудников.Отборы,
		"ДатаУвольнения", "<=", КонецМесяца(ТекущийМесяц));
	УволенныеСотрудники = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыСотрудников);
	
	ПроблемныеСотрудники = Новый Соответствие;
	Для Каждого Сотрудник Из УволенныеСотрудники Цикл
		Остатки = ОстаткиОтпусков.ОстатокОтпускаСотрудникаНаДату(Сотрудник.Сотрудник, Сотрудник.ДатаУвольнения, , Истина);
		Если Остатки.ОстатокНаДату <> 0 Тогда
			ПроблемныеСотрудники.Вставить(Сотрудник.Сотрудник, ТекущийМесяц);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПроблемныеСотрудники;
	
КонецФункции

#КонецОбласти

#КонецЕсли
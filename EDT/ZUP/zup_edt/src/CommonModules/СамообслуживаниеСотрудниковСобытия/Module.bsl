
#Область СлужебныеПроцедурыИФункции

Процедура СозданиеПользователейФизическихЛиц() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.СозданиеПользователейФизическихЛиц);
	
	Если Не ПолучитьФункциональнуюОпцию("СоздаватьПользователейДляПринятыхНаРаботуСотрудниковАвтоматически") Тогда 
		Возврат;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПараметрыСозданияПользователейФизическихЛиц.ВидАдресаЭлектроннойПочты КАК ВидАдресаЭлектроннойПочты,
		|	ПараметрыСозданияПользователейФизическихЛиц.ВключатьВебСсылкуВУведомление КАК ВключатьВебСсылкуВУведомление,
		|	ПараметрыСозданияПользователейФизическихЛиц.ЗаголовокУведомления КАК ЗаголовокУведомления,
		|	ПараметрыСозданияПользователейФизическихЛиц.РассылатьУведомленияАвтоматически КАК РассылатьУведомленияАвтоматически,
		|	ПараметрыСозданияПользователейФизическихЛиц.ШаблонУведомления КАК ШаблонУведомления,
		|	ПараметрыСозданияПользователейФизическихЛиц.ГруппаПользователей КАК ГруппаПользователей,
		|	ИСТИНА КАК АутентификацияСтандартная,
		|	ПараметрыСозданияПользователейФизическихЛиц.ЗапрещеноИзменятьПароль КАК ЗапрещеноИзменятьПароль,
		|	ПараметрыСозданияПользователейФизическихЛиц.ПоказыватьВСпискеВыбора КАК ПоказыватьВСпискеВыбора
		|ИЗ
		|	РегистрСведений.ПараметрыСозданияПользователейФизическихЛиц КАК ПараметрыСозданияПользователейФизическихЛиц";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда 
		Возврат;
	КонецЕсли;
	
	НастройкиПользователей = СамообслуживаниеСотрудниковКлиентСервер.ОписаниеНастроекСозданияПользователей();
	ЗаполнитьЗначенияСвойств(НастройкиПользователей, Выборка);
	
	НастройкиСообщений = СамообслуживаниеСотрудниковКлиентСервер.ОписаниеНастроекОтправкиУведомлений();
	ЗаполнитьЗначенияСвойств(НастройкиСообщений, Выборка);
	
	ДатаПолученияДанных = ТекущаяДатаСеанса();
	
	ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудников.НачалоПериода = ДатаПолученияДанных;
	ПараметрыПолученияСотрудников.ОкончаниеПериода = ДатаПолученияДанных;
	ПараметрыПолученияСотрудников.КадровыеДанные = "ФизическоеЛицо, Фамилия, Имя, Отчество";
	
	СотрудникиОрганизации = КадровыйУчет.СотрудникиОрганизации(Ложь, ПараметрыПолученияСотрудников);
	
	СписокФизическихЛиц = ОбщегоНазначения.ВыгрузитьКолонку(СотрудникиОрганизации, "ФизическоеЛицо", Истина);
	ПользователиФизическихЛиц = СамообслуживаниеСотрудников.ПользователиФизическихЛиц(СписокФизическихЛиц);
	
	ВидКонтактнойИнформации = ?(ЗначениеЗаполнено(Выборка.ВидАдресаЭлектроннойПочты), Выборка.ВидАдресаЭлектроннойПочты, Неопределено);
	ЭлектронныеАдреса = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(СписокФизическихЛиц, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, ВидКонтактнойИнформации);
	
	СозданныеПользователи = Новый Массив;
	
	Для Каждого СтрокаСотрудника Из СотрудникиОрганизации Цикл 
		Если ПользователиФизическихЛиц[СтрокаСотрудника.ФизическоеЛицо] <> Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		ДанныеСотрудника = Новый Структура("ФизическоеЛицо, Фамилия, Имя, Отчество, АдресЭлектроннойПочты");
		ЗаполнитьЗначенияСвойств(ДанныеСотрудника, СтрокаСотрудника);
		
		ДанныеЭлектронногоАдреса = ЭлектронныеАдреса.Найти(ДанныеСотрудника.ФизическоеЛицо, "Объект");
		Если ДанныеЭлектронногоАдреса <> Неопределено Тогда 
			ДанныеСотрудника.АдресЭлектроннойПочты = ДанныеЭлектронногоАдреса.Представление;
		КонецЕсли;
		
		Пользователь = СамообслуживаниеСотрудников.НовыйПользователь(ДанныеСотрудника, НастройкиПользователей, НастройкиСообщений);
		ПользователиФизическихЛиц.Вставить(СтрокаСотрудника.ФизическоеЛицо, Пользователь);
		СозданныеПользователи.Добавить(Пользователь);
	КонецЦикла;	
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПараметрыСозданияПользователейФизическихЛицГруппыДоступа.ГруппаДоступа КАК ГруппаДоступа
		|ИЗ
		|	РегистрСведений.ПараметрыСозданияПользователейФизическихЛицГруппыДоступа КАК ПараметрыСозданияПользователейФизическихЛицГруппыДоступа";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		ГруппаДоступаОбъект = Выборка.ГруппаДоступа.ПолучитьОбъект();
		Для Каждого СозданныйПользователь Из СозданныеПользователи Цикл 
			ГруппаДоступаОбъект.Пользователи.Добавить().Пользователь = СозданныйПользователь;
		КонецЦикла;
		ГруппаДоступаОбъект.Записать();
	КонецЦикла;	

КонецПроцедуры

Процедура ЗапретНаВходВПрограммуУволеннымРаботникам() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ЗапретНаВходВПрограммуУволеннымРаботникам);
	
	Если Не ПолучитьФункциональнуюОпцию("ЗапрещатьВходВПрограммуУволеннымСотрудникамАвтоматически") Тогда 
		Возврат;
	КонецЕсли;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФизическиеЛицаИммунитет.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	РегистрСведений.ПараметрыЗапретаДоступаУволеннымСотрудникамИсключаемыеФизическиеЛица КАК ФизическиеЛицаИммунитет";
	ФизическиеЛицаИммунитет = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
	
	ПараметрыСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыСотрудников.КадровыеДанные = "ДатаУвольнения";
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыСотрудников.Отборы, "ДатаУвольнения", "<>", "ДАТАВРЕМЯ(1, 1, 1)");
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыСотрудников.Отборы, "ДатаУвольнения", "<", НачалоДня(ТекущаяДатаСеанса()));
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыСотрудников.Отборы, "ФизическоеЛицо", "НЕ В", ФизическиеЛицаИммунитет);
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(МенеджерВТ, Ложь, ПараметрыСотрудников, "ВТУволенныеСотрудники");
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛицаУволенных
		|ИЗ
		|	ВТУволенныеСотрудники КАК Сотрудники";
	Запрос.Выполнить();
	
	ПараметрыСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоВременнойТаблице();
	ПараметрыСотрудников.ИмяВТФизическиеЛица = "ВТФизическиеЛицаУволенных";
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыСотрудников.Отборы, "ДатаУвольнения", "=", "ДАТАВРЕМЯ(1, 1, 1)");
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(МенеджерВТ, Ложь, ПараметрыСотрудников, "ВТРаботающиеСотрудники");
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	ВТФизическиеЛицаУволенных КАК ФизическиеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРаботающиеСотрудники КАК РаботающиеСотрудники
		|		ПО (РаботающиеСотрудники.ФизическоеЛицо = ФизическиеЛица.ФизическоеЛицо)
		|ГДЕ
		|	РаботающиеСотрудники.Сотрудник ЕСТЬ NULL";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	УволенныеФизическиеЛица = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
	
	ПользователиФизическихЛиц = СамообслуживаниеСотрудников.ПользователиФизическихЛиц(УволенныеФизическиеЛица);
	Для Каждого КлючИЗначение Из ПользователиФизическихЛиц Цикл 
		Для Каждого ОписаниеПользователя Из КлючИЗначение.Значение Цикл
			Если Не ОписаниеПользователя.Недействителен Тогда 
				СамообслуживаниеСотрудников.УстановитьАутентификациюПользователя(ОписаниеПользователя.Пользователь, Ложь);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьИспользованиеМоегоОбученияРазвития(Источник, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьЭлектронноеОбучение = Источник.Значение;
	ИспользоватьПубликацииМероприятийОбученияРазвития = ПолучитьФункциональнуюОпцию("ИспользоватьПубликацииМероприятийОбученияРазвития");
	
	СамообслуживаниеСотрудников.УстановитьИспользованиеМоегоОбученияРазвития(ИспользоватьЭлектронноеОбучение, ИспользоватьПубликацииМероприятийОбученияРазвития);
	
КонецПроцедуры

#КонецОбласти


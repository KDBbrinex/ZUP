
#Область СлужебныйПрограммныйИнтерфейс

Процедура ОткрытьФормуНастройкиОснованияПодписи(Форма, ИмяПоля, Организация, ОписаниеПодписи, СтандартнаяОбработка, ДатаСведений = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("ИмяПоля", ИмяПоля);
	ДополнительныеПараметры.Вставить("ОписаниеПолей", ОписаниеПодписи);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбновитьПредставлениеОснованияПодписиВФорме", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("ФизическоеЛицо",
		ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ОписаниеПодписи.ФизическоеЛицо));
	ПараметрыФормы.Вставить("Должность", 
		ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ОписаниеПодписи.Должность));
	ПараметрыФормы.Вставить("ОснованиеПодписи", 
		ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ОписаниеПодписи.ОснованиеПодписи));
	ПараметрыФормы.Вставить("ДатаСведений", ДатаСведений);
	
	ФормаНастроекПодписи = ОткрытьФорму("ОбщаяФорма.НастройкаОснованияПодписиОтветственногоЛица", ПараметрыФормы, Форма, Форма.УникальныйИдентификатор,,, ОповещениеОЗакрытии);
	СтандартнаяОбработка = ФормаНастроекПодписи = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяФормыНастройкиОснованияПолномочий() Экспорт
	Возврат "РегистрСведений.ОснованияПолномочийОтветственныхЛиц.Форма.ФормаСписка";
КонецФункции

Процедура ОбновитьПредставлениеОснованияПодписиВФорме(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Или Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	ИмяПоля = ДополнительныеПараметры.ИмяПоля;
	ОписаниеПолей = ДополнительныеПараметры.ОписаниеПолей;
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма, ОписаниеПолей.Должность, Результат.Должность);
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма, ОписаниеПолей.ОснованиеПодписи, Результат.ОснованиеПодписи);
	
	ПодписиДокументовКлиентСервер.УстановитьПредставлениеПодписи(Форма, ИмяПоля, ОписаниеПолей);  
	
	Форма.Модифицированность = Форма.Модифицированность Или Результат.Модифицированность;
	
КонецПроцедуры

#КонецОбласти
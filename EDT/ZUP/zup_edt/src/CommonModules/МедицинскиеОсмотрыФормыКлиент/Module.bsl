#Область СлужебныйПрограммныйИнтерфейс

#Область ШтатноеРасписание

Процедура ШтатноеРасписаниеПриВыполненииКоманды(Форма, Команда) Экспорт
	ПриВыполненииКомандыСкопироватьИзДругойПозиции(Форма, Команда);
КонецПроцедуры

Процедура ШтатноеРасписаниеЭлементПриИзменении(Форма, Элемент) Экспорт
	ПриИзмененииВредногоФактора(Форма, Элемент);
	ПриИзмененииВидаРабот(Форма, Элемент);
КонецПроцедуры

Процедура ШтатноеРасписаниеЭлементПриОкончанииРедактирования(Форма, Элемент, НоваяСтрока, ОтменаРедактирования) Экспорт
	ПриОкончанииРедактированияСпискаВредныхФакторов(Форма, Элемент, НоваяСтрока, ОтменаРедактирования);
	ПриОкончанииРедактированияСпискаВидовРабот(Форма, Элемент, НоваяСтрока, ОтменаРедактирования);
КонецПроцедуры

Процедура ШтатноеРасписаниеЭлементПослеУдаления(Форма, Элемент) Экспорт
	ПослеУдаленияВредногоФактора(Форма, Элемент);
	ПослеУдаленияВидаРабот(Форма, Элемент);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВредныеФакторыВидыРабот

Процедура ПроверитьИзменениеПериодичностиФактораВидаРабот(Форма, Отказ, ПараметрыЗаписи) Экспорт
	
	Если ПараметрыЗаписи.Свойство("ПроверятьПериодичность") И Не ПараметрыЗаписи["ПроверятьПериодичность"] Тогда
		Возврат;
	КонецЕсли;
	
	Если Форма.Параметры.Ключ.Пустая() Тогда
		// Новый объект, пока нигде не мог быть задействован.
		Возврат;
	КонецЕсли;
	
	НоваяПериодичность = МедицинскиеОсмотрыКлиентСервер.ОписаниеПериодичностиОсмотров();
	МедицинскиеОсмотрыКлиентСервер.ЗаполнитьОписаниеПериодичностиПоВредномуФакторуВидуРабот(НоваяПериодичность, Форма.Объект);
	Если Не МедицинскиеОсмотрыВызовСервера.ПериодичностьВредногоФактораВидаРаботИзменилась(НоваяПериодичность, Форма.ПрежняяПериодичность) Тогда
		// Периодичность не изменилась.
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ЗаписатьПослеВопроса", Форма);
	ПоказатьПредупреждениеОбИзмененииПериодичности(ОбработчикОповещения);
	
КонецПроцедуры

Процедура ПоказатьПредупреждениеОбИзмененииПериодичности(ОбработчикОповещения)
	
	ТекстВопроса = 
		НСтр("ru = 'Периодичность медицинских осмотров была изменена. 
              |Данное изменение может привести к необходимости обновления сведений о периодических медицинских осмотрах, 
              |которое займет длительное время и, вероятно, повлияет на дату проведения следующего осмотра.'");
	СписокКнопок = Новый СписокЗначений;
	СписокКнопок.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Продолжить'"));
	СписокКнопок.Добавить(КодВозвратаДиалога.Отмена);
	
	ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, СписокКнопок);
	
КонецПроцедуры

#КонецОбласти

#Область ШтатноеРасписание

Процедура ПриВыполненииКомандыСкопироватьИзДругойПозиции(Форма, Команда)
	
	Если Команда.Имя <> "СкопироватьВредныеФакторыВидыРабот" Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ИсключаяПозицию");
	ПараметрыФормы.ИсключаяПозицию = Форма.Параметры.Ключ;
	
	ПараметрыОповещения = Новый Структура(
		"Форма, 
		|Команда");
	ПараметрыОповещения.Форма = Форма;
	ПараметрыОповещения.Команда = Команда;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("СкопироватьИзДругойПозицииПослеВыбора", ЭтотОбъект, ПараметрыОповещения);
	ОткрытьФорму("Обработка.МедицинскиеОсмотры.Форма.ВыборПозиции", ПараметрыФормы, Форма, , , , ОбработчикОповещения);
	
КонецПроцедуры

Процедура СкопироватьИзДругойПозицииПослеВыбора(ДругаяПозиция, ДополнительныеПараметры) Экспорт
	
	Если ДругаяПозиция = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	ДополнительныеПараметры.Вставить("ДругаяПозиция", ДругаяПозиция);
	
	Если Форма.ВредныеФакторы.Количество() = 0 И Форма.ВидыРаботМедицинскихОсмотров.Количество() = 0 Тогда
		НачатьКопированиеИзДругойПозиции(ДругаяПозиция, ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Списки вредных факторов и видов работ будут очищены перед копированием.
                         |Продолжить?'");
	ОбработчикОповещения = Новый ОписаниеОповещения("СкопироватьИзДругойПозицииПослеВопроса", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

Процедура СкопироватьИзДругойПозицииПослеВопроса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	НачатьКопированиеИзДругойПозиции(ДополнительныеПараметры.ДругаяПозиция, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура НачатьКопированиеИзДругойПозиции(ДругаяПозиция, ДополнительныеПараметры)
	
	ПараметрыОповещения = Новый Структура("ИмяКоманды");
	ПараметрыОповещения.ИмяКоманды = ДополнительныеПараметры.Команда.Имя;
	
	ДополнительныеПараметры.Форма.Подключаемый_ОбработчикКомандыЗавершение(ДругаяПозиция, ПараметрыОповещения);
	
КонецПроцедуры

Процедура ПриИзмененииВредногоФактора(Форма, Элемент)
	
	Если Элемент.Имя <> "ВредныеФакторыВредныйФактор" Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Форма.Элементы["ВредныеФакторы"].ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ПериодичностьОсмотра = МедицинскиеОсмотрыВызовСервера.ПериодичностьОсмотраПоВредномуФактору(ТекущиеДанные.ВредныйФактор);
	
КонецПроцедуры

Процедура ПриИзмененииВидаРабот(Форма, Элемент)
	
	Если Элемент.Имя <> "ВидыРаботМедицинскихОсмотровВидРабот" Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Форма.Элементы["ВидыРаботМедицинскихОсмотров"].ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ПериодичностьОсмотра = МедицинскиеОсмотрыВызовСервера.ПериодичностьОсмотраПоВредномуФактору(ТекущиеДанные.ВидРабот);
	
КонецПроцедуры

Процедура ПриОкончанииРедактированияСпискаВредныхФакторов(Форма, Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если Элемент.Имя <> "ВредныеФакторы" Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Подключаемый_МедицинскиеОсмотрыШтатноеРасписаниеЭлементФормыПриОкончанииРедактированияЗавершение(Элемент.Имя);
	
КонецПроцедуры

Процедура ПриОкончанииРедактированияСпискаВидовРабот(Форма, Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если Элемент.Имя <> "ВидыРаботМедицинскихОсмотров" Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Подключаемый_МедицинскиеОсмотрыШтатноеРасписаниеЭлементФормыПриОкончанииРедактированияЗавершение(Элемент.Имя);
	
КонецПроцедуры

Процедура ПослеУдаленияВредногоФактора(Форма, Элемент)
	
	Если Элемент.Имя <> "ВредныеФакторы" Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Подключаемый_МедицинскиеОсмотрыШтатноеРасписаниеЭлементФормыПослеУдаленияЗавершение(Элемент.Имя);
	
КонецПроцедуры

Процедура ПослеУдаленияВидаРабот(Форма, Элемент)
	
	Если Элемент.Имя <> "ВидыРаботМедицинскихОсмотров" Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Подключаемый_МедицинскиеОсмотрыШтатноеРасписаниеЭлементФормыПослеУдаленияЗавершение(Элемент.Имя);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
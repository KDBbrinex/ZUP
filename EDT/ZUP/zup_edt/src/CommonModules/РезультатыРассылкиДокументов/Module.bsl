#Область СлужебныйПрограммныйИнтерфейс

// Считывает результаты отправки.
// 
// Параметры:
// 	ТаблицаОтбор - ТаблицаЗначений - отбор результатов.
// 	* ФизическоеЛицо - СправочникСсылка.ФизическиеЛица - физическое лицо рассылаемого документа.
// 	* Получатель - СправочникСсылка.ФизическиеЛица - получатель сообщения.
// 	* РассылаемыйДокумент - ОпределяемыйТип.РассылаемыйДокумент - отправляемый документ.
// Возвращаемое значение:
// 	ТаблицаЗначений - см. РассылкаДокументов.ТаблицаРезультатовОтправки.
Функция РезультатыОтправки(ТаблицаОтбор) Экспорт
	Возврат РегистрыСведений.РезультатыРассылкиДокументов.РезультатыОтправки(ТаблицаОтбор);
КонецФункции

// Конструктор описания результата отправки.
// 
// Возвращаемое значение:
// 	Структура:
//   * Отправлено - Булево - Истина, если успешно отправлено, Ложь - в противном случае.
//   * ОписаниеОшибки - Строка - содержит подробности ошибки, заполняется при неуспешной отправке.
//   * ДатаОтправления - Дата - дата успешной отправки или неудачной попытки отправки.
//
Функция НовыйРезультатОтправки() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Отправлено", Ложь);
	Результат.Вставить("ОписаниеОшибки", "");
	Результат.Вставить("ДатаОтправления", Дата(1, 1, 1));

	Возврат Результат;

КонецФункции

// Регистрирует результаты отправки рассылки документов.
// 
// Параметры:
// 	ФизическоеЛицо - СправочникСсылка.ФизическиеЛица - физическое лицо, по поводу которого выполняется отправка документа.
// 	Получатель - СправочникСсылка.ФизическиеЛица - получатель сообщения, 
// 		часто то же самое физическое лицо, но может быть руководитель, например.
// 	РассылаемыйДокумент - ОпределяемыйТип.РассылаемыйДокумент - документ, печатная форма которого направляется получателю.
// 	РезультатОтправки - Структура - см. НовыйРезультатОтправки - описание результата отправки.
//
Процедура ЗаписатьРезультатОтправки(ФизическоеЛицо, Получатель, РассылаемыйДокумент, РезультатОтправки) Экспорт
	РегистрыСведений.РезультатыРассылкиДокументов.ЗаписатьРезультатОтправки(
		ФизическоеЛицо, Получатель, РассылаемыйДокумент, РезультатОтправки);
КонецПроцедуры

// Заполняет результат в случае успешной отправки.
// 
// Параметры:
// 	РезультатОтправки - Структура - см. НовыйРезультатОтправки - описание результата отправки.
//
Процедура ЗаполнитьУспешнымРезультатом(РезультатОтправки) Экспорт
	РезультатОтправки.Отправлено = Истина;
	РезультатОтправки.ДатаОтправления = ТекущаяДатаСеанса();
КонецПроцедуры

// Заполняет описание результата в случае неуспешной отправки.
// 
// Параметры:
// 	РезультатОтправки - Структура - см. НовыйРезультатОтправки - описание результата отправки.
// 	ПричиныОшибки - Строка - описание ошибки, возникшей в процессе отправки.
//
Процедура ЗаполнитьНеуспешнымРезультатом(РезультатОтправки, ПричиныОшибки) Экспорт
	РезультатОтправки.Отправлено = Ложь;
	РезультатОтправки.ОписаниеОшибки = ПричиныОшибки;
	РезультатОтправки.ДатаОтправления = ТекущаяДатаСеанса();
КонецПроцедуры

// Добавляет роли для профиля, которому доступна работа с рассылкой.
// 
// Параметры:
// 	ОписаниеПрофиля - Структура - см. УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа.
//
Процедура ДобавитьРолиЧтенияРезультатовРассылкиДокументов(ОписаниеПрофиля) Экспорт
	ОписаниеПрофиля.Роли.Добавить(Метаданные.Роли.ЧтениеРезультатовРассылкиДокументов.Имя);
КонецПроцедуры

#КонецОбласти
#Область СлужебныеПроцедурыИФункции

// Обработчик подписки на событие ЗаполнитьНаборЗначенийДоступаСПредметомКандидатом.
// 
// Параметры:
// 	НаборЗаписей - РегистрСведенийНаборЗаписей.ПредметыПапкиВзаимодействий - 
// 	Отказ - Булево
// 	Замещение - Булево
//
Процедура ЗаполнитьНаборЗначенийДоступаСПредметомКандидатом(НаборЗаписей, Отказ, Замещение) Экспорт

	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(НаборЗаписей) Тогда
		Возврат;
	КонецЕсли;
	
	Если НаборЗаписей.Отбор["Взаимодействие"].Использование = Ложь 
		Или Не ЗначениеЗаполнено(НаборЗаписей.Отбор.Взаимодействие.Значение) Тогда
		Возврат;
	КонецЕсли;
	
	ПрежнийПредмет = НаборЗаписей.ДополнительныеСвойства.СтруктураЗаписи.Предмет; // СправочникСсылка - 
	
	Если Не ПредметИзменился(НаборЗаписей, ПрежнийПредмет) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПредметЯвляетсяКандидатом(НаборЗаписей, ПрежнийПредмет) Тогда 
		УправлениеДоступом.ОбновитьНаборыЗначенийДоступа(НаборЗаписей.Отбор.Взаимодействие.Значение);
	КонецЕсли;
	
КонецПроцедуры

Функция ПредметИзменился(ТекущийНабор, ПрежнийПредмет)
	
	Если ТекущийНабор.Количество() = 0 Или Не ЗначениеЗаполнено(ТекущийНабор[0].Предмет) Тогда
		Возврат ЗначениеЗаполнено(ПрежнийПредмет);
	КонецЕсли;
	
	Возврат ТекущийНабор[0].Предмет <> ПрежнийПредмет;
	
КонецФункции

Функция ПредметЯвляетсяКандидатом(ТекущийНабор, ПрежнийПредмет)
	
	Если ТипЗнч(ПрежнийПредмет) = Тип("СправочникСсылка.Кандидаты") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ТекущийНабор.Количество() > 0 И ТипЗнч(ТекущийНабор[0].Предмет) = Тип("СправочникСсылка.Кандидаты") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//	Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//										общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.10.10";
	Обработчик.Процедура = "ОтпускаУведомления.ПриНачальномЗаполненииДанныхПодсистемыУведомлений";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("42761a10-a39c-471d-90ed-4eb595d753e0");
	Обработчик.Комментарий = НСтр("ru = 'Начальное заполнение данных системы уведомлений: отпуск по графику.'");
	
КонецПроцедуры

#КонецОбласти

#Область Уведомления

// Процедура выполняет начальное заполнение данных подсистемы уведомлений
//
Процедура ПриНачальномЗаполненииДанныхПодсистемыУведомлений(ПараметрыОбновления = Неопределено) Экспорт
	
	ВидУведомления = УведомленияСервер.ВидУведомленияПоИмени("ОтпускПоГрафику");
	Если ЗначениеЗаполнено(ВидУведомления) И Не УведомленияСервер.ЕстьРассылкиПоВидуУведомления(ВидУведомления) Тогда 
		ПараметрыШаблона = Новый Структура();
		
		ПараметрыШаблона.Вставить("ВладелецШаблона", ВидУведомления);
		ПараметрыШаблона.Вставить("ПредназначенДляЭлектронныхПисем", Истина);
		ПараметрыШаблона.Вставить("ПредназначенДляВводаНаОсновании", Истина);
		ПараметрыШаблона.Вставить("Тема", НСтр("ru='Приближается отпуск по графику'"));
		ПараметрыШаблона.Вставить("ФорматПисьма", Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст);
		ПараметрыШаблона.Вставить("Текст", НСтр("ru='Здравствуйте, [ДанныеУведомления.ФизическоеЛицо]!
			|
			|Приближается плановый отпуск: 
			|Дата начала: [ДанныеУведомления.ДатаНачала] (на [ДанныеУведомления.КоличествоДней] д.).
			|Письмо сформировано автоматически.'"));
		ПараметрыШаблона.Вставить("ПолноеИмяТипаПараметраВводаНаОсновании", "Справочник.ФизическиеЛица");
		ПараметрыШаблона.Вставить("Назначение", "ФизическиеЛица");
		
		ШаблонСообщенияСсылка = ШаблоныСообщений.СоздатьШаблон(НСтр("ru='Уведомление о приближающемся отпуске'"), ПараметрыШаблона);
		
		// 	Рассылка уведомления
		РассылкаОбъект = Справочники.РассылкиУведомлений.СоздатьЭлемент();
		РассылкаОбъект.ВидУведомления 	= ВидУведомления;
		РассылкаОбъект.ВидТранспорта 	= Перечисления.ВидыТранспортаУведомлений.Email;
		РассылкаОбъект.УчетнаяЗаписьЭлектроннойПочты = РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись();
		РассылкаОбъект.Наименование 	= НСтр("ru='Уведомление о приближающемся отпуске по графику'");
		СтрокаПолучателя = РассылкаОбъект.ПолучателиУведомленийДинамические.Добавить();
		СтрокаПолучателя.Получатель = "ФизическоеЛицо";
		СтрокаПолучателя.ВидКонтактнойИнформации = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.EMailФизическиеЛица");
		СтрокаПолучателя.ШаблонСообщения = ШаблонСообщенияСсылка;
		РассылкаОбъект.СпособОпределенияМоментаУведомления = Перечисления.СпособыОпределенияМоментаУведомления.ДоСобытия;
		РассылкаОбъект.ИнтервалУведомления = 14;
		РассылкаОбъект.ЕдиницаИзмеренияИнтервалаУведомления = Перечисления.ЕдиницыИзмеренияИнтервалаУведомлений.День;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(РассылкаОбъект, Ложь, Ложь);
	КонецЕсли;
	
	Если ПараметрыОбновления <> Неопределено Тогда
		ПараметрыОбновления.ОбработкаЗавершена = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Процедура позволяет дополнить структуру видов уведомлений своими видами уведомлений. 
//
// Параметры:
//  СтруктураДоступныхВидовУведомлений - структура - перечень уведомлений, подключенных в подсистеме
//     * Ключ - Строка - Имя вида уведомления
//     * Значение - Структура - Описание вида уведомления (Структура)
//        * ВидУведомления - Строка, строковый идентификатор вида уведомления.
//        * Описание - Строка, Текстовое описание получаемых данных. Доступно пользователю в справочнике Видов уведомлений.
//        * Инициализация - Строка, <ИмяМодуля>.<ИмяПроцедуры> описывающей данные вида уведомления без выполнения
//                          запросов к базе данных.
//        * ПолучениеДанных - Строка, <ИмяМодуля>.<ИмяПроцедуры> выполняемой для получения данных уведомления.
//
Процедура ПриЗаполненииСпискаВидовУведомлений(СтруктураДоступныхВидовУведомлений) Экспорт
	
	// Отпуска по графику
	СтруктураОтпускПоГрафику = УведомленияСервер.ПустоеОписаниеВидаУведомления();
	СтруктураОтпускПоГрафику.ВидУведомления 	= НСтр("ru='Отпуска по графику'");
	СтруктураОтпускПоГрафику.GUID 				= "d1f02bf1-a019-4223-9bbc-87accfc68cf8";
	СтруктураОтпускПоГрафику.Описание 			= НСтр("ru='Уведомление об отпусках по графику.'");
	СтруктураОтпускПоГрафику.Инициализация 		= "ОтпускаУведомления.ИнициализацияВидаУведомленияОтпускПоГрафику";
	СтруктураОтпускПоГрафику.ПолучениеДанных 	= "ОтпускаУведомления.ПолучениеДанныхВидаУведомленияОтпускПоГрафику";
	
	СтруктураДоступныхВидовУведомлений.Вставить("ОтпускПоГрафику", СтруктураОтпускПоГрафику);
	
КонецПроцедуры

// Позволяет переопределить имя типа шаблона сообщений по виду уведомления
//
// Параметры:
//   ИмяТипаШаблонаСообщений - Строка - имя типа предмета шаблона, которое необходимо переопределить
//   ВидУведомления - СправочникСсылка.ВидУведомления - связанный вид уведомления
Процедура ПриОпределенииИмениТипаШаблонаСообщений(ИмяТипаШаблонаСообщений, ВидУведомления) Экспорт
	
	Если ВидУведомления = Справочники.ВидыУведомлений.НайтиПоРеквизиту("Имя", "ОтпускПоГрафику") Тогда
		ИмяТипаШаблонаСообщений = "Справочник.ФизическиеЛица";
	КонецЕсли;
	
КонецПроцедуры

// Процедура вызывается при подготовке шаблона сообщения и позволяет установить предмет 
// сообщения, отличный от предмета уведомления.
//
// Параметры:
//  СтрокаСообщения	 - СтрокаТЗ - строка данных уведомления, источник данных для переопределения предмета.
//  Предмет			 - ДокументСсылка, СправочникСсылка - предмет сообщения, который можно переопределить.
//
Процедура ПриОпределенииПредметаШаблонаСообщения(СтрокаСообщения, Предмет) Экспорт
	
	Если СтрокаСообщения.ВидУведомления = Справочники.ВидыУведомлений.НайтиПоРеквизиту("Имя", "ОтпускПоГрафику") Тогда
		Предмет = СтрокаСообщения.ПараметрыСобытия.Получить().ФизическоеЛицо;
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при подготовке шаблонов сообщений и позволяет заполнить поля, доступные для шаблона
//  соответствующего вида уведомлений.
//
// Параметры:
//  ВидУведомления           - СправочникСсылка.ВидУведомления - вид уведомления - владелец шаблона
//  Реквизиты                - ДеревоЗначений - список реквизитов шаблона.
//         ** Имя            - Строка - Уникальное имя общего реквизита.
//         ** Представление  - Строка - Представление общего реквизита.
//         ** Тип            - Тип    - Тип реквизита. По умолчанию строка.
//         ** Формат         - Строка - Формат вывода значения для чисел, дат, строк и булевых значений.
//                                      и др.
//  Вложения                - ТаблицаЗначений - печатные формы и вложения
//         ** Имя            - Строка - Уникальное имя вложения.
//         ** Представление  - Строка - Представление варианта.
//         ** ТипФайла       - Строка - Тип вложения, который соответствует расширению файла: "pdf", "png", "jpg", mxl"
//                                      и др.
//  НазначениеШаблона        - Строка  - Имя назначения шаблон сообщения.
//  ДополнительныеПараметры  - Структура - дополнительные сведения о шаблоне сообщений.
//
Процедура ПриПодготовкеШаблонаСообщенияПоУведомлению(ВидУведомления, Реквизиты, Вложения, НазначениеШаблона, ДополнительныеПараметры) Экспорт
	
	Если ВидУведомления = Справочники.ВидыУведомлений.НайтиПоРеквизиту("Имя", "ОтпускПоГрафику") Тогда
		УведомленияСервер.ДобавитьПоле(Реквизиты, "ДанныеУведомления.ФизическоеЛицо",НСтр("ru='Физическое лицо'"), Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
		УведомленияСервер.ДобавитьПоле(Реквизиты, "ДанныеУведомления.ДатаНачала", НСтр("ru='Первый день отпуска'"), Новый ОписаниеТипов("Строка"));
		УведомленияСервер.ДобавитьПоле(Реквизиты, "ДанныеУведомления.ДатаОкончания", НСтр("ru='Последний день отпуска'"), Новый ОписаниеТипов("Строка"));
		УведомленияСервер.ДобавитьПоле(Реквизиты, "ДанныеУведомления.КоличествоДней", НСтр("ru='Количество дней отпуска'"), Новый ОписаниеТипов("Число"));
		УведомленияСервер.ДобавитьПоле(Реквизиты, "ДанныеУведомления.Организация", НСтр("ru='Организация'"), Новый ОписаниеТипов("СправочникСсылка.Организации"));
		УведомленияСервер.ДобавитьПоле(Реквизиты, "ДанныеУведомления.Подразделение", НСтр("ru='Подразделение'"), Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
		УведомленияСервер.ДобавитьПоле(Реквизиты, "ДанныеУведомления.Должность", НСтр("ru='Должность'"), Новый ОписаниеТипов("СправочникСсылка.Должности"));
	КонецЕсли;
	
КонецПроцедуры

// Заполняет параметры вида уведомления "Отпуск по графику"
// 
// Параметры:
//   ОписаниеДанных - Структура - Содержит элементы, описывающий вид уведомления
//      * ТипПредмета - ОписаниеТипов - указывает тип предмета уведомления
//      * Отборы - ТаблицаЗначений - описывает отборы, которые будут доступны пользователю, при настройке рассылки
//         * Имя - Строка - Имя поля отбора, должно быть уникально в пределах таблицы отборов
//                          и совпадать с одним из полей данных уведомления
//         * Представление - Строка - пользовательское представление поля отбора
//         * ОписаниеТипов - ОписаниеТипов - тип значения отбора
//      * Получатели - ТаблицаЗначений - описывает получателей, предлагаемых разработчиком
//                                       Значение получателей будет выбрано из данных уведомления
//         * Имя - Строка - Имя поля получателя, должно быть уникально в пределах таблицы отборов
//                          и совпадать с одним из полей данных уведомления
//         * Представление - Строка - пользовательское представление получателя
//         * ОписаниеТипов - ОписаниеТипов - тип значения получателя
//
Процедура ИнициализацияВидаУведомленияОтпускПоГрафику(ОписаниеДанных) Экспорт
	
	ОписаниеДанных.ТипПредмета = Новый ОписаниеТипов("ДокументСсылка.ГрафикОтпусков");
	
	УведомленияСервер.ДобавитьПолучателя(ОписаниеДанных, "ФизическоеЛицо", НСтр("ru='Сотрудник, отправляющийся в отпуск'"), Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	
	УведомленияСервер.ДобавитьОтбор(ОписаниеДанных, "Организация", 	НСтр("ru='Организация'"), Новый ОписаниеТипов("СправочникСсылка.Организации"));
	УведомленияСервер.ДобавитьОтбор(ОписаниеДанных, "ВидОтпуска", 	НСтр("ru='Вид отпуска'"), Новый ОписаниеТипов("СправочникСсылка.ВидыОтпусков"));
	
КонецПроцедуры

// Процедура заполняет таблицу уведомлений по переданным параметрам и отборам.
//
// Параметры:
//    СтруктураПериода - Структура - два элемента - НачалоПериода и ОкончаниеПериода
//    Отборы - ТаблицаЗначений - перечень отборов событий, заданный пользователем
//       * ЛевоеЗначение - Строка - имя поля отбора в источнике данных.
//       * ВидСравнения - ВидСравнения - Вид сравнения
//       * ЛевоеЗначение - Булево, Число, Строка, Дата, ЛюбаяСсылка - значение отбора.
//    ТаблицаРезультата - ТаблицаЗначений - содержит события изменения объекта.
//
Процедура ПолучениеДанныхВидаУведомленияОтпускПоГрафику(СтруктураПериода, Отборы, ТаблицаРезультата) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", СтруктураПериода.НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", СтруктураПериода.ОкончаниеПериода);
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПлановыеЕжегодныеОтпуска.Перенесен
	|			ТОГДА ПлановыеЕжегодныеОтпуска.ПеренесеннаяДатаНачала
	|		ИНАЧЕ ПлановыеЕжегодныеОтпуска.ДатаНачала
	|	КОНЕЦ КАК ДатаСобытия,
	|	ВЫБОР
	|		КОГДА ПлановыеЕжегодныеОтпуска.Перенесен
	|			ТОГДА ПлановыеЕжегодныеОтпуска.ПеренесеннаяДатаНачала
	|		ИНАЧЕ ПлановыеЕжегодныеОтпуска.ДатаНачала
	|	КОНЕЦ КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА ПлановыеЕжегодныеОтпуска.Перенесен
	|			ТОГДА ЕСТЬNULL(ПереносОтпускаПереносы.ДатаОкончания, ПлановыеЕжегодныеОтпуска.ДатаОкончания)
	|		ИНАЧЕ ПлановыеЕжегодныеОтпуска.ДатаОкончания
	|	КОНЕЦ КАК ДатаОкончания,
	|	ВЫБОР
	|		КОГДА ПлановыеЕжегодныеОтпуска.Перенесен
	|			ТОГДА ЕСТЬNULL(ПереносОтпускаПереносы.КоличествоДней, ПлановыеЕжегодныеОтпуска.КоличествоДней)
	|		ИНАЧЕ ПлановыеЕжегодныеОтпуска.КоличествоДней
	|	КОНЕЦ КАК КоличествоДней,
	|	ПлановыеЕжегодныеОтпуска.ВидОтпуска КАК ВидОтпуска,
	|	ПлановыеЕжегодныеОтпуска.Организация КАК Организация,
	|	ПлановыеЕжегодныеОтпуска.Примечание КАК Примечание,
	|	ПлановыеЕжегодныеОтпуска.ДокументПланирования КАК Предмет,
	|	ПлановыеЕжегодныеОтпуска.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТПлановыеЕжегодныеОтпуска
	|ИЗ
	|	РегистрСведений.ПлановыеЕжегодныеОтпуска КАК ПлановыеЕжегодныеОтпуска
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПереносОтпуска.Переносы КАК ПереносОтпускаПереносы
	|		ПО ПлановыеЕжегодныеОтпуска.ДокументПереноса = ПереносОтпускаПереносы.Ссылка
	|			И ПлановыеЕжегодныеОтпуска.ПеренесеннаяДатаНачала = ПереносОтпускаПереносы.ДатаНачала
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ПлановыеЕжегодныеОтпуска.Перенесен
	|				ТОГДА ПлановыеЕжегодныеОтпуска.ПеренесеннаяДатаНачала
	|			ИНАЧЕ ПлановыеЕжегодныеОтпуска.ДатаНачала
	|		КОНЕЦ МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТПлановыеЕжегодныеОтпуска.ДатаСобытия КАК ДатаСобытия,
	|	ВТПлановыеЕжегодныеОтпуска.ДатаНачала КАК ДатаНачала,
	|	ВТПлановыеЕжегодныеОтпуска.ДатаОкончания КАК ДатаОкончания,
	|	ВТПлановыеЕжегодныеОтпуска.КоличествоДней КАК КоличествоДней,
	|	ВТПлановыеЕжегодныеОтпуска.ВидОтпуска КАК ВидОтпуска,
	|	ВТПлановыеЕжегодныеОтпуска.Организация КАК Организация,
	|	ВТПлановыеЕжегодныеОтпуска.Примечание КАК Примечание,
	|	ВТПлановыеЕжегодныеОтпуска.Предмет КАК Предмет,
	|	ВТПлановыеЕжегодныеОтпуска.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	ВТПлановыеЕжегодныеОтпуска КАК ВТПлановыеЕжегодныеОтпуска");

	
	Если Отборы.Количество() > 0 Тогда
		ОтборЗапроса = СхемаЗапроса.ПакетЗапросов[1].Операторы[0].Отбор;
		Для Каждого СтрокаОтбора Из Отборы Цикл
			Если СтрокаОтбора.Использование Тогда
				ТекстУсловия = УведомленияСервер.ПредставлениеУсловияДляЗапроса(
					СтрокаОтбора.ВидСравнения,
					СтрокаОтбора.ЛевоеЗначение,
					"ПлановыеЕжегодныеОтпуска." + СтрокаОтбора.ЛевоеЗначение);
				ОтборЗапроса.Добавить(ТекстУсловия);
				Запрос.УстановитьПараметр(СтрокаОтбора.ЛевоеЗначение, СтрокаОтбора.ПравоеЗначение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		ТаблицаДанных = Запрос.Выполнить().Выгрузить();
		Для Каждого СтрокаРезультатаЗапроса Из ТаблицаДанных Цикл
			ПараметрыСобытия = Новый Структура();
			Для Каждого КолонкаРезультата Из СхемаЗапроса.ПакетЗапросов[0].Колонки Цикл
				ПараметрыСобытия.Вставить(КолонкаРезультата.Псевдоним);
			КонецЦикла;
			СтрокаТаблицы = ТаблицаРезультата.Добавить();
			СтрокаТаблицы.Предмет = СтрокаРезультатаЗапроса.Предмет;
			СтрокаТаблицы.ДатаСобытия = СтрокаРезультатаЗапроса.ДатаСобытия;
			ЗаполнитьЗначенияСвойств(ПараметрыСобытия, СтрокаРезультатаЗапроса);
			ПараметрыСобытия.ДатаНачала = Формат(СтрокаРезультатаЗапроса.ДатаНачала, "ДФ=dd.MM.yyyy");
			ПараметрыСобытия.ДатаОкончания = Формат(СтрокаРезультатаЗапроса.ДатаОкончания, "ДФ=dd.MM.yyyy");
			СтрокаТаблицы.ПараметрыСобытия = ПараметрыСобытия;
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти


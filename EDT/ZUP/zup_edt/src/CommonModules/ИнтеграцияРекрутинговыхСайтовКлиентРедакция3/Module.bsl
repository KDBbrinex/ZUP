
#Область СлужебныеПроцедурыИФункции

Процедура ПолучитьОтклики(Сайт, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьОткликиАвторизацияЗавершение", ИнтеграцияРекрутинговыхСайтовКлиент, ДополнительныеПараметры);
	Авторизация(Сайт, Оповещение);

КонецПроцедуры

Процедура ОткрытьФормуКандидата(СтруктураДанныхКандидатаССайта, Сайт, Вакансия, ИдентификаторРезюме) Экспорт

	ПараметрыФормы = Новый Структура("СтруктураРезюме, Сайт");
	ПараметрыФормы.СтруктураРезюме = СтруктураДанныхКандидатаССайта;
	ПараметрыФормы.Сайт = Сайт;
	
	ЗначенияЗаполнения = Неопределено;
	
	Если ЗначениеЗаполнено(Вакансия) Тогда
		ЗначенияЗаполнения = Новый Структура("Вакансия", Вакансия);
	КонецЕсли;
	
	ПодборПерсоналаКлиент.ОткрытьФормуНовогоКандидата(ЗначенияЗаполнения, ПараметрыФормы, ИдентификаторРезюме);
	
КонецПроцедуры

Процедура ЗагрузитьВакансииССайта(Сайт, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьВакансииССайтаАвторизацияЗавершение", ИнтеграцияРекрутинговыхСайтовКлиент, ДополнительныеПараметры);
	Авторизация(Сайт, Оповещение);
	
КонецПроцедуры

Процедура ОпубликоватьНаСайте(Сайт, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ОпубликоватьНаСайтеАвторизацияЗавершение", ИнтеграцияРекрутинговыхСайтовКлиент, ДополнительныеПараметры);
	Авторизация(Сайт, Оповещение);
	
КонецПроцедуры

Процедура ОбновитьПубликациюНаСайте(Сайт, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ОбновитьПубликациюНаСайтеАвторизацияЗавершение", ИнтеграцияРекрутинговыхСайтовКлиент, ДополнительныеПараметры);
	Авторизация(Сайт, Оповещение);
	
КонецПроцедуры

Процедура СнятьCПубликацииНаСайте(Сайт, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("СнятьСПубликацииНаСайтеАвторизацияЗавершение", ИнтеграцияРекрутинговыхСайтовКлиент, ДополнительныеПараметры);
	Авторизация(Сайт, Оповещение);
	
КонецПроцедуры

Процедура АвторизацияНаСайте(Сайт, ИмяПользователя, Пароль, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("АвторизацияЗавершение", ИнтеграцияРекрутинговыхСайтовКлиент, ДополнительныеПараметры);
	Авторизация(Сайт, Оповещение, ИмяПользователя, Пароль);
	
КонецПроцедуры

Процедура ОткрытьФормуАвторизации(Форма, Сайт) Экспорт
	
	ДополнительныеПараметры = Новый Структура("Сайт", Сайт);
	Оповещение = Новый ОписаниеОповещения("АвторизацияЗавершение", ИнтеграцияРекрутинговыхСайтовКлиент, ДополнительныеПараметры);
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
		ОткрытьФормуПараметровДоступа(Форма, Оповещение);
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьКомандыРаботыСПубликацией(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Вакансия = Форма.ВакансияСсылка;
	Публикации = Элементы.Публикации;
	
	ПустаяДата = '00010101';
	ЕстьСнятыеПубликации = Ложь;
	ЕстьНеДоступныеПубликации = Ложь;
	
	ИспользуемыеСайты = Форма.ИспользуемыеСайты;
	Если Не ЗначениеЗаполнено(ИспользуемыеСайты) Тогда
		Возврат;
	КонецЕсли;
	
	ВыделенныеСтроки = Публикации.ВыделенныеСтроки;
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
		ТекущаяСтрока = Публикации.ДанныеСтроки(ВыделеннаяСтрока);
		Если ТекущаяСтрока <> Неопределено Тогда 
			
			Если ИспользуемыеСайты.Найти(ТекущаяСтрока.МестоПубликации) = Неопределено Тогда
				ЕстьНеДоступныеПубликации = Истина;
			Иначе
				Если ТекущаяСтрока.ДатаСнятияСПубликации <> ПустаяДата Тогда
					ЕстьСнятыеПубликации = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВыделенныеСтроки.Количество() > 0 Тогда
		
		Элементы.ОбновитьПубликацию.Доступность = Не ЕстьНеДоступныеПубликации;
		Элементы.СнятьСПубликации.Доступность = Не (ЕстьНеДоступныеПубликации Или ЕстьСнятыеПубликации);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтобразитьПредупреждение(ТекстПредупреждения) Экспорт
	
	ПоказатьПредупреждение(, ТекстПредупреждения);
	
КонецПроцедуры

Процедура АвторизацияИЗагрузкаКлассификаторовССайта(Сайт, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузкаКлассификаторовАвторизацияЗавершение", ИнтеграцияРекрутинговыхСайтовКлиент, ДополнительныеПараметры);
	Авторизация(Сайт, Оповещение);
	
КонецПроцедуры

Процедура СоздатьКандидатаПоСсылкеЗавершениеВводаСтроки(Сайт, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("СоздатьКандидатаПоСсылкеАвторизацияЗавершение", ИнтеграцияРекрутинговыхСайтовКлиент, ДополнительныеПараметры);
	Авторизация(Сайт, Оповещение);
	
КонецПроцедуры

Процедура СоздатьКандидатаПоСсылке(СтрокаИзБуфера, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("СоздатьКандидатаПоСсылкеЗавершениеВводаСтроки", ИнтеграцияРекрутинговыхСайтовКлиент, ДополнительныеПараметры);
	ПоказатьВводСтроки(Оповещение, СтрокаИзБуфера, НСтр("ru = 'Введите веб-адрес резюме кандидата'"));
	
КонецПроцедуры

Функция ОткрытьФормуОбработки(ИмяФормы, ПараметрыФормы, Владелец, Оповещение = Неопределено, Модально = Ложь) Экспорт
	
	ОткрытьФорму(ИмяФормы, ПараметрыФормы, Владелец, Истина, , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Возврат Неопределено;
	
КонецФункции

Процедура Авторизация(Сайт, Оповещение, ИмяПользователя = Неопределено, Пароль = Неопределено) 
	
	МаркерДоступаАктивен = ИнтеграцияРекрутинговыхСайтовКлиент.МаркерДоступаАктивен(Сайт, ИмяПользователя);
	
	Если МаркерДоступаАктивен Тогда
		ВыполнитьОбработкуОповещения(Оповещение, Истина);
	Иначе
		ВыполнитьАвторизацию(ЭтотОбъект, Сайт, Оповещение, ИмяПользователя, Пароль);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьАвторизацию(Форма, Сайт, Оповещение, ИмяПользователя = Неопределено, Пароль = Неопределено) 
	
	Если Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter()
		Или Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Zarplata() Тогда
		
		ОткрытьФормуПараметровДоступа(Форма, Оповещение, Сайт);
		
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
		
		Результат = ИнтеграцияРекрутинговыхСайтовВызовСервера.АвторизацияRabota(ИмяПользователя, Пароль);
		ВыполнитьОбработкуОповещения(Оповещение, Результат);
		
	ИначеЕсли Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
		
		Результат = ИнтеграцияРекрутинговыхСайтовВызовСервера.АвторизацияSuperJob(ИмяПользователя, Пароль);
		ВыполнитьОбработкуОповещения(Оповещение, Результат);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормуПараметровДоступа(Форма, Оповещение, Сайт = Неопределено)
	
	Параметры = Новый Структура("Форма, Сайт", Форма, Сайт);
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("ПослеВопросаОткрытьФормуАвторизации", ИнтеграцияРекрутинговыхСайтовКлиентРедакция3, Параметры),
		НСтр("ru = 'Для продолжения необходимо авторизоваться на сайте. Открыть форму настройки доступа к рекрутинговым сайтам?'"),
		РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

Процедура ПослеВопросаОткрытьФормуАвторизации(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОткрытьФорму("Обработка.НастройкаДоступаКРекрутинговымСайтам.Форма.ФормаНастройкиДоступа",
			Новый Структура("Сайт", Параметры.Сайт),
			Параметры.Форма,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;
	
КонецПроцедуры

Процедура РегионНажатие(Форма, СтруктураПараметров) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("РегионНачалоВыбораЗавершение", Форма);
	ОткрытьФорму("Обработка.ПубликацияВакансийНаРекрутинговыхСайтах.Форма.ВыборИзДереваЗначений", СтруктураПараметров, Форма,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);  	
	
КонецПроцедуры

Процедура НачалоОдиночногоВыбораИзСписка(Форма, СписокВыбора, ДополнительныеПараметры) Экспорт
	
	Оповещение  = Новый ОписаниеОповещения("ВыборИзСпискаЗавершение", Форма, ДополнительныеПараметры);
	Форма.ПоказатьВыборИзСписка(Оповещение, СписокВыбора);
	
КонецПроцедуры

Процедура НайтиВДлительнойОперации(Форма, ФормаДлительнойОперации, Результат, АдресХранилища, ПараметрыОбработчикаОжидания, ИдентификаторЗадания) Экспорт
	
	Если Не Результат.Статус = "Выполнено" Тогда
		
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища		 = Результат.АдресРезультата;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(Форма, ИдентификаторЗадания);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура КомандаПоискРезюмеВИнтернете(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОткрытьФорму("Обработка.ПоискРезюмеВИнтернете.Форма", , ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	
КонецПроцедуры

Процедура КомандаПоискРезюмеВИнтернетеПоВакансии(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ПараметрыФормы = Новый Структура;
	
	НаименованиеВакансии = Строка(ПараметрКоманды);
	ПозицияСимвола = 0;
	ПозицияСимвола = СтрНайти(НаименованиеВакансии, "/");
	НаименованиеВакансии = Лев(НаименованиеВакансии, ?(ПозицияСимвола = 0, СтрДлина(НаименованиеВакансии), ПозицияСимвола - 1)); 
	
	ПараметрыФормы.Вставить("КлючевоеСлово",  НаименованиеВакансии);
	ПараметрыФормы.Вставить("Вакансия", ПараметрКоманды);
		
	ОткрытьФорму("Обработка.ПоискРезюмеВИнтернете.Форма", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	
КонецПроцедуры

Процедура ОпубликоватьВакансиюЗавершение(Форма) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("РаботаСПубликациейЗавершение", Форма);
	ПоказатьПредупреждение(Оповещение, НСтр("ru = 'Публикация вакансии прошла успешно.'"));
	
КонецПроцедуры

Процедура ОбновитьПубликациюВакансииЗавершение(Форма) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("РаботаСПубликациейЗавершение", Форма);
	ПоказатьПредупреждение(Оповещение, НСтр("ru = 'Обновление вакансии прошло успешно.'"));
	
КонецПроцедуры

Процедура ВыборИзДереваЗначений(Форма, СтруктураПараметров, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ВыборИзДереваЗначенийЗавершение", Форма, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.ПубликацияВакансийНаРекрутинговыхСайтах.Форма.ВыборИзДереваЗначений", СтруктураПараметров, Форма,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ОдиночныйВыборИзСписка(Форма, ДополнительныеПараметры) Экспорт
	
	Оповещение  = Новый ОписаниеОповещения("ПоказатьВыборЭлементаЗавершение", Форма, ДополнительныеПараметры);
	Форма.ПоказатьВыборИзСписка(Оповещение, ДополнительныеПараметры.СписокВыбора, , ?(ТипЗнч(ДополнительныеПараметры.ТекущиеДанные.ПубликацияВакансии) = Тип("СписокЗначений") И ДополнительныеПараметры.ТекущиеДанные.ПубликацияВакансии.Количество() > 0, ДополнительныеПараметры.СписокВыбора.НайтиПоЗначению(ДополнительныеПараметры.ТекущиеДанные.ПубликацияВакансии[0].Значение), Неопределено));
		
КонецПроцедуры

Процедура МножественныйВыборИзСписка(Форма, ДополнительныеПараметры) Экспорт
	
	Оповещение  = Новый ОписаниеОповещения("ПоказатьОтметкуЭлементовЗавершение", Форма, ДополнительныеПараметры);
	ДополнительныеПараметры.СписокВыбора.ПоказатьОтметкуЭлементов(Оповещение, НСтр("ru = 'Выберите'") + " " + ДополнительныеПараметры.ТекущиеДанные.Показатель);
	
КонецПроцедуры

Процедура РедактированиеМногострочногоТекста(Форма, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("РедактированиеМногострочногоТекстаЗавершение", Форма, ДополнительныеПараметры);
	ПоказатьВводСтроки(Оповещение, ДополнительныеПараметры.ТекущиеДанные.ПубликацияВакансииТекст, НСтр("ru = 'Введите'") + " " + ДополнительныеПараметры.ТекущиеДанные.Показатель, 10000, Истина);
	
КонецПроцедуры

Процедура РедактированиеУровняВладенияЯзыками(Форма, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьСпециальнуюФормуРедактированияУровняВладенияЯзыкамиЗавершение", Форма, ДополнительныеПараметры);
	ИнтеграцияРекрутинговыхСайтовКлиент.ОткрытьСпециальнуюФормуРедактированияСписка(Форма, ДополнительныеПараметры.ТекущиеДанные.Путь, ДополнительныеПараметры.СписокЯзыков, Оповещение, ДополнительныеПараметры.Сайт);
	
КонецПроцедуры

Процедура РедактированияСпискаТелефонов(Форма, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьСпециальнуюФормуРедактированияСпискаТелефоновЗавершение", Форма, ДополнительныеПараметры);
	ИнтеграцияРекрутинговыхСайтовКлиент.ОткрытьСпециальнуюФормуРедактированияСписка(Форма, ДополнительныеПараметры.ТекущиеДанные.Путь, ДополнительныеПараметры.СписокТелефонов, Оповещение);
	
КонецПроцедуры

Процедура РедактированиеТекстаВHTML(Форма, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("РедактированиеТекстаВHTMLЗавершение", Форма, ДополнительныеПараметры);
	ИнтеграцияРекрутинговыхСайтовКлиент.ОткрытьФормуРедактированияТекстаВHTML(Форма, ДополнительныеПараметры.ТекущиеДанные.Показатель, ДополнительныеПараметры.ТекущиеДанные.ПубликацияВакансииТекст, Оповещение);
	
КонецПроцедуры

Процедура НачалоВыбораЗначения(Форма, ДополнительныеПараметры) Экспорт
	
	Оповещение  = Новый ОписаниеОповещения("ПоказатьВыборЭлементаЗавершение", Форма, ДополнительныеПараметры);
	ДополнительныеПараметры.СписокВыбора.ПоказатьВыборЭлемента(Оповещение, НСтр("ru = 'Выберите'") + " " + ДополнительныеПараметры.ИмяЭлемента, ?(ТипЗнч(ДополнительныеПараметры.ТекущиеДанные[ДополнительныеПараметры.ИмяЭлемента]) = Тип("СписокЗначений") И ДополнительныеПараметры.ТекущиеДанные[ДополнительныеПараметры.ИмяЭлемента].Количество() > 0, ДополнительныеПараметры.СписокВыбора.НайтиПоЗначению(ДополнительныеПараметры.ТекущиеДанные[ДополнительныеПараметры.ИмяЭлемента][0].Значение), Неопределено));
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ЗарплатаКадрыОтчеты.ПриКомпоновкеРезультатаВТабличныйДокумент(
		ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если КлючСхемы <> КлючВарианта Тогда
		
		Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
			ПолеСтатья = СхемаКомпоновкиДанных.НаборыДанных.НаборДанныхНДФЛ.Поля.Найти("СтатьяРасходов");
			УстановитьОграничениеИспользованияПоля(ПолеСтатья);
		КонецЕсли;
		
		Если КлючВарианта = "РасчетыСБюджетомПоНДФЛ" И ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
			ПолеСтатья = СхемаКомпоновкиДанных.НаборыДанных.НаборДанныхНДФЛ.Поля.Найти("СтатьяФинансирования");
			УстановитьОграничениеИспользованияПоля(ПолеСтатья);
			ПолеСтатья = СхемаКомпоновкиДанных.НаборыДанных.НаборДанныхНДФЛ.Поля.Найти("СтатьяРасходов");
			УстановитьОграничениеИспользованияПоля(ПолеСтатья);
		КонецЕсли;
		
		ЗарплатаКадрыОтчеты.ПодключитьСхему(Контекст, ЭтотОбъект, КлючСхемы, КлючВарианта, НовыеПользовательскиеНастройкиКД);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОграничениеИспользованияПоля(ПолеНабораДанных)

	Если ПолеНабораДанных <> Неопределено Тогда
		ПолеНабораДанных.ОграничениеИспользования.Поле 		= Истина;
		ПолеНабораДанных.ОграничениеИспользования.Группировка = Истина;
		ПолеНабораДанных.ОграничениеИспользования.Порядок 	= Истина;
		ПолеНабораДанных.ОграничениеИспользования.Условие 	= Истина;
		ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Поле 			= Истина;
		ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Группировка 	= Истина;
		ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Порядок 		= Истина;
		ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Условие 		= Истина;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
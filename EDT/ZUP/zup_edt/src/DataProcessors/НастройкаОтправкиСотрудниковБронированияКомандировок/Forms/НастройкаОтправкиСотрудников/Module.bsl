#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	РегулярнаяОтправкаСотрудников.ПроверитьИспользуетсяАвтоматическаяОтправкаСотрудников();

	СистемаБронирования = БронированиеКомандировок.ИспользуемаяСистемаБронирования();
	СформироватьЗаголовокФормы();
	ЗаполнитьПредопределенныеЗначения();
	СохранитьНастройкиДоИзменений();
	ОтправлятьИнформациюЧисло = ?(ИспользоватьОтборПоПодразделениям, 1, 0);
	УстановитьДоступностьЭлементовФормы(ЭтотОбъект);
	УстановитьСвойстваЭлементовРезультатаПоследнейОтправки(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидСписка", НавигационнаяСсылкаФорматированнойСтроки);
	ПараметрыФормы.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	ПараметрыФормы.Вставить("ДатаПоследнейОперации", Неопределено);
	ПараметрыФормы.Вставить("СистемаБронирования", СистемаБронирования);

	Если НавигационнаяСсылкаФорматированнойСтроки = "ПоследниеОтправленныеСотрудники" 
		Или НавигационнаяСсылкаФорматированнойСтроки = "ПоследниеНеотправленныеСотрудники" Тогда
		ПараметрыФормы.ДатаПоследнейОперации = НастройкиДоИзменений.ДатаПоследнейОперации;
	КонецЕсли;

	ПутьКОткрываемойФорме = "Обработка.НастройкаОтправкиСотрудниковБронированияКомандировок.Форма.СписокСотрудников";
	ОткрытьФорму(ПутьКОткрываемойФорме, ПараметрыФормы, ЭтаФорма, НавигационнаяСсылкаФорматированнойСтроки, , , , РежимОткрытияОкнаФормы.Независимый);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если Модифицированность И Не ЗавершениеРаботы Тогда
		Отказ = Истина;
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтаФорма);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Данные были изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтправлятьИнформациюЧислоПриИзменении(Элемент)

	ИспользоватьОтборПоПодразделениям = ?(ОтправлятьИнформациюЧисло = 1, Истина, Ложь);
	УстановитьСвойствоМодифицированностьФормы("ИспользоватьОтборПоПодразделениям", ИспользоватьОтборПоПодразделениям);
	УстановитьДоступностьЭлементовФормы(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ПодразделенияОтбораОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ПодразделенияОрганизаций")
		Или ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
		Массив = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыбранноеЗначение);
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		Если ВыбранноеЗначение.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		Массив = ВыбранноеЗначение;
	Иначе
		Возврат;
	КонецЕсли;

	Для Каждого ЭлементМассива Из Массив Цикл
		Если ТипЗнч(ЭлементМассива) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
			ЭлементСправочникаСтруктураПредприятия = ЭлементСправочникаСтруктураПредприятияПоПодразделению(ЭлементМассива);
		Иначе
			ЭлементСправочникаСтруктураПредприятия = ЭлементМассива;
		КонецЕсли;
		Если ПодразделенияОтбора.НайтиПоЗначению(ЭлементСправочникаСтруктураПредприятия) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ПодразделенияОтбора.Добавить(ЭлементСправочникаСтруктураПредприятия);
	КонецЦикла;

	УстановитьСвойствоМодифицированностьФормы(Элемент.Имя, ПодразделенияОтбора);

КонецПроцедуры

&НаКлиенте
Процедура ПодразделенияОтбораПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НеотправляемыеСотрудникиПриИзменении(Элемент)
	УстановитьСвойствоМодифицированностьФормы(Элемент.Имя, НеотправляемыеСотрудники.ВыгрузитьЗначения());
КонецПроцедуры

&НаКлиенте
Процедура НеотправляемыеСотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	УстановитьСвойствоМодифицированностьФормы(Элемент.Имя, ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ОтправляемыеСотрудникиПриИзменении(Элемент)
	УстановитьСвойствоМодифицированностьФормы(Элемент.Имя, ОтправляемыеСотрудники.ВыгрузитьЗначения());
КонецПроцедуры

&НаКлиенте
Процедура ОтправляемыеСотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	УстановитьСвойствоМодифицированностьФормы(Элемент.Имя, ВыбранноеЗначение);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)

	Если Модифицированность Тогда
		НастройкиКорректны = ПроверитьКорректностьНастроекПередСохранением();
		Если НастройкиКорректны Тогда
			СохранитьНастройкиИВыполнитьОтправку(Ложь);
		КонецЕсли;
	Иначе
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОтправкуСейчас(Команда)

	Если Модифицированность Тогда
		ПоказатьВопросПередОтправкой();
	Иначе
		Если Не ЕстьДанныеДляОтправки() Тогда
			ТекстСообщения = НСтр("ru = 'Данные для отправки не найдены.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Иначе
			ЗарегистрироватьИзмененияСотрудниковПринудительноНаСервере(СистемаБронирования);
			НачатьОтправкуСотрудников();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройки(Команда)

	Если Модифицированность	Тогда
		Оповещение = Новый ОписаниеОповещения("ОбновитьНастройкиЗавершение", ЭтаФорма);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Настройки отправки были изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		СохранитьНастройкиДоИзменений();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		НастройкиКорректны = ПроверитьКорректностьНастроекПередСохранением();
		Если НастройкиКорректны Тогда
			СохранитьНастройкиИВыполнитьОтправку(Ложь);
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементовФормы(Форма)

	Если Форма.ИспользоватьОтборПоПодразделениям Тогда
		Форма.Элементы.ПодразделенияОтбора.Доступность = Истина;
		Форма.Элементы.НеотправляемыеСотрудники.Доступность = Истина;
		Форма.Элементы.ОтправляемыеСотрудники.Доступность = Истина;
	Иначе
		Форма.Элементы.ПодразделенияОтбора.Доступность = Ложь;
		Форма.Элементы.НеотправляемыеСотрудники.Доступность = Истина;
		Форма.Элементы.ОтправляемыеСотрудники.Доступность = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовРезультатаПоследнейОтправки(Форма)

	Если Форма.НастройкиДоИзменений.ДатаПоследнейОперации = Неопределено Тогда
		Форма.Элементы.РезультатПоследнейОтправкиПояснение.Заголовок = НСтр("ru = 'Отправка не выполнялась'");
		Форма.Элементы.РезультатПоследнейОтправкиПояснение.ЦветФона = Форма.ЦветаФона["Информация"];
		Форма.Элементы.РезультатПоследнейОтправкиГруппа.ЦветФона = Форма.ЦветаФона["Информация"];
		Форма.Элементы.РезультатПоследнейОтправкиКартинка.Картинка = Форма.КартинкиФормы["Информация"];
	Иначе
		Форма.Элементы.РезультатПоследнейОтправкиПояснение.ЦветФона = Форма.ЦветаФона["Информация"];
		Форма.Элементы.РезультатПоследнейОтправкиГруппа.ЦветФона = Форма.ЦветаФона["Информация"];
		Форма.Элементы.РезультатПоследнейОтправкиКартинка.Картинка = Форма.КартинкиФормы["Информация"];

		ДатаПоследнейОперации = Форма.НастройкиДоИзменений.ДатаПоследнейОперации;
		ЧислоОтправленных = Форма.НастройкиДоИзменений.ЧислоОтправленных;
		ЧислоНеотправленных = Форма.НастройкиДоИзменений.ЧислоНеотправленных;

		РезультатПоследнейОтправкиТекст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Последняя отправка выполнялась %1'"), Формат(ДатаПоследнейОперации, "ДЛФ=DT"));

		Если ЧислоОтправленных <> 0 Тогда
			РезультатПоследнейОтправкиТекст = РезультатПоследнейОтправкиТекст + Символы.НПП
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<a href=""ПоследниеОтправленныеСотрудники"">Отправлено %1</a>",
			Формат(ЧислоОтправленных, "ЧЦ=6; ЧН=; ЧГ=0"));
		КонецЕсли;

		Если ЧислоНеотправленных <> 0 Тогда
			РезультатПоследнейОтправкиТекст = РезультатПоследнейОтправкиТекст + Символы.НПП
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("<a href=""ПоследниеНеотправленныеСотрудники"">Не отправлено %1</a>",
			Формат(ЧислоНеотправленных, "ЧЦ=6; ЧН=; ЧГ=0"));
		КонецЕсли;

		Если Форма.РезультатВыполненияДлительнойОперации <> Неопределено Тогда
			Если ЗначениеЗаполнено(Строка(Форма.РезультатВыполненияДлительнойОперации.ТекстОшибки)) Тогда
				РезультатПоследнейОтправкиТекст = РезультатПоследнейОтправкиТекст + Символы.НПП
				+ Строка(Форма.РезультатВыполненияДлительнойОперации.ТекстОшибки);
				Форма.Элементы.РезультатПоследнейОтправкиПояснение.ЦветФона = Форма.ЦветаФона["НеудачнаяОтправка"];
				Форма.Элементы.РезультатПоследнейОтправкиГруппа.ЦветФона = Форма.ЦветаФона["НеудачнаяОтправка"];
			КонецЕсли;
		КонецЕсли;

		Форма.Элементы.РезультатПоследнейОтправкиПояснение.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(РезультатПоследнейОтправкиТекст);

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СформироватьЗаголовокФормы()

	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Настройка отправки информации о сотрудниках в систему бронирования командировок %1'"),
		Строка(СистемаБронирования));

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредопределенныеЗначения()

	ЦветаСтруктура = Новый Структура("НеудачнаяОтправка, Информация");
	ЦветаСтруктура.НеудачнаяОтправка = ЦветаСтиля.ОшибкиБронированияКомандировокФонЦвет;
	ЦветаСтруктура.Информация = ЦветаСтиля.ЦветФонаПодсказки;
	ЦветаФона = Новый ФиксированнаяСтруктура(ЦветаСтруктура);

	КартинкиСтруктура = Новый Структура("НеудачнаяОтправка, Информация");
	КартинкиСтруктура.НеудачнаяОтправка = БиблиотекаКартинок.Удалить;
	КартинкиСтруктура.Информация = БиблиотекаКартинок.Информация;
	КартинкиФормы = Новый ФиксированнаяСтруктура(КартинкиСтруктура);

	ТекстОтправкаСотрудников = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Отправка информации о сотрудниках в систему %1'"), Строка(СистемаБронирования));

КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиДоИзменений()

	НастройкиДоИзменений = РегулярнаяОтправкаСотрудников.НастройкиОтправкиСотрудников(СистемаБронирования);
	ИспользоватьОтборПоПодразделениям = НастройкиДоИзменений.ИспользоватьОтборПоПодразделениям;

	ПодразделенияОтбора.Очистить();
	Для Каждого Строка Из НастройкиДоИзменений.ПодразделенияОтбора Цикл
		ПодразделенияОтбора.Добавить(Строка.Значение,,Строка.Пометка);
	КонецЦикла;

	ОтправляемыеСотрудники.ЗагрузитьЗначения(НастройкиДоИзменений.ОтправляемыеСотрудники.ВыгрузитьЗначения());
	НеотправляемыеСотрудники.ЗагрузитьЗначения(НастройкиДоИзменений.НеотправляемыеСотрудники.ВыгрузитьЗначения());

КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервере(РезультатСохранения)

	НовыеНастройки = ПрочитатьНовыеНастройки();
	РегулярнаяОтправкаСотрудников.СохранитьНастройкиОтправкиСотрудников(НовыеНастройки, РезультатСохранения);

КонецПроцедуры

&НаСервере
Процедура УдалитьИзСпискаПустыеСтроки(Список)

	УдаляемыеСтроки = Новый Массив;

	Для Каждого СтрокаСписка Из Список Цикл
		Если Не ЗначениеЗаполнено(СтрокаСписка.Значение) Тогда
			УдаляемыеСтроки.Добавить(СтрокаСписка);
		КонецЕсли;
	КонецЦикла;

	Если УдаляемыеСтроки.Количество() > 0 Тогда
		Для Каждого Элемент Из УдаляемыеСтроки Цикл
			Список.Удалить(Элемент);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПроверитьКорректностьНастроекПередСохранением()

	УдалитьИзСпискаПустыеСтроки(ПодразделенияОтбора);
	УдалитьИзСпискаПустыеСтроки(НеотправляемыеСотрудники);
	УдалитьИзСпискаПустыеСтроки(ОтправляемыеСотрудники);

	УдалитьДублиВСписке(НеотправляемыеСотрудники);
	УдалитьДублиВСписке(ОтправляемыеСотрудники);

	Если НеотправляемыеСотрудники.Количество() > ОтправляемыеСотрудники.Количество() Тогда
		МассивОбхода = ОтправляемыеСотрудники.ВыгрузитьЗначения();
		МассивПоиска = НеотправляемыеСотрудники.ВыгрузитьЗначения();
	Иначе
		МассивОбхода = НеотправляемыеСотрудники.ВыгрузитьЗначения();
		МассивПоиска = ОтправляемыеСотрудники.ВыгрузитьЗначения();
	КонецЕсли;

	ЕстьОшибки = ЕстьСотрудникиВключенныеВОбаСпискаИсключений(МассивОбхода, МассивПоиска);
	Возврат Не ЕстьОшибки;

КонецФункции

&НаКлиенте
Процедура ПоказатьВопросПередОтправкой()

	ВариантыОтвета = Новый СписокЗначений;
	ВариантыОтвета.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Да'"));
	ВариантыОтвета.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Нет'"));
	ОбработчикОтвета = Новый ОписаниеОповещения("ВыполнитьОтправкуПослеОтвета", ЭтотОбъект);
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Выполнить отправку информации о сотрудниках в систему %1 сейчас?'"), Строка(СистемаБронирования));
	ПоказатьВопрос(ОбработчикОтвета, ТекстВопроса, ВариантыОтвета,,,ТекстОтправкаСотрудников);

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОтправкуПослеОтвета(Результат, ДополнительныеПараметры) Экспорт

	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	СохранитьНастройкиИВыполнитьОтправку(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиИВыполнитьОтправку(ПринудительнаяОтправка)

	РезультатСохранения = СтруктураРезультатСохраненияНастройкиНаСервере();
	СохранитьНастройкиНаСервере(РезультатСохранения);

	Если РезультатСохранения.Выполнено Тогда
		Если ПринудительнаяОтправка Тогда
			ЗарегистрироватьИзмененияСотрудниковПринудительноНаСервере(СистемаБронирования);
		КонецЕсли;
		НачатьОтправкуСотрудников();
	Иначе
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Настройки отправки не сохранены. %1'"), РезультатСохранения.ТекстОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗарегистрироватьИзмененияСотрудниковПринудительноНаСервере(СистемаБронирования)

	РегулярнаяОтправкаСотрудников.ЗарегистрироватьИзмененияСотрудниковПринудительно(СистемаБронирования);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСвойствоМодифицированностьФормы(ИмяЭлемента, РезультатВыбора)

	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если НастройкиДоИзменений.ИспользоватьОтборПоПодразделениям <> ?(ИмяЭлемента = "ИспользоватьОтборПоПодразделениям", РезультатВыбора, ИспользоватьОтборПоПодразделениям)
		Или Не СпискиИдентичныНаСервере(НастройкиДоИзменений.ПодразделенияОтбора, ?(ИмяЭлемента = "ПодразделенияОтбора", РезультатВыбора, ПодразделенияОтбора))
		Или Не СпискиИдентичныНаСервере(НастройкиДоИзменений.ОтправляемыеСотрудники.ВыгрузитьЗначения(), ?(ИмяЭлемента = "ОтправляемыеСотрудники", РезультатВыбора, ОтправляемыеСотрудники))
		Или Не СпискиИдентичныНаСервере(НастройкиДоИзменений.НеотправляемыеСотрудники.ВыгрузитьЗначения(), ?(ИмяЭлемента = "НеотправляемыеСотрудники", РезультатВыбора, НеотправляемыеСотрудники)) Тогда
		Модифицированность = Истина;
	Иначе
		Модифицированность = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УдалитьДублиВСписке(Список)

	Если Список.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	МассивЭлементов = Список.ВыгрузитьЗначения();
	МассивБезДублей = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивЭлементов);

	Список.ЗагрузитьЗначения(МассивБезДублей);

КонецПроцедуры

&НаСервере
Функция ЭлементСправочникаСтруктураПредприятияПоПодразделению(Подразделение)

	ЭлементСтруктураПредприятия = Справочники.СтруктураПредприятия.ПустаяСсылка();
	ОрганизационнаяСтруктура.ЗаполнитьДанныеИсточникаВыбранногоПодразделения(ЭлементСтруктураПредприятия, Подразделение);

	Возврат ЭлементСтруктураПредприятия;

КонецФункции

&НаСервере
Функция СпискиИдентичныНаСервере(СписокДоИзменений,СписокПослеИзменений)

	Возврат ОбщегоНазначенияКлиентСервер.СпискиЗначенийИдентичны(СписокДоИзменений, СписокПослеИзменений);

КонецФункции

&НаСервере
Функция ЕстьСотрудникиВключенныеВОбаСпискаИсключений(МассивОбхода, МассивПоиска)

	МассивОшибок = Новый Массив;

	Для Каждого Элемент Из МассивОбхода Цикл
		Если МассивПоиска.Найти(Элемент) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если МассивОшибок.Найти(Элемент) = Неопределено Тогда
			МассивОшибок.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;

	Если МассивОшибок.Количество() > 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Сотрудник может находиться только в одном списке исключений. Пожалуйста, исправьте информацию.'"));
		Для Каждого Элемент Из МассивОшибок Цикл
			ОбщегоНазначения.СообщитьПользователю(Строка(Элемент));
		КонецЦикла;
	КонецЕсли;

	ЕстьОшибки = ?(МассивОшибок.Количество() = 0, Ложь, Истина);

	Возврат ЕстьОшибки;

КонецФункции

&НаСервере
Функция ПрочитатьНовыеНастройки()

	НовыеНастройки = РегулярнаяОтправкаСотрудников.СтруктураНастройкаОтправкиСотрудников();
	НовыеНастройки.СистемаБронирования = СистемаБронирования;
	НовыеНастройки.ИспользоватьОтборПоПодразделениям = ИспользоватьОтборПоПодразделениям;
	НовыеНастройки.НеотправляемыеСотрудники.ЗагрузитьЗначения(НеотправляемыеСотрудники.ВыгрузитьЗначения());
	НовыеНастройки.ОтправляемыеСотрудники.ЗагрузитьЗначения(ОтправляемыеСотрудники.ВыгрузитьЗначения());

	Для Каждого Элемент Из ПодразделенияОтбора Цикл
		НовыеНастройки.ПодразделенияОтбора.Добавить(Элемент.Значение,,Элемент.Пометка);
	КонецЦикла;

	Возврат НовыеНастройки;

КонецФункции

&НаСервере
Функция ЕстьДанныеДляОтправки()

	НовыеНастройки = ПрочитатьНовыеНастройки();
	Если НовыеНастройки.ИспользоватьОтборПоПодразделениям Тогда
		Если НовыеНастройки.ПодразделенияОтбора.Количество() = 0
			И НовыеНастройки.НеотправляемыеСотрудники.Количество() = 0
			И НовыеНастройки.ОтправляемыеСотрудники.Количество() = 0 Тогда
			Возврат Ложь;
		Иначе
			Возврат Истина;
		КонецЕсли;
	Иначе
		Сотрудники = РегулярнаяОтправкаСотрудников.СотрудникиБронированияКомандировок(СистемаБронирования, Неопределено, Неопределено, Неопределено);
		Возврат ?(Сотрудники.Количество() = 0, Ложь, Истина);
	КонецЕсли;

КонецФункции

&НаСервере
Функция СтруктураРезультатСохраненияНастройкиНаСервере()

	Возврат РегулярнаяОтправкаСотрудников.СтруктураРезультатСохраненияНастройки();

КонецФункции

&НаКлиенте
Процедура НачатьОтправкуСотрудников()

	РезультатВыполненияДлительнойОперации = Неопределено;
	ДлительнаяОперация = НачатьОтправкуСотрудниковНаСервере(УникальныйИдентификатор, СистемаБронирования);

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст = ТекстОтправкаСотрудников;
	ПараметрыОжидания.ОповещениеПользователя.Пояснение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 завершена'"), ТекстОтправкаСотрудников);

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершитьОтправкуСотрудников", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервереБезКонтекста
Функция НачатьОтправкуСотрудниковНаСервере(ИдентификаторФормы, СистемаБронирования)

	ПараметрыПроцедуры = Новый Структура("СистемаБронирования, ФизическоеЛицо");
	ПараметрыПроцедуры.СистемаБронирования = СистемаБронирования;
	ПараметрыПроцедуры.ФизическоеЛицо = Неопределено;

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Отправка информации о сотрудниках в систему %1'"), Строка(СистемаБронирования));

	Возврат ДлительныеОперации.ВыполнитьВФоне("РегулярнаяОтправкаСотрудников.ОтправитьСотрудниковВСистемуБронированияДлительнаяОперация",
		ПараметрыПроцедуры, ПараметрыВыполнения);

КонецФункции

&НаКлиенте
Процедура ЗавершитьОтправкуСотрудников(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.ПодробноеПредставлениеОшибки;
	КонецЕсли;

	ЗаполнитьРезультатОтправкиСотрудников(Результат.АдресРезультата);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРезультатОтправкиСотрудников(АдресРезультата)

	РезультатВыполненияДлительнойОперации = ПолучитьИзВременногоХранилища(АдресРезультата);
	СохранитьНастройкиДоИзменений();
	УстановитьСвойстваЭлементовРезультатаПоследнейОтправки(ЭтотОбъект);
	Модифицированность = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройкиЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		РезультатСохранения = СтруктураРезультатСохраненияНастройкиНаСервере();
		СохранитьНастройкиНаСервере(РезультатСохранения);
		Модифицированность = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
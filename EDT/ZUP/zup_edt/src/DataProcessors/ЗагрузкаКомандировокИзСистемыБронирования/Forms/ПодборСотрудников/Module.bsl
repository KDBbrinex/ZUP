
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДанныеФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого СтрокаТаблицы Из ФизическиеЛица Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.СотрудникСсылка) Тогда
			ИндексСтроки = ФизическиеЛица.Индекс(СтрокаТаблицы);
			ТекстСообщения = НСтр("ru = 'Сотрудник не выбран.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ФизическиеЛица[" + ИндексСтроки + "].СотрудникСсылка", , Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = РезультатПодбораСотрудников();
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДанныеФормы()
	
	ПредставлениеПоездки = Параметры.ПредставлениеПоездки;
	Организация = Параметры.Организация;
	
	ЕстьНеЗаполненныеСотрудники = Ложь;
	Для Каждого СтрокаТаблицы Из Параметры.ФизическиеЛица Цикл
		НоваяСтрока = ФизическиеЛица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.ФИО = СтрокаТаблицы.Фамилия + " " + СтрокаТаблицы.Имя + " " + СтрокаТаблицы.Отчество;
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.СотрудникСсылка) Тогда
			ЕстьНеЗаполненныеСотрудники = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ПредупреждениеГруппа.Видимость = ЕстьНеЗаполненныеСотрудники;
	
КонецПроцедуры

&НаСервере
Функция РезультатПодбораСотрудников()
	
	ТаблицаФизическихЛиц = ФизическиеЛица.Выгрузить();
	
	МассивСотрудников = ТаблицаФизическихЛиц.ВыгрузитьКолонку("СотрудникСсылка");
	СоответствиеФизическихЛиц = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивСотрудников, "ФизическоеЛицо");
	
	Результат = Новый Массив;
	Для Каждого СтрокаТаблицы Из ТаблицаФизическихЛиц Цикл
		ОписаниеСтроки = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаТаблицы);
		ОписаниеСтроки.Ссылка = СоответствиеФизическихЛиц[СтрокаТаблицы.СотрудникСсылка];
		Результат.Добавить(ОписаниеСтроки);
	КонецЦикла;
	
	Возврат ОбщегоНазначения.ФиксированныеДанные(Результат);
	
КонецФункции

#КонецОбласти


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ИспользуемыеСайты(ПараметрыПроцедуры, АдресХранилища) Экспорт
	
	Результат = Новый Массив;
	НоваяСтруктураОтвета = Новый ФиксированнаяСтруктура(
		"Сайт, ИмяПользователя, Ошибка",
		Справочники.ИсточникиИнформацииОКандидатах.ПустаяСсылка(), "", Ложь);

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииРекрутинговыхСайтов.РекрутинговыйСайт КАК Сайт,
	|	НастройкиИнтеграцииРекрутинговыхСайтов.ИмяПользователя КАК ИмяПользователя
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииРекрутинговыхСайтов КАК НастройкиИнтеграцииРекрутинговыхСайтов
	|ГДЕ
	|	НастройкиИнтеграцииРекрутинговыхСайтов.Пользователь = &ТекущийПользователь
	|	И НастройкиИнтеграцииРекрутинговыхСайтов.Использовать");
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СтруктураОтвета = Новый Структура(НоваяСтруктураОтвета);
			ЗаполнитьЗначенияСвойств(СтруктураОтвета, Выборка);
			МаркерДоступаАктивен = ИнтеграцияРекрутинговыхСайтов.МаркерДоступаАктивен(Выборка.Сайт, Выборка.ИмяПользователя);
			Если Не МаркерДоступаАктивен Тогда
				СтруктураОтвета.Ошибка = Истина;
			КонецЕсли;
			Результат.Добавить(СтруктураОтвета);
		КонецЦикла;
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецФункции

#КонецОбласти

#КонецЕсли
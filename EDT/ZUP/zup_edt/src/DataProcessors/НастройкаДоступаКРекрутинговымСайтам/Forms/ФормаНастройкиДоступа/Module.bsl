
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Сайт") Тогда
		Если Параметры.Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
			ПодключениеHeadHunter = Истина;
		ИначеЕсли Параметры.Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Zarplata() Тогда
			ПодключениеZarplata = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ПодключениеHeadHunter Или ПодключениеZarplata Тогда
		ОбновитьВидимостьЭлементов(ЭтаФорма);
	Иначе
		ПодключитьНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЛоготипHeadHunterНажатие(Элемент)
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://hh.ru/");
КонецПроцедуры

&НаКлиенте
Процедура ЛоготипSuperJobНажатие(Элемент)
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://www.superjob.ru/");
КонецПроцедуры

&НаКлиенте
Процедура ЛоготипRabotaНажатие(Элемент)
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://www.rabota.ru/");
КонецПроцедуры

&НаКлиенте
Процедура ЛоготипZarplataНажатие(Элемент)
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://www.zarplata.ru/");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПройтиАвторизациюHeadHunter(Команда)

	ПодключениеHeadHunter = Истина;
	ОбновитьВидимостьЭлементов(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКодHeadHunter(Команда)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(СтрокаАвторизацииHeadHunter());
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьHeadHunter(Команда)
	
	Если Не ЗначениеЗаполнено(КодАвторизацииHeadHunter) Тогда
		ТекстПредупреждения = НСтр("ru = 'Введите код авторизации.'");
	Иначе
		Если СтруктураМаркеровHeadHunterПолучена() Тогда
			ПодключитьНаКлиенте();
			ТекстПредупреждения = НСтр("ru = 'Авторизация на сайте HeadHunter пройдена успешно.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Не удалось авторизоваться. Введен неверный код авторизации.'");
		КонецЕсли;
	КонецЕсли;
	
	ИнтеграцияРекрутинговыхСайтовВнутреннийКлиент.ОтобразитьПредупреждение(ТекстПредупреждения);

КонецПроцедуры

&НаКлиенте
Процедура ОтключитьHeadHunter(Команда)
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter();
	ОтключитьСайт(Сайт);
	ПодключенHeadHunter = Ложь;
	ОбновитьВидимостьЭлементов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПройтиАвторизациюSuperJob(Команда)
	
	ПодключениеSuperJob = Истина;
	ОбновитьВидимостьЭлементов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьSuperJob(Команда)
	
	Если Не ЗначениеЗаполнено(ПарольSuperJob) Тогда
		ТекстПредупреждения = НСтр("ru = 'Введите пароль SuperJob.'");
	Иначе
		Если СтруктураМаркеровSuperJobПолучена() Тогда
			ПодключитьНаКлиенте();
			ОбновитьВидимостьЭлементов(ЭтаФорма);
			ТекстПредупреждения = НСтр("ru = 'Авторизация на сайте SuperJob пройдена успешно.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Не удалось авторизоваться. Введена неправильная пара логин и пароль.'");
		КонецЕсли;
	КонецЕсли;
	
	ИнтеграцияРекрутинговыхСайтовВнутреннийКлиент.ОтобразитьПредупреждение(ТекстПредупреждения);

КонецПроцедуры

&НаКлиенте
Процедура ОтключитьSuperJob(Команда)
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob();
	ОтключитьСайт(Сайт);
	ПодключенSuperJob = Ложь;
	ОбновитьВидимостьЭлементов(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ПройтиАвторизациюRabota(Команда)
	
	ПодключениеRabota = Истина;
	ОбновитьВидимостьЭлементов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьRabota(Команда)
	
	Если Не ЗначениеЗаполнено(ПарольRabota) Тогда
		ТекстПредупреждения = НСтр("ru = 'Введите пароль RABOTA.RU.'");
	Иначе
		Если СтруктураМаркеровRabotaПолучена() Тогда
			ПодключитьНаКлиенте();
			ОбновитьВидимостьЭлементов(ЭтаФорма);
			ТекстПредупреждения = НСтр("ru = 'Авторизация на сайте RABOTA.RU пройдена успешно.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Не удалось авторизоваться. Введена неправильная пара логин и пароль.'");
		КонецЕсли;
	КонецЕсли;

	ИнтеграцияРекрутинговыхСайтовВнутреннийКлиент.ОтобразитьПредупреждение(ТекстПредупреждения);

КонецПроцедуры

&НаКлиенте
Процедура ОтключитьRabota(Команда)
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota();
	ОтключитьСайт(Сайт);
	ПодключенRabota = Ложь;
	ОбновитьВидимостьЭлементов(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ПройтиАвторизациюZarplata(Команда)
	
	ПодключениеZarplata = Истина;
	ОбновитьВидимостьЭлементов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКодZarplata(Команда)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(СтрокаАвторизацииZarplata());
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьZarplata(Команда)
	
	Если Не ЗначениеЗаполнено(КодАвторизацииZarplata) Тогда
		ТекстПредупреждения = НСтр("ru = 'Введите код авторизации.'");
	Иначе
		Если СтруктураМаркеровZarplataПолучена() Тогда
			ПодключитьНаКлиенте();
			ТекстПредупреждения = НСтр("ru = 'Авторизация на сайте Зарплата.ру пройдена успешно.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Не удалось авторизоваться. Введен неверный код авторизации.'");
		КонецЕсли;
	КонецЕсли;
	
	ИнтеграцияРекрутинговыхСайтовВнутреннийКлиент.ОтобразитьПредупреждение(ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьZarplata(Команда)
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Zarplata();
	ОтключитьСайт(Сайт);
	ПодключенZarplata = Ложь;
	ОбновитьВидимостьЭлементов(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодключитьНаКлиенте()
	
	ДлительнаяОперация = ДлительнаяОперацияПодключения();
	Если ЗначениеЗаполнено(ДлительнаяОперация) Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершитьНаКлиентеПодключениеВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДлительнаяОперацияПодключения()

	ДлительнаяОперация = Неопределено;
	ПараметрыПроцедуры = Новый Структура();
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.Вставить("НаименованиеФоновогоЗадания",
			НСтр("ru = 'Подключение к рекрутинговым сайтам'"));
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.НастройкаДоступаКРекрутинговымСайтам.ИспользуемыеСайты",
		ПараметрыПроцедуры,
		ПараметрыВыполненияВФоне);
	Возврат ДлительнаяОперация;

КонецФункции

&НаКлиенте
Процедура ЗавершитьНаКлиентеПодключениеВФоне(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	Если Не ДлительнаяОперация.Статус = "Выполнено" Тогда
		Возврат;
	КонецЕсли;
		
	Результат = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтруктураОтвета Из Результат Цикл
		Если СтруктураОтвета.Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter() Тогда
			ИмяПользователяHeadHunter = СтруктураОтвета.ИмяПользователя;
			ПодключенHeadHunter = Не СтруктураОтвета.Ошибка;
		ИначеЕсли СтруктураОтвета.Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob() Тогда
			ИмяПользователяSuperJob = СтруктураОтвета.ИмяПользователя;
			ПодключенSuperJob = Не СтруктураОтвета.Ошибка;
		ИначеЕсли СтруктураОтвета.Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota() Тогда
			ИмяПользователяRabota = СтруктураОтвета.ИмяПользователя;
			ПодключенRabota = Не СтруктураОтвета.Ошибка;
		ИначеЕсли СтруктураОтвета.Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Zarplata() Тогда
			ИмяПользователяZarplata = СтруктураОтвета.ИмяПользователя;
			ПодключенZarplata = Не СтруктураОтвета.Ошибка;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьВидимостьЭлементов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьВидимостьЭлементов(Форма)
	
	Для Каждого Сайт Из СписокСайтов() Цикл
		Если Форма["Подключен" + Сайт] Тогда
			Форма.Элементы["Страницы" + Сайт].ТекущаяСтраница = Форма.Элементы["СтраницаПодключен" + Сайт];
		ИначеЕсли Форма["Подключение" + Сайт] Тогда
			Форма.Элементы["Страницы" + Сайт].ТекущаяСтраница = Форма.Элементы["СтраницаПодключение" + Сайт];
		Иначе
			Форма.Элементы["Страницы" + Сайт].ТекущаяСтраница = Форма.Элементы["СтраницаНеПодключен" + Сайт];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СписокСайтов()
	
	Результат = Новый Массив;
	Результат.Добавить("HeadHunter");
	Результат.Добавить("SuperJob");
	Результат.Добавить("Rabota");
	Результат.Добавить("Zarplata");

	Возврат Результат;
	
КонецФункции

&НаСервере
Функция СтрокаАвторизацииHeadHunter()
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter();
	Возврат ИнтеграцияРекрутинговыхСайтов.СтрокаАвторизацииOAuth(Сайт);
	
КонецФункции

&НаСервере
Функция СтрокаАвторизацииZarplata()
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Zarplata();
	Возврат ИнтеграцияРекрутинговыхСайтов.СтрокаАвторизацииOAuth(Сайт);
	
КонецФункции

&НаСервере
Функция СтруктураМаркеровHeadHunterПолучена()
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.HeadHunter();
	СтруктураМаркеров = ИнтеграцияРекрутинговыхСайтов.СтруктураМаркеров(Сайт, КодАвторизацииHeadHunter);
	Возврат Не (СтруктураМаркеров = Неопределено);
	
КонецФункции

&НаСервере
Функция СтруктураМаркеровSuperJobПолучена()
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.SuperJob();
	СтруктураМаркеров = ИнтеграцияРекрутинговыхСайтов.СтруктураМаркеров(Сайт, Неопределено, Ложь, ИмяПользователяSuperJob, ПарольSuperJob);
	Возврат Не (СтруктураМаркеров = Неопределено);
	
КонецФункции

&НаСервере
Функция СтруктураМаркеровRabotaПолучена()
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Rabota();
	СтруктураМаркеров = ИнтеграцияРекрутинговыхСайтов.СтруктураМаркеров(Сайт, Неопределено, Ложь, ИмяПользователяRabota, ПарольRabota);
	Возврат Не (СтруктураМаркеров = Неопределено);
	
КонецФункции

&НаСервере
Функция СтруктураМаркеровZarplataПолучена()
	
	Сайт = ИнтеграцияРекрутинговыхСайтовКлиентСервер.Zarplata();
	СтруктураМаркеров = ИнтеграцияРекрутинговыхСайтов.СтруктураМаркеров(Сайт, КодАвторизацииZarplata);
	Возврат Не (СтруктураМаркеров = Неопределено);
	
КонецФункции

&НаСервере
Процедура ОтключитьСайт(Сайт)
	
	МенеджерЗаписи = РегистрыСведений.НастройкиИнтеграцииРекрутинговыхСайтов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Пользователь = Пользователи.ТекущийПользователь();
	МенеджерЗаписи.РекрутинговыйСайт = Сайт;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Использовать = Ложь;
		МенеджерЗаписи.Записать();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
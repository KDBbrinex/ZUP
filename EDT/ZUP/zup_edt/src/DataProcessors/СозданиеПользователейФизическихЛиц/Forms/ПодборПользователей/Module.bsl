
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого КлючИЗначение Из Параметры.ФизическиеЛицаПользователей Цикл 
		НоваяСтрока = Сотрудники.Добавить();
		НоваяСтрока.Пользователь = КлючИЗначение.Ключ;
		НоваяСтрока.ФизическоеЛицо = КлючИЗначение.Значение;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Отказ = Ложь;
	УстановитьФизическиеЛицаПользователей(Отказ);
	
	Если Не Отказ Тогда 
		Оповестить("УстановленыФизическиеЛицаПользователей", , ЭтотОбъект.ВладелецФормы);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьФизическиеЛицаПользователей(Отказ)
	
	Для Каждого СтрокаСотрудника Из Сотрудники Цикл 
		
		Если Не ЗначениеЗаполнено(СтрокаСотрудника.ФизическоеЛицо) Тогда 
			ИндексСтроки = Сотрудники.Индекс(СтрокаСотрудника);
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не указан сотрудник'"), , "Сотрудники[" + Формат(ИндексСтроки, "ЧН=0; ЧГ=0") + "].ФизическоеЛицо", , Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаСотрудника.Пользователь) Тогда 
			ИндексСтроки = Сотрудники.Индекс(СтрокаСотрудника);
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не указан пользователь'"), , "Сотрудники[" + Формат(ИндексСтроки, "ЧН=0; ЧГ=0") + "].Пользователь", , Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаСотрудника Из Сотрудники Цикл 
		ПользовательОбъект = СтрокаСотрудника.Пользователь.ПолучитьОбъект();
		ПользовательОбъект.ФизическоеЛицо = СтрокаСотрудника.ФизическоеЛицо;
		ПользовательОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ПериодРегистрации,Организация");
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтотОбъект, "ПериодРегистрации", "ПериодРегистрацииСтрокой");
	
	ТаблицаОтсутствий = Неопределено;
	Параметры.Свойство("ТаблицаОтсутствий", ТаблицаОтсутствий);
	
	Если ТаблицаОтсутствий = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	Для Каждого СтрокаТаблицыОтсутствий Из ТаблицаОтсутствий Цикл
		
		СтрокаСоздаваемыхДокументов = СоздаваемыеДокументы.Добавить();
		СтрокаСоздаваемыхДокументов.ДокументСоздан = -1;
		
		ЗаполнитьЗначенияСвойств(СтрокаСоздаваемыхДокументов, СтрокаТаблицыОтсутствий);
		СтрокаСоздаваемыхДокументов.ПредставлениеПериода = ОтсутствияСотрудниковКлиентСервер.ПредставлениеПериодаОтсутствия(
			СтрокаСоздаваемыхДокументов.Начало,
			СтрокаСоздаваемыхДокументов.Окончание,
			СтрокаСоздаваемыхДокументов.ЧастьСмены,
			СтрокаСоздаваемыхДокументов.КоличествоЧасов);
		
		ПолноеИмяМетаданныхДокумента = ОтсутствияСотрудниковКлиентСервер.ИменаДокументовВидовОтсутствия().Получить(СтрокаСоздаваемыхДокументов.ВидОтсутствия);
		МетаданныеДокумента = Метаданные.НайтиПоПолномуИмени(ПолноеИмяМетаданныхДокумента);
		
		Если ОтсутствияСотрудников.СозданиеДокументаДоступно(МетаданныеДокумента) Тогда
			СтрокаСоздаваемыхДокументов.ПолноеИмяМетаданныхДокумента = ПолноеИмяМетаданныхДокумента;
			СтрокаСоздаваемыхДокументов.ПредставлениеСоздаваемогоДокумента = ПредставлениеКомадыОформленияНачислений(МетаданныеДокумента.Синоним);
		Иначе
			СтрокаСоздаваемыхДокументов.ПредставлениеСоздаваемогоДокумента = НСтр("ru='Оформить начисление'");
		КонецЕсли;
		
	КонецЦикла;
	
	ОтсутствияСотрудников.СоздатьКонтекстноеМенюОформленияОтсутствий(
		ЭтотОбъект, "СоздаваемыеДокументыКонтекстноеМенюГруппа", "СоздаваемыеДокументыКонтекстноеМенюПрочиеГруппа");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы Тогда
		
		Если Не ВсеДокументыПроведены() Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыСоздаваемыеДокументы

&НаКлиенте
Процедура СоздаваемыеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Поле.Имя = "СоздаваемыеДокументыПредставлениеСоздаваемогоДокумента" Тогда
		
		ТекущиеДанные = Элементы.СоздаваемыеДокументы.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено
			И Не ПустаяСтрока(ТекущиеДанные.ПолноеИмяМетаданныхДокумента) Тогда
			
			Оповещение = Новый ОписаниеОповещения("ОбновитьТекущиеДанные", ЭтотОбъект);
			Если ТекущиеДанные.ДокументСоздан > 0 Тогда
				
				ОткрытьФорму(
					ТекущиеДанные.ПолноеИмяМетаданныхДокумента + ".ФормаОбъекта",
					Новый Структура("Ключ", ТекущиеДанные.ДанныеСоздаваемогоДокумента),
					ЭтотОбъект, Истина, , , Оповещение);
				
			Иначе
				
				ТекущиеДанные.ДанныеСоздаваемогоДокумента = СсылкаНовогоДокументаНачисленияОтсутствий(ТекущиеДанные.ПолноеИмяМетаданныхДокумента);
				
				ОписаниеОтсутствия = ОтсутствияСотрудниковКлиентСервер.ОписаниеОтсутствия();
				ЗаполнитьЗначенияСвойств(ОписаниеОтсутствия, ТекущиеДанные);
				
				ОписаниеОтсутствия.Организация = Организация;
				ОписаниеОтсутствия.ПериодРегистрации = ПериодРегистрации;
				
				ПараметрыОткрытия = Новый Структура;
				ПараметрыОткрытия.Вставить("СсылкаНового", ТекущиеДанные.ДанныеСоздаваемогоДокумента);
				
				ОтсутствияСотрудниковКлиент.ОткрытьФормуНовогоДокументаНачисленияОтсутствий(
					ТекущиеДанные.ПолноеИмяМетаданныхДокумента, ОписаниеОтсутствия, ЭтотОбъект, Оповещение, ПараметрыОткрытия);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздаваемыеДокументыПриАктивизацииСтроки(Элемент)
	
	УстановитьКомандыОформленияОтсутствий(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокументыНачисления(Команда)
	
	СоздатьДокументыНачисленияНаСервере();
	Если ВсеДокументыСозданыИПроведены() Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ПредупреждениеОбАвтоматическиСозданныхДокументах",
			"Видимость",
			Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ФормаСоздатьДокументы",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ФормаЗакрыть",
			"КнопкаПоУмолчанию",
			Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОформитьОтсутствие(Команда)
	
	ТекущиеДанные = Элементы.СоздаваемыеДокументы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ТекущиеДанные.ПолноеИмяМетаданныхДокумента = "Документ." + Команда.Имя;
		
		ЭлементСписка = ДоступныеДляСозданияДокументыНачислений.НайтиПоЗначению(ТекущиеДанные.ПолноеИмяМетаданныхДокумента);
		Если ЭлементСписка <> Неопределено Тогда
			
			ТекущиеДанные.ПредставлениеСоздаваемогоДокумента = ПредставлениеКомадыОформленияНачислений(ЭлементСписка.Представление);
			
			Если ЕстьСтрокиСНеВыбраннымДокументомНачислений() Тогда
				
				ТекстВопроса = СтрШаблон(НСтр("ru='Выбрать ""%1"" для всех отсутствий,
					|для которых не выбран документ начисления?'"),
					ЭлементСписка.Представление);
				
				ДополнительныеПараметры = Новый Структура;
				ДополнительныеПараметры.Вставить("ПолноеИмяМетаданныхДокумента", ТекущиеДанные.ПолноеИмяМетаданныхДокумента);
				ДополнительныеПараметры.Вставить("ПредставлениеСоздаваемогоДокумента", ТекущиеДанные.ПредставлениеСоздаваемогоДокумента);
				
				Оповещение = Новый ОписаниеОповещения("УстановитьДокументВсемСтрокамБезДокументов", ЭтотОбъект, ДополнительныеПараметры);
				ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьТекущиеДанные(Результат, ДополнительныеПараметры) Экспорт
	
	ТекущиеДанные = Элементы.СоздаваемыеДокументы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ДанныеДокумента = СведенияОДокументе(ТекущиеДанные.ДанныеСоздаваемогоДокумента);
		Если ЗначениеЗаполнено(ДанныеДокумента.Ссылка) Тогда
			ОбновитьСтрокуСоздаваемогоДокумента(ТекущиеДанные, ДанныеДокумента);
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьКомандыОформленияОтсутствий(ЭтотОбъект);
	
	ЕстьНеПроведенные = Ложь;
	ЕстьНеОбработанные = Ложь;
	Для Каждого СтрокаСоздаваемогоДокумента Из СоздаваемыеДокументы Цикл
		
		Если СтрокаСоздаваемогоДокумента.ДокументСоздан = - 1 Тогда
			ЕстьНеОбработанные = Истина;
		ИначеЕсли СтрокаСоздаваемогоДокумента.ДокументСоздан <> 14 Тогда
			ЕстьНеПроведенные = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПредупреждениеОбАвтоматическиСозданныхДокументах",
		"Видимость",
		Не ЕстьНеОбработанные И ЕстьНеПроведенные);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСтрокуСоздаваемогоДокумента(СтрокаСоздаваемогоДокумента, ДанныеДокумента)
	
	Если ДанныеДокумента.Проведен Тогда
		СтрокаСоздаваемогоДокумента.ДокументСоздан = 14;
	ИначеЕсли ДанныеДокумента.ПометкаУдаления Тогда
		СтрокаСоздаваемогоДокумента.ДокументСоздан = 13;
	Иначе
		СтрокаСоздаваемогоДокумента.ДокументСоздан = 12;
	КонецЕсли;
	
	СтрокаСоздаваемогоДокумента.ПредставлениеСоздаваемогоДокумента = Строка(СтрокаСоздаваемогоДокумента.ДанныеСоздаваемогоДокумента);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СведенияОДокументе(ПроверяемаяСсылка)
	
	СведенияОДокументе = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПроверяемаяСсылка, "Ссылка,Проведен,ПометкаУдаления");
	Возврат СведенияОДокументе;
	
КонецФункции

&НаСервере
Процедура СоздатьДокументыНачисленияНаСервере()
	
	Для Каждого СтрокаСоздаваемогоДокумента Из СоздаваемыеДокументы Цикл
		
		Если СтрокаСоздаваемогоДокумента.ДокументСоздан < 0 Тогда
			
			Если Не ЗначениеЗаполнено(СтрокаСоздаваемогоДокумента.ПолноеИмяМетаданныхДокумента) Тогда
				Продолжить;
			КонецЕсли;
			
			ПолноеИмяМетаданныхДокумента = СтрокаСоздаваемогоДокумента.ПолноеИмяМетаданныхДокумента;
			СтрокаСоздаваемогоДокумента.ДанныеСоздаваемогоДокумента = СсылкаНовогоДокументаНачисленияОтсутствий(ПолноеИмяМетаданныхДокумента);
			
			ОписаниеОтсутствия = ОтсутствияСотрудниковКлиентСервер.ОписаниеОтсутствия();
			ЗаполнитьЗначенияСвойств(ОписаниеОтсутствия, СтрокаСоздаваемогоДокумента);
			
			ОписаниеОтсутствия.Организация = Организация;
			ОписаниеОтсутствия.ПериодРегистрации = ПериодРегистрации;
			
			ЗначенияЗаполнения = ОтсутствияСотрудниковКлиентСервер.ЗначенияЗаполненияДокумента(
				ПолноеИмяМетаданныхДокумента, ОписаниеОтсутствия);
			
			ОбъектДокумента = Документы[Метаданные.НайтиПоПолномуИмени(ПолноеИмяМетаданныхДокумента).Имя].СоздатьДокумент();
			ОбъектДокумента.Заполнить(ЗначенияЗаполнения);
			ОбъектДокумента.УстановитьСсылкуНового(СтрокаСоздаваемогоДокумента.ДанныеСоздаваемогоДокумента);
			
			ОбъектДокумента.Записать(РежимЗаписиДокумента.Запись);
			
			ОбновитьСтрокуСоздаваемогоДокумента(СтрокаСоздаваемогоДокумента, ОбъектДокумента);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ЕстьСтрокиСНеВыбраннымДокументомНачислений()
	
	Для Каждого СтрокаСоздаваемогоДокумента Из СоздаваемыеДокументы Цикл
		
		Если ПустаяСтрока(СтрокаСоздаваемогоДокумента.ПолноеИмяМетаданныхДокумента) Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Функция ВсеДокументыСозданыИПроведены()
	
	Для Каждого СтрокаСоздаваемогоДокумента Из СоздаваемыеДокументы Цикл
		
		Если СтрокаСоздаваемогоДокумента.ДокументСоздан = -1 Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКомандыОформленияОтсутствий(Форма)
	
	ТекущийВидОтсутствия = Неопределено;
	
	УстанавливатьКомандыКонтекстногоМеню = Ложь;
	Если Форма.Элементы.СоздаваемыеДокументы.ТекущаяСтрока <> Неопределено Тогда
		
		ДанныеСтроки = Форма.СоздаваемыеДокументы.НайтиПоИдентификатору(Форма.Элементы.СоздаваемыеДокументы.ТекущаяСтрока);
		
		Если ДанныеСтроки <> Неопределено И ДанныеСтроки.ДокументСоздан < 0 Тогда
			
			ТекущийВидОтсутствия = ДанныеСтроки.ВидОтсутствия;
			УстанавливатьКомандыКонтекстногоМеню = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"СоздаваемыеДокументыКонтекстноеМенюГруппа",
		"Видимость",
		УстанавливатьКомандыКонтекстногоМеню);
	
	Если УстанавливатьКомандыКонтекстногоМеню Тогда
		
		ОтсутствияСотрудниковКлиентСервер.УстановитьОтображениеКомандСозданияДокументовНачисленияКонтекстногоМеню(
			Форма, ТекущийВидОтсутствия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеКомадыОформленияНачислений(СинонимДокумента)
	
	Возврат НСтр("ru='Оформить'") + """" + СинонимДокумента + """";
	
КонецФункции

&НаСервереБезКонтекста
Функция СсылкаНовогоДокументаНачисленияОтсутствий(ПолноеИмяМетаданныхДокумента)
	
	МетаданныеДокумента = Метаданные.НайтиПоПолномуИмени(ПолноеИмяМетаданныхДокумента);
	Возврат Документы[МетаданныеДокумента.Имя].ПолучитьСсылку();
	
КонецФункции

&НаКлиенте
Процедура УстановитьДокументВсемСтрокамБезДокументов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Для Каждого СтрокаСоздаваемогоДокумента Из СоздаваемыеДокументы Цикл
			
			Если ПустаяСтрока(СтрокаСоздаваемогоДокумента.ПолноеИмяМетаданныхДокумента) Тогда
				
				СтрокаСоздаваемогоДокумента.ПолноеИмяМетаданныхДокумента = ДополнительныеПараметры.ПолноеИмяМетаданныхДокумента;
				СтрокаСоздаваемогоДокумента.ПредставлениеСоздаваемогоДокумента = ДополнительныеПараметры.ПредставлениеСоздаваемогоДокумента;
				
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ВсеДокументыПроведены()
	
	СписокДокументов = Новый Массив;
	Для Каждого СтрокаСоздаваемогДокумента Из СоздаваемыеДокументы Цикл
		
		Если ЗначениеЗаполнено(СтрокаСоздаваемогДокумента.ДанныеСоздаваемогоДокумента) Тогда
			СписокДокументов.Добавить(СтрокаСоздаваемогДокумента.ДанныеСоздаваемогоДокумента);
		КонецЕсли;
		
	КонецЦикла;
	
	Если СписокДокументов.Количество() > 0
		И Не ДокументыПроведены(СписокДокументов) Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВсеДокументыПроведеныЗавершение", ЭтотОбъект);
		
		ТекстВопроса = НСтр("ru='В таблице остались не проведенные документы.
			|Все равно закрыть форму?'");
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура ВсеДокументыПроведеныЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СоздаваемыеДокументы.Очистить();
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДокументыПроведены(СписокДокументов)
	
	ЗначенияРеквизита = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокДокументов, "Проведен");
	Для Каждого ЗначениеРеквизита Из ЗначенияРеквизита Цикл
		
		Если ЗначениеРеквизита.Значение <> Истина Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

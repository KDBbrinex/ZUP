
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрочитатьНастройки();
	УстановитьУсловноеОформлениеГрафика();
	УстановитьОтборПоказыватьНедействительные();
	УстановитьОтборРассылокУведомлений();
	УстановитьДоступностьЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьУведомленияПриИзменении(Элемент)
	
	ИспользоватьУведомленияПриИзмененииНаСервере();
	
	ПодключитьОбработчикОжиданияОбновленияИнтерфейса();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьНедействительные(Команда)
	
	ПоказыватьНедействительные = Не ПоказыватьНедействительные;
	УстановитьОтборПоказыватьНедействительные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементов(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ИспользоватьУведомления",
		"Доступность",
		ПравоДоступа("Изменение", Метаданные.Константы.ИспользоватьУведомления));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеГрафика()
	
	ЭлементОформления = ГрафикКонтроляСамочувствия.УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементОформления.Использование = Истина;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование		= Истина;
	ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("Недействительная");
	ЭлементОтбора.ПравоеЗначение	= Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоказыватьНедействительные()
	
	Параметр = ГрафикКонтроляСамочувствия.Параметры.НайтиЗначениеПараметра(
		Новый ПараметрКомпоновкиДанных("ОтборПоказыватьНедействительные"));
	Параметр.Использование = Ложь;
	
	Элементы.ФормаПоказыватьНедействительные.Пометка = ПоказыватьНедействительные;
	
	Если Не ПоказыватьНедействительные Тогда
		ГрафикКонтроляСамочувствия.Параметры.УстановитьЗначениеПараметра("ОтборПоказыватьНедействительные", Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборРассылокУведомлений()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		РассылкиУведомлений, "ЭтоКонтрольСамочувствия", Истина,,, Истина, 
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
КонецПроцедуры

&НаСервере
Процедура ИспользоватьУведомленияПриИзмененииНаСервере()
	
	Константы.ИспользоватьУведомления.Установить(ИспользоватьУведомления);
	Если ИспользоватьУведомления И Не ИспользоватьШаблоныСообщений Тогда
		ИспользоватьШаблоныСообщений = Истина;
		Константы.ИспользоватьШаблоныСообщений.Установить(ИспользоватьШаблоныСообщений);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОбработчикОжиданияОбновленияИнтерфейса()
	
	ПодключитьОбработчикОжидания("ОбработчикОжиданияОбновленияИнтерфейса", 1, Истина);
	
КонецПроцедуры

&НаКлиенте 
Процедура ОбработчикОжиданияОбновленияИнтерфейса()
	
	ОбновитьИнтерфейс();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройки()
		
	ИспользоватьУведомления = Константы.ИспользоватьУведомления.Получить();
	ИспользоватьШаблоныСообщений = Константы.ИспользоватьШаблоныСообщений.Получить();
	
КонецПроцедуры

#КонецОбласти

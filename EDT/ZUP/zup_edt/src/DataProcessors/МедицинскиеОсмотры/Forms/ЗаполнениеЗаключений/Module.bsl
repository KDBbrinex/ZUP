#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗаключения(Параметры.ДанныеЗаключений);
	Организация = Параметры.Организация;
	
	Если Заключения.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствуют выданные направления на медосмотр.
                               |Заключение можно ввести только при наличии направления.'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого СтрокаТаблицы Из Заключения Цикл
		ИндексСтроки = Заключения.Индекс(СтрокаТаблицы);
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ДатаОсмотра) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Дата осмотра не заполнена в строке %1'"),
				ИндексСтроки + 1);
			Поле = "Заключения[" + ИндексСтроки + "].ДатаОсмотра";
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , Поле, , Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.РезультатОсмотра) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Результат осмотра не заполнен в строке %1'"),
				ИндексСтроки + 1);
			Поле = "Заключения[" + ИндексСтроки + "].РезультатОсмотра";
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , Поле, , Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗаключенияПояснениеРезультатаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НачатьРедактированиеПояснениеРезультата();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаключений = ОписаниеЗаключений();
	ЗаполнитьПараметрыЗаключений(ПараметрыЗаключений);
	
	Закрыть(ПараметрыЗаключений);
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеБезПротивопоказаний(Команда)
	ВсеБезПротивопоказанийНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеЗаключений()
	
	Описание = Новый Структура(
		"Организация,
		|ОписанияЗаключений,
		|Печатать");
	
	Описание.ОписанияЗаключений = Новый Массив;
	Описание.Печатать = Ложь;
	
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеЗаключения()
	
	Описание = Новый Структура(
		"ФизическоеЛицо,
		|Позиция,
		|ДатаОсмотра,
		|Направление,
		|Заключение,
		|РезультатОсмотра,
		|ПояснениеРезультата");
	
	Возврат Описание;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПараметрыЗаключений(ПараметрыЗаключений)
	
	ПараметрыЗаключений.Организация = Организация;
	
	ЗаполнитьОписанияЗаключений(ПараметрыЗаключений.ОписанияЗаключений);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписанияЗаключений(ОписанияНаправлений)
	
	Для Каждого СтрокаТаблицы Из Заключения Цикл
		Описание = ОписаниеЗаключения();
		ЗаполнитьЗначенияСвойств(Описание, СтрокаТаблицы);
		ОписанияНаправлений.Добавить(Описание);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗаключения(ДанныеЗаключений)
	
	МедицинскиеОсмотры.ЗаполнитьВыданныеРанееНаправления(ДанныеЗаключений, Ложь);
	
	НаправленияМассив = ОбщегоНазначения.ВыгрузитьКолонку(ДанныеЗаключений, "Направление");
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(НаправленияМассив, Неопределено);
	ДатыОсмотраНаправлений = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(НаправленияМассив, "ДатаОсмотра");
	
	ЗаключенияМассив = ОбщегоНазначения.ВыгрузитьКолонку(ДанныеЗаключений, "Заключение");
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ЗаключенияМассив, Неопределено);
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ЗаключенияМассив, Документы.ЗаключениеМедицинскогоОсмотра.ПустаяСсылка());
	РеквизитыЗаключений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ЗаключенияМассив, "ДатаОсмотра,РезультатОсмотра,ПояснениеРезультата");
	
	Для Каждого Описание Из Параметры.ДанныеЗаключений Цикл
		Если Не ЗначениеЗаполнено(Описание.Направление) Тогда
			// Нет направления.
			Элементы.НетНаправленийГруппа.Видимость = Истина;
			Продолжить;
		КонецЕсли;
		НоваяСтрока = Заключения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Описание);
		Если ЗначениеЗаполнено(Описание.Заключение) Тогда
			ЗаполнитьЗначенияСвойств(НоваяСтрока, РеквизитыЗаключений[Описание.Заключение]);
		Иначе
			Если ЗначениеЗаполнено(Описание.Направление) Тогда
				НоваяСтрока.ДатаОсмотра = ДатыОсмотраНаправлений[Описание.Направление];
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВсеБезПротивопоказанийНаСервере()
	
	Для Каждого СтрокТаблицы Из Заключения Цикл
		СтрокТаблицы.РезультатОсмотра = Перечисления.РезультатыМедицинскогоОсмотра.НеИмеетПротивопоказаний;
	КонецЦикла;
	
КонецПроцедуры

#Область РедактированиеПояснения

&НаКлиенте
Процедура НачатьРедактированиеПояснениеРезультата()
	
	ТекущиеДанные = Элементы.Заключения.ТекущиеДанные;
	
	ПараметрыОповещения = Новый Структура("ИдентификаторСтроки");
	ПараметрыОповещения.ИдентификаторСтроки = ТекущиеДанные.ПолучитьИдентификатор();
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ЗавершитьРедактированиеПоясненияРезультата", ЭтотОбъект, ПараметрыОповещения);
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(
		ОбработчикОповещения,
		ТекущиеДанные.ПояснениеРезультата,
		НСтр("ru = 'Пояснение результата'"));
		
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактированиеПоясненияРезультата(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Заключения.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторСтроки);
	ТекущиеДанные.ПояснениеРезультата = Результат;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

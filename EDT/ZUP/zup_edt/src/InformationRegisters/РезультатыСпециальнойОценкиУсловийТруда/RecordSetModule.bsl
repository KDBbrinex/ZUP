#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПредыдущийНабор.Должность КАК Должность,
	|	ПредыдущийНабор.Период КАК Период,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРегистрацииИзменений,
	|	NULL КАК КлассУсловийТруда,
	|	NULL КАК Основание,
	|	ИСТИНА КАК УдалитьЗапись
	|ИЗ
	|	РегистрСведений.КлассыУсловийТрудаПоДолжностям КАК ПредыдущийНабор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РезультатыСпециальнойОценкиУсловийТруда КАК ТекущийНабор
	|		ПО ПредыдущийНабор.Должность = ТекущийНабор.РабочееМесто
	|			И ПредыдущийНабор.Период = ТекущийНабор.Период
	|			И ПредыдущийНабор.Основание = ТекущийНабор.Регистратор
	|ГДЕ
	|	ПредыдущийНабор.Основание = &Регистратор
	|	И ТекущийНабор.РабочееМесто ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущийНабор.РабочееМесто,
	|	ТекущийНабор.Период,
	|	НАЧАЛОПЕРИОДА(ТекущийНабор.Период, МЕСЯЦ),
	|	ТекущийНабор.КлассУсловийТруда,
	|	ТекущийНабор.Регистратор,
	|	ЛОЖЬ
	|ИЗ
	|	РегистрСведений.РезультатыСпециальнойОценкиУсловийТруда КАК ТекущийНабор
	|ГДЕ
	|	ТекущийНабор.Регистратор = &Регистратор
	|	И ТекущийНабор.РабочееМесто <> ЗНАЧЕНИЕ(Справочник.ШтатноеРасписание.ПустаяСсылка)"; 
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.КлассыУсловийТрудаПоДолжностям.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Должность.Установить(Выборка.Должность);
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		
		Если Выборка.УдалитьЗапись Тогда
			НаборЗаписей.Записать();
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.КлассУсловийТруда) Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЕсли;
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаписьРС = РеквизитФормыВЗначение("Запись");
	
 	ПараметрыСобытияРС = ЗаписьРС.ДополнительныеСвойства.Получить();
	
	ДеревоОбъект = Новый ДеревоЗначений;
	ДеревоОбъект.Колонки.Добавить("Параметр");
	ДеревоОбъект.Колонки.Добавить("Значение");
	
	Если ТипЗнч(ПараметрыСобытияРС) = Тип("Структура") Тогда
		Для Каждого ЭлементСтруктуры Из ПараметрыСобытияРС Цикл
			ДобавитьСтрокуРекурсивно(ДеревоОбъект.Строки, ЭлементСтруктуры);
		КонецЦикла;
	КонецЕсли;
	ЗначениеВРеквизитФормы(ДеревоОбъект, "ДополнительныеСвойстваЗаписи");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьСтрокуРекурсивно(СтрокиРодителя, ЭлементСтруктуры)
	
	Строка = СтрокиРодителя.Добавить();
	Строка.Параметр = ЭлементСтруктуры.Ключ;
	Если ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Массив") Тогда
		Строка.Значение = "<" + НСтр("ru='Массив значений'") + ">";
	ИначеЕсли ТипЗнч(ЭлементСтруктуры.Значение) = Тип("ТаблицаЗначений") Тогда
		Строка.Значение = "<" + НСтр("ru='Таблица значений'") + ">";
	ИначеЕсли ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Структура") Тогда
		Строка.Значение = "Структура";
		Для Каждого ПодчиненныйЭлемент Из ЭлементСтруктуры.Значение Цикл
			ДобавитьСтрокуРекурсивно(Строка.Строки, ПодчиненныйЭлемент);
		КонецЦикла;
	Иначе
		Строка.Значение = ЭлементСтруктуры.Значение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
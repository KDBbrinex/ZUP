#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Отбор.ФизическоеЛицо.Значение = Отбор.Сотрудник.Значение.ФизическоеЛицо;
	Отбор.ФизическоеЛицо.Использование = Отбор.Сотрудник.Использование;
	
	Сотрудники = ВыгрузитьКолонку("Сотрудник");
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудники, "ВидЗанятости");
	
	Если КадровыеДанные.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось зарегистрировать причину увольнения для сотрудников.
                               |Регистрация причин увольнения возможна только для сотрудников на основном месте работы и внешних совместителей.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	ВидыЗанятости = Новый Массив;
	ВидыЗанятости.Добавить(Перечисления.ВидыЗанятости.ОсновноеМестоРаботы);
	ВидыЗанятости.Добавить(Перечисления.ВидыЗанятости.Совместительство);
	
	ОтборСтрок = Новый Структура("Сотрудник");
	Для Каждого СтрокаНабора Из ЭтотОбъект Цикл
		ОтборСтрок.Сотрудник = СтрокаНабора.Сотрудник;
		НайденныеСтроки = КадровыеДанные.НайтиСтроки(ОтборСтрок);
		Если НайденныеСтроки.Количество() = 0 Тогда
			СообщитьОшибкуПоСотруднику(Отказ, СтрокаНабора.Сотрудник);
			Возврат;
		КонецЕсли;
		Если ВидыЗанятости.Найти(НайденныеСтроки[0].ВидЗанятости) = Неопределено Тогда
			СообщитьОшибкуПоСотруднику(Отказ, СтрокаНабора.Сотрудник);
			Возврат;
		КонецЕсли;
		СтрокаНабора.ФизическоеЛицо = НайденныеСтроки[0].ФизическоеЛицо;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура СообщитьОшибкуПоСотруднику(Отказ, Сотрудник)
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Не удалось зарегистрировать причину увольнения для сотрудника %1.
              |Регистрация причин увольнения возможна только для сотрудников на основном месте работы и внешних совместителей.'"), 
		Сотрудник);
		
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Сотрудник, , , Отказ);
	
КонецПроцедуры

#КонецОбласти 

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
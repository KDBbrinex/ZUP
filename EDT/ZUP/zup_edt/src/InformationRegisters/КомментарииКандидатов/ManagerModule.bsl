#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	// Ограничения доступа в ролях:
	//
	// Чтение:
	// КомментарииКандидатов ГДЕ (КомментарииКандидатов.Пользователь = &ТекущийПользователь
	// 			ИЛИ КомментарииКандидатов.Доступность = ЗНАЧЕНИЕ(Перечисление.ДоступностьКомментарияКандидата.Общедоступный))
	//
	// Изменение:
	// ГДЕ Пользователь = &ТекущийПользователь
	//
	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	Доступность = ЗНАЧЕНИЕ(Перечисление.ДоступностьКомментарияКандидата.Общедоступный)
	|	ИЛИ ЭтоАвторизованныйПользователь(Пользователь)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ЭтоАвторизованныйПользователь(Пользователь)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнениеДоступностиПоПризнакуСкрытый(ПараметрыОбновления) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
		|	КомментарииКандидатов.Кандидат КАК Кандидат
		|ИЗ
		|	РегистрСведений.КомментарииКандидатов КАК КомментарииКандидатов
		|ГДЕ
		|	КомментарииКандидатов.Доступность = ЗНАЧЕНИЕ(Перечисление.ДоступностьКомментарияКандидата.ПустаяСсылка)";
	РезультатЗапроса = Запрос.Выполнить();
	
	ПараметрыОбновления.ОбработкаЗавершена = Ложь;
	Если РезультатЗапроса.Пустой() Тогда
		ПараметрыОбновления.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;	
	
	МассивКандидатов = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Кандидат");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Кандидаты", МассивКандидатов);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КомментарииКандидатов.Период КАК Период,
		|	КомментарииКандидатов.Кандидат КАК Кандидат,
		|	КомментарииКандидатов.Пользователь КАК Пользователь,
		|	КомментарииКандидатов.Комментарий КАК Комментарий,
		|	ВЫБОР
		|		КОГДА КомментарииКандидатов.Доступность = ЗНАЧЕНИЕ(Перечисление.ДоступностьКомментарияКандидата.ПустаяСсылка)
		|			ТОГДА ВЫБОР
		|					КОГДА КомментарииКандидатов.УдалитьСкрытый = ИСТИНА
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ДоступностьКомментарияКандидата.ТолькоДляАвтора)
		|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ДоступностьКомментарияКандидата.Общедоступный)
		|				КОНЕЦ
		|		ИНАЧЕ КомментарииКандидатов.Доступность
		|	КОНЕЦ КАК Доступность,
		|	КомментарииКандидатов.УдалитьСкрытый КАК УдалитьСкрытый,
		|	КомментарииКандидатов.МестоПубликации КАК МестоПубликации,
		|	КомментарииКандидатов.Идентификатор КАК Идентификатор
		|ИЗ
		|	РегистрСведений.КомментарииКандидатов КАК КомментарииКандидатов
		|ГДЕ
		|	КомментарииКандидатов.Кандидат В(&Кандидаты)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Кандидат";
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Кандидат") Цикл
		ПространствоБлокировки = "РегистрСведений.КомментарииКандидатов";
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, ПространствоБлокировки, "Кандидат", Выборка.Кандидат) Тогда
			Продолжить;
		КонецЕсли;
		НаборЗаписей = РегистрыСведений.КомментарииКандидатов.СоздатьНаборЗаписей();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла;
		НаборЗаписей.Отбор.Кандидат.Установить(Выборка.Кандидат);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область ОписаниеПеременных

&НаКлиенте
Перем СохранятьИзменения;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(Запись, Параметры);
	Если Запись.Доступность = Перечисления.ДоступностьКомментарияКандидата.ТолькоДляАвтора Тогда
		ТолькоДляАвтора = Истина;
	КонецЕсли;
	
	Если ОбщегоНазначения.СсылкаСуществует(Запись.Пользователь) Тогда
		Заголовок = Строка(Запись.Пользователь);
	КонецЕсли;
	
	Если Запись.Пользователь <> Пользователи.АвторизованныйПользователь() Тогда
		ТолькоПросмотр = Истина;
		Элементы.ТолькоДляАвтора.Видимость = Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Запись.МестоПубликации) Тогда
		Элементы.ИнформационнаяНадписьМестоПубликации.Видимость = Ложь;
	Иначе
		ТолькоПросмотр = Истина;
		Элементы.ИнформационнаяНадписьМестоПубликации.Видимость = Истина;
		Элементы.ИнформационнаяНадписьМестоПубликации.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Добавлен на сайте %1'"), 
			Запись.МестоПубликации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если СохранятьИзменения = Истина Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(Запись.Комментарий) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Комментарий не заполнен.'"), , , "Запись.Комментарий", Отказ);
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Запись");
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СохранятьИзменения = Истина;
	
	Если ТолькоДляАвтора Тогда
		Запись.Доступность = ПредопределенноеЗначение("Перечисление.ДоступностьКомментарияКандидата.ТолькоДляАвтора");
	Иначе
		Запись.Доступность = ПредопределенноеЗначение("Перечисление.ДоступностьКомментарияКандидата.Общедоступный");
	КонецЕсли;
	
	Закрыть(Запись);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбновитьХарактеристикиПозицийШтатногоРасписанияПоПрофилюДолжности(ПрофильДолжности, ХарактеристикиПерсонала) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НастройкиДействийХарактеристикШтатногоРасписания.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания
		|ПОМЕСТИТЬ ВТПозиции
		|ИЗ
		|	РегистрСведений.НастройкиДействийХарактеристикШтатногоРасписания КАК НастройкиДействийХарактеристикШтатногоРасписания
		|ГДЕ
		|	НастройкиДействийХарактеристикШтатногоРасписания.СоответствуютПрофилюДолжности = ИСТИНА
		|	И НастройкиДействийХарактеристикШтатногоРасписания.ПрофильДолжности = &ПрофильДолжности
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХарактеристикиПерсонала.Характеристика КАК ХарактеристикаПерсонала,
		|	ХарактеристикиПерсонала.Значение КАК Значение,
		|	ХарактеристикиПерсонала.Вес КАК Вес,
		|	ХарактеристикиПерсонала.ТребуетсяПроверка КАК ТребуетсяПроверка,
		|	ХарактеристикиПерсонала.ТребуетсяОбучение КАК ТребуетсяОбучение,
		|	ХарактеристикиПерсонала.ВесЗначения КАК ВесЗначения
		|ПОМЕСТИТЬ ВТХарактеристикиПрофиля
		|ИЗ
		|	&ХарактеристикиПерсонала КАК ХарактеристикиПерсонала
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПозиции.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ХарактеристикиПрофиля.ХарактеристикаПерсонала КАК ХарактеристикаПерсонала,
		|	ХарактеристикиПрофиля.Значение КАК Значение,
		|	ХарактеристикиПрофиля.Вес КАК Вес,
		|	ХарактеристикиПрофиля.ТребуетсяПроверка КАК ТребуетсяПроверка,
		|	ХарактеристикиПрофиля.ТребуетсяОбучение КАК ТребуетсяОбучение,
		|	ХарактеристикиПрофиля.ВесЗначения КАК ВесЗначения
		|ПОМЕСТИТЬ ВТХарактеристикиПозиций
		|ИЗ
		|	ВТПозиции КАК ВТПозиции
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТХарактеристикиПрофиля КАК ХарактеристикиПрофиля
		|		ПО (ИСТИНА)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Позиции.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ХарактеристикиПерсоналаПозицийШтатногоРасписания.ХарактеристикаПерсонала КАК ХарактеристикаПерсонала,
		|	ХарактеристикиПерсоналаПозицийШтатногоРасписания.Значение КАК Значение,
		|	ХарактеристикиПерсоналаПозицийШтатногоРасписания.Вес КАК Вес,
		|	ХарактеристикиПерсоналаПозицийШтатногоРасписания.ТребуетсяПроверка КАК ТребуетсяПроверка,
		|	ХарактеристикиПерсоналаПозицийШтатногоРасписания.ТребуетсяОбучение КАК ТребуетсяОбучение,
		|	ХарактеристикиПерсоналаПозицийШтатногоРасписания.ВесЗначения КАК ВесЗначения
		|ПОМЕСТИТЬ ВТТекущиеХарактеристики
		|ИЗ
		|	ВТПозиции КАК Позиции
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХарактеристикиПерсоналаПозицийШтатногоРасписания КАК ХарактеристикиПерсоналаПозицийШтатногоРасписания
		|		ПО Позиции.ПозицияШтатногоРасписания = ХарактеристикиПерсоналаПозицийШтатногоРасписания.ПозицияШтатногоРасписания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(ТекущиеХарактеристики.ПозицияШтатногоРасписания, ВТХарактеристикиПозиций.ПозицияШтатногоРасписания) КАК ПозицияШтатногоРасписания
		|ПОМЕСТИТЬ ВТИзменяемыеПозиции
		|ИЗ
		|	ВТТекущиеХарактеристики КАК ТекущиеХарактеристики
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТХарактеристикиПозиций КАК ВТХарактеристикиПозиций
		|		ПО ТекущиеХарактеристики.ПозицияШтатногоРасписания = ВТХарактеристикиПозиций.ПозицияШтатногоРасписания
		|			И ТекущиеХарактеристики.ХарактеристикаПерсонала = ВТХарактеристикиПозиций.ХарактеристикаПерсонала
		|			И ТекущиеХарактеристики.Значение = ВТХарактеристикиПозиций.Значение
		|			И ТекущиеХарактеристики.Вес = ВТХарактеристикиПозиций.Вес
		|			И ТекущиеХарактеристики.ТребуетсяПроверка = ВТХарактеристикиПозиций.ТребуетсяПроверка
		|			И ТекущиеХарактеристики.ТребуетсяОбучение = ВТХарактеристикиПозиций.ТребуетсяОбучение
		|			И ТекущиеХарактеристики.ВесЗначения = ВТХарактеристикиПозиций.ВесЗначения
		|ГДЕ
		|	(ТекущиеХарактеристики.ПозицияШтатногоРасписания ЕСТЬ NULL
		|			ИЛИ ВТХарактеристикиПозиций.ПозицияШтатногоРасписания ЕСТЬ NULL)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТХарактеристикиПозиций.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ВТХарактеристикиПозиций.ХарактеристикаПерсонала КАК ХарактеристикаПерсонала,
		|	ВТХарактеристикиПозиций.Значение КАК Значение,
		|	ВТХарактеристикиПозиций.Вес КАК Вес,
		|	ВТХарактеристикиПозиций.ТребуетсяПроверка КАК ТребуетсяПроверка,
		|	ВТХарактеристикиПозиций.ТребуетсяОбучение КАК ТребуетсяОбучение,
		|	ВТХарактеристикиПозиций.ВесЗначения КАК ВесЗначения
		|ИЗ
		|	ВТХарактеристикиПозиций КАК ВТХарактеристикиПозиций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзменяемыеПозиции КАК ВТИзменяемыеПозиции
		|		ПО ВТХарактеристикиПозиций.ПозицияШтатногоРасписания = ВТИзменяемыеПозиции.ПозицияШтатногоРасписания
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПозиции
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТХарактеристикиПрофиля
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТХарактеристикиПозиций
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТТекущиеХарактеристики
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТИзменяемыеПозиции";
	
	Запрос.УстановитьПараметр("ПрофильДолжности", ПрофильДолжности);
	Запрос.УстановитьПараметр("ХарактеристикиПерсонала", ХарактеристикиПерсонала);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ПозицияШтатногоРасписания") Цикл
		НаборЗаписей = РегистрыСведений.ХарактеристикиПерсоналаПозицийШтатногоРасписания.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПозицияШтатногоРасписания.Установить(Выборка.ПозицияШтатногоРасписания);
		
		Пока Выборка.Следующий() Цикл
			Если Выборка.ХарактеристикаПерсонала = Null Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла; 
		
		НаборЗаписей.Записать(Истина);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти  

#КонецЕсли



#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(Вакансии, ПараметрыВыполненияКоманды)
	НачатьОтклонение(Вакансии, ПараметрыВыполненияКоманды.Источник);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьОтклонение(Вакансии, ФормаИсточник)
	
	УникальныйИдентификатор = ФормаИсточник.УникальныйИдентификатор;
	ДлительнаяОперация = ОтклонитьНаСервере(Вакансии, УникальныйИдентификатор);
	
	ПараметрыОповещения = Новый Структура("Вакансии");
	ПараметрыОповещения.Вакансии = Вакансии;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ФормаИсточник);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Отклонение вакансий'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершитьОтклонение", ЭтотОбъект, ПараметрыОповещения);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ОтклонитьНаСервере(Вакансии, УникальныйИдентификатор)
	
	ПараметрыПроцедуры = Новый Структура("Вакансии");
	ПараметрыПроцедуры.Вакансии = Вакансии;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отклонение вакансий'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.СогласованиеВакансий.ОтклонитьДлительнаяОперация", ПараметрыПроцедуры, ПараметрыВыполнения);   	
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьОтклонение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось отклонить по причине: 
                  |%1'"),
			Результат.КраткоеПредставлениеОшибки);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ОтклоненныеВакансии = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	Вакансии = ДополнительныеПараметры.Вакансии;
	
	Если ОтклоненныеВакансии.Количество() = Вакансии.Количество() Тогда
		ЗаголовокОповещения = НСтр("ru = 'Отклонение'");
		ТекстСообщения = НСтр("ru = 'Отклонение успешно завершено'");
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстСообщения, БиблиотекаКартинок.Информация32);
	Иначе
		ЗаголовокОповещения = НСтр("ru = 'Отклонение'");
		ТекстСообщения = НСтр("ru = 'Не все удалось отклонить'");
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстСообщения, БиблиотекаКартинок.Ошибка32, СтатусОповещенияПользователя.Важное);
	КонецЕсли;
	
	Оповестить("СогласованиеВакансий_Завершено", ОтклоненныеВакансии); 
	
КонецПроцедуры

#КонецОбласти
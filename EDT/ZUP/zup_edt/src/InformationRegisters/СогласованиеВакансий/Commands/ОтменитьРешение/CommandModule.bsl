
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(Вакансии, ПараметрыВыполненияКоманды)
	НачатьОтменуРешения(Вакансии, ПараметрыВыполненияКоманды.Источник);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьОтменуРешения(Вакансии, ФормаИсточник)
	
	УникальныйИдентификатор = ФормаИсточник.УникальныйИдентификатор;
	ДлительнаяОперация = ОтменитьРешениеНаСервере(Вакансии, УникальныйИдентификатор);
	
	ПараметрыОповещения = Новый Структура("Вакансии");
	ПараметрыОповещения.Вакансии = Вакансии;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ФормаИсточник);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Отмена решения по вакансиям и заявкам'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершитьОтменуРешения", ЭтотОбъект, ПараметрыОповещения);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ОтменитьРешениеНаСервере(Вакансии, УникальныйИдентификатор)
	
	ПараметрыПроцедуры = Новый Структура("Вакансии");
	ПараметрыПроцедуры.Вакансии = Вакансии;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отмена решения по вакансиям и заявкам'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.СогласованиеВакансий.ОтменитьРешениеДлительнаяОперация", ПараметрыПроцедуры, ПараметрыВыполнения);   	
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьОтменуРешения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось отменить решение по причине: 
                  |%1'"),
			Результат.КраткоеПредставлениеОшибки);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	СогласованныеВакансии = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	Вакансии = ДополнительныеПараметры.Вакансии;
	
	Если СогласованныеВакансии.Количество() = Вакансии.Количество() Тогда
		ЗаголовокОповещения = НСтр("ru = 'Отмена решения'");
		ТекстСообщения = НСтр("ru = 'Отмена решения успешно завершена'");
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстСообщения, БиблиотекаКартинок.Информация32);
	Иначе
		ЗаголовокОповещения = НСтр("ru = 'Отмена решения'");
		ТекстСообщения = НСтр("ru = 'Не все решения удалось отменить'");
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстСообщения, БиблиотекаКартинок.Ошибка32, СтатусОповещенияПользователя.Важное);
	КонецЕсли;
	
	Оповестить("СогласованиеВакансий_Завершено", СогласованныеВакансии); 
	
КонецПроцедуры

#КонецОбласти
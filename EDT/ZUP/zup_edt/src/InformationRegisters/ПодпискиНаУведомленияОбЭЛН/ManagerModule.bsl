#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

#Область НаборЗаписей

// АПК:326-выкл. Методы поставляются комплектом и предназначены для совместного последовательного использования.
// АПК:325-выкл. Методы поставляются комплектом и предназначены для совместного последовательного использования.
// Транзакция открывается в методе НачатьЗаписьНабора, закрывается в ЗавершитьЗаписьНабора, отменяется в ОтменитьЗаписьНабора.

// Транзакционный вариант (с управляемой блокировкой) получения набора записей, соответствующего значениям измерений.
//
// Параметры:
//   Организация - ОпределяемыйТип.Организация - Значение отбора по соответствующему измерению.
//   Сотрудник   - СправочникСсылка.Сотрудники - Значение отбора по соответствующему измерению.
//
// Возвращаемое значение:
//   РегистрСведенийНаборЗаписей.ПодпискиНаУведомленияОбЭЛН - Если удалось установить блокировку
//       и прочитать набор записей. При необходимости, открывает свою локальную транзакцию. Для закрытия транзакции
//       следует использовать одну из терминирующих процедур: ЗавершитьЗаписьНабора, либо ОтменитьЗаписьНабора.
//   Неопределено - Если не удалось установить блокировку и прочитать набор записей.
//       Вызывать процедуры ЗавершитьЗаписьНабора, ОтменитьЗаписьНабора не требуется,
//       поскольку если перед блокировкой функции потребовалось открыть локальную транзакцию,
//       то после неудачной блокировки локальная транзакция была отменена.
//
Функция НачатьЗаписьНабора(Организация, Сотрудник) Экспорт
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	ПолныеПраваИлиПривилегированныйРежим = Пользователи.ЭтоПолноправныйПользователь();
	Если Не ПолныеПраваИлиПривилегированныйРежим
		И Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПодпискиНаУведомленияОбЭЛН) Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Недостаточно прав для изменения регистра ""%1"".'"),
			Метаданные.РегистрыСведений.ПодпискиНаУведомленияОбЭЛН.Представление());
	КонецЕсли;
	ЛокальнаяТранзакция = Не ТранзакцияАктивна();
	Если ЛокальнаяТранзакция Тогда
		НачатьТранзакцию();
	КонецЕсли;
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПодпискиНаУведомленияОбЭЛН");
		ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
		ЭлементБлокировки.УстановитьЗначение("Сотрудник", Сотрудник);
		Блокировка.Заблокировать();
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Организация);
		НаборЗаписей.Отбор.Сотрудник.Установить(Сотрудник);
		НаборЗаписей.Прочитать();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ЛокальнаяТранзакция", ЛокальнаяТранзакция);
	Исключение
		Если ЛокальнаяТранзакция Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось изменить сведения о подписках на уведомления %1 об ЭЛН %2 по причине: %3'"),
			Организация,
			Сотрудник,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.ПодпискиНаУведомленияОбЭЛН,
			Сотрудник,
			ТекстСообщения);
		НаборЗаписей = Неопределено;
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат НаборЗаписей;
КонецФункции

// Записывает набор и фиксирует локальную транзакцию, если она была открыта в функции НачатьЗаписьНабора.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.ПодпискиНаУведомленияОбЭЛН
//
Процедура ЗавершитьЗаписьНабора(НаборЗаписей) Экспорт
	НаборЗаписей.Записать(Истина);
	ЛокальнаяТранзакция = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НаборЗаписей.ДополнительныеСвойства, "ЛокальнаяТранзакция");
	Если ЛокальнаяТранзакция = Истина Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// Отменяет запись набора и отменяет локальную транзакцию, если она была открыта в функции НачатьЗаписьНабора.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.ПодпискиНаУведомленияОбЭЛН
//
Процедура ОтменитьЗаписьНабора(НаборЗаписей) Экспорт
	ЛокальнаяТранзакция = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НаборЗаписей.ДополнительныеСвойства, "ЛокальнаяТранзакция");
	Если ЛокальнаяТранзакция = Истина Тогда
		ОтменитьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// АПК:326-вкл.
// АПК:325-вкл.

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция СотрудникиСДействующейПодпиской(Организация, Сотрудники) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Подписки.Организация КАК Организация,
	|	Подписки.Сотрудник КАК Сотрудник
	|ИЗ
	|	РегистрСведений.ПодпискиНаУведомленияОбЭЛН КАК Подписки
	|ГДЕ
	|	Подписки.Сотрудник В(&Сотрудники)
	|	И Подписки.Организация = &Организация
	|	И Подписки.Действует";
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция КоличествоОжидаемыхСообщений(Организация) Экспорт
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат 0;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК Поле1
	|ИЗ
	|	РегистрСведений.ПодпискиНаУведомленияОбЭЛН КАК Подписки
	|ГДЕ
	|	Подписки.Организация = &Организация
	|	И Подписки.ДатаОтправки > &ДатаНачалаАктуальности";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаНачалаАктуальности", ДатаНачалаАктуальности());
	
	Возврат Запрос.Выполнить().Выбрать().Количество();
КонецФункции

Функция ДатаНачалаАктуальности() Экспорт
	Возврат НачалоДня(ДобавитьМесяц(ТекущаяДатаСеанса(), -1));
КонецФункции

Функция ДатаОтправкиЗагруженногоСообщения() Экспорт
	Возврат '00010101';
КонецФункции

#Область СообщенияФСС

Процедура ПослеОтправкиЗапросаНаИзменениеПодписки(Организация, МассивСотрудников, ДатаОтправки, БудетДействовать, ИдентификаторЗапроса) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого Сотрудник Из МассивСотрудников Цикл
		Набор = НачатьЗаписьНабора(Организация, Сотрудник);
		
		Если Набор.Количество() = 0 Тогда
			Запись = Набор.Добавить();
			Запись.Организация    = Организация;
			Запись.Сотрудник      = Сотрудник;
			Запись.ФизическоеЛицо = СЭДОФСС.ФизическоеЛицоСотрудника(Запись.Сотрудник);
		Иначе
			Запись = Набор[0];
		КонецЕсли;
		
		Запись.ДатаОтправки = ДатаОтправки;
		Запись.БудетДействовать = БудетДействовать;
		Запись.ИдентификаторЗапроса = ИдентификаторЗапроса;
		
		ЗавершитьЗаписьНабора(Набор);
	КонецЦикла;
КонецПроцедуры

Процедура ПослеРасшифровкиСообщенияОбИзмененииПодписки(Организация, ИдентификаторОтвета, ТекстXML, Результат) Экспорт
	// Пример:
	// <subscriptionRecipientResponse xmlns="http://www.fss.ru/integration/types/eln/event/v01" xmlns:ns2="http://www.fss.ru/integration/types/common/v01">
	//	<resultList>
	//		<result>
	//			<snils>00000060003</snils>
	//			<status>ERROR</status>
	//			<error>
	//				<ns2:code>E_ELN_0011</ns2:code>
	//				<ns2:message>Данный застрахованный уже прикреплен к страхователю 2019122401</ns2:message>
	//			</error>
	//		</result>
	//	</resultList>
	//</subscriptionRecipientResponse>
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураDOM = СериализацияБЗК.СтруктураDOM(ТекстXML);
	НераспределенныеОшибки = Новый Массив;
	
	РезультатXPath = СериализацияБЗК.ВычислитьВыражениеXPath(СтруктураDOM, "//*[local-name() = 'snils']/..");
	Пока Истина Цикл
		ЭлементDOM = РезультатXPath.ПолучитьСледующий();
		Если ЭлементDOM = Неопределено Тогда
			Прервать;
		КонецЕсли;
		ФрагментXML = СериализацияБЗК.ОбъектDOMВСтрокуXML(ЭлементDOM);
		УзелResult = СериализацияБЗК.ОбъектXDTOИзСтрокиXML(ФрагментXML);
		
		РеквизитыResult = ОбщегоНазначенияБЗК.ЗначенияСвойств(УзелResult, "snils, status, error");
		ТипРезультата = ВРег(СокрЛП(Строка(РеквизитыResult.Status)));
		СНИЛС = УчетПособийСоциальногоСтрахованияКлиентСервер.СНИЛСВФорматеИБ(РеквизитыResult.SNILS);
		
		Успех = Ложь;
		Ошибки = Новый Массив;
		Если ТипРезультата = "1" Или ТипРезультата = "SUCCESS" Тогда
			Успех = Истина;
		ИначеЕсли ТипРезультата = "2" Или ТипРезультата = "ERROR" Тогда
			Если РеквизитыResult.Error <> Неопределено Тогда
				РеквизитыError = ОбщегоНазначенияБЗК.ЗначенияСвойств(РеквизитыResult.Error, "code, message");
				Ошибки.Добавить(НСтр("ru = 'Ответ ФСС:'") + Символы.ПС + СокрЛП(РеквизитыError.Code + " " + РеквизитыError.Message));
			Иначе
				Ошибки.Добавить(СтрШаблон(
					НСтр("ru = 'Значение поля %1 = ""%2"", что свидетельствует об ошибке, однако текст ошибки пустой.'"),
					"status",
					ТипРезультата));
			КонецЕсли;
		Иначе
			Ошибки.Добавить(СтрШаблон(НСтр("ru = 'Неизвестное значение поля %1: %2.'"), "status", ТипРезультата));
		КонецЕсли;
		
		РезультатПоиска = ОбменЛисткамиНетрудоспособностиФСС.НайтиСотрудникаПоСНИЛС(Организация, ТекущаяДатаСеанса(), СНИЛС);
		Если Не РезультатПоиска.Успех Тогда
			Ошибки.Добавить(СтрШаблон(НСтр("ru = 'Не удалось найти сотрудника по СНИЛС: %1: %2'"), СНИЛС, РезультатПоиска.ТекстОшибки));
			НераспределенныеОшибки.Добавить(СтрСоединить(Ошибки, Символы.ПС));
			Продолжить;
		КонецЕсли;
		
		// Организация подписки может не совпадать с текущей организацией сотрудника
		// из-за лага между отправкой сообщения и приемом ответа.
		Набор = НачатьЗаписьНабора(Организация, РезультатПоиска.Сотрудник);
		Если Набор.Количество() = 0 Тогда
			Запись = Набор.Добавить();
			Запись.Организация    = Организация;
			Запись.Сотрудник      = РезультатПоиска.Сотрудник;
			Запись.ФизическоеЛицо = СЭДОФСС.ФизическоеЛицоСотрудника(Запись.Сотрудник);
		Иначе
			Запись = Набор[0];
		КонецЕсли;
		
		Запись.ИдентификаторОтвета = ИдентификаторОтвета;
		Запись.ДатаОтправки = ДатаОтправкиЗагруженногоСообщения();
		
		Если Успех Тогда
			// Этот флажок следует получать из ФСС, но формат типов документов 1.8 не подразумевает его передачу.
			Запись.Действует = Запись.БудетДействовать;
		КонецЕсли;
		
		Если Ошибки.Количество() > 0 Тогда
			Ошибки.Добавить(НСтр("ru = 'Фрагмент XML:'") + Символы.ПС + ФрагментXML);
			Запись.ТекстОшибки = СтрСоединить(Ошибки, Символы.ПС);
		Иначе
			Запись.ТекстОшибки = "";
		КонецЕсли;
		
		ЗавершитьЗаписьНабора(Набор);
	КонецЦикла;
	
	Результат.Обработано = Истина;
	
	Если НераспределенныеОшибки.Количество() > 0 Тогда
		Результат.ОшибкаОбработки = Истина;
		Результат.ОписаниеОшибки = СтрСоединить(НераспределенныеОшибки, Символы.ПС)
			+ Символы.ПС + НСтр("ru = 'Текст XML:'") + Символы.ПС + ТекстXML;
		
		СтруктураКлюча = Новый Структура("Идентификатор, Организация", ИдентификаторОтвета, Организация);
		КлючЗаписи = РегистрыСведений.ВходящиеСообщенияСЭДОФСС.СоздатьКлючЗаписи(СтруктураКлюча);
		ТекстЖурнала = СтрШаблон(
			НСтр("ru = 'В процедуре %1 получено сообщение %2, при разборе которого обнаружены ошибки:
				|%3'"),
			"ПослеРасшифровкиСообщенияОбИзмененииПодписки",
			ИдентификаторОтвета,
			Результат.ОписаниеОшибки);
		ЗаписьЖурналаРегистрации(
			СЭДОФСС.ИмяСобытияЖурнала(),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.РегистрыСведений.ВходящиеСообщенияСЭДОФСС,
			КлючЗаписи,
			ТекстЖурнала);
		
		СообщенияПользователюБЗК.СообщениеОбъекта(Результат.ОписаниеОшибки, Неопределено, Неопределено).Сообщить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
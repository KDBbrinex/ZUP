#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбновитьПрограммыОбученияПозицийШтатногоРасписанияПоПрофилюДолжности(МенеджерВременныхТаблиц, ПрограммаОбучения) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПрограммаОбучения.Мероприятие КАК Мероприятие,
		|	ПрограммаОбучения.Ответственный КАК Ответственный,
		|	ПрограммаОбучения.Начало КАК Начало,
		|	ПрограммаОбучения.Окончание КАК Окончание
		|ПОМЕСТИТЬ ВТПрограммаПрофиля
		|ИЗ
		|	&ПрограммаОбучения КАК ПрограммаОбучения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПозиции.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ПрограммаПрофиля.Мероприятие КАК Мероприятие,
		|	ПрограммаПрофиля.Ответственный КАК Ответственный,
		|	ПрограммаПрофиля.Начало КАК Начало,
		|	ПрограммаПрофиля.Окончание КАК Окончание
		|ПОМЕСТИТЬ ВТПрограммаПозиций
		|ИЗ
		|	ВТПозиции КАК ВТПозиции
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПрограммаПрофиля КАК ПрограммаПрофиля
		|		ПО (ИСТИНА)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Позиции.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ПрограммыОбученияПозицийШтатногоРасписания.Мероприятие КАК Мероприятие,
		|	ПрограммыОбученияПозицийШтатногоРасписания.Ответственный КАК Ответственный,
		|	ПрограммыОбученияПозицийШтатногоРасписания.Начало КАК Начало,
		|	ПрограммыОбученияПозицийШтатногоРасписания.Окончание КАК Окончание
		|ПОМЕСТИТЬ ВТТекущиеПрограммы
		|ИЗ
		|	ВТПозиции КАК Позиции
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрограммыОбученияПозицийШтатногоРасписания КАК ПрограммыОбученияПозицийШтатногоРасписания
		|		ПО Позиции.ПозицияШтатногоРасписания = ПрограммыОбученияПозицийШтатногоРасписания.ПозицияШтатногоРасписания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(ТекущиеПрограммы.ПозицияШтатногоРасписания, ПрограммаПозиций.ПозицияШтатногоРасписания) КАК ПозицияШтатногоРасписания
		|ПОМЕСТИТЬ ВТИзменяемыеПозиции
		|ИЗ
		|	ВТТекущиеПрограммы КАК ТекущиеПрограммы
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТПрограммаПозиций КАК ПрограммаПозиций
		|		ПО ТекущиеПрограммы.ПозицияШтатногоРасписания = ПрограммаПозиций.ПозицияШтатногоРасписания
		|			И ТекущиеПрограммы.Мероприятие = ПрограммаПозиций.Мероприятие
		|			И ТекущиеПрограммы.Ответственный = ПрограммаПозиций.Ответственный
		|			И ТекущиеПрограммы.Начало = ПрограммаПозиций.Начало
		|			И ТекущиеПрограммы.Окончание = ПрограммаПозиций.Окончание
		|ГДЕ
		|	(ТекущиеПрограммы.ПозицияШтатногоРасписания ЕСТЬ NULL
		|			ИЛИ ПрограммаПозиций.ПозицияШтатногоРасписания ЕСТЬ NULL)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПрограммаПозиций.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ПрограммаПозиций.Мероприятие КАК Мероприятие,
		|	ПрограммаПозиций.Ответственный КАК Ответственный,
		|	ПрограммаПозиций.Начало КАК Начало,
		|	ПрограммаПозиций.Окончание КАК Окончание
		|ИЗ
		|	ВТПрограммаПозиций КАК ПрограммаПозиций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзменяемыеПозиции КАК ВТИзменяемыеПозиции
		|		ПО ПрограммаПозиций.ПозицияШтатногоРасписания = ВТИзменяемыеПозиции.ПозицияШтатногоРасписания
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПрограммаПрофиля
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПрограммаПозиций
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТТекущиеПрограммы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТИзменяемыеПозиции";
	
	Запрос.УстановитьПараметр("ПрограммаОбучения", ПрограммаОбучения);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ПозицияШтатногоРасписания") Цикл
		НаборЗаписей = РегистрыСведений.ПрограммыОбученияПозицийШтатногоРасписания.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПозицияШтатногоРасписания.Установить(Выборка.ПозицияШтатногоРасписания);
		
		Пока Выборка.Следующий() Цикл
			Если Выборка.Мероприятие = Null Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла; 
		
		НаборЗаписей.Записать(Истина);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти  

#КонецЕсли

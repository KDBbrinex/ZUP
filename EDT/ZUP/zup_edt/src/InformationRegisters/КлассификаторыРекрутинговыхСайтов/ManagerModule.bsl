#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьКлассификатор(Сайт, ИмяКлассификатора, Значение, ДатаАктуальности = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.КлассификаторыРекрутинговыхСайтов.СоздатьМенеджерЗаписи();
	Запись.Сайт = Сайт;
	Запись.ИмяКлассификатора = ИмяКлассификатора;
	Запись.ДатаАктуальности = ?(ДатаАктуальности = Неопределено, НачалоДня(ТекущаяДатаСеанса()), ДатаАктуальности);
	Запись.Значение = Новый ХранилищеЗначения(Значение);
	Запись.Записать(Истина);
	
КонецПроцедуры

Функция УстаревшиеКлассификаторы(Сайт, Знач ИменаКлассификаторов) Экспорт
	
	Если Не ЗначениеЗаполнено(ИменаКлассификаторов) Тогда
		Возврат ИменаКлассификаторов;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КлассификаторыРекрутинговыхСайтов.ИмяКлассификатора КАК ИмяКлассификатора
	|ИЗ
	|	РегистрСведений.КлассификаторыРекрутинговыхСайтов КАК КлассификаторыРекрутинговыхСайтов
	|ГДЕ
	|	КлассификаторыРекрутинговыхСайтов.Сайт = &Сайт
	|	И КлассификаторыРекрутинговыхСайтов.ДатаАктуальности >= &ДатаАктуальности");
	Запрос.УстановитьПараметр("Сайт", Сайт);
	Запрос.УстановитьПараметр("ДатаАктуальности", НачалоДня(ТекущаяДатаСеанса()));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ИменаКлассификаторов;
	КонецЕсли;
	
	АктуальныеКлассификаторы = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ИмяКлассификатора");
	Для Каждого Классификатор Из АктуальныеКлассификаторы Цикл
		Индекс = ИменаКлассификаторов.Найти(Классификатор);
		Если Индекс = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ИменаКлассификаторов.Удалить(Индекс);
	КонецЦикла;
	
	Возврат ИменаКлассификаторов;
	
КонецФункции

Функция ЗначениеКлассификатора(Сайт, ИмяКлассификатора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КлассификаторыРекрутинговыхСайтов.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.КлассификаторыРекрутинговыхСайтов КАК КлассификаторыРекрутинговыхСайтов
	|ГДЕ
	|	КлассификаторыРекрутинговыхСайтов.Сайт = &Сайт
	|	И КлассификаторыРекрутинговыхСайтов.ИмяКлассификатора = &ИмяКлассификатора");
	Запрос.УстановитьПараметр("Сайт", Сайт);
	Запрос.УстановитьПараметр("ИмяКлассификатора", ИмяКлассификатора);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Значение.Получить();
	
КонецФункции

#КонецОбласти

#КонецЕсли
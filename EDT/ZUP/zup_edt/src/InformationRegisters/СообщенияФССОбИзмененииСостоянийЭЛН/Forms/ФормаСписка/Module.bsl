#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Константы.
	Список.Параметры.УстановитьЗначениеПараметра("ПустаяДата", '00010101');
	
	// Значения по умолчанию.
	РежимФильтрации = 1;
	
	// Заголовок формы должен соответствовать заголовку команды.
	Команда = Метаданные.РегистрыСведений.СообщенияФССОбИзмененииСостоянийЭЛН.Команды.СообщенияФССОбИзмененииЭЛН;
	НавигационнаяСсылка = "e1cib/command/" + Команда.ПолноеИмя();
	Заголовок = Команда.Представление();
	
	Команда = Метаданные.РегистрыСведений.СогласияНаУведомленияОбЭЛН.Команды.СогласияНаУведомленияОбЭЛН;
	Команды.СогласияИПодпискиНаУведомленияОбЭЛН.Заголовок = Команда.Представление();
	Команды.СогласияИПодпискиНаУведомленияОбЭЛН.Подсказка = Команда.Подсказка;
	
	Организация = Параметры.Организация;
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ЗначенияДляЗаполнения = Новый Структура;
		ЗначенияДляЗаполнения.Вставить("Организация", "Организация");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтотОбъект, ЗначенияДляЗаполнения);
	КонецЕсли;
	
	ЭлементУО = ОбщегоНазначенияБЗК.ДобавитьУсловноеОформление(Список, "Больничный");
	ОбщегоНазначенияБЗК.ДобавитьОтборУсловногоОформления(ЭлементУО, "Больничный", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияБЗК.ДобавитьОтборУсловногоОформления(ЭлементУО, "ИндексКартинки", "=", 2);
	ОбщегоНазначенияБЗК.ДобавитьОтборУсловногоОформления(ЭлементУО, "СостояниеЭЛН", "=", Перечисления.СостоянияЭЛНВФСС.Закрыт);
	ОбщегоНазначенияБЗК.УстановитьПараметрУсловногоОформления(ЭлементУО, "Текст", НСтр("ru = '<Создать больничный...>'"));
	
	// Добавление отборов и сохранение их идентификаторов для быстрого поиска.
	НастройкиКД = Список.КомпоновщикНастроек.Настройки;
	
	ЭлементОтбораКД = ОбщегоНазначенияБЗК.ДобавитьОтборУсловногоОформления(НастройкиКД, "ТребуетОбработки", "=", Истина);
	ЭлементОтбораКД.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ИдентификаторОтбораТолькоТребующиеОбработки = НастройкиКД.Отбор.ПолучитьИдентификаторПоОбъекту(ЭлементОтбораКД);
	
	ЭлементОтбораКД = ОбщегоНазначенияБЗК.ДобавитьОтборУсловногоОформления(НастройкиКД, "Последнее", "=", Истина);
	ЭлементОтбораКД.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ИдентификаторОтбораТолькоПоследние = НастройкиКД.Отбор.ПолучитьИдентификаторПоОбъекту(ЭлементОтбораКД);
	
	ФиксированныеПараметрыСписка = ФиксированныеПараметрыСписка();
	Для Каждого Элемент Из ФиксированныеПараметрыСписка Цикл
		Список.Параметры.УстановитьЗначениеПараметра(Элемент.Значение, Элемент.Представление);
	КонецЦикла;
	
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, "Список.ДатаПолучения", "ДатаПолучения");
	
	Элементы.ПолучитьСообщенияИзФСС.Видимость = СЭДОФСС.ЕстьПравоОбмена();
	
	ОбновитьФорму();
	УчетПособийСоциальногоСтрахованияРасширенный.ОбновитьФорму(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	Если ЗначениеЗаполнено(Организация) Тогда
		Настройки.Удалить("Организация");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	ОбновитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_БольничныйЛист"
		Или ИмяСобытия = "Запись_НастройкиСЭДОФСС"
		Или СЭДОФССКлиент.ТребуетсяОбновитьНапоминаниеОбОтключенииПодпискиНаЭЛН(ИмяСобытия) Тогда
		ОтключитьОбработчикОжидания("ОбновитьСписок");
		ПодключитьОбработчикОжидания("ОбновитьСписок", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОбновитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьТолькоОжидающиеОбработкиПриИзменении(Элемент)
	ОбновитьФорму();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(ТаблицаФормы, ИдентификаторСтроки, ПолеФормы, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СтрокаТаблицы = ТаблицаФормы.ДанныеСтроки(ИдентификаторСтроки);
	
	Если СтрокаТаблицы = Неопределено Тогда
		// Пустая строка.
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтрокаТаблицы.НомерЛН) Тогда
		// Выбрано нерасшифрованное сообщение.
		СЭДОФССКлиент.ПолучитьСообщенияИзФСС(Организация);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормыБольничного = Новый Структура("Ключ", СтрокаТаблицы.Больничный);
	Если Не ЗначениеЗаполнено(СтрокаТаблицы.Больничный) Тогда
		Если СтрокаТаблицы.СостояниеЭЛН <> ПредопределенноеЗначение("Перечисление.СостоянияЭЛНВФСС.Закрыт") Тогда
			// Выбрано сообщение с состоянияем, которое запрещено загружать в программу.
			Возврат;
		КонецЕсли;
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Организация", Организация);
		ЗначенияЗаполнения.Вставить("Сотрудник", СтрокаТаблицы.Сотрудник);
		ЗначенияЗаполнения.Вставить("ФизическоеЛицо", СтрокаТаблицы.ФизическоеЛицо);
		ЗначенияЗаполнения.Вставить("НомерЛисткаНетрудоспособности", СтрокаТаблицы.НомерЛН);
		ПараметрыФормыБольничного.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ПараметрыФормыБольничного.Вставить("ПолучитьЭЛНИзФСС", Истина);
	КонецЕсли;
	ОткрытьФорму("Документ.БольничныйЛист.Форма.ФормаДокумента", ПараметрыФормыБольничного);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПолучитьСообщенияИзФСС(Команда)
	СЭДОФССКлиент.ПолучитьСообщенияИзФСС(Организация);
КонецПроцедуры

&НаКлиенте
Процедура ПовторноПолучитьСообщенияИзФСС(Команда)
	Идентификаторы = ИдентификаторыВыбранныхСообщений(Элементы.Список.ВыделенныеСтроки);
	Обработчик = Новый ОписаниеОповещения("ПослеПовторногоПолученияСообщенийИзФСС", ЭтотОбъект);
	ЭлектронныйДокументооборотСФССКлиент.ПолучитьСообщенияСЭДО(Обработчик, Организация, Идентификаторы);
КонецПроцедуры

&НаКлиенте
Процедура СогласияИПодпискиНаУведомленияОбЭЛН(Команда)
	ПараметрыФормы = Новый Структура("Организация", Организация);
	ОткрытьФорму("РегистрСведений.СогласияНаУведомленияОбЭЛН.Форма.ФормаСписка", ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеСообщения(Команда)
	РежимФильтрации = 0;
	ОбновитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьТолькоОжидающиеОбработки(Команда)
	РежимФильтрации = 1;
	ОбновитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастроекЭДО(Команда)
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура("ОрганизацияСсылка", Организация);
	ОткрытьФорму("РегистрСведений.НастройкиОбменаФСС.ФормаЗаписи", ПараметрыФормы, , , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСведенияОбУведомленияхОжидающихОбработки(Команда)
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	ОбновитьСведенияОбУведомленияхОжидающихОбработкиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацииПодключенныеКСЭДО(Команда)
	ПараметрыФормы = ПараметрыФормыНастройкиОрганизацийПодключенныехКСЭДО();
	Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	Обработчик = Новый ОписаниеОповещения("ОрганизацииПодключенныеКСЭДОЗавершениеВыбора", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВводЗначенийСпискомСФлажками", ПараметрыФормы, ЭтотОбъект, , , , Обработчик, Режим);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьФорму()
	ТолькоТребующиеОбработки = (РежимФильтрации = 1);
	ТолькоПоследние = (РежимФильтрации = 2);
	Элементы.ВсеСообщения.Пометка = Не ТолькоТребующиеОбработки;
	Элементы.ТолькоОжидающиеОбработки.Пометка = ТолькоТребующиеОбработки;
	Элементы.Список.ВертикальнаяПолосаПрокрутки = ?(
		ТолькоТребующиеОбработки,
		ИспользованиеПолосыПрокрутки.НеИспользовать,
		ИспользованиеПолосыПрокрутки.ИспользоватьВсегда);
	Список.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", ТекущаяДатаСеанса());
	Список.Параметры.УстановитьЗначениеПараметра("Организация", Организация);
	
	НастройкиКД = Список.КомпоновщикНастроек.Настройки;
	
	ЭлементОтбораКД = НастройкиКД.Отбор.ПолучитьОбъектПоИдентификатору(ИдентификаторОтбораТолькоТребующиеОбработки);
	ЭлементОтбораКД.Использование = ТолькоТребующиеОбработки;
	
	ЭлементОтбораКД = НастройкиКД.Отбор.ПолучитьОбъектПоИдентификатору(ИдентификаторОтбораТолькоПоследние);
	ЭлементОтбораКД.Использование = ТолькоПоследние;
	
	Элементы.Организация.СписокВыбора.ЗагрузитьЗначения(РегистрыСведений.НастройкиСЭДОФСС.ОрганизацииПолучающиеСостоянияЭЛН());
КонецПроцедуры

&НаСервере
Функция ФиксированныеПараметрыСписка()
	Результат = Новый СписокЗначений;
	Результат.Добавить("ТекстЭЛНАннулирован", НСтр("ru = 'ЭЛН аннулирован (действия прекращены)'"));
	Результат.Добавить("ТекстСообщениеНеЗагружено", НСтр("ru = '<Не получено>'"));
	Результат.Добавить("ТекстОтсутствуетСогласие", НСтр("ru = 'Отсутствует согласие сотрудника'"));
	Результат.Добавить("ТекстОтсутствуетБольничный", НСтр("ru = 'ЭЛН готов к загрузке'"));
	Результат.Добавить("ТекстСозданБольничный", НСтр("ru = 'ЭЛН загружен'"));
	Результат.Добавить("ТекстЭЛНПринятФСС", НСтр("ru = 'Реестр ЭЛН принят ФСС'"));
	Результат.Добавить("ТекстВМедицинскойОрганизации", НСтр("ru = 'На оформлении в медицинской организации'"));
	Результат.Добавить("ТекстНеНайден", НСтр("ru = 'Не найден в программе'"));
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ОбновитьСписок()
	ОбновитьСписокНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()
	Элементы.Список.Обновить();
	УчетПособийСоциальногоСтрахованияРасширенный.ОбновитьФорму(ЭтотОбъект);
	Элементы.Организация.СписокВыбора.ЗагрузитьЗначения(РегистрыСведений.НастройкиСЭДОФСС.ОрганизацииПолучающиеСостоянияЭЛН());
КонецПроцедуры

&НаСервере
Процедура ОбновитьСведенияОбУведомленияхОжидающихОбработкиНаСервере()
	Если Не СЭДОФСС.ЕстьПравоОбмена() Тогда
		ТекстОшибки = НСтр("ru = 'Недостаточно прав на обмен с сервисом электронного документооборота ФСС.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	РегистрыСведений.СообщенияФССОбИзмененииСостоянийЭЛН.ОбновитьВторичныеДанные(Организация);
	ОбновитьСписокНаСервере();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИдентификаторыВыбранныхСообщений(Знач ВыделенныеСтроки)
	Массив = Новый Массив;
	Для Каждого КлючЗаписи Из ВыделенныеСтроки Цикл
		Массив.Добавить(КлючЗаписи.ИдентификаторСообщения);
	КонецЦикла;
	Возврат Массив;
КонецФункции

&НаКлиенте
Процедура ПослеПовторногоПолученияСообщенийИзФСС(Результат, ПустойПараметр) Экспорт
	ТекстыОшибок = Новый Массив;
	Для Каждого РезультатПолучения Из Результат.РезультатыПолучения Цикл
		Если ЗначениеЗаполнено(РезультатПолучения.ОписаниеОшибки) Тогда
			ТекстыОшибок.Добавить(РезультатПолучения.ОписаниеОшибки);
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеЗаполнено(ТекстыОшибок) Тогда
		ИнформированиеПользователяКлиент.ПоказатьПодробности(
			СтрСоединить(ТекстыОшибок, Символы.ПС + Символы.ПС),
			НСтр("ru = 'Информация об ошибке'"),
			БиблиотекаКартинок.Предупреждение32);
	КонецЕсли;
	Оповестить(СЭДОФССКлиент.ИмяСобытияПослеПолученияСообщенийОтФСС(), Результат);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацииПодключенныеКСЭДОЗавершениеВыбора(СписокОрганизаций, ПустойПараметр) Экспорт
	Если ТипЗнч(СписокОрганизаций) <> Тип("СписокЗначений") Тогда
		Возврат;
	КонецЕсли;
	ОрганизацииПодключенныеКСЭДОЗавершениеВыбораНаСервере(СписокОрганизаций);
	Оповестить("Запись_НастройкиСЭДОФСС", Новый Структура, ЭтотОбъект);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОрганизацииПодключенныеКСЭДОЗавершениеВыбораНаСервере(СписокОрганизаций)
	РегистрыСведений.НастройкиСЭДОФСС.УстановитьФлажкиОрганизаций(СписокОрганизаций);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыФормыНастройкиОрганизацийПодключенныехКСЭДО()
	НастройкиОрганизаций = РегистрыСведений.НастройкиСЭДОФСС.НастройкиОрганизаций();
	Отмеченные = НастройкиОрганизаций.Скопировать(Новый Структура("ПолучатьСостоянияЭЛН", Истина));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отмеченные", ОтчетыКлиентСервер.ЗначенияСписком(Отмеченные.ВыгрузитьКолонку("Ссылка")));
	ПараметрыФормы.Вставить("ОписаниеТипов", Метаданные.РегистрыСведений.НастройкиСЭДОФСС.Измерения.Организация.Тип);
	ПараметрыФормы.Вставить("ЗначенияДляВыбора", ОтчетыКлиентСервер.ЗначенияСписком(НастройкиОрганизаций.ВыгрузитьКолонку("Ссылка")));
	ПараметрыФормы.Вставить("ЗначенияДляВыбораЗаполнены", Истина);
	ПараметрыФормы.Вставить("ОграничиватьВыборУказаннымиЗначениями", Истина);
	ПараметрыФормы.Вставить("Представление", НСтр("ru = 'Организации подключенные к СЭДО'"));
	
	Возврат ПараметрыФормы;
КонецФункции

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбновитьДействияПозицийШтатногоРасписанияПоПрофилюДолжности(ПрофильДолжности, ДействияСотрудников) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НастройкиДействийХарактеристикШтатногоРасписания.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания
		|ПОМЕСТИТЬ ВТПозиции
		|ИЗ
		|	РегистрСведений.НастройкиДействийХарактеристикШтатногоРасписания КАК НастройкиДействийХарактеристикШтатногоРасписания
		|ГДЕ
		|	НастройкиДействийХарактеристикШтатногоРасписания.СоответствуютПрофилюДолжности = ИСТИНА
		|	И НастройкиДействийХарактеристикШтатногоРасписания.ПрофильДолжности = &ПрофильДолжности
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДействияСотрудников.ДействиеСотрудника КАК ДействиеСотрудника
		|ПОМЕСТИТЬ ВТДействияПрофиля
		|ИЗ
		|	&ДействияСотрудников КАК ДействияСотрудников
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПозиции.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ВТДействияПрофиля.ДействиеСотрудника КАК ДействиеСотрудника
		|ПОМЕСТИТЬ ВТДействияПозиций
		|ИЗ
		|	ВТПозиции КАК ВТПозиции
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДействияПрофиля КАК ВТДействияПрофиля
		|		ПО (ИСТИНА)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания,
		|	ДействиеСотрудника
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Позиции.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ДействияПозицийШтатногоРасписания.ДействиеСотрудника КАК ДействиеСотрудника
		|ПОМЕСТИТЬ ВТТекущиеДействия
		|ИЗ
		|	ВТПозиции КАК Позиции
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДействияПозицийШтатногоРасписания КАК ДействияПозицийШтатногоРасписания
		|		ПО Позиции.ПозицияШтатногоРасписания = ДействияПозицийШтатногоРасписания.ПозицияШтатногоРасписания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания,
		|	ДействиеСотрудника
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(ТекущиеДействия.ПозицияШтатногоРасписания, ВТДействияПозиций.ПозицияШтатногоРасписания) КАК ПозицияШтатногоРасписания
		|ПОМЕСТИТЬ ВТИзменяемыеПозиции
		|ИЗ
		|	ВТТекущиеДействия КАК ТекущиеДействия
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТДействияПозиций КАК ВТДействияПозиций
		|		ПО ТекущиеДействия.ПозицияШтатногоРасписания = ВТДействияПозиций.ПозицияШтатногоРасписания
		|			И ТекущиеДействия.ДействиеСотрудника = ВТДействияПозиций.ДействиеСотрудника
		|ГДЕ
		|	(ТекущиеДействия.ПозицияШтатногоРасписания ЕСТЬ NULL
		|			ИЛИ ВТДействияПозиций.ПозицияШтатногоРасписания ЕСТЬ NULL)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТДействияПозиций.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ВТДействияПозиций.ДействиеСотрудника КАК ДействиеСотрудника
		|ИЗ
		|	ВТДействияПозиций КАК ВТДействияПозиций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзменяемыеПозиции КАК ВТИзменяемыеПозиции
		|		ПО ВТДействияПозиций.ПозицияШтатногоРасписания = ВТИзменяемыеПозиции.ПозицияШтатногоРасписания
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПозиции
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДействияПрофиля
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДействияПозиций
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТТекущиеДействия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТИзменяемыеПозиции";
	
	Запрос.УстановитьПараметр("ПрофильДолжности", ПрофильДолжности);
	Запрос.УстановитьПараметр("ДействияСотрудников", ДействияСотрудников);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ПозицияШтатногоРасписания") Цикл
		НаборЗаписей = РегистрыСведений.ДействияПозицийШтатногоРасписания.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПозицияШтатногоРасписания.Установить(Выборка.ПозицияШтатногоРасписания);
		
		Пока Выборка.Следующий() Цикл
			Если Выборка.ДействиеСотрудника = Null Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла; 
		
		НаборЗаписей.Записать(Истина);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти  

#КонецЕсли

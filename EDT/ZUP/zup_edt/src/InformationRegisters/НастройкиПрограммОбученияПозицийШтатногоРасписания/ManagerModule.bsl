#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбновитьНастройкиПрограммОбученияПозицийШтатногоРасписанияПоПрофилюДолжности(МенеджерВременныхТаблиц, ПродолжительностьПланаОбучения, РазмерностьПродолжительностиПланаОбучения) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&ПродолжительностьПланаОбучения КАК ПродолжительностьПланаОбучения,
		|	&РазмерностьПродолжительностиПланаОбучения КАК РазмерностьПродолжительностиПланаОбучения
		|ПОМЕСТИТЬ ВТНастройкиПрофиля
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПозиции.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ВТНастройкиПрофиля.ПродолжительностьПланаОбучения КАК ПродолжительностьПланаОбучения,
		|	ВТНастройкиПрофиля.РазмерностьПродолжительностиПланаОбучения КАК РазмерностьПродолжительностиПланаОбучения
		|ПОМЕСТИТЬ ВТНастройкиПозиций
		|ИЗ
		|	ВТПозиции КАК ВТПозиции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиПрофиля КАК ВТНастройкиПрофиля
		|		ПО (ИСТИНА)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Позиции.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	НастройкиПрограммОбученияПозицийШтатногоРасписания.ПродолжительностьПланаОбучения КАК ПродолжительностьПланаОбучения,
		|	НастройкиПрограммОбученияПозицийШтатногоРасписания.РазмерностьПродолжительностиПланаОбучения КАК РазмерностьПродолжительностиПланаОбучения
		|ПОМЕСТИТЬ ВТТекущиеНастройки
		|ИЗ
		|	ВТПозиции КАК Позиции
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПрограммОбученияПозицийШтатногоРасписания КАК НастройкиПрограммОбученияПозицийШтатногоРасписания
		|		ПО Позиции.ПозицияШтатногоРасписания = НастройкиПрограммОбученияПозицийШтатногоРасписания.ПозицияШтатногоРасписания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(ТекущиеНастройки.ПозицияШтатногоРасписания, НастройкиПозиций.ПозицияШтатногоРасписания) КАК ПозицияШтатногоРасписания
		|ПОМЕСТИТЬ ВТИзменяемыеПозиции
		|ИЗ
		|	ВТТекущиеНастройки КАК ТекущиеНастройки
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТНастройкиПозиций КАК НастройкиПозиций
		|		ПО ТекущиеНастройки.ПозицияШтатногоРасписания = НастройкиПозиций.ПозицияШтатногоРасписания
		|			И ТекущиеНастройки.ПродолжительностьПланаОбучения = НастройкиПозиций.ПродолжительностьПланаОбучения
		|			И ТекущиеНастройки.РазмерностьПродолжительностиПланаОбучения = НастройкиПозиций.РазмерностьПродолжительностиПланаОбучения
		|ГДЕ
		|	(ТекущиеНастройки.ПозицияШтатногоРасписания ЕСТЬ NULL
		|			ИЛИ НастройкиПозиций.ПозицияШтатногоРасписания ЕСТЬ NULL)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТНастройкиПозиций.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ВТНастройкиПозиций.ПродолжительностьПланаОбучения КАК ПродолжительностьПланаОбучения,
		|	ВТНастройкиПозиций.РазмерностьПродолжительностиПланаОбучения КАК РазмерностьПродолжительностиПланаОбучения,
		|	ИСТИНА КАК СоответствуютПрофилюДолжности
		|ИЗ
		|	ВТНастройкиПозиций КАК ВТНастройкиПозиций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзменяемыеПозиции КАК ВТИзменяемыеПозиции
		|		ПО ВТНастройкиПозиций.ПозицияШтатногоРасписания = ВТИзменяемыеПозиции.ПозицияШтатногоРасписания
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПозицияШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНастройкиПрофиля
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНастройкиПозиций
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТТекущиеНастройки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТИзменяемыеПозиции";
	
	Запрос.УстановитьПараметр("ПродолжительностьПланаОбучения", ПродолжительностьПланаОбучения);
	Запрос.УстановитьПараметр("РазмерностьПродолжительностиПланаОбучения", РазмерностьПродолжительностиПланаОбучения);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ПозицияШтатногоРасписания") Цикл
		НаборЗаписей = РегистрыСведений.НастройкиПрограммОбученияПозицийШтатногоРасписания.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПозицияШтатногоРасписания.Установить(Выборка.ПозицияШтатногоРасписания);
		
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла; 
		
		НаборЗаписей.Записать(Истина);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти  

#КонецЕсли

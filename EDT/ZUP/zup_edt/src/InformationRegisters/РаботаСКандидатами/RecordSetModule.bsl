#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьПрежнийНаборЗаписей();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьТекущийЭтапРаботы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СохранитьПрежнийНаборЗаписей()
	
	Если Отбор["Кандидат"].Использование = Ложь Или Не ЗначениеЗаполнено(Отбор.Кандидат.Значение) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Кандидат", Отбор.Кандидат.Значение);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеНабора.Период КАК Период,
		|	ДанныеНабора.Кандидат КАК Кандидат,
		|	ДанныеНабора.ЭтапРаботы КАК ЭтапРаботы,
		|	ДанныеНабора.Комментарий КАК Комментарий,
		|	ДанныеНабора.СостояниеЭтапа КАК СостояниеЭтапа,
		|	ДанныеНабора.ШаблонАнкеты КАК ШаблонАнкеты,
		|	ДанныеНабора.ПричинаОтклонения КАК ПричинаОтклонения,
		|	ДанныеНабора.ПериодОкончание КАК ПериодОкончание,
		|	ДанныеНабора.Бронь КАК Бронь
		|ИЗ
		|	РегистрСведений.РаботаСКандидатами КАК ДанныеНабора
		|ГДЕ
		|	ДанныеНабора.Кандидат = &Кандидат";
	
	ДополнительныеСвойства.Вставить("ПрежнийНабор", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ОбновитьТекущийЭтапРаботы(НаборЗаписей)
	
	Если Не ДополнительныеСвойства.Свойство("ПрежнийНабор") Тогда
		Возврат;
	КонецЕсли;
	
	ПрежнийНабор = ДополнительныеСвойства["ПрежнийНабор"];
	
	Если ПрежнийНабор.Количество() > 0 Или Количество() > 0 Тогда
		Если ОбщегоНазначения.КоллекцииИдентичны(ПрежнийНабор, ЭтотОбъект) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	КандидатыМассив = Новый Массив;
	КандидатыМассив.Добавить(Отбор.Кандидат.Значение);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КандидатыМассив, ВыгрузитьКолонку("Кандидат"), Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КандидатыМассив, ПрежнийНабор.ВыгрузитьКолонку("Кандидат"), Истина);
	
	УстановитьПривилегированныйРежим(Истина);
	ПодборПерсонала.ОбновитьТекущийЭтапРаботыКандидатов(КандидатыМассив);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция РезультатыОтправки(ТаблицаОтбор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаОтбор", ТаблицаОтбор);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаОтбор.ФизическоеЛицо,
		|	ТаблицаОтбор.РассылаемыйДокумент
		|ПОМЕСТИТЬ ВТТаблицаОтбор
		|ИЗ
		|	&ТаблицаОтбор КАК ТаблицаОтбор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РезультатыРассылки.ФизическоеЛицо КАК ФизическоеЛицо,
		|	РезультатыРассылки.РассылаемыйДокумент КАК РассылаемыйДокумент,
		|	РезультатыРассылки.ДатаОтправления КАК ДатаОтправления,
		|	РезультатыРассылки.Отправлено,
		|	РезультатыРассылки.Комментарий КАК ОписаниеОшибки
		|ИЗ
		|	РегистрСведений.РезультатыРассылкиДокументов КАК РезультатыРассылки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТаблицаОтбор КАК ТаблицаОтбор
		|		ПО ТаблицаОтбор.ФизическоеЛицо = РезультатыРассылки.ФизическоеЛицо
		|		И ТаблицаОтбор.РассылаемыйДокумент = РезультатыРассылки.РассылаемыйДокумент
		|УПОРЯДОЧИТЬ ПО
		|	ФизическоеЛицо,
		|	РассылаемыйДокумент,
		|	ДатаОтправления";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	РезультатыОтправки = РассылкаДокументов.ТаблицаРезультатовОтправки();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = РезультатыОтправки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Результат = РезультатыРассылкиДокументов.НовыйРезультатОтправки();
		ЗаполнитьЗначенияСвойств(НоваяСтрока.Результат, Выборка);
	КонецЦикла;
	
	Возврат РезультатыОтправки;
	
КонецФункции

Процедура ЗаписатьРезультатОтправки(ФизическоеЛицо, Получатель, РассылаемыйДокумент, РезультатОтправки) Экспорт
	
	НаборЗаписей = РегистрыСведений.РезультатыРассылкиДокументов.СоздатьНаборЗаписей();
	
	СтрокаНабора = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаНабора, РезультатОтправки);

	СтрокаНабора.ФизическоеЛицо = ФизическоеЛицо;
	СтрокаНабора.Получатель = Получатель;
	СтрокаНабора.РассылаемыйДокумент = РассылаемыйДокумент;
	
	СтрокаНабора.Комментарий = РезультатОтправки.ОписаниеОшибки;
	
	НаборЗаписей.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицо);
	НаборЗаписей.Отбор.Получатель.Установить(Получатель);
	НаборЗаписей.Отбор.РассылаемыйДокумент.Установить(РассылаемыйДокумент);
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей.Записать();
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПрочитатьЗапись(Отказ);
	УстановитьСвойстваЭлементовФормы();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьЗапись(Отказ)
	
	МенеджерЗаписи = РегистрыСведений.РезультатыРассылкиДокументов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ФизическоеЛицо = Параметры.ФизическоеЛицо;
	МенеджерЗаписи.Получатель = Параметры.Получатель;
	МенеджерЗаписи.РассылаемыйДокумент = Параметры.РассылаемыйДокумент;
	МенеджерЗаписи.Прочитать();
	
	Если Не МенеджерЗаписи.Выбран() Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сведения об отправке документа %1 получателю %2 отсутствуют.'"),
			Параметры.РассылаемыйДокумент,
			Параметры.Получатель);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(МенеджерЗаписи, "Запись");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	ТолькоПросмотр = Истина;
	УстановитьСвойстваЭлементовФормыДляОтправленногоДокумента();
	УстановитьСвойстваЭлементовФормыДляНеОтправленногоДокумента();
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормыДляОтправленногоДокумента()
	
	Если Не Запись.Отправлено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.РезультатКартинка.Картинка = БиблиотекаКартинок.ЗеленыйШар;
	Элементы.Получатель.Заголовок = НСтр("ru = 'отправлено получателю'");
	Элементы.ДатаОтправления.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Элементы.Комментарий.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормыДляНеОтправленногоДокумента()
	
	Если Запись.Отправлено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.РезультатКартинка.Картинка = БиблиотекаКартинок.КрасныйШар;
	Элементы.Получатель.Заголовок = НСтр("ru = 'не отправлено получателю'");
	Элементы.ДатаОтправления.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	Элементы.ДатаОтправления.Заголовок = НСтр("ru = 'последняя попытка'");
	Элементы.Комментарий.Видимость = Истина;
	
КонецПроцедуры

#КонецОбласти